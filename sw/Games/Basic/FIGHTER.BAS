10 PRINT CHR$(26)
11 T=0
15 BLANK$="       "
20 Y5=3:GOSUB 670
30 PRINT "YOU HAVE BEEN ASKED TO GO ON A MISSION TO DESTROY"
40 PRINT "FIVE ENEMY CRAFT THAT THREATEN THE GALAXY...."
50 PRINT "YOU MUST POSITION YOUR CRAFT SO THAT THE ENEMY"
60 PRINT "IS IN A POSITION IN THE CENTER OF YOUR GUNSIGHT IN"
70 PRINT "ORDER TO DESTROY HIM."
80 PRINT:PRINT "THE AIMING IS DONE AS FOLLOWS:"
90 PRINT "HIT THE ARROW KEYS TO MOVE YOUR SIGHTS.FOR EXAMPLE,"
100 PRINT "TO MOVE THE GUNSIGHT UP (TARGET DOWN),"
110 PRINT "HIT THE UP ARROW. HIT RETURN TO FIRE AND ESCAPE"
120 PRINT "TO REDRAW THE GUNSIGHT."
125 PRINT
130 PRINT "IF YOU FINISH WITH A RATING OVER 75%, YOU WILL"
135 PRINT "NOTICE THE TIME BETWEEN TARGET-MOVES SPEEDING UP."
140 PRINT "ALSO, IF YOUR RATING IS 50% OR LESS, THE TIME"
145 PRINT "WILL DECREASE. AS YOU GET BETTER, YOUR RATING"
150 PRINT "WILL ALSO IMPROVE.	GOOD LUCK!!!!!!"
170 FOR N=0 TO 1500: NEXT N
175 PRINT:RANDOMIZE
180 PRINT "TYPE N FOR NOVICE PILOT, E FOR EXPERIENCED,"
185 INPUT "OR A FOR ACE"; V$
190 IF V$="E" THEN V8=200:GOTO 220
200 IF V$="N" THEN V8=350:GOTO 220
210 IF V$ = "A" THEN V8=100:GOTO 220
215 GOTO 180
220 PRINT CHR$(26)
230 Y5=10:X5=10:GOSUB 670
240 PRINT "GOOD LUCK ON YOUR MISSION --- ON INTO BATTLE!!!"
250 FOR N=0 TO 1000:NEXT N
270 D2=1:D1=1:PRINT CHR$(26)
280 GOSUB 1140
290 Y5=6:X5=0:GOSUB 670:GOSUB 500
300 Y5=INT(10*RND+7)
310 X5=INT(20*RND+16)
320 GOSUB 670
330 GOSUB 640
340 GOSUB 690
350 Z$="":WHILE Z$=""
355 Z$=INKEY$
360 GOSUB 1080
365 WEND
370 D=(ASC(Z$) AND 127)
380 GOSUB 670
390 IF D=8 THEN X5=X5+1
400 IF D=12 THEN X5=X5-1
410 IF D=11 THEN Y5=Y5+1
420 IF D=10 THEN Y5=Y5-1
430 IF D=13 THEN 710
440 IF D=27 THEN P=X5:Q=Y5:Y5=6:X5=0:GOSUB 670:GOSUB 500:X5=P:Y5=Q
450 GOSUB 1050
460 GOTO 320
500 PRINT "                    ----------------------"
510 PRINT "                               |"
520 PRINT "                               |"
530 PRINT "                               |"
540 PRINT "           |                                       |"
550 PRINT "           |          |                 |          |"
560 PRINT "           |----------|                 |----------|"
570 PRINT "           |          |                 |          |"
580 PRINT "           |                                       |"
590 PRINT "                               |"
600 PRINT "                               |"
610 PRINT "                               |"
620 PRINT "                    ---------------------"
630 RETURN
640 PRINT "I-0-I"
650 P=Y5:Q=X5
660 RETURN
670 PRINT CHR$(27)+"="+CHR$(32+Y5)+CHR$(32+X5);
680 RETURN
690 PRINT CHR$(27)+"="+CHR$(32)+CHR$(32)
700 RETURN
710 IF Y5=12 THEN 740
720 GOSUB 880
730 GOTO 320
740 IF X5=29 THEN 770
750 GOSUB 880
760 GOTO 320
770 Y5=12:X5=28:GOSUB 670:PRINT "   POW   "
780 FOR N=0 TO 100:PRINT CHR$(7);:NEXT N
790 FOR N=0 TO 100:NEXT N
800 GOSUB 670:PRINT BLANK$+CHR$(0)
810 FOR N=0 TO 100:NEXT N
820 Y5=22:X5=0:GOSUB 670
830 PRINT "M A Y   T H E   F O R C E   B E   W I T H   Y O U !!"
840 Y5=1:X5=0:GOSUB 670
850 PRINT "ENEMY DESTROYED=";D1
860 D1=D1+1
865 IF D1=6 THEN 920
870 GOTO 300
880 P=Y5:Q=X5:Y5=2:X5=0:GOSUB 670
890 PRINT "NUMBER MISSED =";D2:D2=D2+1
900 Y5=P:X5=Q:GOSUB 670
910 RETURN
920 Y5=22:X5=10:GOSUB 670
930 D2=D2-1
940 Y5=20:X5=0:GOSUB 670
950 PRINT "THE HOME BASE HAS BEEN SAVED-CONGRATULATIONS"
960 PRINT "YOUR MISSION RECORD: ";5/(5+D2)*100;"% PERFORMANCE"
970 IF 5/(5+D2)*100 >75 THEN V8=V8-50:IF V8<=50 THEN 1200
980 IF 5/(5+D2)*100 <51 THEN V8=V8+50
990 PRINT "DO YOU WANT ANOTHER MISSION?";:PRINT SPACE$(30)
1000 Q$="":WHILE Q$="":Q$=INKEY$:WEND
1010 X=ASC(Q$):IF X>96 THEN X=X-32
1020 IF X<>89 THEN STOP
1030 GOTO 270
1040 Y5=0:X5=0:GOSUB 670
1050 PRINT CHR$(27)+"="+CHR$(32+P)+CHR$(32+Q);
1060 PRINT BLANK$+CHR$(0)
1070 RETURN
1080 T=T+1
1090 IF T>V8 THEN 1110
1100 RETURN
1110 IF X5>31 OR X5<7 THEN 1116
1112 X5=X5-1:Y5=Y5+1:GOSUB 1050: GOSUB 670:GOSUB 640: GOSUB 690: GOTO 1120
1116 X5=X5+1:Y5=Y5-1:GOSUB 1050:GOSUB 670:GOSUB 640:GOSUB 690:GOTO 1120
1120 IF Y5>18 OR Y5<5 THEN PRINT CHR$(26):PRINT "YOU'RE SHOT DOWN!!!":GOTO 1125
1122 T=0:RETURN
1125 X5=0:Y5=6:GOSUB 670:GOSUB 500:V8=V8+50:GOTO 990
1140 IF V8<=100 THEN Q$="RATING=ACE PILOT":GOTO 1170
1150 IF V8>= 350 THEN Q$="RATING=NOVICE PILOT":GOTO 1170
1160 Q$="RATING=EXPERIENCED PILOT"
1170 Y5=2:X5=25:GOSUB 670
1180 PRINT Q$
1190 RETURN
1200 PRINT CHR$(26)
1210 FOR I=1 TO 100:PRINT CHR$(7):NEXT I
1220 PRINT:PRINT " C O N G R A T U L A T I O N S !!!!"
1230 PRINT:PRINT "	YOU BEAT THE COMPUTER!!"
1240 END
