5 PRINT CHR$(26):WIDTH 80
10 PRINT:PRINT:PRINT:PRINT:Print TAB(14); "LUNAR LANDER"  
11 PRINT:PRINT:PRINT
12 FOR XX=1 TO 3000:NEXT
30 PRINT "THIS IS A COMPUTER SIMULATION OF AN APOLLO LUNAR"
40 PRINT "LANDING CAPSULE.":PRINT
50 PRINT "THE ON-BOARD COMPUTER HAS FAILED, SO YOU HAVE TO"
60 PRINT "LAND THE CAPSULE MANUALLY."
70 PRINT: PRINT "SET THE BURN RATE OF THE RETRO ROCKETS TO ANY VALUE BETWEEN"
80 PRINT "0 (FREE FALL) AND 200 (MAXIMUM BURN) POUNDS PER SECOND."
90 PRINT "YOU MAY SET A NEW BURN RATE EVERY 10 SECONDS.": PRINT
100 PRINT "THE CAPSULE WEIGHS 32,500 POUNDS, AND YOU HAVE 16,500"
105 PRINT "POUNDS OF FUEL."
110 PRINT:PRINT "GOOD LUCK!"
120 L=0
130 PRINT: PRINT "SECONDS","  MI + FT","   MPH","LB. FUEL","BURN RATE"
135 PRINT "-------","-----------","---------","--------","---------":PRINT
140 A=120:V=1:M=33000!:N=16500:G=.001:Z=1.8
150 PRINT USING "  ###";L;:PRINT"",
151 PRINT USING " ###  #,###";Int(A),INT(5280*(A-INT(A)));
152 PRINT"",:PRINT USING " #,###.##";3600*V;:PRINT"",
153 PRINT USING " ##,###";M-N;:PRINT"","  ";:INPUT K:T=10
160 IF M-N<.001 THEN 240
170 IF T<.001 THEN 150
180 S=T: IF M>=N+S*K THEN 200
190 S=(M-N)/K
200 GOSUB 420: IF I<=0 THEN 340
210 IF V<=0 THEN 230
220 IF J<0 THEN 370
230 GOSUB 330: GOTO 160
240 PRINT:PRINT:PRINT CHR$(22);"  FUEL OUT AT";L;"SECONDS  ";CHR$(22):Print
241 S=(-V+SQR(V*V+2*A*G))/G
250 V=V+G*S: L=L+S
260 PRINT:PRINT:W=3600*V: PRINT "ON MOON AT";L;"SECONDS - IMPACT VELOCITY";W;"MPH.":PRINT
270 IF W<=1.2 THEN PRINT "PERFECT LANDING!": GOTO 440
280 IF W<=10 THEN PRINT "GOOD LANDING (COULD BE BETTER)":GOTO 440
282 IF W>60 THEN 300
284 PRINT "CRAFT DAMAGE... YOU'RE STRANDED HERE UNTIL A RESCUE"
286 PRINT "PARTY ARRIVES.  HOPE YOU HAVE ENOUGH OXYGEN!"
288 GOTO 440
300 PRINT "SORRY THERE WERE NO SURVIVORS.  YOU BLEW IT!"
310 PRINT "IN FACT, YOU BLASTED A NEW LUNAR CRATER";W*.277;"FEET DEEP!"
320 GOTO 440
330 L=L+S: T=T-S: M=M-S*K: A=I: V=J: RETURN
340 IF S<.005 THEN 260
350 D=V+SQR(V*V+2*A*(G-Z*K/M)):S=2*A/D
360 GOSUB 420: GOSUB 330: GOTO 340
370 W=(1-M*G/(Z*K))/2: S=M*V/(Z*K*(W+SQR(W*W+V/Z)))+.05:GOSUB 420
380 IF I<=0 THEN 340
390 GOSUB 330: IF J>0 THEN 160
400 IF V>0 THEN 370
410 GOTO 160
420 Q=S*K/M: J=V+G*S+Z*(-Q-Q*Q/2-Q^3/3-Q^4/4-Q^5/5)
430 I=A-G*S*S/2-V*S+Z*S*(Q/2+Q^2/6+Q^3/12+Q^4/20+Q^5/30):RETURN
440 PRINT:PRINT:INPUT "TRY AGAIN";RR$:IF LEFT$(RR$,1)="Y" OR LEFT$(RR$,1)="y" 