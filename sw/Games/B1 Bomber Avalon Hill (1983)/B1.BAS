1 REM     B1-NG4
5 DEF FNUPPER$(X$)=CHR$(32*(X$>="a")+ASC(X$))
6 GOTO 10
7 FF$=INKEY$:IF FF$="" THEN RETURN ELSE GOTO 9
8 FF$=INPUT$(1):IF FF$="" THEN RETURN
9 FF$=FNUPPER$(FF$):RETURN 
10 REM
100 DIM C(10,2),C$(12)
101 N0=10:N1=20:N2=N0+N1
102 DIM N$(N2),T(N2,3)
105 REM PRINT CHR$(27)+"x5"
110 WIDTH 80:PRINT CHR$(27)+"E":ROW=0:COLM=0:GOSUB 40000
120 FOR I=1 TO 5:PRINT:NEXT I:PRINT TAB(25);"** B-1 NUCLEAR BOMBER GAME **":PRINT
130 PRINT TAB(38);"BY":PRINT:PRINT TAB(30);"MICROCOMPUTER GAMES"
140 PRINT TAB(33);"A DIVISION OF":PRINT TAB(25);"THE AVALON HILL GAME COMPANY":FOR I=1 TO 4:PRINT:NEXT
145 FOR I=1 TO 5000:NEXT:PRINT CHR$(27)+"E"
146 FOR I=1 TO 6:PRINT:NEXT I
147 PRINT TAB(20);"COMPUTER PROGRAM AND AUDIOVISUAL DISPLAY"
148 PRINT TAB(20);"COPYRIGHTED 1983.  ALL RIGHTS RESERVED."
149 FOR I=1 TO 8:PRINT:NEXT I
151 PRINT TAB(25);"  PRESS ANY KEY TO CONTINUE";:FOR I=1 TO 10:A$=INKEY$:NEXT I
152 A$=INKEY$:IF A$="" THEN J=J+1:IF J>10000 THEN J=1:GOTO 152 ELSE GOTO 152
153 J=J+VAL(RIGHT$(TIME$,2)):RANDOMIZE(J)
160 PRINT:PRINT
190 FOR I=1 TO 12:READ C$(I):NEXT
200 DATA"AU","EC","EV","PH","NA","AL"
210 DATA"CO","ST","RA","SE","AR","BO"
220 FOR I=1 TO N2:READ N$(I),T(I,1),T(I,2):NEXT
230 DATA"ARKHANGELSK",8261,5922,"ASTRAKHAN",8631,7947
240 DATA"KIYEV",7391,7467,"LENINGRAD",7661,6427
250 DATA"MOSKVA",7956,6937,"MURMANSK",8056,5452
260 DATA"SEVASTOPOL",7506,8152,"SVERDLOVSK",9311,6682
270 DATA"VOLGOGRAD",8331,7692,"YEREVAN",8381,8657
280 DATA"DUBOVKA",8350,7650,"KHARKOV",8850,7450
290 DATA"KONOSHA",8153,6325,"LIPETSK",8100,7210
300 DATA"OCHAMCHIRA",8060,8400,"ODESSA",7230,8050
310 DATA"ONEGA",8147,5976,"PECHENGA",7976,5410
320 DATA"PINSK",7115,7235,"PODOLSK",7900,7010
330 DATA"PSKOV",7475,6780,"ROSTOV",7990,8010
340 DATA"SARPA",8515,7930,"SYKTYVKAR",8825,6200
350 DATA"TALLINN",7060,6415,"TBILISI",8415,8435
360 DATA"UKHTA",8960,5925,"VINNITSA",7200,7585
370 DATA"VYBORG",7575,6300,"YARANSK",8575,6740
380 N$(0)="THULE AFB"
390 A9=25000:A0=100:C0=10:S=4500:P=6:F9=0:T=0:E=1.75
400 X=5500+RND*1500:Y=3500+RND*1000
410 FOR I=1 TO N2:T(I,3)=0:NEXT:A=INT(25000*RND):A1=A:R9=0
420 C=INT(RND*360):C1=C:T9=INT(RND*N0+1):T8=0:F=18500
430 FOR I=1 TO 10:C(I,1)=0:NEXT:F$="":FOR I=1 TO 5
440 F$=F$+CHR$(INT(RND*26)+65):NEXT:L9=.3
450 PRINT CHR$(27)+"E":FOR I=1 TO 5:PRINT:NEXT I:PRINT TAB(18);"  YOU ARE FLYING A B-1 BOMBER OUT OF":PRINT TAB(18);"THULE AFB.  YOU ARE IN AN ALERT STATUS":PRINT TAB(24);"ORBITING OVER THE ARCTIC.":PRINT
460 FOR I=1 TO 4000:NEXT:PRINT CHR$(27)+"E":PRINT TAB(29);"** FLASH-HOT WAR **"
470 FOR I=1 TO 30:PRINT CHR$(7);:PRINT CHR$(7);:PRINT CHR$(7);:NEXT:PRINT:PRINT TAB(18);"YOUR PRIMARY TARGET IS ";:PRINT N$(T9);:PRINT"."
490 PRINT:PRINT TAB(18);"YOUR ALTERNATE TARGETS  ARE:":J=0:FOR I=1 TO N0:IF I=T9 THEN 520
500 J=J+1:PRINT TAB((J-1)*15+18);N$(I);:IF J<3 GOTO 520
510 J=0:IF I<9 THEN PRINT
520 NEXT:PRINT
537 F$="":FOR I=1 TO 5:F$=F$+CHR$(INT(RND*26)+65):NEXT:PRINT
538 PRINT TAB(18);"FAILSAFE CODE IS: ";:PRINT F$;:PRINT ". MEMORIZE IT!":PRINT
540 PRINT TAB(18);"SOVIET DEFENSE COMPLEXES ARE:"
550 J=0:FOR I=N0+1 TO N2:J=J+1
560 PRINT TAB((J-1)*15+18);N$(I);:IF J<3 THEN 580
570 J=0:PRINT
580 NEXT:PRINT:PRINT:PRINT:PRINT TAB(18);"  GOOD LUCK! (HIT ANY KEY TO BEGIN)"
582 FOR Z1=1 TO 10:AX$=INKEY$:NEXT Z1:AX$=INPUT$(1)
585 GOSUB 10000
590 GOSUB 1000:GOSUB 20040:REM PRINT CHR$(27)+"y5"
591 ROW=12:COLM=44:GOSUB 40000:FOR Z1=1 TO 10:AX$=INKEY$:NEXT Z1:PRINT "COMMAND: ";:AX$="":FOR Z1=1 TO 350: IF AX$="" THEN GOSUB 7:AX$=FF$
592 IF AX$="" THEN NEXT Z1:GOTO 980 
593 IF AX$="+" THEN GOTO 585
596 IF AX$=CHR$(13) THEN 590
597 IF AX$<>"A" AND AX$<>"C" AND AX$<>"R" AND AX$<>"B" AND AX$<>"N" AND AX$<>"P" AND AX$<>"E" AND AX$<>"S" THEN PRINT SPC(1);: GOTO 591 ELSE PRINT AX$;
598 GOSUB 8:AY$=FF$:IF AY$=CHR$(8) THEN PRINT CHR$(8);" ";CHR$(8):GOTO 591 ELSE PRINT AY$;:A$=AX$+AY$
599 ROW=22:COLM=30:GOSUB 40000
600 A$=LEFT$(A$,2):I=0
610 I=I+1:IF A$=C$(I) THEN 670
620 IF I<12 THEN 610
630 GOSUB 20050:FOR I=1 TO 3000:NEXT I
660 GOTO 590
670 ON I GOTO 750,1330,1070,1180,880,680,700,630,980,940,710,790
680 GOSUB 20040:ROW=12:COLM=44:GOSUB 40000:PRINT"NEW ALTITUDE? ";:I=58:J=12:XY=1:GOSUB 30300:XY=0:A1=VAL(ZI$):IF A1>25000 THEN GOTO 694 ELSE IF A1<100 THEN GOTO 696 ELSE ROW=J:COLM=56:GOSUB 40000:PRINT " ";:PRINT A1;SPC(9)
685 IF A1>A9 THEN A1=A9 ELSE IF A1<A0 THEN A1=A0
690 T0=T+13+INT(15*RND):GOTO 1390
694 ROW=12:COLM=44:GOSUB 40000:PRINT "MAXIMUM ALTITUDE = 25000";:PRINT SPC(12):GOTO 685
696  ROW=12:COLM=44:GOSUB 40000:PRINT "MINIMUM ALTITUDE = 100";SPC(14):GOTO 685
700 GOSUB 20040:ROW=12:COLM=44:GOSUB 40000:PRINT "NEW COURSE? ";:J=12:I=56:XY=2:GOSUB 30300:XY=0:C1=VAL(ZI$):IF C1=>360 THEN 700 ELSE ROW=J:COLM=54:GOSUB 40000:PRINT " ";:PRINT C1;SPC(13);:ROW=J:COLM=70:GOSUB 40000:PRINT SPC(9):GOTO 690
710 IF F9=2 THEN 740 ELSE IF F9<>0 THEN 845 ELSE GOSUB 20040:ROW=12:COLM=44:GOSUB 40000:PRINT"FAIL SAFE CODE? ":J=12:I=60:GOSUB 30300:A$=ZI$:IF A$=F$ THEN F9=1
720 IF F9=0 THEN 730 ELSE ROW=4:COLM=2:GOSUB 40000:PRINT"+BOMB ARMED+";:ROW=14:COLM=49:GOSUB 40000:PRINT "++ BOMB ARMED ++";:L9=10*L9:GOTO 690
730 ROW=14:COLM=44:GOSUB 40000:PRINT "WRONG CODE.";:GOTO 690
740 ROW=14:COLM=44:GOSUB 40000:PRINT "BOMB ALREADY DROPPED.";:GOTO 690
750 I=0:J=0:K=0:GOSUB 20040:ROW=11:COLM=46:GOSUB 40000:PRINT"LENGTH OF TIME FOR AUTOPILOT";:ROW=13:COLM=46:GOSUB 40000:PRINT "SEC:";
751 GOSUB 758:IF AY$=CHR$(13) THEN 760 ELSE I=VAL(AY$):PRINT AY$;
752 GOSUB 758:IF AY$=CHR$(13) THEN 760 ELSE I=I*10+VAL(AY$):PRINT AY$;" MIN:";
753 GOSUB 758:IF AY$=CHR$(13) THEN 760 ELSE J=VAL(AY$):PRINT AY$;
754 GOSUB 758:IF AY$=CHR$(13) THEN 760 ELSE J=J*10+VAL(AY$):PRINT AY$;" HRS:";
755 GOSUB 758:IF AY$=CHR$(13) THEN 760 ELSE K=VAL(AY$):PRINT AY$:GOTO 760
758 GOSUB 7:AY$=FF$:IF AY$<>CHR$(13) AND (AY$<"0" OR AY$>"9") THEN 758 ELSE RETURN
760 I=-I*(I>0):J=-J*(J>0):K=-K*(K>0):T2=T+I+60*J+3600*K
770 IF T2=T THEN T2=T2+1
780 GOTO 1390
790 GOSUB 20040:ROW=12:COLM=44:GOSUB 40000:IF F9=2 THEN ELSE IF F9=0 THEN 850 ELSE PRINT"TARGET? ";:J=12:I=52:GOSUB 30300:A$=ZI$
800 ROW=14:COLM=42:GOSUB 40000:IF LEN(A$)<2 THEN 860 ELSE A$=LEFT$(A$,2):GOSUB 9010:ZX=I:IF I=0 OR I>N0 THEN 860
810 GOSUB 9040:IF FR>250 THEN 870 ELSE A$="":ROW=13:COLM=44:GOSUB 40000:PRINT"CONFIRM DROP (Y/N)? ";:J=13:I=64:GOSUB 30300:A$=ZI$
815 I=ZX:Z=LEN(N$(I))+11:B1$="+"+STRING$(Z,61)+"+":B2$=B1$
820 IF A$="" THEN 690 ELSE IF LEFT$(A$,1)<>"Y" THEN 690 ELSE GOSUB 20030:ROW=16:COLM=10:GOSUB 40000:PRINT N$(I);" DESTROYED!":PRINT CHR$(7);:FOR ZX=1 TO 10:FOR ZQ=1 TO 250:NEXT ZQ:NEXT ZX
830 L9=L9/15:PRINT CHR$(7):ROW=4:COLM=2:GOSUB 40000:PRINT "BOMB DROPPED";
840 T8=I:S=S+250:A0=A0+50:F9=2:T0=T+3+INT(5*RND):GOTO 1390
845 ROW=14:COLM=44:GOSUB 40000:PRINT "BOMB ALREADY ARMED.";:GOTO 690
850 PRINT"BOMB NOT YET ARMED.";:GOTO 690
860 PRINT"IMPROPER TARGET.";:GOTO 690
870 PRINT N$(I);" NOT IN RANGE.";:GOTO 690
880 A$="":GOSUB 20040:ROW=12:COLM=44:GOSUB 40000:PRINT"WHERE TO? ";:J=12:I=54:GOSUB 30300:A$=ZI$:IF LEN(A$)<2 THEN 930 ELSE A$=LEFT$(A$,2)
890 GOSUB 9010:IF A$="TH" THEN 900 ELSE IF I=0 THEN 930
900 GOSUB 9040:GOSUB 9060:GOSUB 20040:ROW=12:COLM=49:GOSUB 40000:PRINT N$(I);" BEARS";:PRINT USING"####.#";FA;:PRINT "T"
910 ROW=14:COLM=59:GOSUB 40000:PRINT "RANGE:";:PRINT INT(FR+.5);"KM.";:FOR I=1 TO 1000:NEXT I
920 T0=T+40+INT(41*RND):GOTO 1390
930 ROW=14:COLM=42:GOSUB 40000:PRINT"NOT ON THE MAP.";:GOTO 920
940 I=0:J=10000!:FOR K=N0+1 TO N2:FI=I:I=K:GOSUB 9040:L=FR:I=FI:IF L>J THEN 970
950 IF T(K,3)=2 THEN 970
960 FI=I:I=K:GOSUB 9060:I=FI:IF ABS(C-FA)>45 AND ABS(C-FA)<315 THEN 970 ELSE J=L:I=K
970 NEXT:IF I>0 THEN 900 ELSE ROW=14:COLM=42:GOSUB 40000:PRINT"NO COMPLEX FOUND.";:GOTO 920
980 GOSUB 20040:GOSUB 1000:ZZ=J:IF J=0 THEN:PRINT "NO CONTACTS.";
990 T0=T+5+INT(RND*11):GOTO 1390
992 FOR I=1 TO 10:IF C(I,1)<>0 THEN RETURN ELSE NEXT I
1000 GOSUB 20030:ROW=14:COLM=6:GOSUB 40000:PRINT "INTERCEPT TABLE: ";:J=0:FOR  I=1 TO 10:IF C(I,1)=0 THEN 1020 ELSE J=1
1010 GOSUB 9080
1013 IF INT(I/2)=I/2 THEN ROW=14+I/2:COLM=23:GOSUB 40000:PRINT Z1$:ROW=14+I/2:COLM=23:GOSUB 40000:PRINT FM$;"=";:GOTO 1017
1014 ROW=14+I/2:COLM=2:GOSUB 40000:PRINT Z2$:ROW=14+I/2:COLM=4:GOSUB 40000:PRINT FM$;"=";
1017 IF C(I,2)-T<100 THEN PRINT " ";
1018 IF C(I,2)-T<10 THEN PRINT " ";
1019 PRINT C(I,2)-T;"SEC"
1020 Z(I,1)=C(I,2)-T:NEXT:RETURN
1040 PRINT"ENOUGH FUEL REMAINING TO FLY";:PRINT F;:PRINT"KM."
1050 IF F9<>2 THEN PRINT"PRIMARY TARGET: ";:PRINT N$(T9);:PRINT"."
1060 PRINT P;:PRINT"PHOENIX MISSILES LEFT.":T0=T+20+INT(RND*21):GOTO 1390
1070 A1=200+INT(300*RND):IF RND<.5 THEN A1=-A1
1080 A=A+A1
1090 IF A<0 THEN 1870 ELSE A1=A:C1=60+INT(60*RND)
1100 IF RND>.5 THEN C1=-C1
1110 C=C+C1:C=C+360*(C>360)-360*(C<0)
1120 C1=C:FOR I=1 TO 10:IF C(I,1)=0 THEN 1170
1130 J=(8+2*(C(I,1)<10))/(C(I,2)-T):IF J>.95 THEN J=.95
1140 IF RND>J THEN 1170 ELSE GOSUB 9080:GOSUB 20030:ROW=22:COLM=30:GOSUB 40000:PRINT CHR$(7):ROW=15:COLM=2:GOSUB 40000:PRINT Z2$:ROW=15:COLM=2:GOSUB 40000:PRINT FM$;" LOSES TARGET LOCK";
1150 IF C(I,1)>10 THEN ROW=16:COLM=2:GOSUB 40000:PRINT " AND RETURNS TO BASE";
1160 PRINT".":C(I,1)=0
1170 NEXT:T0=T+10+INT(21*RND):GOTO 1390
1180 GOSUB 20040:ROW=11:COLM=44:GOSUB 40000:IF P=0 THEN 1230 ELSE A$="":PRINT"PHOENIX MISSILE ARMED.":ROW=13:COLM=44:GOSUB 40000:PRINT"TARGET? ":J=13:I=52:GOSUB 30300:A$=ZI$
1190 IF A$="" THEN 1260 ELSE IF LEN(A$)<2 THEN 1240 ELSE A$=LEFT$(A$,2):GOSUB 9010
1200 IF I<N0+1 THEN 1240 ELSE GOSUB 9040:IF FR>200 THEN 1250 ELSE ROW=14:COLM=42:GOSUB 40000:PRINT Z2$:ROW=14:COLM=42:GOSUB 40000:PRINT"PHOENIX MISSILE AWAY!":FOR ZQ=1 TO 500:NEXT ZQ
1210 GOSUB 20030:ROW=16:COLM=3:GOSUB 40000:PRINT N$(I);" DEFENSE COMPLEX DESTROYED":T(I,3)=2:P=P-1
1220 T0=T+3+INT(RND*5):GOTO 1390
1230 ROW=14:COLM=42:GOSUB 40000:PRINT"NO MORE MISSILES.":GOTO 1220
1240 ROW=14:COLM=42:GOSUB 40000:PRINT"INVALID TARGET.":GOTO 1220
1250 ROW=14:COLM=45:GOSUB 40000:PRINT N$(I);" OUT OF RANGE.":GOTO 1220
1260 J=10000!:K=0:FOR I=1 TO 10:IF C(I,1)=0 THEN 1280
1270 IF C(I,2)-T>J THEN 1280 ELSE J=C(I,2)-T:K=I
1280 NEXT:ROW=14:COLM=42:GOSUB 40000:PRINT"PHOENIX MISSILE AWAY!":FOR ZQ=1 TO 500:NEXT ZQ:P=P-1:IF K=0 THEN 1320
1290 IF J>10 THEN 1320 ELSE IF C(K,1)<10 AND RND>.6 THEN 1320
1300 GOSUB 20040:ROW=12:COLM=42:GOSUB 40000:IF C(K,1)>10 AND RND>.85 THEN 1320 ELSE FI=I:I=K:GOSUB 9080:I=FI:PRINT FM$;" DESTROYED.":FOR ZQ=1 TO 200:NEXT ZQ
1310 C(K,1)=0:GOTO 1220
1320 ROW=15:COLM=65:GOSUB 40000:PRINT" MISSED!":GOTO 1220
1330 FOR I=1 TO 10:IF C(I,1)=0 THEN 1380 ELSE J=E*(.5+5/(C(I,2)-T))
1340 IF J>.97 THEN J=.97
1350 IF RND>J THEN 1380 ELSE GOSUB 9080:GOSUB 20030:ROW=19:COLM=5:GOSUB 40000:PRINT FM$;
1360 IF C(I,1)>10 THEN PRINT" LOSES TARGET LOCK":ROW=20:COLM=12:GOSUB 40000:PRINT "AND RETURNS TO BASE."  ELSE PRINT" SELF-DESTRUCTS."
1370 C(I,1)=0:FOR ZX=1 TO 5:PRINT CHR$(7);:NEXT ZX:FOR ZX=1 TO 200:NEXT ZX
1380 NEXT:E=E*(.7+.3*RND):T0=T+3+INT(5*RND):GOTO 1390
1390 IF F<1 THEN 1890 ELSE T3=T0:I9=1:IF T0>T THEN 1430 ELSE I9=0:T3=INT(RND*301)+T
1400 FOR I=1 TO 10:IF C(I,1)=0 THEN 1420 ELSE IF C(I,2)<T3 THEN T3=C(I,2)
1410 IF T>=C(I,2)-10 THEN 1420 ELSE IF C(I,2)-10<T3 THEN T3=C(I,2)-10
1420 NEXT:IF (T2<=T) OR (T3<T2) THEN 1430 ELSE T3=T2:I9=1
1430 T0=T3-T:A2=A0*T0:C2=C0*T0
1440 IF A2>ABS(A-A1) THEN A=A1 ELSE A=A+A2*SGN(A1-A)
1450 IF C2>ABS(360*ABS(C-C1)>180)-ABS(C-C1) THEN 1470
1460 C=C+C2*SGN(C1-C):C=C-360*(C<0)+360*(C>360):GOTO 1480
1470 C=C1
1480 X=X+S*T0*SIN(C*3.14159265#/180)/3600
1490 F=F-INT(S*(T3-T)/3600)
1500 Y=Y-S*T0*COS(C*3.14159265#/180)/3600:T=T3
1510 IF F<0 THEN 1890
1520 I=0
1530 I=I+1:IF C(I,1)=0 THEN GOTO 1590 ELSE IF C(I,2)>T THEN GOTO 1580
1540 GOSUB 20030:ROW=15:COLM=13:GOSUB 40000:PRINT"-----------------":ROW=16:COLM=13:GOSUB 40000:PRINT"NUCLEAR AIRBURST!":ROW=17:COLM=13:GOSUB 40000:PRINT"-----------------":R9=1:C(I,1)=0:I9=1
1550 PRINT CHR$(7);:FOR ZX=1 TO 1500:NEXT:A=A+INT(3000*RND*SGN(.5-RND)):GOSUB 20030:IF A<0 THEN 1870
1560 A1=A:F=INT(F*RND):S=INT(S*(.5+.5*RND))
1570 IF RND<.15 THEN 1880 ELSE 1590
1580 IF C(I,2)>T+10 THEN 1590 ELSE GOSUB 9080:GOSUB 20030:ROW=19:COLM=2:GOSUB 40000:PRINT FM$;" IN PHOENIX RANGE.":I9=1
1590 IF I<10 THEN 1530 ELSE IF F9=2 THEN 1650
1600 FOR I=1 TO N0:GOSUB 9040:J=FR:IF J>250 THEN 1620 ELSE IF T(I,3)<>0 THEN 1620
1610 ROW=18:COLM=2:GOSUB 40000:PRINT Z2$:ROW=18:COLM=5:GOSUB 40000:PRINT N$(I);" IN BOMB RANGE.":T(I,3)=1:I9=1
1620 IF T(I,3)<>1 THEN 1640 ELSE IF J<250 THEN 1640
1630 ROW=19:COLM=2:GOSUB 40000:PRINT Z2$:ROW=19:COLM=2:GOSUB 40000:PRINT N$(I);" NO LONGER IN BOMB RANGE.":T(I,3)=0
1640 NEXT
1650 FOR I=N0+1 TO N2:IF T(I,3)=2 THEN 1840
1660 IF ABS(X-T(I,1))>750 THEN 1840
1670 IF ABS(Y-T(I,2))>750 THEN 1840 ELSE GOSUB 9040:J=FR:IF J>750 THEN 1840
1680 IF J>200 THEN 1700 ELSE IF T(I,3)=1 THEN 1700 ELSE T(I,3)=1
1690 ROW=19:COLM=2:GOSUB 40000:PRINT Z2$:ROW=19:COLM=2:GOSUB 40000:PRINT N$(I);:PRINT" COMPLEX IN PHOENIX RANGE":I9=1
1700 IF T(I,3)<>1 THEN 1720 ELSE IF J<200 THEN 1720 ELSE T(I,3)=0
1710 ROW=19:COLM=2:GOSUB 40000:PRINT Z2$:ROW=19:COLM=2:GOSUB 40000:PRINT N$(I);:PRINT" COMPLEX OUT OF PH RANGE":ROW=22:COLM=30:GOSUB 40000:PRINT SPC(1)
1720 IF RND(0)>L9*(T0/300)*(A^.125) THEN 1840
1730 K=0
1740 K=K+1:IF C(K,1)=0 THEN 1760 ELSE IF K<10 THEN 1740
1750 GOTO 1840
1760 IF RND>.5 THEN 1780
1770 C(K,1)=INT(9*RND+1):M=6500:GOTO 1790
1780 C(K,1)=21+2*INT(RND*7):M=5000
1790 GOSUB 9060:L=(FA-C)*.0174532:S1=S*SIN(L):S2=S*COS(L)
1800 L=SQR(M*M-S1*S1)+SGN(3.14159-ABS(L))*ABS(S2)
1810 GOSUB 9040:FI=I:I=K:GOSUB 9080:I=FI:C(K,2)=T+INT(3600*FR/L+1)
1820 ROW=14:COLM=2:GOSUB 40000:PRINT Z2$:ROW=14:COLM=2:GOSUB 40000:PRINT N$(I);" COMPLEX LAUNCHES A ";FM$;".":FOR ZX=37 TO 77 STEP 10:ROW=22:COLM=30:GOSUB 40000:PRINT CHR$(7):NEXT
1830 I9=1
1840 NEXT:I=0:GOSUB 9060:L=ABS(C-FA):GOSUB 9040
1850 IF (L<30 OR L>330) AND FR<6500 AND T>2000 THEN 1900
1860 GOSUB 10080:IF I9=1 THEN 590 ELSE 1390
1870 PRINT "B-1 CRASHES INTO HILLSIDE.":GOSUB 20100:R9=2:PRINT CHR$(27)+"E":WIDTH 40:GOTO 1910
1880 PRINT CHR$(27)+"E":PRINT "B-1 DESTROYED.":PRINT:R9=2:GOTO 1910
1890 PRINT CHR$(27)+"E":PRINT "B-1 RUNS OUT OF FUEL.":GOTO 1870
1900 WIDTH 80:PRINT CHR$(27)+"E":PRINT TAB(10);"B-1 LANDS AT THULE AFB.":PRINT:PRINT TAB(13);"MISSION COMPLETE.":PRINT
1910 PRINT TAB(5);"MISSION SUMMARY:":IF T8=0 THEN 1940
1920 IF T8=T9 THEN PRINT "PRIMARY"; ELSE PRINT "SECONDARY";
1930 PRINT " TARGET, ";N$(T8);:PRINT" DESTROYED."
1940 PRINT:PRINT "DEFENSE COMPLEXES DESTROYED:":J=0:FOR I=N0 TO N2
1950 IF T(I,3)<>2 THEN 1960 ELSE PRINT TAB(3);N$(I):J=1
1960 NEXT:IF J=0 THEN PRINT "NONE."
1970 IF R9=1 THEN PRINT "CREW WILL DIE FROM RADIATION EXPOSURE."
1980 IF R9=2 THEN PRINT"CREW DEAD."
1990 PRINT:A$="":INPUT "GO AGAIN (Y/N)";A$:A$=A$+" ":IF LEFT$(A$,1)="Y" OR LEFT$(A$,1)="y" THEN CLEAR:GOTO 1: ELSE 44444
9000 REM ******************** SUBROUTINES **************************
9010 I=0
9020 I=I+1:IF LEFT$(N$(I),2)=A$ THEN RETURN ELSE IF I<N2 THEN 9020
9030 I=0:RETURN
9040 IF I=0 THEN FR=SQR(X*X+Y*Y):RETURN
9050 FX=X-T(I,1):FY=Y-T(I,2):FR=SQR(FX*FX+FY*FY):RETURN
9060 FX=X-T(I,1):FY=Y-T(I,2):IF FY=0 THEN FA=90-180*(FX>0):RETURN
9070 FA=180*(ATN(-FX/FY)/3.14159-(FY<0)-2*(FY>0 AND FX>0)):RETURN
9080 FM$=STR$(C(I,1)):FM$=RIGHT$(FM$,LEN(FM$)-1)
9090 IF C(I,1)<10 THEN FM$="SAM-"+FM$ ELSE FM$="MIG-"+FM$
9100 RETURN
9999 REM ** SCREEN SETUP ** (USES ALL ZBASED VARIABLES)
10000 PRINT CHR$(27)+"E";
10005 Z1$=STRING$(12,32):Z2$=STRING$(38,32):Z3$=STRING$(38,61):Z4$="+"+STRING$(38,45)+"+"
10010 PRINT "+";Z3$;"+"
10020 FOR I=2 TO 20:ROW=I:COLM=1:GOSUB 40000:PRINT "|":ROW=I:COLM=40:GOSUB 40000:PRINT "|":NEXT
10030 ROW=13:COLM=1:GOSUB 40000:PRINT Z4$:ROW=21:COLM=1:GOSUB 40000
10040 PRINT "+";Z3$;"+"
10050 ROW=1:COLM=14: GOSUB 40000:PRINT "+":ROW=1:COLM=27:GOSUB 40000:PRINT "+":FOR I=2 TO 12:ROW=I:COLM=14:GOSUB 40000:PRINT "|"
10052 ROW=I:COLM=27:GOSUB 40000:PRINT "|":NEXT:ROW=13:COLM=14:GOSUB 40000:PRINT "+":ROW=13:COLM=27:GOSUB 40000:PRINT "+"
10060 ROW=3:COLM=1:GOSUB 40000:PRINT "+"+STRING$(12,45)+"+":ROW=3:COLM=27:GOSUB 40000:PRINT "+"+STRING$(12,45)+"+"
10070 ROW=5:COLM=1:GOSUB 40000:PRINT "+"+STRING$(12,45)+"+":ROW=12:COLM=21:GOSUB 40000:PRINT "^"
10080 ROW=2:COLM=31:GOSUB 40000:PRINT "STATUS"
10081 ROW=4:COLM=29: GOSUB 40000:PRINT "ALT:";:PRINT A:IF A1=A AND A>25000 THEN A1=25000
10082 ROW=6:COLM=29:GOSUB 40000:PRINT "CO:";:PRINT USING "####.#";C
10083 ROW=8:COLM=29:GOSUB 40000:PRINT "SPEED:":ROW=9:COLM=29:GOSUB 40000:PRINT STRING$(10,32):ROW=9:COLM=29:GOSUB 40000:PRINT S;"KPH"
10084 ROW=11:COLM=28:GOSUB 40000:PRINT "FUEL TO FLY:":ROW=12:COLM=29:GOSUB 40000:PRINT STRING$(10,32):ROW=12:COLM=29:GOSUB 40000:PRINT F;"KM."
10085 ROW=2:COLM=4:GOSUB 40000: PRINT "STATUS"
10086 ROW=6:COLM=2:GOSUB 40000:PRINT "PRI TARGET:":ROW=7:COLM=3:GOSUB 40000:PRINT N$(T9)
10087 ROW=9:COLM=2:GOSUB 40000:PRINT "PH MISSILES:":ROW=10:COLM=6:GOSUB 40000: PRINT P
10088 ROW=12:COLM=2:GOSUB 40000:PRINT "TIME:";T
10090 Z3$=CHR$(42):Z4$=" ":FOR ZX=15 TO 26
10100 ROW=2:COLM=ZX:GOSUB 40000:PRINT Z3$;:FOR I=3 TO 11:ROW=I:COLM=ZX:GOSUB 40000:PRINT Z4$;:NEXT I:ROW=2:COLM=ZX:GOSUB 40000:PRINT Z4$;
10110 IF ZX=21 THEN GOSUB 29980
10120 NEXT ZX:PRINT CHR$(7)
10130 ROW=4:COLM=2:GOSUB 40000
10135 IF F9=0 THEN PRINT "<BOMB SAFE>"
10140 IF F9=1 THEN PRINT "<BOMB ARMED>"
10145 IF F9=2 THEN PRINT "BOMB DROPPED"
10150 RETURN
20000 REM ***** CLEAR SCREEN PARTS ***
20010 FOR ZS=2 TO 12:ROW=ZS:COLM=2:GOSUB 40000:PRINT Z1$:NEXT ZS:RETURN
20020 FOR ZS=2 TO 12:ROW=ZS:COLM=28:GOSUB 40000:PRINT Z1$:NEXT ZS:RETURN
20030 FOR ZS=14 TO 20:ROW=ZS:COLM=2:GOSUB 40000:PRINT Z2$:ROW=22:COLM=7:GOSUB 40000:PRINT SPC(1):NEXT:RETURN
20040 FOR ZS=10 TO 20:ROW=ZS:COLM=42:GOSUB 40000:PRINT Z2$:NEXT:RETURN
20050 GOSUB 20040
20060 ROW=11:COLM=42:GOSUB 40000:PRINT "VALID COMMANDS ARE:";
20070 ROW=12:COLM=42:GOSUB 40000:PRINT "COurse, ALtitude, NAvigator, ARm bomb,"
20080 ROW=13:COLM=42:GOSUB 40000:PRINT "AUtopilot, EVasive action, ECm, BOmb,"
20085 ROW=14:COLM=42:GOSUB 40000:PRINT "SEarch, RAdar"
20090 RETURN
20100 FOR I=1 TO 4000:NEXT:RETURN
29978 REM
29979 REM    UPDATE RADAR BLIPS
29980 IF ZZ=0 THEN RETURN ELSE FOR I=1 TO 10:IF C(I,1)=0 OR Z(I,1)<1 OR Z(I,1)>199 THEN 30100
30010 IF Z(I,2)=0 THEN Z(I,2)=INT(RND*4+17):GOTO 30100
30030 IF SGN(21-Z(I,2))=1 THEN Z(I,2)=Z(I,2)+1 ELSE Z(I,2)=Z(I,2)-1
30050 ROW=INT((200-Z(I,1))*.044+2) :COLM=Z(I,2):GOSUB 40000:PRINT "."
30100 NEXT I:RETURN
30300 FOR ZI=1 TO 20:GOSUB 7:ZJ$=FF$:NEXT ZI:ZI$=""
30303 REM PRINT CHR$(27)+"y5"
30305 PRINT CHR$(22);:ROW=J:COLM=I:GOSUB 40000
30310 GOSUB 7:ZJ$=FF$:IF ZJ$=""THEN 30310
30315 IF LEN(ZI$)>=11 THEN PRINT SPC(1):PRINT:RETURN
30320 IF ASC(ZJ$)=13 THEN PRINT " ":PRINT:RETURN
30325 IF LEN(ZI$)=0 AND ASC(ZJ$)=8 THEN GOTO 30310
30330 IF ASC(ZJ$)=8 THEN I=I-2:ROW=J:COLM=I:GOSUB 40000:PRINT SPC(2);:ZI$=LEFT$(ZI$,LEN(ZI$)-1):ROW=J:COLM=I:GOSUB 40000:GOTO 30305
30332 IF XY=2 THEN GOTO 30359
30333 IF XY=1 THEN GOTO 30360
30335 I=I+2
30340 PRINT ZJ$;CHR$(22):ROW=J:COLM=I:GOSUB 40000:ZI$=ZI$+ZJ$:GOTO 30310
30359 IF ASC(ZJ$)=46 THEN GOTO 30335
30360 IF ASC(ZJ$)>=48 AND ASC(ZJ$)<=57 THEN GOTO 30335 ELSE 30310
40000 PRINT CHR$(27)+"Y"+CHR$(31+ROW)+CHR$(31+COLM);:RETURN
44444 PRINT :END
