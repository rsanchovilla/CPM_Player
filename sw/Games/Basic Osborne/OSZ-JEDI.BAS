100 REM STARWAR3 - STARWARS GAME MODIFIED TO WORK ON OSBORNE 1 SCREEN
105 REM OSBORNE 1 SHOULD BE IN 80 COLUMN MODE
110 REM UTILIZES THE INKEY$ FUNCTION OF MICROSOFT'S MBASIC
115 REM MODIFIED FOR THE OSBORNE 1 BY CARL WARNER ON 8/14/83
1000 WIDTH 80
1010 CL$=CHR$(26):REM                 CLEARS SCREEN & HOMES CURSOR
1020 HY$=CHR$(27)+CHR$(41):REM        HALF-INTENSITY ENABLED
1030 HN$=CHR$(27)+CHR$(40):REM        FULL INTENSITY RESTORED
1040 GY$=CHR$(27)+CHR$(103):REM       GRAPHIC MODE ENABLED
1050 GN$=CHR$(27)+CHR$(71):REM        GRAPHIC MODE DISABLED       
1060 LET E$=CHR$(27):PRINT CL$;E$;"=#'";
1070 PRINT GY$;TAB(15);"faaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaf";GN$
1080 PRINT TAB(18);GY$;"`";SPC(43);"`";GN$
1090 PRINT TAB(18);GY$;"`";GN$;"              'S T A R W A R S'            ";GY$;"`";GN$
1100 PRINT TAB(18);GY$;"`";SPC(43);"`";GN$
1110 PRINT TAB(18);GY$;"`";GN$;"     This program presented courtesy of    ";
1120 PRINT GY$;"`";GN$
1130 PRINT TAB(18);GY$;"`";SPC(43);"`";GN$
1140 PRINT TAB(18);GY$;"`";GN$;HY$;" C U S T O M   S O F T W A R E   G R O U P ";
1150 PRINT HN$;GY$;"`";GN$
1160 PRINT TAB(18);GY$;"`";GN$;"   Specializing in custom programming for  ";
1170 PRINT E$;"g";"`";GN$
1180 PRINT TAB(18);GY$;"`         HEATH/ZENITH DATA SYSTEMS         `";GN$
1190 PRINT TAB(18);GY$;"`";GN$;SPC(5);"(Modified by Carl Warner 8/14/83)";SPC(5);GY$;"`";GN$
1200 PRINT TAB(18);GY$;"`";GN$;SPC(12);"(For the Osborne 1)";SPC(12);GY$;"`";GN$
1210 PRINT TAB(18);GY$;"`                                           `";GN$
1220 PRINT TAB(18);GY$;"`           CUSTOM SOFTWARE GROUP           `";GN$
1230 PRINT TAB(18);GY$;"`            BELLEVUE, NE  68005            `";GN$
1240 PRINT TAB(18);GY$;"`              PHONE 291-4622               `";GN$
1250 PRINT TAB(18);GY$;"`                    291-5819               `";GN$
1260 PRINT TAB(18);GY$;"`";SPC(43);"`";GN$
1270 PRINT TAB(18);GY$;"raaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaar";GN$
1280 PRINT TAB(28);"PRESS RETURN TO CONTINUE"
1290 A$=INPUT$(1)
1300 PRINT GN$
1310 CLEAR 1000
1320 CL$=CHR$(26):REM                 CLEARS SCREEN & HOMES CURSOR
1330 HY$=CHR$(27)+CHR$(41):REM        HALF-INTENSITY ENABLED      
1340 HN$=CHR$(27)+CHR$(40):REM        FULL INTENSITY RESTORED     
1350 GY$=CHR$(27)+CHR$(103):REM       GRAPHIC MODE ENABLED        
1360 GN$=CHR$(27)+CHR$(71):REM        GRAPHIC MODE DISABLED       
1370 E$=CHR$(27)
1380 PRINT CL$;E$;"=! "
1390 PRINT ,"   ********   **********    ******    *********"
1400 PRINT ,"  **      **      **       **    **   **      **"
1410 PRINT ,"  **              **      **      **  **      **"
1420 PRINT ,"   ********       **      **********  *********"
1430 PRINT ,"          **      **      **      **  **    **"
1440 PRINT ,"  **      **      **      **      **  **     **"
1450 PRINT ,"   ********       **      **      **  **      **"
1460 PRINT:PRINT
1470 PRINT ,"  **      **    ******    *********    ********"
1480 PRINT ,"  **      **   **    **   **      **  **      **"
1490 PRINT ,"  **      **  **      **  **      **  **"
1500 PRINT ,"  **  **  **  **********  *********    ********"
1510 PRINT ,"  **  **  **  **      **  **    **            **"
1520 PRINT ,"   ********   **      **  **     **   **      **"
1530 PRINT ,"    **  **    **      **  **      **   ********"
1540 FOR I=1 TO 1000:NEXT I
1550 PRINT E$;"=7 "
1560 PRINT ,"A LONG TIME AGO IN A GALAXY FAR, FAR AWAY, A GREAT"
1570 FOR I=1 TO 1000:NEXT I:PRINT
1580 PRINT ,"ADVENTURE TOOK PLACE. IT IS A PERIOD OF CIVIL WAR."
1590 FOR I=1 TO 1000:NEXT I:PRINT
1600 PRINT ,"REBEL SPACE SHIPS STRIKING FROM A HIDDEN BASE HAVE"
1610 FOR I=1 TO 1000:NEXT I:PRINT
1620 PRINT ,"WON THEIR FIRST VICTORY AGAINST THE EVIL  GALACTIC"
1630 FOR I=1 TO 1000:NEXT I:PRINT
1640 PRINT ,"EMPIRE.  DURING THE BATTLE, REBEL SPIES MANAGED TO"
1650 FOR I=1 TO 1000:NEXT I:PRINT
1660 PRINT ,"STEAL SECRET PLANS TO THE EMPIRE'S ULTIMATE WEAPON"
1670 FOR I=1 TO 1000:NEXT I:PRINT
1680 PRINT ,"THE DEATH STAR, AN ARMOURED SPACE STATION WITH THE"
1690 FOR I=1 TO 1000:NEXT I:PRINT
1700 PRINT ,"FIRE POWER TO DESTROY AN ENTIRE PLANET. "
1710 FOR I=1 TO 1000:NEXT I:PRINT:PRINT
1720 PRINT ,"     YOUR MISSION AS ONE OF THE REBEL PILOTS IS TO"
1730 FOR I=1 TO 1000:NEXT I:PRINT
1740 PRINT ,"ATTACK AND DESTROY THE 'DEATH STAR'.  WHILE MAKING"
1750 FOR I=1 TO 1000:NEXT I:PRINT
1760 PRINT ,"YOUR ATTACK YOU HAVE ENCOUNTERED 'DARTH VADER' AND"
1770 FOR I=1 TO 1000:NEXT I:PRINT
1780 PRINT ,"HIS IMPERIAL STORM TROOPERS IN THEIR TIE FIGHTERS."
1790 FOR I=1 TO 1000:NEXT I:PRINT
1800 PRINT ,"YOU MUST DESTROY THEM BEFORE THEY CAN DESTROY YOU."
1810 FOR I=1 TO 1000:NEXT I:PRINT:PRINT
1820 PRINT ,"    GOOD LUCK AND MAY THE 'FORCE' BE WITH YOU     "
1830 FOR I=1 TO 2000:NEXT I
1840 PRINT CL$
1850 REM
1860 REM .........................INSTRUCTIONS ROUTINE.......................
1870 REM
1880 PRINT E$;"=*6";"DO YOU NEED:":PRINT
1890 PRINT TAB(30);"FULL INSTRUCTIONS <F>?"
1900 PRINT TAB(30);"BRIEF INSTRUCTIONS <B>?"
1910 PRINT TAB(30);"NO INSTRUCTIONS <N>?"
1920 A$=INPUT$(1):IF A$="F" THEN 2010
1930 IF A$="B" THEN 2410
1940 IF A$="N" THEN 1950 ELSE 1920
1950 PRINT CL$
1960 PRINT:PRINT TAB(29);"WHAT IS YOUR SKILL RATING?"
1970 A$=INPUT$(1):IF A$<"1" OR A$>"9" THEN 1990
1980 LET A$=A$+"0":LET A9=CVI(A$):LET A9=A9-12336:GOTO 2680
1990 PRINT:PRINT TAB(26);"EVIDENTLY YOU NEED INSTRUCTIONS!"
2000 FOR I=1 TO 500:NEXT I:PRINT
2010 PRINT CL$;E$;"=! "
2020 PRINT "YOU HAVE BEEN EQUIPPED WITH AN X-WING   ";
2030 PRINT "   AS IN A REAL DOG-FIGHT YOU MUST  LEAD"
2040 PRINT "FIGHTER BY THE REBEL ALLIANCE.   YOUR   ";
2050 PRINT "   THE TARGET WHEN FIRING YOUR LASER. IF"
2060 PRINT "FIGHTER IS COMPUTER CONTROLLED BY THE   ";
2070 PRINT "   YOU WAIT UNTIL HE IS EXACTLY CENTERED"
2080 PRINT "NUMERIC KEYPAD ON YOUR KEY BOARD. THE   ";
2090 PRINT "   TO FIRE, THEN YOU WILL MISS HIM.  YOU"
2100 PRINT "DISPLAY YOU WILL SEE IS YOUR  ONBOARD   ";
2110 PRINT "   MAY CONTINUE TO FIRE AT HIM, BUT WHEN"
2120 PRINT "SCANNER.  IN ORDER TO KILL  AN  ENEMY   ";
2130 PRINT "   YOU ARE FIRING YOUR WEAPON, YOUR CON-"
2140 PRINT "FIGHTER HE MUST RECEIVE A DIRECT  HIT   ";
2150 PRINT "   TROL OF DIRECTION IS NOT AS  GOOD  AS"
2160 PRINT "OR MULTIPLE DAMAGING HITS.   HE  WILL   ";
2170 PRINT "   WHEN YOU AREN'T FIRING AND THE TARGET"
2180 PRINT "RECEIVE DAMAGE IF HIT WHEN HE IS  +/-   ";
2190 PRINT "   MAY FLY OUT OF YOUR SIGHTS.          "
2200 PRINT "10 DEGREES IN ELEVATION AND HE IS +/-   ";
2210 PRINT " "
2220 PRINT "9 DEGREES IN BEARING.  IN ADDITION TO   ";
2230 PRINT "   IN ORDER TO CENTER YOUR TARGET ON THE"
2240 PRINT "THIS HE MUST BE WITHIN 6500 KMS.        ";
2250 PRINT "   SCANNER YOU MUST FLY TOWARDS HIM.    "
2260 PRINT "                                        ";
2270 PRINT "   TO DO THIS YOU PRESS THE KEY  ON  THE"
2280 PRINT "WARNING:  THE 'TIE' FIGHTER  HAS  THE   ";
2290 PRINT "   NUMERIC KEY PAD THAT IS IN HIS DIREC-"
2300 PRINT "CAPABILITY TO POP IN AND OUT OF  HYP-   ";
2310 PRINT "   TION. IE: IF HE IS IN THE UPPER-RIGHT"
2320 PRINT "ERSPACE.  THEREFORE HE MAY  DISAPPEAR   ";
2330 PRINT "   QUADRANT YOU WOULD PRESS KEY 9 TO FLY"
2340 PRINT "RIGHT FROM BEFORE YOUR SIGHTS.          ";
2350 PRINT "   TOWARDS HIM. IF HE IS BELOW, PRESS 2."
2360 PRINT "                                        ";
2370 PRINT " "
2380 PRINT "      MAY THE FORCE BE WITH YOU         ";
2390 PRINT "         PRESS RETURN TO CONT           ";
2400 LINE INPUT A$
2410 PRINT CL$
2420 PRINT GY$;
2430 PRINT ,"     UP &              UP             UP &";GN$
2440 PRINT E$;"g","     LEFT   fkkkkkg fkkkkkg fkkkkkg   RIGHT";GN$
2450 PRINT E$;"g","            `  7  ` `  8  ` `  9  `";GN$
2460 PRINT E$;"g","            rkkkkkt rkkkkkt rkkkkkt";GN$
2470 PRINT E$;"g","            fkkkkkg fkkkkkg fkkkkkg";GN$
2480 PRINT E$;"g","     LEFT   `  4  ` `  5  ` `  6  `   RIGHT";GN$
2490 PRINT E$;"g","            rkkkkkt rkkkkkt rkkkkkt";GN$
2500 PRINT E$;"g","            fkkkkkg fkkkkkg fkkkkkg";GN$
2510 PRINT E$;"g","            `  1  ` `  2  ` `  3  `";GN$
2520 PRINT E$;"g","     DOWN   rkkkkkt rkkkkkt rkkkkkt   RIGHT";GN$
2530 PRINT E$;"g","     LEFT   fkkkkkg   DOWN            DOWN";GN$
2540 PRINT E$;"g","            `  0  `";GN$
2550 PRINT E$;"g","            rkkkkkt";GN$
2560 PRINT ,"             FIRE"
2570 PRINT TAB(18);"<<PRESS '5' TO STOP ANY TURNS OR CLIMBS.>>":PRINT
2580 PRINT TAB(23);"ENTER A SKILL RATING FROM 1 TO 9"
2590 PRINT TAB(27);"NOVICE.................1"
2600 PRINT TAB(27);"EXPERT.................9":PRINT
2610 PRINT "AFTER SCANNER CONSTRUCTION IS COMPLETE, PRESS ANY CONTROL KEY TO"
2620 PRINT "START YOUR SCAN"
2630 A$=INPUT$(1):IF A$<"1" OR A$>"9" THEN 2630
2640 LET A$=A$+"0":LET A9=CVI(A$):LET A9=A9-12336
2650 REM
2660 REM ......................GRID CONSTRUCTION ROUTINE....................
2670 REM
2680 PRINT CL$
2690 GY$=CHR$(27)+CHR$(103)
2700 GN$=CHR$(27)+CHR$(71)
2710 PRINT TAB(16);GY$;"RANGE:m9999 KM    ELEVATION:m90 DEG  BEARING:m90 DEG";GN$
2720 PRINT:ED=0
2730 PRINT TAB(8);GY$;"-90  -75  -60  -45  -30  -15   0   +15  +30  +45  +60  +75  +90";GN$
2740 PRINT TAB(5);GY$;"+90 bhhhhbhhhhbhhhhbhhhhbhhhhbhhhhbhhhhbhhhhbhhhhbhhhhbhhhhbhhhhb +90";GN$
2750 PRINT TAB(9);GY$;"v";SPC(59);"v";GN$:PRINT TAB(9);GY$;"v";SPC(59);"v";GN$
2760 PRINT TAB(5);GY$;"+60 k";SPC(59);"k +60";GN$
2770 PRINT TAB(9);GY$;"v";SPC(59);"v";GN$:PRINT TAB(9);GY$;"v";SPC(59);"v";GN$
2780 PRINT TAB(5);GY$;"+30 k";SPC(59);"k +30";GN$
2790 PRINT TAB(9);GY$;"v";SPC(59);"v";GN$:PRINT TAB(9);GY$;"v";SPC(59);"v";GN$
2800 PRINT TAB(6);GY$;"0  k";SPC(59);"k  0";GN$
2810 PRINT TAB(9);GY$;"v";SPC(59);"v";GN$:PRINT TAB(9);GY$;"v";SPC(59);"v";GN$
2820 PRINT TAB(5);GY$;"-30 k";SPC(59);"k -30";GN$
2830 PRINT TAB(9);GY$;"v";SPC(59);"v";GN$:PRINT TAB(9);GY$;"v";SPC(59);"v";GN$
2840 PRINT TAB(5);GY$;"-60 k";SPC(59);"k -60";GN$
2850 PRINT TAB(9);GY$;"v";SPC(59);"v";GN$:PRINT TAB(9);GY$;"v";SPC(59);"v";GN$
2860 PRINT GY$;"    -90 buuuubuuuubuuuubuuuubuuuubuuuubuuuubuuuubuuuubuuuubuuuubuuuub -90";GN$
2870 PRINT GY$;"        -90  -75  -60  -45  -30  -15   0   +15  +30  +45  +60  +75  +90";GN$;
2880 REM
2890 REM ......................INPUT CONTROL SCAN ROUTINE...................
2900 REM
2910 CD=200+INT(200*RND(1)):FL$="5"
2920 IF CT=CD THEN 4020 ELSE CT=CT+1
2930 IF FL$="0" THEN 3050
2940 KP$=INKEY$
2950 IF KP$>="0" AND KP$<="9" THEN FL$=KP$ ELSE IF KP$<>"" THEN DM$=KP$
2960 KP$=INKEY$
2970 IF KP$>"0" AND KP$<="9" THEN FL$=KP$ ELSE IF KP$<>"" THEN DM$=KP$
2980 GOSUB 3440:REM ..FIGHTER UPDATE
2990 PRINT E$;"=&3+";E$;"=23+";E$;"=)=+";E$;"=/=+";E$;"=,G+";E$;"=)Q+";E$;"=/Q+";E$;"=&[+";E$;"=2[+"
3000 FOR I=1 TO 100:NEXT I
3010 GOTO 2920
3020 REM
3030 REM .......................FIRE WEAPON ROUTINE........................
3040 REM
3050 FOR I=8 TO 1 STEP -1
3060 PRINT E$;"=";CHR$(44+I);CHR$(71-I);GY$;"b";GN$
3070 PRINT E$;"=";CHR$(44+I);CHR$(71+I);GY$;"n";GN$
3080 IF I > 6 THEN 3110
3090 PRINT E$;"=";CHR$(46+I);CHR$(69-I);" "
3100 PRINT E$;"=";CHR$(46+I);CHR$(73+I);" "
3110 NEXT I
3120 PRINT E$;"=.E ";E$;"=.I ";E$;"=-F ";E$;"=-H "
3130 IF FX<>44 OR FY<>70 THEN 3290
3140 IF KM>5000 THEN 2960
3150 PRINT HY$;E$;"=8? ENEMY DESTROYED ":DG=0
3160 PRINT HY$:ED=ED+1
3170 PRINT E$;"=";CHR$(FX-1);CHR$(FY);"<|>"
3180 PRINT E$;"=";CHR$(FX);CHR$(FY-1);"     "
3190 PRINT HN$;E$;"=";CHR$(FX+1);CHR$(FY);"<|>"
3200 FOR I=1 TO 25:PRINT CHR$(7);:NEXT I
3210 PRINT E$;"=";CHR$(FX);CHR$(FY);"***"
3220 FOR I=1 TO 25: NEXT I
3230 PRINT E$;"=";CHR$(FX-1);CHR$(FY);"   "
3240 PRINT E$;"=";CHR$(FX);CHR$(FY-1);"***"
3250 PRINT E$;"=";CHR$(FX+1);CHR$(FY);"   "
3260 FOR I=1 TO 25: NEXT I
3270 PRINT E$;"=";CHR$(FX);CHR$(FY);"   "
3280 FQ=0:IF ED=5 THEN 3960 ELSE 2960
3290 IF FX<43 OR FX>45 THEN 3410
3300 IF FY<68 OR FY>72 THEN 3410
3310 LET DG=DG+1:IF DG=3 THEN 3150
3320 PRINT E$;"=";CHR$(FX-1);CHR$(FY);"[=]"
3330 PRINT E$;"=";CHR$(FX);CHR$(FY-1);"I"
3340 PRINT E$;"=";CHR$(FX);CHR$(FY+3);"I"
3350 PRINT E$;"=";CHR$(FX+1);CHR$(FY);"[=]"
3360 FOR I=1 TO 250: NEXT I
3370 PRINT E$;"=";CHR$(FX-1);CHR$(FY);"   "
3380 PRINT E$;"=";CHR$(FX);CHR$(FY-1);" "
3390 PRINT E$;"=";CHR$(FX);CHR$(FY+3);" "
3400 PRINT E$;"=";CHR$(FX+1);CHR$(FY);"   ":GOTO 2960
3410 PRINT E$;"=8 ";E$;"T";
3420 GOTO 2960
3430 REM
3440 REM ...................FIGHTER POSITION UPDATE ROUTINE................
3450 REM
3460 IF FQ=1 THEN 3530 ELSE FQ=1
3470 LET FX=INT(100*RND(1))
3480 IF FX<36 OR FX>52 THEN 3470
3490 LET FY=INT(200*RND(1))
3500 IF FY<44 OR FY>97 THEN 3490
3510 LET KM=INT(10000*RND(1))
3520 FZ=INT(16*RND(1))
3530 IF INT(100*RND(1))>6 THEN 3550
3540 FZ=INT (16*RND(1))
3550 IF INT(10*RND(1))<A9 THEN 3560 ELSE X=FX:Y=FY:GOTO 3720
3560 IF FZ<>0 THEN 3570 ELSE X=FX-1:Y=FY+1
3570 IF FZ<>1 THEN 3580 ELSE S=FX:Y=FY+1
3580 IF FZ<>2 THEN 3590 ELSE X=FX+1:Y=FY+1
3590 IF FZ<>3 THEN 3600 ELSE X=FX+1:Y=FY
3600 IF FZ<>4 THEN 3610 ELSE X=FX+1:Y=FY-1
3610 IF FZ<>5 THEN 3620 ELSE X=FX:Y=FY-1
3620 IF FZ<>6 THEN 3630 ELSE X=FX-1:Y=FY-1
3630 IF FZ<>7 THEN 3640 ELSE X=FX-1:Y=FY
3640 IF FZ<>8 THEN 3650 ELSE X=FX-1:Y=FY+2
3650 IF FZ<>9 THEN 3660 ELSE X=FX-1:Y=FY+3
3660 IF FZ<>10 THEN 3670 ELSE X=FX+1:Y=FY+3
3670 IF FZ<>11 THEN 3680 ELSE X=FX+1:Y=FY+2
3680 IF FZ<>12 THEN 3690 ELSE X=FX+1:Y=FY-2
3690 IF FZ<>13 THEN 3700 ELSE X=FX+1:Y=FY-3
3700 IF FZ<>14 THEN 3710 ELSE X=FX-1:Y=FY-3
3710 IF FZ<>15 THEN 3720 ELSE X=FX-1:Y=FY-2
3720 IF FL$<>"1" THEN 3730 ELSE X=X-1:Y=Y+1
3730 IF FL$<>"2" THEN 3740 ELSE X=X-2:Y=Y
3740 IF FL$<>"3" THEN 3750 ELSE X=X-1:Y=Y-1
3750 IF FL$<>"4" THEN 3760 ELSE X=X:Y=Y+2
3760 IF FL$<>"6" THEN 3770 ELSE X=X:Y=Y-2
3770 IF FL$<>"7" THEN 3780 ELSE X=X+1:Y=Y+1
3780 IF FL$<>"8" THEN 3790 ELSE X=X+2:Y=Y
3790 IF FL$<>"9" THEN 3800 ELSE X=X+1:Y=Y-1
3800 IF X>39 AND X<50 AND Y>55 AND Y<87 THEN 3840
3810 IF SGN(KM)=+1 THEN KM=KM+INT(200*RND(1))
3820 IF SGN(KM)=-1 THEN KM=KM-INT(200*RND(1))
3830 IF KM<10000 THEN 3860 ELSE 3930
3840 IF SGN(KM)=+1 THEN KM=KM-INT(200*RND(1))
3850 IF SGN(KM)=-1 THEN KM=KM+INT(200*RND(1))
3860 IF X<36 OR X>52 OR Y<42 OR Y>98 THEN 3930
3870 IF X=FX AND Y=FY THEN RETURN
3880 PRINT E$;"=";CHR$(FX);CHR$(FY);"   ":FX=X:FY=Y
3890 PRINT E$;"=";CHR$(FX);CHR$(FY);GY$;"hmj";GN$
3900 PRINT E$;"= 5";KM
3910 EV=10*(44-FX):PRINT E$;"= K";EV
3920 BR=3*(FY-70):PRINT E$;"= \";BR:RETURN
3930 PRINT E$;"=";CHR$(FX);CHR$(FY);"   "
3940 FQ=0:RETURN
3950 PRINT E$;"=8 ";E$;"T":RETURN
3960 FOR I=1 TO 500:NEXT I:PRINT E$;"=  ";CL$
3970 PRINT HN$;E$;"=( ";GN$
3980 PRINT CL$
3990 PRINT ,"CONGRATULATIONS ON A JOB WELL DONE.  YOU HAVE"
4000 PRINT ,"DESTROYED DARTH VADER AND HIS STORM TROOPERS "
4010 PRINT ,"AND HAVE SAVED THE REBELLION.":GOTO 4070
4020 PRINT E$;"=( "
4030 PRINT CL$
4040 PRINT ,"YOU HAVE ONLY DESTROYED";ED;"TIE FIGHTERS AND"
4050 PRINT ,"YOU LET DARTH VADER GET AWAY. OH WELL,I GUESS";GN$
4060 PRINT ,"WE WANT HIM AROUND FOR THE SEQUEL ANYWAY!"
4070 PRINT:PRINT ,"DO YOU WANT TO PLAY AGAIN <Y or N>?"
4080 A$=INPUT$(1):IF A$="Y" THEN 1950 ELSE IF A$<>"N"THEN 4080 ELSE 4090
4090 PRINT CL$
4100 PRINT:PRINT:PRINT:PRINT:PRINT
4110 PRINT "       FIGHTING EVIL CAN BE TIRING.  COME BACK WHEN YOU'RE READY FOR ACTION!"
4120 END
