10 REM     IFR JET FLIGHT SIMULATOR
15 REM     FLIGHT AND LANDING MODULE
20 REM     CREATED BY GENE SZYMANSKI; PUBLISHED IN BYTE, NOV. 82
25 REM     OSBORNE VERSION BY ROBERT A. FELDMAN, JANUARY, 1983
30 REM     CHICAGO'S FIRST OSBORNE GROUP
35 CLEAR ,,5000:CL$=CHR$(26):PRINT CL$
40 PRINT "IFR JET FLIGHT SIMULATOR"
45 PRINT "FLIGHT AND LANDING MODULE"
50 PRINT:PRINT
55 DIM RS$(3), F(9), PDQ(2), FA(2), CC(2), AL(2), DP(7), LL(1), GL(1)
60 DIM LS(1), GS(1), VO(5), VO$(1), GX(1), GY(1), TW(1), BL(5), LB(6)
65 DIM KM(3), TM(3), N$(3), CP(7)
70 PRINT"SPEED UP TIME (5X) WHEN ABOVE 5000 FT (Y/N)?"
75 B$=INKEY$:SS=SS+1:IF B$="" THEN 75
80 RANDOMIZE SS:PRINT CL$
85 REM    *BEGIN CRUISE MODULE
90 KR=57.2958:XQ=64:Q$=CHR$(27)+"=":RS=1:IF RND(0)<.5 THEN RS=-1
95 RW=(15-5)*RND(0)+5:RW=RS*RW:TD=28905
100 REM    *SET UP WIND TABLE
105 DIM M$(20), WA(10,1)
110 FOR I=0 TO 7:WA(I,0)=INT(359*RND(0)+1):NEXT
115 FOR I=8 TO 10:WA(I,0)=90*RND(0)+225:NEXT
120 A=0
125 FOR I=1 TO 10:WA(I,1)=25*RND(0)+A:A=A+25:NEXT
130 REM    *LANDING RUNWAY VARIABLES
135 RS$(0)=STRING$(31,152):RS$(1)=SPACE$(13)+"*   *"+SPACE$(13)
140 RS$(2)=SPACE$(11)+"*       *":RS$(3)=SPACE$(9)+STRING$(13,152)
145 REM    *VOR STATION FREQUENCY TABLE
150 DIM VF(15)
155 VF(0)=115.9:VF(1)=113.8:VF(2)=112.7:VF(3)=117.7:VF(4)=117.8
160 VF(5)=112.2:VF(6)=117.4:VF(7)=115.5:VF(8)=116.4:VF(9)=113.6
165 VF(10)=116.9:VF(11)=117!:VF(12)=112.3:VF(13)=117.9
170 VF(14)=115.7:VF(15)=112.8
175 REM    *ILS CONSTANTS FOR AIRPORTS
180 DIM VG(15,1)
185 VG(0,0)=238:VG(0,1)=28:VG(2,0)=240:VG(2,1)=30:VG(5,0)=90:VG(5,1)=217
190 VG(7,0)=299:VG(7,1)=164:VG(8,0)=166:VG(8,1)=42:VG(15,0)=341:VG(15,1)=75
195 REM    *VOR STATION COORDINATES
200 DIM VP(15,1)
205 VP(0,0)=40.633:VP(0,1)=73.773:VP(1,0)=40.202:VP(1,1)=74.495
210 VP(2,0)=42.358:VP(2,1)=70.993:VP(3,0)=41.282:VP(3,1)=70.027
215 VP(4,0)=42.743:VP(4,1)=73.802:VP(5,0)=46.412:VP(5,1)=84.315
220 VP(6,0)=38.35:VP(6,1)=81.77:VP(7,0)=40.917:VP(7,1)=77.993
225 VP(8,0)=42.928:VP(8,1)=78.647:VP(9,0)=41.358:VP(9,1)=82.162
230 VP(10,0)=42.967:VP(10,1)=83.742:VP(11,0)=44.555:VP(11,1)=88.195
235 VP(12,0)=41.547:VP(12,1)=88.318:VP(13,0)=39.495:VP(13,1)=76.978
240 VP(14,0)=42.048:VP(14,1)=83.458:VP(15,0)=39.637:VP(15,1)=75.303
245 REM    *PANEL HEADERS
250 DATA FUEL,LBS,%,VHF,MHZ,THRUST,MAX,IDLE,REV
255 DATA PITCH," +"," -",DEG,FLAPS,UP,DWN,WHEELS,UP,MID,DOWN
260 DATA COMPASS,AIRSPEED,KTS," VERT",SPEED,FPM
265 DATA ALTITUDE,FEET,CLOCK
270 DIM P$(28):FOR I=0 TO 28:READ P$(I):NEXT
275 DIM S$(25):FOR I=1 TO 25:S$(I)=SPACE$(I):NEXT
280 FOR I=0 TO 9:F(I)=0:NEXT
285 REM    *INITIALIZE VARIABLES AFTER TAKEOFF
290 FU=195480!:FP=63:CC=75:PDQ=380:RC=6704:AL=1900
295 MZ=77:TR=4:FL=1:FA=10:BR=10:WH=10
300 RA=0:PDQ(1)=380:FA(1)=10:CC(1)=75:AL(1)=1900:RP=40
305 DP(4)=39:DP(5)=52:DP(6)=75:DP(7)=15
310 L1=39.8667:G1=75.25:LL(1)=L1:GL(1)=G1:LS(1)=L1:GS(1)=G1
315 VO(1)=0!:VO$(1)="OUT ":VO(3)=0:VO(4)=999.9
320 GX(0)=0:GY(0)=0:XO=36481!:YO=0
325 GOSUB 2870:TL=TD:TW(1)=TD:GOSUB 355
330 GOSUB 580
335 K$=INKEY$:IF LEN(K$)=0 GOTO 345
340 GOSUB 825
345 GOSUB 1120
350 REM    *DISPLAY RANEL HEADERS
355 PRINT Q$;CHR$(32);CHR$(35);P$(0);S$(4);P$(5);S$(3);P$(9);S$(12);P$(20)
360 PRINT Q$;CHR$(41);CHR$(86);P$(13);S$(3);P$(16)
365 PRINT Q$;CHR$(42);CHR$(88);P$(14);S$(6);P$(14)
370 PRINT Q$;CHR$(44);CHR$(88);P$(15);S$(5);P$(15)
375 PRINT Q$;CHR$(33);CHR$(45);P$(6);S$(6);P$(10);S$(6)
380 PRINT Q$;CHR$(33);CHR$(100);P$(28)
385 PRINT Q$;CHR$(34);CHR$(33);P$(1);S$(4);P$(2);S$(21);S$(3);S$(13);P$(23)
390 PRINT Q$;CHR$(35);CHR$(54);P$(11);S$(13);P$(21);S$(2);P$(24);S$(2);P$(26)
395 PRINT Q$;CHR$(36);CHR$(32);P$(3)
400 PRINT Q$;CHR$(37);CHR$(71);P$(22);S$(6);P$(25);S$(5);P$(27)
405 PRINT Q$;CHR$(38);CHR$(32);P$(4);S$(10);P$(7);S$(3);P$(12);S$(7)
410 PRINT Q$;CHR$(39);CHR$(45);P$(8)
415 PRINT Q$;CHR$(35);CHR$(100);"VLF OMEGA"
420 PRINT Q$;CHR$(36);CHR$(97);"LAT"
425 PRINT Q$;CHR$(37);CHR$(96);"LONG"
430 PRINT Q$;CHR$(38);CHR$(64);". . . . : . . . ."
435 PRINT Q$;CHR$(40);CHR$(71);"RUD"
440 PRINT Q$;CHR$(41);CHR$(64);STRING$(17,95)
445 FOR X=32 TO 48 STEP 16:FOR Y=10 TO 22
450 PRINT Q$;CHR$(Y+32);CHR$(X+32);CHR$(124);
455 NEXT Y:NEXT X
460 PRINT Q$;CHR$(55);CHR$(64);STRING$(17,95);
465 PRINT Q$;CHR$(42);CHR$(71);"VOR"
470 PRINT Q$;CHR$(43);CHR$(76);"MHZ"
475 PRINT Q$;CHR$(46);CHR$(70);"RANGE"
480 PRINT Q$;CHR$(48);CHR$(67);".....:....."
485 PRINT Q$;CHR$(51);CHR$(70);"RADIAL"
490 PRINT Q$;CHR$(53);CHR$(71);"DME"
495 PRINT Q$;CHR$(54);CHR$(76);"NM";
500 FOR Y=1 TO 7:PRINT Q$;CHR$(Y+32);CHR$(44);CHR$(95):NEXT
505 FOR Y=1 TO 3:PRINT Q$;CHR$(Y+32);CHR$(53);CHR$(95):NEXT
510 PRINT Q$;CHR$(46);CHR$(32);STRING$(17,95)
515 FOR X=0 TO 16 STEP 16:FOR Y=15 TO 21
520 PRINT Q$;CHR$(Y+32);CHR$(X+32);CHR$(124);
525 NEXT Y:NEXT X
530 PRINT Q$;CHR$(54);CHR$(32);STRING$(17,95)
535 PRINT Q$;CHR$(45);CHR$(39);"ILS"
540 PRINT Q$;CHR$(49);CHR$(51);"MARKER"
545 PRINT Q$;CHR$(50);CHR$(52);">  <"
550 GX(1)=0:GY(1)=0:GOSUB 2305
555 PRINT Q$;CHR$(42);CHR$(38);"     "
560 PRINT Q$;CHR$(41);CHR$(36);"RADAR ALT";S$(6);"STALL"
565 PRINT Q$;CHR$(42);CHR$(50);">     <"
570 RETURN
575 REM    *DISPLAY INSTRUMENT READINGS
580 GOTO 760
585 YP=1:XP=0:F$="######":V1=FU:GOSUB 2910
590 YP=1:XP=7:F$="###":V1=FP:GOSUB 2910
595 YP=1:XP=39:V1=CC:GOSUB 2910
600 YP=4:XP=39:V1=PDQ:GOSUB 2910
605 YP=4:XP=46:F$="######":V1=RC:GOSUB 2910
610 YP=4:XP=55:F$="##,###":V1=AL:GOSUB 2910
615 YP=5:XP=0:F$="###":V1=MZ:GOSUB 2910
620 IF F(2)=1 THEN YP=10:XP=6:F$="#####":V1=AL:GOSUB 2910
625 REM    *DISPLAY THRUST
630 FOR I=1 TO 7:PRINT Q$;CHR$(I+32);CHR$(43);"|":NEXT
635 PRINT Q$;CHR$(TR+32);CHR$(43);">";
640 REM    *DISPLAY FLAPS
645 FOR I=1 TO 3:PRINT Q$;CHR$(I+32);CHR$(52);"|":NEXT
650 PRINT Q$;CHR$(FL+32);CHR$(52);">"
655 F$="+###":YP=5:XP=20:V1=FA:GOSUB 2910
660 IF F(6)=0 THEN SX=25:ST$="     ":GOTO 675
665 IF SX=25 THEN SX=26:ST$=STRING$(5,150):GOTO 675
670 SX=25
675 IF F(7)=0 THEN PRINT Q$;CHR$(42);CHR$(51);ST$
680 REM    *DISPLAY BRAKES
685 FOR I=10 TO 12:PRINT Q$;CHR$(I+32);CHR$(86);"|":NEXT
690 PRINT Q$;CHR$(BR+32);CHR$(86);">"
695 REM    *DISPLAY WHEELS
700 FOR I=10 TO 12:PRINT Q$;CHR$(I+32);CHR$(94);"|":NEXT
705 PRINT Q$;CHR$(WH+32);CHR$(94);">"
710 IF F(2)=1 GOTO 770
715 REM    *DISPLAY LAT/LONG
720 YP=4:XP=69:F$="###":V1=DP(4):GOSUB 2910
725 YP=4:XP=73:F$="##.#":V1=DP(5):GOSUB 2910
730 YP=5:XP=69:F$="###":V1=DP(6):GOSUB 2910
735 YP=5:XP=73:F$="##.#":V1=DP(7):GOSUB 2910
740 PRINT Q$;CHR$(36);CHR$(110);"N"
745 PRINT Q$;CHR$(37);CHR$(110);"W"
750 GOTO 770
755 REM    *DISPLAY RUDDER
760 PRINT Q$;CHR$(39);CHR$(64);STRING$(17,126)
765 PRINT Q$;CHR$(39);CHR$(RP+32);"^":GOTO 585
770 IF F(2)=1 THEN RETURN
775 REM    *DISPLAY VOR
780 YP=11:XP=38:F$="###.#":V1=VO(1):GOSUB 2910
785 PRINT Q$;CHR$(45);CHR$(71);VO$(1)
790 YP=18:XP=39:F$="###":V1=VO(3):GOSUB 2910
795 YP=22:XP=38:F$="###.#":V1=VO(4):GOSUB 2910
800 PRINT Q$;CHR$(49);CHR$(66);STRING$(13,126)
805 IF VO$(1)="OUT " GOTO 815
810 PRINT Q$;CHR$(49);CHR$(VO(2)+32);"^"
815 RETURN
820 REM    *KEY POLL SUBROUTINE
825 IF K$="F" OR K$="S" THEN KK=1:GOTO 885
830 IF K$="," OR K$="." THEN KK=2:GOTO 885
835 IF K$="/" THEN RA=0:GOTO 890
840 IF ASC(K$)=10 OR ASC(K$)=11 THEN KK=3:GOTO 885
845 IF K$="W" THEN KK=4:GOTO 885
850 IF K$="V" THEN KK=5:GOTO 885
855 IF K$="R" THEN KK=6:GOTO 885
860 IF K$="L" THEN KK=7:GOTO 885
865 IF K$="A" THEN KK=8:GOTO 885
870 IF K$="Q" THEN KK=9:GOTO 885
875 IF K$="M" THEN KK=10:GOTO 885
880 IF ASC(K$)=12 THEN KK=11:GOTO 885
885 ON KK GOSUB 900,925,955,995,1010,1030,1050,1065,1075,1090,1105
890 RETURN
895 REM    *THRUST KEY
900 IF K$="F" THEN TR=TR-1 ELSE TR=TR+1
905 IF TR<1 THEN TR=1:RETURN
910 IF TR>6 THEN TR=6:RETURN
915 RETURN
920 REM    *RUDDER KEYS
925 IF K$="," GOTO 940 ELSE RA=RA+1
930 IF RA>4 THEN RA=4
935 RETURN
940 RA=RA-1:IF RA<-4 THEN RA=-4
945 RETURN
950 REM    *ELEVATOR KEYS
955 J=5:IF BR=12 THEN J=1
960 IF K$=CHR$(10) THEN FA=FA+J ELSE FA=FA-J
965 IF FA>40 THEN FA=40
970 IF FA<-40 THEN FA=-40
975 IF FA>0 THEN FL=1:RETURN
980 IF FA=0 THEN FL=2:RETURN
985 FL=3:RETURN
990 REM    *WHEEL KEYS
995 IF WH=10 THEN WH=12:RETURN
1000 IF WH=12 THEN WH=10:RETURN
1005 REM    *SET VOR FREQ
1010 PRINT Q$;CHR$(46);CHR$(87);
1015 LINE INPUT "VOR FREQ ";VX$:VO(1)=VAL(VX$)
1020 PRINT Q$;CHR$(46);CHR$(87);SPACE$(16):RETURN
1025 REM    *SET RADIAL
1030 PRINT Q$;CHR$(46);CHR$(87);
1035 LINE INPUT "VOR RADIAL ";VX$:VO(3)=VAL(VX$)
1040 PRINT Q$;CHR$(46);CHR$(87);SPACE$(14):RETURN
1045 REM    *FLAPS KEY
1050 IF BR=10 THEN BR=12:RETURN
1055 IF BR=12 THEN BR=10:RETURN
1060 REM    *AUTO OBS KEY
1065 F(3)=1:RETURN
1070 REM    *REVERSE THRUST KEY
1075 IF F(7)=1 THEN TR=7
1080 RETURN
1085 REM    *MISSED APPROACH KEY
1090 IF F(2)=1 THEN F(5)=1
1095 RETURN
1100 REM    *FLARE KEY
1105 IF F(2)=0 THEN FA=0:FL=2:RETURN
1110 FA=1:FL=1:RETURN
1115 REM    *SITUATION UPDATE ROUTINE
1120 GOSUB 2870:TJ=TD-TL:TL=TD
1125 GOSUB 1320:GOSUB 1405:GOSUB 1480
1130 IF TR=7 THEN GOSUB 2185 ELSE GOSUB 1390
1135 GOSUB 1330:GOSUB 1360
1140 IF F(2)=1 GOTO 1995
1145 GOSUB 1720:GOSUB 1805
1150 IF AL<=0 THEN AF=1:GOTO 2390
1155 IF F(2)=0 GOTO 1310
1160 IF AL>0 GOTO 1235
1165 IF F(7)=1 GOTO 1290
1170 F(7)=1
1175 TY=YN:TX=XN-750
1180 IF FA>1 THEN AF=2:GOTO 2390
1185 IF FA<0 THEN AF=3:GOTO 2390
1190 IF F(6)=0 GOTO 1230
1195 IF AX>100 THEN AF=4:GOTO 2390
1200 IF AX>80 THEN AF=5:GOTO 2390
1205 IF WH=10 THEN AF=6:GOTO 2390
1210 IF TX>0 THEN AF=7:GOTO 2390
1215 IF TX<-10550 THEN AF=7:GOTO 2390
1220 IF ABS(TY)>100 THEN AF=7:GOTO 2390
1225 GOTO 1310
1230 AF=8:GOTO 2390
1235 IF F(5)=1 GOTO 1280
1240 IF FA>0 GOTO 1255
1245 IF FA<0 THEN F(6)=0
1250 GOTO 1310
1255 IF F(6)=0 THEN AX=AL:F(6)=1
1260 IF TR=6 GOTO 1275
1265 IF FA<2 THEN RC=-1800:GOTO 1310
1270 FA=2:FL=1:RC=-1800:GOTO 1310
1275 FA=1:FL=1:RC=-300:GOTO 1310
1280 IF AL<20 THEN AF=9:GOTO 2390
1285 TR=3:FA=0:FL=2:AF=10:GOTO 2390
1290 IF XN<-9750 OR ABS(YN)>100 GOTO 1305
1295 IF PDQ>0 GOTO 1310
1300 AF=0:GOTO 2390
1305 AF=9:GOTO 2390
1310 GOTO 2175
1315 REM    *UPDATE FUEL
1320 FU=FU-(14-TR)*TJ:IF FU<0 THEN FU=0
1325 FP=FU/3120:RETURN
1330 IF F(7)=1 THEN RC=0:RETURN
1335 IF F(6)=1 THEN RETURN
1340 RC=PDQ*SIN(ABS(FA/KR))*101.6
1345 IF FA<0 THEN RC=-1*RC
1350 RETURN
1355 REM    *UPDATE ALTITUDE
1360 IF F(7)=1 THEN RETURN
1365 AL=AL+TJ*RC/60
1370 IF AL<0 THEN AL=0:RETURN
1375 IF AL>45000! THEN AL=45000!
1380 RETURN
1385 REM    *UPDATE AIRSPEED
1390 PDQ=800-100*TR:PDQ=PDQ-2*FA
1395 IF BR=10 THEN RETURN
1400 PDQ=PDQ/3+20:RETURN
1405 IF F(5)=1 GOTO 1415
1410 IF F(2)=1 GOTO 1460
1415 IF RA=0 THEN RT=0:RETURN
1420 IF RA<0 THEN J=-1 ELSE J=1
1425 RA=ABS(RA):IF RA=1 THEN RT=1:GOTO 1440
1430 IF RA=2 THEN RT=10:GOTO 1440
1435 IF RA=3 THEN RT=20 ELSE RT=30
1440 RT=J*RT:RA=J*RA:CC=CC+RT
1445 IF CC=>360 THEN CC=CC-360:RETURN
1450 IF CC<0 THEN CC=360+CC
1455 RETURN
1460 IF RA=0 THEN RETURN
1465 IF RA<0 THEN CC=CC-1 ELSE CC=CC+1
1470 RA=0:GOTO 1445
1475 REM    *UPDATE RUDDER POSITION VARIABLE
1480 RP=40+RA+RA:RETURN
1485 REM    *COMPUTE DISTANCE AND BEARING
1490 VL=ABS(LB-LC):BL(5)=-1:IF LB=>LC THEN BL(5)=1
1495 VG=ABS(GB-GC):BL(4)=1:IF GB=>GC THEN BL(4)=-1
1500 LA=LC:GOSUB 2350:M1=MP:LA=LB:GOSUB 2350
1505 M2=MP:DM=ABS(M2-M1):IF DM=0 GOTO 1520
1510 QO=VG/(DM/60):IF QO>114.59 GOTO 1520
1515 CA=KR*ATN(QO):DR=VL*(1/COS(CA/KR)):GOTO 1525
1520 CA=90:DR=VG*COS(LC/KR)
1525 DR=DR*60:IF BL(5)=-1 GOTO 1540
1530 IF BL(4)=1 THEN CR=CA ELSE CR=360-CA
1535 RETURN
1540 IF BL(4)=1 THEN CR=180-CA ELSE CR=180+CA
1545 RETURN
1550 REM    *GET WIND FACTOR FOR ALTITUDE
1555 I=FIX(AL/4000):IF I=>10 THEN I=10
1560 WD=WA(I,0):WV=WA(I,1):RETURN
1565 REM    *SOLVE WIND TRIANGLE
1570 A=CC:L=AJ:GOSUB 1605
1575 MX=LX:MY=LY:GOSUB 1555
1580 A=WD+180:IF A=>360 THEN A=A-360
1585 L=WV:GOSUB 1605
1590 MX=MX+LX:MY=MY+LY:GOSUB 1655
1595 TK=MA:GS=VZ:RETURN
1600 REM    *RESOLVE VECTOR TO RECT COOR
1605 IF A<=90 THEN Q=1:B=90-A:GOTO 1625
1610 IF A<=180 THEN Q=2:B=A-90:GOTO 1625
1615 IF A<=270 THEN Q=3:B=270-A:GOTO 1625
1620 Q=4:B=A-270
1625 LX=L*COS(B/KR):LY=L*SIN(B/KR)
1630 IF Q=1 THEN RETURN
1635 IF Q=2 THEN LY=-1*LY:RETURN
1640 IF Q=3 THEN LX=-1*LX:LY=-1*LY:RETURN
1645 LX=-1*LX:RETURN
1650 REM    *COMPOSES X,Y INTO POLAR VECTOR
1655 VZ=SQR(MX^2+MY^2)
1660 IF MX<=0 GOTO 1675
1665 IF MY<=0 THEN Q=2 ELSE Q=1
1670 GOTO 1680
1675 IF MY<=0 THEN Q=3 ELSE Q=4
1680 IF MX=0 THEN MA=90:GOTO 1695
1685 MQ=ABS(MY/MX)
1690 MA=ATN(MQ)*KR
1695 IF Q=1 THEN MA=90-MA:RETURN
1700 IF Q=2 THEN MA=90+MA:RETURN
1705 IF Q=3 THEN MA=270-MA:RETURN
1710 MA=270+MA:RETURN
1715 REM    *GET POSIT FOR OMEGA
1720 PDQ(2)=PDQ:FA(2)=FA:CC(2)=CC:AL(2)=AL:PDQ=PDQ(1):FA=FA(1):CC=CC(1):AL=AL(1)
1725 IF PDQ(2)<>PDQ OR FA(2)<>FA GOTO 1770
1730 IF CC(2)<>CC OR AL(2)<>AL GOTO 1770
1735 F(0)=1:IF TD<TW(1)+6 GOTO 1775
1740 AJ=PDQ*COS(ABS(FA)/KR):GOSUB 1570
1745 DN=GS*(TD-TW(1))/3600:CN=TK:L1=LL(1):G1=GL(1):GOSUB 2210
1750 F(0)=0:TW(1)=TD:LL(1)=L2:GL(1)=G2:LS(1)=L2:GS(1)=G2
1755 FOR J=4 TO 7:DP(J)=CP(J):NEXT
1760 PDQ(1)=PDQ(2):FA(1)=FA(2):CC(1)=CC(2):AL(1)=AL(2)
1765 PDQ=PDQ(2):FA=FA(2):CC=CC(2):AL=AL(2):RETURN
1770 TW(1)=TD:F(0)=0
1775 AJ=PDQ*COS(ABS(FA)/KR):GOSUB 1570
1780 DN=GS*TJ/3600:CN=TK:L1=LS(1):G1=GS(1):GOSUB 2210
1785 LS(1)=L2:GS(1)=G2:IF F(0)=1 GOTO 1795
1790 LL(1)=L2:GL(1)=G2
1795 GOTO 1755
1800 REM    *VOR ROUTINE
1805 IF VO(1)=0 GOTO 1915
1810 FOR J=0 TO 15:IF VO(1)=VF(J) GOTO 1820
1815 NEXT:GOTO 1915
1820 LC=VP(J,0):GC=VP(J,1):LB=L2:GB=G2
1825 AR=VG(J,0):LL=VG(J,1):LO=360-LL:GOSUB 1490
1830 IF DR>300 GOTO 1915
1835 IF CR=>360 THEN CR=CR-360
1840 GOTO 1980
1845 VO$(1)="FROM"
1850 VO(5)=CR-VO(3):IF VO(5)<=180 GOTO 1860
1855 VO(5)=VO(5)-360:GOTO 1865
1860 IF VO(5)<-180 THEN VO(5)=VO(5)+360
1865 IF ABS(VO(5))>90 THEN I1=VO(3):GOTO 1900
1870 IF VO$(1)="FROM" THEN I=-1 ELSE I=1
1875 IF I=1 THEN VO(3)=I1
1880 VO(2)=40+I*INT(VO(5)):VO(4)=DR
1885 IF VO(2)<35 THEN VO(2)=34
1890 IF VO(2)>45 THEN VO(2)=46
1895 GOTO 1945
1900 VO$(1)=" TO ":VO(3)=VO(3)+180
1905 IF VO(3)=>360 THEN VO(3)=VO(3)-360
1910 GOTO 1850
1915 VO$(1)="OUT ":VO(4)=999.9:F(3)=0:RETURN
1920 IF DR>12 OR DR<10 THEN MK=0:GOTO 1845
1925 IF AL>4000 THEN MK=0:GOTO 1845
1930 IF F(1)=1 THEN J=9 ELSE J=2.5
1935 IF CR>AR+J OR CR<AR-J THEN MK=0:GOTO 1845
1940 F(1)=1:MK=1:GOTO 1845
1945 IF MK=1 THEN MRK$=CHR$(150) ELSE MRK$=" "
1950 PRINT Q$;CHR$(50);CHR$(53);MRK$;MRK$
1955 IF MK=0 THEN F(1)=0:RETURN
1960 IF CC>LL+1 OR CC<LL-1 THEN RETURN
1965 IF RA<>0 THEN RETURN
1970 F(1)=0:F(2)=1
1975 GOTO 1915
1980 IF F(3)=1 THEN VO(3)=CR:F(3)=0
1985 GOTO 1920
1990 REM    *ILS ROUTINE
1995 ZN=AL:TH=2.82471:MK=1:DW=1.69*RW*TJ:IF F(7)=1 THEN DW=0
2000 IF F(7)=1 THEN CC=LL:RA=0
2005 CJ=CC:IF CJ>180 THEN CJ=360-CJ:CJ=-1*CJ
2010 HA=LO+CJ:DC=360-HA:TS=1:IF HA<180 THEN DC=HA:TS=-1
2015 IF DC<0 THEN DC=-1*DC:TS=-1*TS
2020 TL$="W":IF TS=1 THEN TL$="E"
2025 DD=1.69*PDQ*TJ:DY=DD*SIN(DC/KR):DX=DD*COS(DC/KR)
2030 XN=XO-DX:IF TL$="E" THEN DY=-1*DY
2035 YN=YO+DY+DW:LM=KR*ATN(AL/XN)
2040 BE=KR*ATN(ABS(YN)/ABS(XN)):IF BE>2.5 THEN MK=0
2045 J=(LM-TH)/.25:J=FIX(J)
2050 IF J>3 THEN J=3
2055 IF J<-3 THEN J=-3
2060 BE=BE/.25:BE=FIX(BE):IF BE<-7 THEN BE=-7
2065 IF BE>7 THEN BE=7
2070 IF YN=>0 THEN BE=-1*BE
2075 IF MK=0 GOTO 2095
2080 IF XN>34960! AND XN<38000! GOTO 2095
2085 IF XN>2534 AND XN<5574 GOTO 2095
2090 MX=0
2095 GX(1)=BE:GY(1)=J:GOSUB 2305
2100 IF MK=1 THEN MRK$=CHR$(150) ELSE MRK$=" "
2105 PRINT Q$;CHR$(50);CHR$(53);MRK$;MRK$
2110 XO=XN:YO=YN:IF F8=1 GOTO 2135
2115 IF XN>12000 GOTO 2135
2120 F8=1
2125 REM    *LANDING RUNWAY DISPLAY
2130 FOR I=0 TO 3:PRINT Q$;CHR$(45+I);CHR$(81);RS$(I):NEXT
2135 IF F8=0 GOTO 2170
2140 PRINT Q$;CHR$(49);CHR$(XQ+32);" "
2145 YU=FIX(YN/16.7):XC=64+YU
2150 IF XC<49 THEN XC=49
2155 IF XC>79 THEN XC=79
2160 PRINT Q$;CHR$(49);CHR$(XC+32);CHR$(141)
2165 XQ=XC
2170 GOTO 1155
2175 GOTO 330
2180 REM    *REVERSE THRUST
2185 IF F(4)=1 GOTO 2195
2190 VO=PDQ*1.152*1.47:F(4)=1
2195 V=VO-(3.23636)*TJ:IF V<=0 THEN V=0
2200 VO=V:PDQ=V/(1.152*1.47):RETURN
2205 REM    *COMPUTES NEW POSITION
2210 IF CN=0 THEN CA=0:LB(5)=1:LB(6)=1:GOTO 2235
2215 IF CN>0 AND CN<90 THEN CA=CN:LB(5)=1:LB(6)=1:GOTO 2235
2220 IF CN>90 AND CN<180 THEN CA=180-CN:LB(5)=-1:LB(6)=1:GOTO 2235
2225 IF CN>180 AND CN<270 THEN CA=CN-180:LB(5)=-1:LB(6)=-1:GOTO 2235
2230 CA=360-CN:LB(5)=1:LB(6)=-1
2235 IF CA>89.5 THEN GOTO 2265
2240 DL=DN*COS(CA/KR):DL=DL/60:LB(3)=LB(5)
2245 IF LB(3)=1 THEN L2=L1+DL:GOTO 2255
2250 L2=ABS(L1-DL)
2255 LA=L1:GOSUB 2350:M1=MP:LA=L2:GOSUB 2350
2260 M2=MP:DM=ABS(M1-M2):DG=DM*TAN(CA/KR):GOTO 2270
2265 L2=L1:DG=DN/COS(L1/KR)
2270 DG=DG/60:LB(4)=LB(6)
2275 IF LB(4)=-1 GOTO 2285
2280 G2=ABS(G1-DG):GOTO 2290
2285 G2=G1+DG
2290 CP(4)=FIX(L2):CP(5)=(L2-CP(4))*60:CP(6)=FIX(G2):CP(7)=(G2-CP(6))*60
2295 RETURN
2300 REM    *PLOT GLIDESLOPE CROSSHAIRS
2305 IF XO<=750 THEN RETURN
2310 X0=8+GX(0):X1=8+GX(1):Y0=18+GY(0):Y1=18+GY(1)
2315 FOR Y=15 TO 21:PRINT Q$;CHR$(Y+32);CHR$(X0+32);" ":NEXT Y
2320 PRINT Q$;CHR$(Y0+32);CHR$(33);STRING$(15,32)
2325 FOR Y=15 TO 21:PRINT Q$;CHR$(Y+32);CHR$(X1+32);CHR$(43):NEXT Y
2330 PRINT Q$;CHR$(Y1+32);CHR$(33);STRING$(15,43)
2335 PRINT Q$;CHR$(50);CHR$(40);"O"
2340 GX(0)=GX(1):GY(0)=GY(1):RETURN
2345 REM    *COMPUTES MERIDIONAL PARTS, MP
2350 KM(0)=7915.704468#:KM(1)=23.268932#:KM(2)=.0525:KM(3)=.000213
2355 IF LA=0 THEN LA=0+1/60
2360 IF LA>(89+59/60) THEN LA=89+59/60
2365 S1=SIN(LA/KR):S2=S1*S1:S3=S1*S2
2370 TM(0)=TAN((45+LA/2)/KR):TM(0)=KM(0)*LOG(TM(0))/LOG(10)
2375 TM(1)=KM(1)*S1:TM(2)=KM(2)*S3:TM(3)=KM(3)*S2*S3
2380 MP=TM(0)-TM(1)-TM(2)-TM(3):RETURN
2385 REM    *ABORT ROUTINES
2390 M$(1)="----------A CRASH HAS OCCURRED----------"
2395 M$(2)="YOU ACCIDENTALLY STALLED THE AIRCRAFT DURING FINAL APPROACH."
2400 M$(3)="THE STALL OCCURRED AT AN ALTITUDE OF"
2405 M$(4)="THE AIRCRAFT STRUCK THE GROUND IN A NOSE-HIGH ATTITUDE."
2410 M$(5)="THE IMPACT RUPTURED THE TAIL SECTION OF THE FUSELAGE."
2415 M$(6)="     LOCATION OF CRASH":M$(7)="     LANDING POSITION"
2420 M$(8)="        PITCH ANGLE=":M$(9)="        AIRSPEED="
2425 M$(10)="YOU FLARED AT TOO HIGH AN ALTITUDE DURING FINAL APPROACH."
2430 M$(11)="THE RESULTING STALL OCCURRED AT AN ALTITUDE OF"
2435 M$(12)="THE IMPACT RUPTURED THE "
2440 M$(13)="YOU FORGOT TO LOWER THE LANDING GEAR."
2445 M$(14)="YOU FAILED TO TOUCH DOWN  INSIDE THE RUNWAY."
2450 M$(15)="---AN IMPROPER LANDING WAS MADE---"
2455 M$(16)="NO DAMAGE OR INJURIES OCCURRED."
2460 RL=0:IF TX>0 THEN RL=1
2465 RW=0:IF ABS(TY)>100 THEN RW=1
2470 N$(2)="FEET INSIDE OF RUNWAY"
2475 IF TX=>0 THEN N$(2)="FEET SHORT OF RUNWAY"
2480 N$(3)="FEET TO LEFT OF RUNWAY CENTERLINE"
2485 IF TY=>0 THEN N$(3)="FEET RIGHT OF RUNWAY CENTERLINE"
2490 RX=FIX(TX):RX=ABS(RX):RY=FIX(TY):RY=ABS(RY)
2495 IF AF=0 THEN GOTO 2760:AX=INT(AX):FA=INT(FA):PDQ=INT(PDQ)
2500 ON AF GOTO 2505,2535,2550,2580,2595,2645,2670,2685,2710,2745
2505 GOSUB 2815
2510 YP=4:XP=55:F$="##,###":V1=AL:GOSUB 2910
2515 PRINT Q$;CHR$(45);CHR$(32);M$(1)
2520 PRINT"YOU FLEW INTO THE GROUND."
2525 PRINT"THE INSTRUMENT READINGS AT THE TIME OF THE CRASH ARE SHOWN ABOVE."
2530 END
2535 PRINT CL$;M$(1):PRINT M$(2):PRINT M$(3);AX;"FEET."
2540 PRINT M$(4):PRINT M$(5):N$(1)=M$(6):GOSUB 2830
2545 END
2550 PRINT CL$;M$(1)
2555 PRINT"YOU FLEW INTO THE GROUND DURING FINAL APPROACH."
2560 PRINT"AT TIME OF CRASH, THE AIRCRAFT PROFILE WAS AS FOLLOWS:"
2565 PRINT M$(8);FA;"DEGREES":PRINT M$(9);PDQ
2570 N$(1)=M$(6):GOSUB 2830
2575 END
2580 PRINT CL$;M$(1):PRINT M$(10):PRINT M$(11);AX;"FEET."
2585 PRINT M$(12);"FUSELAGE.":N$(1)=M$(6):GOSUB 2830
2590 END
2595 PRINT CL$
2600 IF WH=10 THEN PRINT M$(1)
2605 IF WH=12 THEN PRINT"---A MINOR CRASH HAS OCCURRED---"
2610 PRINT M$(10):PRINT M$(11);AX;"FEET."
2615 IF WH=10 THEN PRINT M$(13)
2620 IF WH=10 THEN PRINT M$(12);"FUSELAGE."
2625 IF WH=12 THEN PRINT"THE IMPACT DAMAGED THE LANDING GEAR."
2630 IF WH=12 THEN PRINT"ALL PASSENGERS HAVE BEEN SAFELY EVACUATED."
2635 N$(1)=M$(6):GOSUB 2830
2640 END
2645 PRINT CL$
2650 PRINT M$(1):PRINT M$(13);"THE AIRCRAFT LANDED ON ITS BELLY, CAUSING MODERATE DAMAGE."
2655 PRINT"ALL PASSENGERS HAVE BEEN SAFELY EVACUATED."
2660 N$(1)=M$(6):GOSUB 2830
2665 END
2670 PRINT CL$;M$(15):PRINT M$(14)
2675 N$(1)=M$(7):GOSUB 2830
2680 END
2685 PRINT CL$;M$(15)
2690 PRINT"YOU FAILED TO EXECUTE A PROPER FLARE PRIOR TO TOUCHING DOWN."
2695 PRINT"THIS IS A VIOLATION OF COMPANY PROCEDURES."
2700 PRINT M$(16):N$(1)=M$(7):GOSUB 2830
2705 END
2710 PRINT CL$;M$(15)
2715 PRINT"YOU ROLLED OFF THE RUNWAY AFTER TOUCHING DOWN."
2720 PRINT M$(16):N$(1)=M$(7):GOSUB 2830
2725 PRINT
2730 IF XN<-9750 THEN PRINT"YOU ROLLED PAST FAR END OF RUNWAY"
2735 IF ABS(YN)>100 THEN PRINT"YOU ROLLED THROUGH RUNWAY SIDE BORDERS"
2740 END
2745 PRINT CL$;"YOUR MISSED APPROACH SIGNAL IS ACKNOWLEDGED"
2750 PRINT"YOU HAVE FOLLOWED PROPER PROCEDURES."
2755 END
2760 PRINT CL$;"***YOU HAVE SUCCESSFULLY COMPLETED THE FLIGHT***"
2765 PRINT"ALL PROCEDURES WERE PROPERLY EXECUTED."
2770 N$(1)=M$(7)
2775 PRINT"CONGRATULATIONS ON A SUCCESSFUL FLIGHT."
2780 GOSUB 2830:PRINT
2785 XN=ABS(XN):JN=FIX(XN+750):YN=ABS(YN):YN=FIX(YN)
2790 PRINT"YOUR AIRCRAFT CAME TO REST AT THE FOLLOWING POSITION:"
2795 PRINT JN;"FEET INSIDE THE RUNWAY"
2800 PRINT YN;"FEET FROM THE RUNWAY CENTERLINE"
2805 END
2810 REM    *CLEAR LOWER PART OF DISPLAY
2815 FOR I=9 TO 23:PRINT Q$;CHR$(I+32);CHR$(32);CHR$(27);"T";:NEXT
2820 RETURN
2825 REM    *LANDING STATS
2830 PRINT:PRINT SPACE$(26);N$(1):PRINT
2835 IF RL=0 THEN PRINT RX;N$(2)
2840 IF RL=1 THEN PRINT RX;N$(2)
2845 PRINT
2850 IF RW=0 THEN PRINT RY;N$(3)
2855 IF RW=1 THEN PRINT RY;N$(3)
2860 PRINT:PRINT"RUNWAY IS 10,500 X 200 FEET":RETURN
2865 REM    *CLOCK UPDATE ROUTINE
2870 HRS=FIX(TD/3600):MIN=FIX((TD-3600*HRS)/60):SEC=TD-(3600*HRS+60*MIN)
2875 IF B$="Y" AND AL=>5000 THEN TA=15 ELSE TA=3
2880 TD=TD+TA
2885 YP=0:XP=66:F$="##.":V1=HRS:GOSUB 2910
2890 YP=0:XP=69:F$="##.":V1=MIN:GOSUB 2910
2895 YP=0:XP=72:F$="##":V1=SEC:GOSUB 2910
2900 RETURN
2905 REM    *PLOT VARIABLE
2910 PRINT Q$;CHR$(YP+32);CHR$(XP+32);
2915 PRINT USING F$;V1
2920 RETURN
2925 END
2930 REM    *END OF LISTING
 VARIABLE
2910 PRINT Q$;CHR$(YP+32);CHR$(XP+3FOLLOWING POSITION:"
2795 PRINT JN;"FEET INSIDE THE RUNWAY"
2800 PRINT YN;"FEET FROM THE RUNWAY CENTERLINE"
2805 END
2810 RE