100 DIM A(50),G$(50),E(50),V(10),W(10),X(10),Y(10),Z(10)
101 DIM D(1000),A$(50),Q(20,20),P3(4),P4(3,10)
102 DIR=1:F$="":WIDTH 128:WIDTH LPRINT 255
103 F$="":OPEN "I",#1,"GFXDATA.CEC"
104 INPUT #1,TIMELIM
105 FOR B=1 TO 37:LINE INPUT #1,A$(B):NEXT B
106 FOR B=0 TO 32:INPUT #1,D(B+32):NEXT B
107 FOR B=1 TO 12:FOR N=1 TO 20:INPUT #1,Q(B,N):NEXT N:NEXT B
108 FOR B=1 TO 198:INPUT #1,D(B+100):NEXT B
109 FOR B=1 TO 3:INPUT #1,P3(B):FOR N=1 TO P3(B):INPUT #1,P4(B,N):NEXT N:NEXT B
110 INPUT #1,P3(4),P3(5)
111 FOR B=1 TO 9:INPUT #1,V(B),W(B),X(B),Y(B),Z(B):NEXT B
112 H=0:N=0:M=0:S1=61440!
113 GOSUB 198
114 B=0:GOSUB 317
115 CLOSE:F$="":C$=""
116 REM * GFX seed ************************************************************
117 T=0:S2=0:S1=61440!:Z=0:N1=0:N3=0:X0=0:X1=0:X2=0:E1=0:E2=0
118 P=1:Y0=0:Y1=95:C=0:C$="":J=60536!:B1=0:B2=0:D(6)=0:G=61440!:H=1:E=1
119 REM * menu & directory routine *********************************************
120 PRINT CHR$(26):FOR B=H TO H+8:PRINT A$(B):NEXT B:PRINT A$(35)
121 IF DIR=1 THEN 122 ELSE 123
122 PRINT A$(19):WIDTH 54:FILES:WIDTH 128
123 IF H=1 THEN 124 ELSE 149
124 PRINT:PRINT:PRINT:GOTO 169
125 REM * NO FILE menu decision ************************************************
126 H=1:ON E GOTO 127,139
127 E=2:E1=1:C1$=C$
128 IF C$="Q" OR C$="q" THEN 363 ELSE 129
129 IF C$="B" OR C$="b" THEN H=3:GOTO 137 ELSE GOTO 130
130 IF C$="E" OR C$="e" THEN H=4:GOTO 137 ELSE GOTO 131
131 IF C$="M" OR C$="m" THEN H=7:GOTO 137 ELSE GOTO 132
132 IF C$="P" OR C$="p" THEN H=10:GOTO 120 ELSE GOTO 133
133 IF C$="S" OR C$="s" THEN H=11:E=1:GOTO 137 ELSE GOTO 134
134 IF C$="D" OR C$="d" THEN 135 ELSE 136
135 IF DIR=1 THEN DIR=0 ELSE DIR=1 
136 C$="":GOTO 116
137 GOTO 169
138 REM * EDIT menu decision ***************************************************
139 E1=2:C1$=C$
140 IF C$="Q" OR C$="q" THEN H=1:E=1:GOTO 116 ELSE GOTO 141
141 IF C$="A" OR C$="a" THEN H=9:GOTO 169 ELSE GOTO 142
142 IF C$="E" OR C$="e" THEN H=12:GOTO 169 ELSE GOTO 143
143 IF C$="M" OR C$="m" THEN H=5:GOTO 169 ELSE GOTO 144
144 IF C$="R" OR C$="r" THEN H=2:GOTO 169 ELSE GOTO 145
145 IF C$="S" OR C$="s" THEN H=6:E=1:GOTO 169 ELSE GOTO 146
146 IF C$="D" OR C$="d" THEN GOTO 147 ELSE 148
147 IF DIR=1 THEN DIR=0: ELSE DIR=0
148 C$="":H=1:GOTO 169
149 REM* PRINTER seed **********************************************************
150 IF J1<61439! THEN J1=61439!
151 C$="":G=0:S1=61440!
152 REM * PRINT menu status ****************************************************
153 PRINT:PRINT A$(28):PRINT A$(37);INT((J1-61440!)/128)+2;A$(29);P;A$(30);F$:PRINT:PRINT 
154 REM * PRINTER menu decision ************************************************
155 N=20:M=1:GOSUB 176
156 IF C$="D" OR C$="d" THEN GOTO 157 ELSE GOTO 159
157 IF DIR=1 THEN DIR=0 ELSE DIR=1
158 GOTO 119
159 IF C$="F" OR C$="f" THEN N=21:M=2:GOSUB 176 ELSE GOTO 161
160 GOTO 119
161 IF C$="P" OR C$="p" THEN GOTO 162 ELSE GOTO 164
162 N=32:M=3:L=16:GOSUB 171
163 J1=(128*Y0)+61439!:S1=61440!:GOTO 119
164 IF C$="Q" OR C$="q" THEN C$="":GOTO 116 ELSE GOTO 165
165 IF C$="S" OR C$="s" THEN N=31:M=1:GOSUB 176 ELSE GOTO 167
166 P=VAL(C$):GOTO 119
167 IF C$="B" OR C$="b" THEN GOTO 168 ELSE C$="":GOTO 119
168 IF P<1 OR P>4 THEN GOTO 119 ELSE GOTO 169
169 REM * decision routine *****************************************************
170 L=1:N=Q(H,L):L=2:M=Q(H,L):L=3
171 ON Q(H,L) GOSUB 176,198,200,202,211,218,223,251,261,353,281,313
172 L=L+1:IF Q(H,L)=0 THEN 173 ELSE 171
173 IF L>19 THEN GOTO 174 ELSE GOTO 175
174 RETURN
175 IF H=10 THEN 119 ELSE 126 
176 REM * 1-prompt routine *****************************************************
177 REM * VARIABLE 'M' IS INPUT CONTROL 'N' IS OUTPUT CONTROL ******************
178 FOR B=0 TO 256:D(B+500)=PEEK (61439!+(22*128)+B):NEXT B
179 PRINT CHR$(11);
180 IF H=1 THEN 181 ELSE 183
181 IF E=2 THEN 182 ELSE 184
182 PRINT A$(N);" - (A,D,E,M,Q,R,S) ";:GOTO 185
183 IF H=10 OR H=9 OR M=4 THEN GOTO 184 ELSE PRINT "(";C1$;") ";:GOTO 184
184 PRINT A$(N);
185 ON M GOTO 190,193,188,186
186 PRINT:TIMELIM=INT(TIMELIM*5):GOSUB 200
187 TIMELIM=INT(TIMELIM/5):GOTO 196
188 PRINT:GOSUB 223
189 GOTO 196
190 C$=INKEY$
191 IF LEN(C$)>1 THEN C$=""
192 IF C$="" OR C$=CHR$(10) OR C$=CHR$(11) THEN 190 ELSE PRINT C$:GOTO 195
193 INPUT C$
194 IF N=21 THEN F$=C$:C$=""
195 GOSUB 201
196 PRINT CHR$(11)CHR$(27)CHR$(82):POKE 64256!,32
197 FOR B=0 TO 256:POKE 61439!+(22*128)+B,D(B+500):NEXT B:RETURN
198 REM * 2-clear screen routine **********************************************
199 PRINT CHR$(26)CHR$(11)CHR$(11):POKE 64384!,32:RETURN
200 REM * 3-time routine ******************************************************
201 FOR B=1 TO TIMELIM:NEXT B:RETURN
202 REM * 4-fill screen routine ***********************************************
203 S2=0:T=0:ON ERROR GOTO 211
204 OPEN "I",#1,F$+".GFX"
205 IF H=5 OR H=7 THEN 206 ELSE 208
206 IF N=23 THEN 207 ELSE 208
207 INPUT #1,T,S2:POKE S1,T
208 WHILE T<>32:INPUT #1,T,S2:IF S1+S2>=64512! THEN 210 ELSE 209
209 POKE S1+S2,T:S1=S1+S2:WEND
210 CLOSE:RETURN
211 REM * 5-error routine *****************************************************
212 IF ERR=53 OR ERR=64 THEN 213 ELSE 363
213 RESUME 214
214 N=27:M=4:C$="":CLOSE:GOSUB 176 
215 IF E1<3 THEN H=1 ELSE H=10
216 IF E1=1 THEN E=1 ELSE E=2
217 ON E1 GOTO 119,125
218 REM * 6-move design routine ***********************************************
219 IF N=23 THEN 221 ELSE 220
220 N=23:M=3:GOTO 222
221 S1=61440!+X0+(Y0*128)
222 RETURN
223 REM * 7-position/alpha/graphics routine ***********************************
224 IF H=9 THEN 225 ELSE 233
225 IF C$="A" OR C$="a" THEN E2=1:X=32:GOTO 237 ELSE GOTO 226
226 IF C$="G" OR C$="g" THEN E2=2:N=26:M=2:GOSUB 176 ELSE GOTO 228
227 IF VAL(C$)>32 THEN E2=1:C=27:GOTO 229 ELSE X=VAL(C$):GOTO 237
228 IF C$="M" OR C$="m" THEN E2=3:GOTO 236 ELSE H=1:GOTO 249
229 ON E2 GOTO 231,232,230
230 IF C=27 THEN C$="":L=0:GOTO 169 ELSE GOTO 236 
231 IF C=27 THEN C$="":L=0:GOTO 169 ELSE X=C:C$=CHR$(12):GOTO 241
232 IF C=27 THEN C$="G":GOTO 226 ELSE GOTO 237 
233 IF H=9 THEN 234 ELSE 236
234 IF E2=3 THEN 236 ELSE 235
235 IF E2=1 THEN X=32:GOTO 237 ELSE GOTO 237
236 X=PEEK(X0+(Y0*128)+61440!)
237 C$="":X1=X:Y1=95
238 C$=INKEY$:X2=X2+1:IF X2>=TIMELIM/20 THEN 239 ELSE 240
239 SWAP X1,Y1:POKE X0+(Y0*128)+61440!,X1:X2=0
240 IF C$="" THEN 238 ELSE 241
241 POKE X0+(Y0*128)+61440!,X
242 C=ASC(C$):IF C>12 OR C<8 THEN 249 ELSE 243
243 ON C-7 GOTO 247,248,245,244,246 
244 IF Y0-1<0 THEN Y0=23:GOTO 233 ELSE Y0=Y0-1:GOTO 233
245 IF Y0+1>=24 THEN Y0=0:GOTO 233 ELSE Y0=Y0+1:GOTO 233
246 IF X0+1=128 THEN X0=0:GOTO 233 ELSE X0=X0+1:GOTO 233
247 IF X0-1<0 THEN X0=127:GOTO 233 ELSE X0=X0-1:GOTO 233
248 IF X0+8>=128 THEN X0=0:GOTO 233 ELSE X0=X0+8:GOTO 233
249 IF H=9 THEN 229 ELSE 250
250 RETURN
251 REM * 8-reverser routine **************************************************
252 IF N=24 THEN 254 ELSE 253
253 G1=X0:G2=Y0*128:N=24:M=3:RETURN
254 G3=X0:G4=Y0*128
255 FOR Y=G+G2 TO G+G4 STEP 128
256 FOR X=G1 TO G3
257 G5=PEEK(Y+X):IF H=12 THEN G5=22:GOTO 259 ELSE 258
258 IF G5>32 THEN 260 ELSE 259
259 POKE (Y+X),D(G5+32)
260 NEXT X:NEXT Y:RETURN
261 REM * 9-letters routine ***************************************************
262 IF J=60536! THEN 263 ELSE 265
263 OPEN "R",#1,"GFXTITLE.CEC",2
264 FIELD #1,2 AS LOT1$
265 J=J+896!:D(3)=J
266 IF C$="" THEN 278 ELSE 267
267 FOR B1=0 TO 25 STEP 5
268 FOR B2=1 TO LEN(C$)
269 D(3)=D(3)+1:Z$=MID$(C$,B2,1)
270 IF ASC(Z$)<32 OR ASC(Z$)=96 THEN Z$=" "
271 IF (ASC(Z$))>96 THEN UPPER=64 ELSE UPPER=32
272 D(4)=((ASC(Z$)-UPPER)*30+1):D(3)=D(3)+1
273 GET #1,B1+D(5)+D(4):D(6)=VAL(LOT1$):D(5)=D(5)+1
274 POKE D(3)+D(5)+((B1/5)*128)+(B2*5),D(6)
275 IF D(5)>4 THEN 276 ELSE 273
276 D(5)=0:NEXT B2
277 D(3)=J:NEXT B1
278 IF J=60536!+(3*896) THEN 279 ELSE 280
279 CLOSE
280 RETURN
281 REM * 10-printer routine **************************************************
282 J=J1-128
283 FOR B=1 TO P3(2)-2:LPRINT CHR$(P4(2,B));:NEXT B
284 IF P>2 THEN 286 ELSE 285
285 LPRINT CHR$(P4(2,B)):GOTO 287
286 LPRINT CHR$(P4(2,B+1))
287 FOR B=1 TO P3(3):LPRINT CHR$(P4(3,B));:NEXT B:GOTO 288
288 J=J+128:G=0:IF J>64256! THEN C$="A" ELSE C$=""
289 FOR Y=1 TO 128:I=PEEK(J+Y):IF I>32 THEN G=1:I=32 
290 FOR B=1 TO 6:X=(I*6)+B+100:K=D(X)
291 IF P>2 THEN 293 ELSE 292
292 IF P=2 THEN 297 ELSE 296
293 IF K>64 THEN K=K-64
294 IF K>32 THEN K=K-32
295 IF P>3 THEN 297 ELSE 296
296 LPRINT CHR$(K);
297 LPRINT CHR$(K);
298 NEXT B:NEXT Y
299 IF P=1 OR P=3 THEN 300 ELSE 304
300 IF G=1 THEN 301 ELSE 305
301 GOSUB 311
302 FOR Y=1 TO P3(5):I=PEEK(J+Y):IF I<32 THEN I=32
303 LPRINT CHR$(I);:NEXT Y:LPRINT CHR$(13);:GOTO 305
304 FOR B=1 TO P3(4)-(128*6):LPRINT CHR$(0);:NEXT B
305 IF C$="A" THEN 309
306 C$=INKEY$:IF C$="" THEN 307 ELSE 309
307 GOSUB 311
308 GOTO 283
309 GOSUB 311
310 LPRINT CHR$(13);:Y0=0:X0=0:H=10:C$="":J1=0:J=0:RETURN
311 FOR B=1 TO P3(1):LPRINT CHR$(P4(1,B));:NEXT B:RETURN
313 REM * 11-SLIDE-SHOW routine ***********************************************
314 IF N=36 THEN 316 ELSE 315
315 N=36:M=4:B=0:F$=F$+".MFX":RETURN
316 F1$=F$:B=0:C$="":OPEN "I",#1,F$+".MFX"
317 B=B+1:INPUT #1,G$(B),A(B),E(B)
318 IF A(B)<1 THEN A(B)=1
319 IF E(B)<1 OR E(B)>10 THEN E(B)=1
320 IF EOF(1) THEN 321 ELSE 317
321 I=B:CLOSE:TIMELIM1=TIMELIM:C$="":S1=61440!:S6=S1:S7=61504!:GOSUB 198
322 S1=61440!:J=0:K=0:F$=""
323 FOR K=1 TO I:J=K-1:F$=G$(K)
324 IF F$="0" THEN F$=F1$:GOTO 326
325 C$="":S1=S6:GOSUB 202
326 IF K=1 THEN 334
327 TIMELIM=INT(TIMELIM*A(J)):GOSUB 200
328 C$=INKEY$:IF C$="" THEN 329 ELSE 335
329 S1=S7:TIMELIM=INT(TIMELIM/A(J)):GOSUB 336
330 IF F$=F1$ THEN 316
331 PRINT CHR$(27)+"S"+CHR$(32);CHR$(32+INT(S6-61440!));
332 IF J=K THEN 334
333 IF K=I THEN J=K:SWAP S6,S7:GOTO 327
334 SWAP S6,S7:NEXT K
335 C$="":H=1:PRINT CHR$(27)+"S"+CHR$(32);CHR$(32);:RETURN
336 REM * erase screen/step routine *******************************************
337 IF E(J)=1 THEN 347 ELSE 338
338 E(J)=E(J)-1:FOR U=X(E(J)) TO Y(E(J)) STEP Z(E(J))
339 B=V(E(J))+U:ON W(E(J)) GOSUB 348,351
340 IF E(J)>4 THEN 341 ELSE 346
341 B=V(E(J))-U:ON W(E(J)) GOSUB 348,351
342 IF E(J)=9 THEN 343 ELSE 346
343 IF INT(U/2)=U/2 THEN 344 ELSE 346
344 B=11+(U/2):GOSUB 348
345 B=13-(U/2):GOSUB 348
346 NEXT U
347 RETURN
348 REM * delete line routine ************************************************
349 W1=S1+B*128:IF W1<61440! THEN W1=61440!
350 FOR W2=W1 TO W1+52:POKE W2,32:NEXT W2:RETURN
351 REM * delete column routine **********************************************
352 FOR W2=S1 TO S1+(23*128) STEP 128:POKE W2+B,32:NEXT W2:RETURN
353 REM * 12-save routine *****************************************************
354 C$="":N=34:M=4:GOSUB 176 
355 IF F$="" THEN 362 ELSE 356
356 ON ERROR GOTO 211
357 OPEN "O",#1,F$+".GFX"
358 FOR N1=0 TO 3072
359 Z=N1+61440!:T=PEEK(Z):IF T<>32 THEN 360 ELSE N3=N3+1:GOTO 361 
360 PRINT#1,T;N3;:POKE Z,127:N3=1
361 NEXT N1:PRINT #1,32;1:CLOSE
362 RETURN
363 END
