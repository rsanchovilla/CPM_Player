;****************************************************************************
;	PLOTTER CONTROL SUBROUTINES FOR MAGIC DA SYSTEM
;	   COPYRIGHT 1981  DASOFT DESIGN SYSTEMS, INC.
;	THESE PROGRAMS ARE PROTECTED BY LICENSING AGREEMENTS
;	AND CANNOT BE USED OR DUPLICATED WITHOUT WRITTEN
;	PERMISSION FROM DASOFT.
;	
;	*************** FOR YOUR INFORMATION *********************
;	THIS DRIVER USES THE CP/M LIST DEVICE TO SEND THE DATA VIA
;	THE RS-232 LINK TO THE DMP-5.  DELAYS HEREIN ARE SET FOR
;	9600 BAUD OUTPUT.  IF YOU ARE GOING TO USE A DIFFERENT BAUD
;	RATE, CONSULT THE TABLE BELOW AND CHANGE THE DELAY EQUATE
;	
;	FOR 9600 BAUD, XDEL2 = 52
;	    4800               26
;	    2400               13
;	    1200                7
;	     600                3  ETCETERA

XDEL2   EQU	55              ;I LIKE TO BE SURE
OUTFUNC	EQU	5		;SEND TO CPM LIST DEVICE

;	ABOVE IS THE EQUATE TO SEND THE DATA TO THE CP/M LIST DEVICE,
;	FUNCTION NUMBER 5.  IF YOU WOULD LIKE IT SENT TO THE PUNCH 
;	DEVICE, ETCETERA, YOU MAY CHANGE THIS EQUATE.  IF USING ANOTHER
;	OUTPUT TECHNIQUE, YOU WILL NEED TO MODIFY THE PROGRAM SEGMENTS.

;	BELOW, VARIABLES FDELAY AND SDELAY ARE SET UP FOR 4MHZ Z80
;	OPERATION.  THESE NUMBERS ARE DELAY WEIGHTS FOR HOW LONG TO WAIT
;	BETWEEN EACH CHARACTER SENT.  AT LESS THAN 9600 BAUD OR WITH SLOWER
;	PROCESSORS, YOU WILL NEED TO FIND NEW VALUES FOR THESE DELAYS



;	*********************CAUTION  CAUTION CAUTION*************

;	DO NOT NOT NOT USE THE INSTALL PROGRAM ON THIS CONTROL
;	ROUTINE, OR IMPORTANT VALUES WILL BE CHANGED FOR THE
;	OTHER PLOTTER WE SUPPORT.  YOU MAY USE THE TEST ROUTINE
;	BY SKIPPING OVER THE INSTALLATION PART, BUT THIS SIMPLE 
;	DRIVER WILL NOT QUITE WRITE THE DASOFT LOGO CORRECTLY.

;	SO DON'T WORRY ABOUT IT.



;	SOME MAGIC DEFINED LOCATIONS USED:

VERS	EQU	0032H	; 
REV	EQU	0034H	; 
EDIT	EQU	0064H	; 
	            
WBOOT	EQU	0000H	; WARM BOOT ADDRESS
TYPEF	EQU	0002H	; BDOS PRINT COMMAND NUMBER
BDOS	EQU	0005H	; ADDRESS  OF BDOS JUMP POINT
BUFF	EQU	0080H	; ADDRESS OF DISK INPUT BUFFER
DMA	EQU	001AH	; SET DMA ADDRESS FUNCT NUMB
READF	EQU	0014H	; DISK READ FUNCTION
WRITEF	EQU	0015H	; WRITE FILE TO DISK NUM
OPENS	EQU	000FH	; OPEN FILE FUNCT NUMBER
CLOSES	EQU	0010H	; CLOSE  FILE FUNCT NUMBER
                   
FCB	EQU	005CH	; FILE CONTROL BLOCK ADRS
FCBDN	EQU	005CH	; FCB DISK NAME
FCBFN	EQU	005DH	; FILE NAME
FCBFT	EQU	0065H	; FILE TYPE
FCBEX	EQU	0068H	; CURRENT EXTENT NUMBER
FCBRC	EQU	006BH	; EXTENT RECORD COUNT (0-128)
FCBCR	EQU	007CH	; CURRENT (NEXT) RECORD NUMBER
FCBR0	EQU	007DH	; LSB OF 16 BIT RANDOM RECORD ADDRESS
FCBR1	EQU	007EH	; MSB OF 16 BIT RANDOM RECORD ADDRESS
FCBR2	EQU	007FH	; RECORD OVERFLOW FLAG
                    
TOPPTR	EQU	0006H	; WAS (AD00H) STACK START 48K
                   
CLEAD	EQU	0001H	; CURSOR POSITIONING STRING CODE
P3LINES	EQU	0003H	; PRINT THREE LINES
CONTMSG	EQU	000BH	; CONTINUE MESSAGE CODE
CLS	EQU	000CH	; CLEAR SCREEN CODE
IVON	EQU	000EH	; INVERSE ON CODE
IVOFF	EQU	000FH	; INVERSE OFF CODE
                   
CR	EQU	000DH	; RETURN CHARACTER
LF	EQU	000AH	; LINE FEED CHARACTER
BEEP	EQU	0007H	; BEL CHARACTER
TAB	EQU	0009H	; TAB CHARACTER
BS	EQU	0008H	; BACK SPACE
ESC	EQU	001BH	; ESCAPE
EOM	EQU	00EEH	; END OF MESSAGE MARKER (OVERLAY FILE)
EOF	EQU	00FFH	; END OF FILE MARKER    (OVERLAY FILE)
MSCNT	EQU	00F9H	; 1 MSEC SOFTWARE DELAY CONSTANT
                   
RDRL	EQU	0103H	; LOADER KERNAL OR TEST PROG
CLEARS	EQU	0108H	; CLEAR SCREEN ROUTINE
PSTRNG	EQU	010EH	; PRINT AN ASCII STRING
INCHNE	EQU	011DH	; ROUTINE TO READ CONSOLE NO ECHO
DUMP	EQU	014DH	; BLOCK MOVE ROUTINE
                  

;	***** REV 1.0 FOR HOUTSTON INSTRUMENTS DMP-5 ******



	ORG 1800H	; START OF DRIVER


;	JUMP TABLE ENTRY POINTS FOR EXTERNAL ENTRY

;	PINIT AND CHGPPR, CHGPEN, MAY NEED TO BE CHAGED BY USER

PINIT	JMP	INIT		;INITIALIZE OUTPUT PORT
PHOME	JMP	HOME		;HOME THE PLOTTER TO 0,0
VECTOR	JMP	MOVE		;MOVE THE PEN
CHGPPR	JMP	CHGPR		;CHANGE PAPER COMMAND
CHGPEN	JMP	CHGPN		;CHANGE PEN COMMAND

TBLAD	DS	4		;PLOTTER COORDINATE TABLE

YASIGN	DB	0		;SIGN OF Y CORRECTION 0=+
YADJUST	DB	0  		;MAGNITUDE OF Y CORRECTION
XASIGN	DB	0		;SIGN OF CORRECTION 0=+
XADJUST	DB	0 		;NUMBER OF GRIDS BEFRE CORRECTION X
SLOW	DB	0		;FLAG FOR DRAWING SLOW LINES


;	**********DELAYS FOR 4MHZ Z-80 9600 BAUD OPERATIONS ****************

FDELAY	DW	0180H		;NORMAL 4MHZ DELAY FACTOR
SDELAY	DW	0300H		;SUPER SLOW DRAW FACTOR


SECND	DB	00H		;RAMP FACTOR
PORT	DB	02H		;PORT NUMBER FOR OUTPUT
PCTL	DB	0BH		;CONTROLL PORT NUMBER
CTLCODE	DB	80H		;CONTROL CODE FOR 8255


;	****** HOME PLOTTER COMMAND **********************
;	FOR THE MAURO ENGINEERING PROAC, HOME IS ACCOMPLISHED
;	BY RUNNING THE PLOTTER INTO THE NEGATIVE STOPS.
;	THATS RIGHT.  THE PLOTTER USES 4 BYTES, X MAGNITUDE,
;	XSIGN, Y MAGNITUDE, Y SIGN, SET TO NEGATIVE MAX X,Y
;	DESTROYS A, NO ENTRY OR EXIT PARAMETERS

HOME	XRA	A
	STA	TBLAD		;ZERO X LSD  MAG
	STA	TBLAD+2		;ZERO Y LSD MAG

	MVI	A,1FH		;MAX NEGATIVE MAG
	STA	TBLAD+1		;NEGATIVE X
	STA	TBLAD+3		;NEGATIVE Y

	CALL	VECTOR		;MOVE PLOTTER
	RET


;	********* CHANGE PAPER ROUTINE ********************
;	SOME PLOTTERS CAN CHANGE THEIR OWN PAPER, WHICH IS WHY
;	THERE  IS A USER ROUTINE FOR THIS. FOR THE MAURO PROAC,
;	IT  IS UNDER 'MANUAL' CONTROL.  THIS UTILITY SENDS 25
;	'CONTROL-G' OR BELLS TO THE CONSOLE WITH A MESSAGE.

PPRMSG	DB	0DH,0AH,'PLEASE CHANGE PLOTTER PAPER THEN HIT ANY KEY$'

BELLS	EQU	BEEP

CHGPR	LXI	D,PPRMSG		;PRINT MESSAGE
	CALL	PSTRNG		;CALL PRINT ROUTINE

DOBELLS	MVI	E,BELLS
	MVI	C,02		;CPM PRINT CHARACTER FUNCTION
	CALL	BDOS
	CALL	INCHNE		;WAIT FOR ANSWER
	RET			;FINISHED


;	********* CHANGE PEN ROUTINE *****************
;	AGAIN, SOME PLOTTEERS CAN CHANGE THEIR OWN PEN
;	**CONTACT DASOFT FOR MORE INFORMATION.

PNMSG1	DB	0AH,0DH,'PLEASE CHANGE PEN THEN HIT ANY KEY'
	DB	0AH,0DH,'       NEW PEN SIZE IS....$'
PNMSG2	DB	'000$'
PNMSG3	DB	'0$'
PNMSG4	DB	'1$'
PNMSG5	DB	'4$'


;	THE PEN SIZE IS SENT IN THE ACCUMULATOR ON CALL, NO
;	OTHER INPUTS, NO OUPUTS.

CHGPN	PUSH	PSW		;SAVE PEN NUMBER
	LXI	D,PNMSG1		;PRINT MAIN MESSGE
	CALL	PSTRNG
	POP	PSW		;GET BACK PEN NUMBER
	CPI	0
	JZ	PEN0

	CPI	1
	JZ	PEN1

	CPI	2
	JZ	PEN2

PEN3	LXI	D,PNMSG5

DB2	CALL	PSTRNG		;PRINT PIN NUMBER
	JMP	DOBELLS

PEN2	LXI	D,PNMSG4
	JMP	DB2		;PRINT NUMBER AND RING CHIMES

PEN1	LXI	D,PNMSG3
	JMP	DB2

PEN0	LXI	D,PNMSG2
	JMP	DB2		;THE END


;	********** PLOTTER CONTROL ROUTINES *****************

DEL	DB	0		;DELAY PARAMETER
GOXY	DB	0
PENNOW	DB	0



;	THIS ROUTINE IS BASED ON THE ASSUMPTION THAT THE DASOFT SYSTEM
;	USES ONLY 45 DEGREE ANGLES, AND AS SUCH MAKES IT MUCH EASIER
;	DATA IS STORED IN TBLAD AS  XLOW, XHI, YLO , YHI
;	WITH BIT 10 (H) AS SIGN, BIT 20(XH) PEN DOWN

;	FIRST CHECK PEN UP, PEN DOWN

MOVE:	LDA	TBLAD+1
	ANI	20H		;CLEAR ALL BUT PEN BIT
	JZ	PENGOUP

;	PEN GOING DOWN?

	LDA	PENNOW		;WHAT IS THE PEN NOW?
	ORA	A
	JNZ	NOPEN		;NO NEED TO SEND PEN COMMAND

	CALL	DELAY1		;DELAY BEFORE SENDING ANY CHARACTER
	MVI	E,'z'		;PEN DOWN CODE
	MVI	C,OUTFUNC		;SEND TO LIST DEVICE
	CALL	BDOS

	CALL	DELAY2		;NOW LONG DELAY WAIT FOR PEN TO DROP

	MVI	A,1
	STA	PENNOW
	JMP	NOPEN

;	PEN GOING UP?

PENGOUP	LDA	PENNOW		;WHAT IS THE PEN NOW?
	ORA	A
	JZ	NOPEN2		

	CALL	DELAY1
	MVI	E,'y'
	MVI	C,OUTFUNC
	CALL	BDOS

	CALL	DELAY2		;LIFT PEN AND WAIT

	MVI	A,0
	STA	PENNOW

;	IF PEN UP MOVE X AND Y

NOPEN2	CALL	XCHAR
	CALL	GOSUBX
	CALL	YCHAR
	CALL	GOSUBY
	RET


;	NOW TO START READING AND LOOP UNTIL COUNTS ARE FINISHED

NOPEN	MVI	A,0
	STA	GOXY		;XY DECODED

;	NOW FIND OUT DIRECTION
;	FIRST DETERMINE IF EITHER IS ZERO MAGNITUDE

	LDA	TBLAD		;SGET LEAST SIGNIF BYTE X
	ORA	A
	JNZ	XMAG		;THERE IS X MAGNITUDE

	LDA	TBLAD+1
	ANI	0FH		;MAGNITUDE BITS ONLY
	JZ	NOXMAG		;NO X MAGNITUDE

XMAG	LDA	TBLAD+2		;GET LEAST SINGIF BYTE Y
	ORA	A
	JNZ	DIRTEST		;BOTH X AND Y, GET SIGNS

	LDA	TBLAD+3
	ANI	0FH
	JZ	NOYMAG		;Y MAGNITUDE IS 0

DIRTEST	LDA	TBLAD+1		;X SIGN
	ANI	10H
	JZ	NOTPOSX

	MVI	A,1
	STA	GOXY

NOTPOSX	LDA	TBLAD+3		;Y SIGN
	ANI	10H
	JZ	NOTPOSY

	LDA	GOXY
	ORI	02H
	STA	GOXY

NOTPOSY	LDA	GOXY		;NOW GET CODE 0,1,2,3
	CPI	0
	JZ	XY0

	CPI	1
	JZ	XY1

	CPI	2
	JZ	XY2

	MVI	A,'u'
	JMP	SENDIT		;OUTPUT TO PLOTTER

XY2	MVI	A,'s'
	JMP	SENDIT

XY1	MVI	A,'w'
	JMP	SENDIT

XY0	MVI	A,'q'

SENDIT	CALL	GOSUBX
	JMP	ENDLOOP


;	NOW LOOP AS LONG AS X HAS MAGNITUDE

GOSUBX	MOV	E,A
	MVI	C,OUTFUNC

SENDLP	LDA	TBLAD		;GET LEAST SIGNIF
	ORA	A
	JNZ	SND1

	LDA	TBLAD+1
	ANI	0FH
	RZ

	DCR	A	
	STA	TBLAD+1

	MVI	A,0FFH
	STA	TBLAD
	JMP	SND2

SND1	DCR	A
	STA	TBLAD		;DECREMTNE COUNT AND GO

SND2	PUSH	B
	PUSH	D

 	CALL	DELAY1
	CALL	BDOS

	POP	D
	POP	B
	JMP	SENDLP

NOXMAG	CALL	YCHAR
	CALL	GOSUBY
	RET

YCHAR	LDA	TBLAD+3		;GET Y SIGN
	ANI	10H
	JZ	YPLUS

	MVI	A,'t'
	RET

YPLUS	MVI	A,'p'
	RET


NOYMAG	CALL	XCHAR
	CALL	GOSUBX
	RET


XCHAR	LDA	TBLAD+1
	ANI	10H
	JZ	XPLUS

	MVI	A,'v'
	RET

XPLUS	MVI	A,'r'
	RET



;	NOW LOOP AS LONG AS Y HAS MAGNITUDE

GOSUBY	MOV	E,A
	MVI	C,OUTFUNC

SENDL2	LDA	TBLAD+2		;GET LEAST SIGNIF
	ORA	A
	JNZ	SND3

	LDA	TBLAD+3
	ANI	0FH
	RZ

	DCR	A	
	STA	TBLAD+3

	MVI	A,0FFH
	STA	TBLAD+2
	JMP	SND4

SND3	DCR	A
	STA	TBLAD+2		;DECREMTNE COUNT AND GO

SND4	PUSH	B
	PUSH	D

	CALL	DELAY1
	CALL	BDOS

	POP	D
	POP	B
	JMP	SENDL2

ENDLOOP	RET


;	*******DELAYS

DELAY1	PUSH	PSW
	PUSH	H

;	NOW CHECK WHETHER FAST OR SLOW, THEN GET THE VALUE FOR
;	DELAY BETWEEN STEPS

	LDA	SLOW
	ORA	A
	JZ	NOTSLOW

	LHLD	SDELAY		;GET SLOW DELAY
	JMP	DL1LP

NOTSLOW	LHLD	FDELAY

DL1LP	DCX	H
	MOV	A,H
	ORA	L
	JNZ	DL1LP

	POP	H
	POP	PSW
	RET

DELAY2	PUSH	B
	PUSH	PSW
	PUSH	D

	MVI	A,XDEL2
	MOV	B,A

DELX2	MOV	A,B
	ORA	A		;CHECK FOR END OF LOOP
	JZ	ENDDEL

	PUSH	B

	MVI	E,20H		;SPACE CHARACTER
	MVI	C,OUTFUNC
	CALL	BDOS

	POP	B
	DCR	B
	JMP	DELX2

ENDDEL	POP	D
	POP	PSW
	POP	B
	RET




;	******* PLOTTER PORT INITIALIZE ROUTINE ***************
;	 FOR 8255, ALL OUTPUTS , CODE IS 80 H

;	FIRST SET THE PORT NUMBERS IN

INIT	MVI	E,'y'
	MVI	C,OUTFUNC
	CALL	BDOS
	CALL	DELAY2
	RET



	END
