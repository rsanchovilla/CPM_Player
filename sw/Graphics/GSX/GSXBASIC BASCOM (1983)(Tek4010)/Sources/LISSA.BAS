10 REM GSX-80 DEMO LISSAJOUS FIGURES
20 REM ORIGINALLY WRITTEN FOR MALLARD BASIC, AUTHOR UNKNOWN
30 REM
40 REM MODIFIED FOR MICROSOFT BASIC AND IMPROVED
50 REM NOVEMBER 2014, UDO MUNK
60 REM
70 REM USE MERGE "GSXFUN" AFTER LOADING AND BEFORE RUNNING THIS PROGRAM
80 REM OR COMPILE WITH BASCOM =LISSA/O
90 REM
100 GOSUB 31000 : REM INITIALIZE GSX
110 GOSUB 32000 : REM OPEN WORKSTATION
140 T=1 : PI=3.1415926#
150 IF T=1  THEN XP=10 : YP=10 : A$="K"
160 IF T=2  THEN XP=3  : YP=5  : A$="P"
170 IF T=3  THEN XP=3  : YP=5  : A$="L"
180 IF T=4  THEN XP=7  : YP=7  : A$="K"
190 IF T=5  THEN XP=5  : YP=5  : A$="L"
200 IF T=6  THEN XP=1  : YP=9  : A$="K"
210 IF T=7  THEN XP=3  : YP=10 : A$="P"
220 IF T=8  THEN XP=3  : YP=5  : A$="K"
230 IF T=9  THEN XP=10 : YP=1  : A$="L"
240 IF T=10 THEN XP=1  : YP=7  : A$="P"
250 IF T=11 THEN XP=10 : YP=3  : A$="K"
260 IF T=12 THEN XP=10 : YP=10 : A$="L"
270 IF T=13 THEN XP=5  : YP=6  : A$="P"
280 IF T=14 THEN XP=3  : YP=9  : A$="K"
290 IF T=15 THEN XP=2  : YP=10 : A$="P"
300 IF T=16 THEN XP=10 : YP=10 : A$="K"
310 IF T=17 THEN XP=10 : YP=1  : A$="P"
340 IF T=18 THEN T=1 : GOTO 150
350 FOR K=10 TO 1 STEP -1
360 IF INT(XP/K)=XP/K AND INT(YP/K)=YP/K THEN XYP=XP*YP/K : GOTO 380
370 NEXT K
380 X2=32000 : Y2=16000
390 FOR W=0 TO 2*PI*XYP STEP PI/30
400 IF A$="N" THEN X1=X2 : Y1=Y2
410 IF A$="P" THEN X1=16000 : Y1=16000
420 IF A$="L" THEN X1=8000*COS(W)+16000 : Y1=16000
430 IF A$="K" THEN X1=8000*COS(W)+16000 : Y1=8000*SIN(W)+16000
440 X2=16000*COS(W/XP)+16000 : Y2=16000*SIN(W/YP)+16000
450 GOSUB 34000 : REM DRAW LINE
460 NEXT W
470 GOSUB 36000 : REM UPDATE WORKSTATION
480 A$=INKEY$ : IF LEN(A$)<>0 THEN 900
490 FOR K=0 TO 15000 : NEXT K
500 GOSUB 35000 : REM CLEAR WORKSTATION
510 T=T+1 : GOTO 150
900 GOSUB 33000 : REM CLOSE WORKSTATION
910 END
920 REM
930 %INCLUDE GSXFUN.LIB
