0 CLEAR 2000:U$=CHR$(26)+CHR$(12)
1 PRINT U$:RESET:PRINT TAB(12);"*******************":PRINT TAB(12);"*DYNABASE DATABASE*":PRINT TAB(12);"*******************":PRINT:PRINT:PRINT TAB(7);"DISTRIBUTED EXCLUSIVELY BY  DYNACOMP, INC.":FOR N=1 TO 3000:NEXT
16 PRINT U$:TE$="TEXT":NU$="NUMERIC":CO$=".CON":DA$=".DAT":DEFINT M,N,J,K,L:DIM DX(25):ON ERROR GOTO 2010
110 TA=0:PRINT U$:PRINT TAB(25)"DATABASE COMMAND MENU":PRINT:PRINT TAB(20)"1)"TAB(25)"CREATE FILE":PRINT TAB(20)"2)"TAB(25)"EDIT/UPDATE FILE":PRINT TAB(20)"3)"TAB(25)"ADD ENTRIES TO FILE":PRINT TAB(20)"4)"TAB(25)"PRINT FILE":PRINT TAB(20)"5)"TAB(25)"SORT
120 PRINT TAB(20)"6)"TAB(25)"DIRECTED SEARCH":PRINT TAB(20)"7)"TAB(25)"GLOBAL SEARCH":PRINT TAB(20)"8)"TAB(25)"EVALUATE FILE":PRINT TAB(20)"9)"TAB(25)"BAR CHART FILE":PRINT TAB(19)"10)"TAB(25)"PRINT LABELS"
121 PRINT TAB(19)"11)"TAB(25)"COPY BY FIELD":PRINT TAB(19)"12)"TAB(25)"RECONFIGURE A FILE":PRINT TAB(19)"13)"TAB(25)"QUIT PROGRAM":PRINT:PRINT TAB(25);:GOSUB 935:IF I<1 OR I>13 THEN GOTO 110
122 IF I=13 THEN PRINT U$:END
123 IF I=5 THEN INPUT" DO YOU WISH TO RUN THE SPECIAL SORT ROUTINE";DE$:IF LEFT$(DE$,1)="Y"THEN INPUT"WILL YOU BE SORTING BY TEXT (1) OR NUMERIC (2) FIELD (1 OR 2)";PS:IF PS=1 THEN RUN"STEP1A"ELSE RUN"STEP1N"
125 II=I:PRINT U$:PS=1:PX=1:SN=1:OP=0:IN=0:DE$="":ON I GOSUB 180,1545,605,370,220,260,300,4920,9000,6005,5440,5710:IF II>3 THEN PRINT:INPUT"PRESS <RETURN> TO RETURN TO COMMAND MENU";I$:
140 GOTO 110
180 GOSUB 1340:F$=FI$+CO$:GOSUB 950:IF CS=2 THEN GOTO 184
182 GOSUB 800
184 GOSUB 1230:GOSUB 1235:CLOSE:F$=FI$+DA$:GOSUB 1230:GOSUB 1060:CLOSE:RETURN
220 GOSUB 2110:GOSUB 2440:IF TA=1 THEN GOSUB 1224:F$=DE$+TA$+CO$:CLOSE 1:GOSUB 1230:GOSUB 1235:CLOSE 1:F$=FI$+DA$:GOSUB 1290:FF$=DE$+TA$+DA$:GOSUB 1211
222 GOSUB 2310
224 GOSUB 2510:PRINT:PRINT TAB(25);:GOSUB 935:IF RT=1 AND(I<1 OR I>TC)THEN 224
226 IF RT=2 AND(I<1 OR I>TN)THEN 224
228 SK=I:GOSUB 1520:GOSUB 1390:RETURN:
260 GOSUB 2110:GOSUB 2440:IF TA=1 THEN GOSUB 1224:F$=DE$+TA$+CO$:CLOSE 1:GOSUB 1230:GOSUB 1235:CLOSE 1:F$=FI$+DA$:GOSUB 1290:FF$=DE$+TA$+DA$:GOSUB 1211
262 GOSUB 2310:GOSUB 2370:GOSUB 2575:IF EM=1 THEN GOSUB 2750
264 IF EM>1 THEN GOSUB 2650
266 GOSUB 1520:GOSUB 1670:CLOSE:RETURN:
300 GOSUB 2110:GOSUB 2440:IF TA=1 THEN GOSUB 1224:F$=DE$+TA$+CO$:CLOSE 1:GOSUB 1230:GOSUB 1235:CLOSE 1:F$=FI$+DA$:GOSUB 1290:FF$=DE$+TA$+DA$:GOSUB 1211
310 RT=1:FOR N=1 TO TC:SF(N)=N:NEXT:NF=TC:GOSUB 2370:IF EM>1 THEN GOSUB 2650
320 IF EM=1 THEN GOSUB 2750
330 GOSUB 1520:GOSUB 1670:CLOSE:RETURN
370 GOSUB 2110:GOSUB 1520:GOSUB 4300
371 IN=IN+1:GOSUB 1307:GOSUB 3925:IF EOF(1)=-1 THEN CLOSE:RETURN
372 GOTO 371
400 REM
495 PP=0:PC=0:PF=0:GOSUB 4500:IF I=2 THEN GOSUB 4440:RETURN
500 PRINT U$:INPUT"HOW MANY FIELDS TO BE PRINTED (1-10)";PF:IF PF<1 OR PF>10 THEN 500
505 PRINT U$:INPUT"HOW MANY COLUMNS ON PRINTER (20-255)";PC:IF PC<20 OR PC>255 THEN 505
510 PRINT U$:I$="":INPUT"DOES PRINTER REQUIRE A DENSITY SELECTION CONTROL SEQUENCE (Y/N)";I$:IF I$<>"Y"AND I$<>"N"THEN 510
515 IF I$="N"THEN GOTO 545:
518 FOR J=1 TO 10:QP(J)=0:NEXT:XD=0
520 PRINT U$:INPUT"HOW MANY CONTROL SEQUENCE CHARACTERS ARE REQUIRED (1-10)";XD:IF XD<1 OR XD>10 THEN 520
525 FOR J=1 TO XD
530 PRINT"ENTER ASCII CODE (IN DECIMAL) FOR CONTROL CHARACTER #";J;:INPUT QP(J):IF QP(J)<0 OR QP(J)>255 THEN PRINT"ERROR":GOTO 530
535 NEXT:GOSUB 820:IF I$="N"THEN 520
540 FOR J=1 TO XD:LPRINT CHR$(QP(J));:NEXT
545 FOR J=1 TO 10:PT(J)=0:PH(J)=0:PG(J)=0:NEXT
550 FOR J=1 TO PF
555 PRINT U$:I$="":PRINT"WILL FIELD #";J;"BE TEXT OR NUMERIC (T/N)";:INPUT I$:IF I$<>"T"AND I$<>"N"THEN 555
560 IF I$="T"THEN PT(J)=1ELSE PT(J)=2:
563 RT=PT(J):GOSUB 2510::PRINT:PRINT"ENTER FIELD SELECTION FOR PRINT FIELD #";J;:INPUT PH(J):IF PH(J)<1 THEN 563
569 IF PT(J)=1 AND PH(J)>TC THEN 563
572 IF PT(J)=2 AND PH(J)>TN THEN 563
575 INPUT"ENTER NUMBER OF COLUMNS FOR THIS PRINT FIELD";PG(J):IF PG(J)<1 OR PG(J)>PC THEN PRINT"ERROR":GOTO 575
578 PP=PP+PG(J)+1:PRINT"COLUMNS USED SO FAR:";PP:PRINT"COLUMNS REMAINING";PC-PP:IF PP>PC THEN PRINT"COLUMN ALLOCATION EXCEEDED":PP=0:GOSUB 910:GOTO 500:
584 FOR TT=1 TO 500:NEXT:NEXT J
587 GOSUB 3790:GOSUB 4350:RETURN:
605 GOSUB 1340:F$=FI$+CO$:GOSUB 1290:GOSUB 1300:CLOSE:GOSUB 1202:FF$=FI$+DA$:GOSUB 1220:F$=DE$+"XXXXXXXX.XXX":GOSUB 1230:EN=1
610 GOSUB 1314:GOSUB 1240:EN=EN+1::IF EOF(2)=-1 THEN GOTO 625:
620 GOTO 610:
625 GOSUB 860:GOSUB 1065::CLOSE:GOSUB 1290:GOSUB 1211
635 GOSUB 1307:GOSUB 1251:IF EOF(1)=-1 THEN CLOSE:KILL F$:RETURN
640 GOTO 635
760 FOR N=1 TO 10:TC$(N)="":TN$(N)="":NEXT:RETURN
800 TC=0:TN=-1:GOSUB 760
801 PRINT U$:INPUT"ENTER NUMBER OF TEXT FIELDS PER RECORD (1-10)";TC:IF TC<1 OR TC>10 THEN GOTO 800
805 PRINT U$:X$=TE$:FOR N=1 TO TC:I$=TC$(N):GOSUB 890:TC$(N)=I$:NEXT
810 PRINT U$:INPUT"ENTER NUMBER OF NUMERIC FIELDS PER RECORD (0-10)";TN:IF TN<0 OR TN>10 THEN GOTO 810
812 IF TN=0 THEN GOTO 818
814 PRINT U$:X$=NU$:FOR N=1 TO TN:I$=TN$(N):GOSUB 890:TN$(N)=I$:NEXT
815 PRINT U$:FOR N=1 TO TC:PRINT TC$(N):NEXT:FOR N=1 TO TN:PRINT TN$(N):NEXT:PRINT:PRINT
818 GOSUB 820:IF I$="N"THEN GOTO 801
819 RETURN
820 I$="":PRINT:INPUT"DATA CORRECT (Y/N)";I$:IF I$<>"N"AND I$<>"Y"AND I$<>"y"AND I$<>"n"THEN 820ELSE IF I$="y"THEN I$="Y"ELSE IF I$="n"THEN I$="N"
825 RETURN
860 FOR N=1 TO 10:DO$(N)=D$(N):D$(N)="":D(N)=0:NEXT:RETURN
890 PRINT"ENTER NAME OF ";X$;" FIELD NUMBER ";N;:LINE INPUT I$:IF I$=""THEN GOTO 890
895 RETURN
910 IF OP=1 THEN RETURN ELSE PRINT TAB(20);:INPUT"PRESS <RETURN> TO CONTINUE";I$:RETURN
921 K=K+1:IF K=2 AND PX=1 THEN K=0:IF PS=1 THEN GOSUB 910
922 IF PX=2 AND K=10 THEN K=0:IF PS=1 THEN GOSUB 910:GOSUB 610
925 RETURN
935 I=0:INPUT"ENTER SELECTION";I:RETURN
950 GOSUB 760:PRINT U$:CS=0:PRINT TAB(25)"CONFIGURATION SOURCE SELECTIONS":PRINT:PRINT TAB(20)"1)"TAB(25)"GENERATE NEW CONFIGURATION":PRINT TAB(20)"2)"TAB(25)"USE A PREVIOUSLY SAVED CONFIGURATION":PRINT:PRINT TAB(25);:GOSUB 935:IF I<1 OR I>2 THEN 950
952 CS=I:IF CS=1 THEN RETURN
954 PRINT U$:I$="":INPUT"NAME OF CONFIGURATION SOURCE FILE (NO EXTENSION OR DRIVE LETTER)";I$:IF I$=""THEN 954
956 INPUT"WHICH DRIVE IS CONFIGURATION ON (A or B)";DR$:F$=DR$+":"+I$+CO$:GOSUB 1290:GOSUB 1300:CLOSE:PRINT U$:PRINT"TEXT FIELDS"TAB(32)"NUMERIC FIELDS(IF ANY)":PRINT:FOR N=1 TO 10:PRINT LEFT$(TC$(N),30)TAB(32)LEFT$(TN$(N),30):NEXT:PRINT
958 I$="":INPUT"CONFIGURATION CORRECT (Y/N)";I$:IF I$<>"Y"AND I$<>"N"THEN GOTO 958ELSE IF I$="N"THEN GOTO 950
960 RETURN
1055 PRINT U$:PRINT:PRINT"USE COMMAND  /EXIT  TO TERMINATE SEQUENCE.":PRINT:RETURN
1060 GOSUB 860:EN=1:
1065 PRINT U$:GOSUB 1055:PRINT"ENTRY # ";EN:PRINT:FOR N=1 TO TC:X$=TC$(N):GOSUB 1120:IF N=1 AND D$(N)="/EXIT"THEN GOTO 1100
1075 NEXT:IF TN=0 THEN GOTO 1090
1080 FOR N=1 TO TN:X$=TN$(N):I=D(N):GOSUB 1150:D(N)=I:NEXT
1090 GOSUB 820:IF I$="N"THEN GOTO 1065
1095 EN=EN+1:GOSUB 1240:GOTO 1065
1100 CLOSE:RETURN
1120 PRINT:PRINT"ENTER ";X$;" ? ";:LINE INPUT I$:IF I$<>""THEN D$(N)=I$
1123 IF I$=CHR$(34)THEN D$(N)=DO$(N):
1125 IF D$(N)=""THEN PRINT"NULL ENTRY":GOTO 1120
1130 RETURN
1150 PRINT:PRINT"ENTER ";X$;" ";:INPUT I:RETURN
1180 RETURN
1202 PRINT U$:DR$="":PRINT"<RETURN> SELECTS DEFAULT DRIVE.":INPUT"WHICH DRIVE IS TO BE USED FOR EDITING DATA STORAGE (A or B)";DR$:IF DR$=""THEN DE$=""ELSE DE$=DR$+":"
1206 RETURN
1211 TQ$=FF$:GOSUB 35000:FF$=TQ$:OPEN"O",2,FF$:RETURN
1220 TQ$=FF$:GOSUB 35000:FF$=TQ$:OPEN"I",2,FF$:RETURN
1224 PRINT U$:DR$="":PRINT"<RETURN> SELECTS THE DEFAULT DRIVE":INPUT"WHICH DRIVE IS TO BE USED FOR DATA STORAGE (A or B)";DR$:IF DR$=""THEN DE$=""ELSE DE$=DR$+":"
1226 RETURN
1230 TQ$=F$:GOSUB 35000:F$=TQ$:OPEN"O",1,F$:RETURN
1235 PRINT#1,TC,TN:FOR O=1 TO TC:PRINT#1,TC$(O):NEXT:IF TN>0 THEN FOR M=1 TO TN:PRINT#1,TN$(M):NEXT
1236 RETURN:
1240 FOR M=1 TO TC:PRINT#1,D$(M):NEXT:IF TN>0 THEN FOR O=1 TO TN:PRINT#1,D(O):NEXT
1242 RETURN
1251 FOR M=1 TO TC:PRINT#2,D$(M):NEXT:IF TN>0 THEN FOR O=1 TO TN:PRINT#2,D(O):NEXT
1252 RETURN
1290 TQ$=F$:GOSUB 35000:F$=TQ$:OPEN"I",1,F$:RETURN:
1300 INPUT#1,TC:INPUT#1,TN:FOR O=1 TO TC:LINE INPUT#1,TC$(O):NEXT:IF TN>0 THEN FOR M=1 TO TN:LINE INPUT#1,TN$(M):NEXT
1302 RETURN
1307 FOR O=1 TO 10:D$(O)="":D(O)=0:NEXT:FOR O=1 TO TC:LINE INPUT#1,D$(O):NEXT:IF TN>0 THEN FOR M=1 TO TN:INPUT#1,D(M):NEXT
1308 RETURN
1314 FOR M=1 TO 10:D$(M)="":D(M)=0:NEXT M:FOR M=1 TO TC:LINE INPUT#2,D$(M):NEXT:IF TN>0 THEN FOR O=1 TO TN:INPUT#2,D(O):NEXT
1316 RETURN
1340 PRINT U$:IF FI$<>""THEN PRINT"DEFAULT FILE NAME =";FI$
1350 I$="":INPUT"ENTER SOURCE FILE NAME (NO EXTENSION OR DRIVE LETTER)";I$:IF I$<>""THEN FI$=I$:INPUT"WHICH DRIVE (A or B)";DE$:IF DE$<>""THEN FI$=DE$+":"+FI$
1352 IF FI$=""THEN 1340
1355 RETURN
1390 N=1:Y$="":Y=-1.70141E+38:X$=CHR$(95):X$=STRING$(255,X$):X=1.70141E+38
1395 GOSUB 1307:IF EOF(1)=-1 THEN GOTO 1400ELSE N=N+1:GOTO 1395:
1400 FS=N:CLOSE 1:GOSUB 4300:FOR N=1 TO FS:GOSUB 1290:SI=N:FOR MM=1 TO FS:GOSUB 1307::IF RT=1 AND D$(SK)<=X$AND D$(SK)>Y$THEN X$=D$(SK):SI=MM
1420 IF RT=2 AND D(SK)<=X AND D(SK)>Y THEN X=D(SK):SI=MM
1425 NEXT MM:CLOSE 1:GOSUB 1290:FOR NM=1 TO SI:GOSUB 1307:NEXT:GOSUB 3925:IF TA=1 THEN GOSUB 1251
1430 Y$=X$:Y=X:X$=CHR$(95):X$=STRING$(255,X$):X=1.70141E+38:CLOSE 1:NEXT N
1435 CLOSE:RETURN
1505 PRINT U$:I$="":INPUT"OPERATOR DISPLAY CONTROL (Y/N)";I$:IF I$<>"Y"AND I$<>"N"THEN 1505
1506 IF I$="N"THEN OP=1ELSE OP=0
1507 RETURN
1520 D5$=F$:OP=0:PRINT U$:PRINT"DEFAULT = N (OUTPUT TO CRT)":I$="":INPUT"OUTPUT TO PRINTER (Y/N)";I$:IF I$="Y"THEN PS=2ELSE PS=1
1521 IF PS=1 THEN PX=1:GOSUB 1505:RETURN
1522 PRINT"PRESS <RETURN> WHEN PRINTER IS READY";:INPUT GX$:IF PS=2 THEN PRINT U$:DZ$="":TI$="":LINE INPUT"ENTER TODAY'S DATE ? ";DZ$:LINE INPUT"ENTER DESIRED PAGE HEADING ? ";TI$:PRINT U$
1525 GOSUB 3880:RETURN
1545 PS=1:GOSUB 2110:GOSUB 2140:GOSUB 3510:IF ET=1 OR ET=3 OR ET=5 THEN GOSUB 3580
1550 IF ET=3 OR ET=4 THEN GOSUB 3680
1555 GOSUB 3310:RETURN
1670 K=1:N=1:GOSUB 4300
1680 IN=IN+1:GOSUB 1307::FOR J=1 TO NS:FOR I=1 TO NF:SR=0:ON RT GOSUB 1780,1820:IF SR=1 THEN GOSUB 3925:K=K+1:IF TA=1 THEN GOSUB 1251:
1730 IF SR=1 THEN GOTO 1760
1740 NEXT
1750 NEXT
1760 IF EOF(1)=-1 THEN CLOSE:FX=K-1:RETURN
1770 GOTO 1680
1780 IF EM=1 AND KL$<=D$(SF(I))AND KH$=>D$(SF(I))THEN SR=1
1790 IF EM=2 AND SK$(J)=D$(SF(I))THEN SR=1
1800 IF EM=3 AND INSTR(D$(SF(I)),SK$(J))>0 THEN SR=1
1810 RETURN
1820 IF EM=1 AND KL<=D(SF(I))AND KH=>D(SF(I))THEN SR=1
1830 IF EM=2 AND SK(J)=D(SF(I))THEN SR=1
1840 RETURN
1900 RETURN:IF PZ=1 THEN LPRINT CHR$(27);CHR$(19):
1901 IF PZ=2 THEN LPRINT CHR$(27);CHR$(20):
1902 RETURN
2010 CLOSE:PRINT U$:PRINT"SYSTEM ERROR":X=ERR+1:IF X=54 THEN PRINT"FILE NOT FOUND"
2030 IF X=62 THEN PRINT"DISK FULL"
2040 IF X=65 THEN PRINT"BAD FILE NAME"
2050 IF X>50 THEN PRINT"DISK ERROR #";X-1;" IN LINE ";ERL
2060 IF X<51 THEN PRINT"ERROR # ";X-1;" IN LINE ";ERL
2070 IF X=8 THEN PRINT"OUT OF MEMORY"
2080 PRINT:INPUT"PRESS <RETURN> TO RETURN TO COMMAND MENU";I$:RESUME 110
2110 GOSUB 1340:F$=FI$+CO$:GOSUB 760:GOSUB 1290:GOSUB 1300:CLOSE:F$=FI$+DA$:GOSUB 1290:RETURN
2140 GOSUB 1202:FF$=DE$+"XXXXXXXX.XXX":GOSUB 1211:RETURN
2170 FF$=DE$+"XXXXXXXX.XXX":GOSUB 1220:RETURN
2199 PRINT U$
2200 FOR NP=1 TO TC:PRINT TC$(NP);":";D$(NP):NEXT:IF PS=2 THEN LPRINT"ENTRY # ";IN:KQ=KQ+1:FOR NP=1 TO TC:LPRINT TC$(NP)" : ";D$(NP):KQ=KQ+1:NEXT
2205 IF TN=0 THEN 2230
2210 FOR NP=1 TO TN STEP 2:PRINT TN$(NP);":";D(NP)TAB(32);:IF PS=2 THEN LPRINT TN$(NP);" : ";D(NP):KQ=KQ+1:
2215 IF NP<TN THEN PRINT TN$(NP+1);":";D(NP+1)ELSE PRINT
2220 IF NP<TN AND PS=2 THEN LPRINT TN$(NP+1);" : ";D(NP+1):KQ=KQ+1:
2225 NEXT:IF PS=2 THEN LPRINT" ":KQ=KQ+1:
2230 IF PS=1 THEN GOSUB 910
2235 RETURN
2310 IF TN=0 THEN RT=1:RETURN:
2320 PRINT U$:PRINT TAB(25)"FIELD TYPE SELECTION":PRINT:PRINT TAB(20)"1)"TAB(25)"TEXT FIELD":PRINT TAB(20)"2)"TAB(25)"NUMERIC FIELD":PRINT:PRINT TAB(25);:GOSUB 935:IF I<1 OR I>2 THEN 2320
2330 RT=I:RETURN
2370 PRINT U$:EM=0:PRINT TAB(25)"SEARCH CONDITION SELECTIONS":PRINT:PRINT TAB(20)"1)"TAB(25)"RANGE SEARCH":PRINT TAB(20)"2)"TAB(25)"EXACT MATCH SEARCH":IF RT=1 THEN PRINT TAB(20)"3)"TAB(25)"INSTRING SEARCH"
2380 PRINT:PRINT TAB(25);:GOSUB 935:IF RT=2 AND I>2 THEN 2370
2390 IF I<1 OR I>3 THEN 2370
2400 EM=I:RETURN
2440 PRINT U$:TA=0:I$="":PRINT"DO YOU WANT THE RESULTS OF THE OPERATION TO":INPUT"BE WRITTEN TO A NEW DISK FILE (Y/N)";I$:IF I$<>"N"AND I$<>"Y"THEN GOTO 2440
2450 IF I$="N"THEN TA=0:RETURN
2460 TA=1:PRINT:TA$="":INPUT"ENTER NAME OF TARGET FILE (NO EXTENSION OR DRIVE LETTER)";TA$:IF TA$=""THEN GOTO 2460
2470 RETURN
2510 PRINT U$:PRINT TAB(20)"FIELD SELECTION MENU":PRINT:IF RT=1 THEN FOR V=1 TO TC:PRINT V;")"TAB(10)TC$(V):NEXT
2530 IF RT=2 THEN FOR V=1 TO TN:PRINT V;")"TAB(10)TN$(V):NEXT
2540 RETURN
2575 FOR N=1 TO 10:SF(N)=0:NEXT:NF=0
2580 GOSUB 2510:PRINT:INPUT"HOW MANY FIELDS ARE TO BE SEARCHED (1-10)";NF:IF NF<1 OR NF>10 THEN GOTO 2580
2585 FOR N=1 TO NF
2590 GOSUB 2510:PRINT:PRINT"ENTER NUMBER OF SEARCH FIELD #";N;" (1-10)";:INPUT SF(N):IF SF(N)>10 OR SF(N)<1 THEN GOTO 2590
2600 NEXT:GOSUB 820:IF I$="N"THEN GOTO 2580
2610 RETURN
2650 NS=0:FOR N=1 TO 10:SK(N)=0:SK$(N)="":NEXT
2660 PRINT U$:INPUT"HOW MANY KEYWORDS ARE TO BE USED IN THE SEARCH (1-10)";NS:IF NS<1 OR NS>10 THEN 2660
2670 FOR N=1 TO NS
2680 IF RT=1 THEN PRINT"ENTER KEYWORD NUMBER ";N;" ? ";:LINE INPUT I$:IF I$<>""THEN SK$(N)=I$
2690 IF RT=1 AND SK$(N)=""THEN PRINT"ERROR":GOTO 2680
2692 IF RT=2 THEN PRINT"ENTER KEYNUMBER # ";N;:INPUT SK(N)
2700 NEXT:GOSUB 820:IF I$="N"THEN GOTO 2660
2710 RETURN
2750 PRINT U$:NS=1
2760 KL=0:KH=0:KL$="":KH$=""
2765 IF RT=2 THEN GOTO 2790
2770 PRINT"ENTER HIGH BOUNDARY KEYWORD ? ";:LINE INPUT I$:IF I$<>""THEN KH$=I$
2775 IF KH$=""THEN PRINT"ERROR":GOTO 2770
2780 PRINT"ENTER LOW BOUNDARY KEYWORD ? ";:LINE INPUT I$:IF I$<>""THEN KL$=I$
2785 IF KL$=""THEN PRINT"ERROR":GOTO 2780
2786 IF KL$>KH$THEN PRINT"ERROR":GOTO 2760
2787 GOTO 2800
2790 INPUT"ENTER HIGH BOUNDARY KEYNUMBER";KH:INPUT"ENTER LOW BOUNDARY KEYNUMBER";KL:IF KL>KH THEN PRINT"ERROR":GOTO 2760
2800 GOSUB 820:IF I$="N"THEN PRINT U$:GOTO 2765
2805 RETURN
2850 FOR N=1 TO TC:X$="NEW "+TC$(N):GOSUB 1120:NEXT
2855 IF TN=0 THEN GOTO 2870
2860 FOR N=1 TO TN:X$="NEW "+TN$(N):I=D(N):GOSUB 1150:D(N)=I:NEXT
2870 GOSUB 820:IF I$="N"THEN 2850
2875 RETURN
2910 FOR J=1 TO NE::IF RN=X(J)THEN GOSUB 2199:GOSUB 2850:
2930 NEXT:RETURN
2970 GOSUB 2199:
2980 PRINT:I$="":INPUT"EDIT ITEM (Y/N)";I$:IF I$<>"Y"AND I$<>"N"THEN 2980
2990 IF I$<>"Y"THEN RETURN
3000 GOSUB 2850::RETURN
3050 FOR J=1 TO NE:IF RN=X(J)THEN GOSUB 3100:RETURN
3060 NEXT:RETURN
3100 ON EZ GOSUB 3110,3160:RETURN
3110 PRINT U$:PRINT TC$(EF);":";D$(EF):PRINT:N=EF:X$="NEW "+TC$(EF):GOSUB 1120:GOSUB 820:IF I$="N"THEN GOTO 3110
3120 RETURN:
3160 PRINT U$:PRINT TN$(EF);":";D(EF):PRINT:N=EF:X$="NEW "+TN$(EF):I=D(EF):GOSUB 1150:D(EF)=I:GOSUB 820:IF I$="N"THEN GOTO 3160
3170 RETURN
3210 FOR J=1 TO NE:IF RN=X(J)THEN DL=1:RETURN
3220 NEXT:RETURN
3260 GOSUB 2199:
3270 PRINT:I$="":INPUT"DELETE ITEM (Y/N)";I$:IF I$<>"Y"AND I$<>"N"THEN 3270ELSE IF I$<>"Y"THEN RETURN ELSE DL=1:RETURN
3310 RN=1
3320 GOSUB 1307::DL=0:ON ET GOSUB 2910,2970,3050,3100,3210,3260:IF DL=1 THEN GOTO 3360:
3350 GOSUB 1251:
3360 IF EOF(1)=-1 THEN GOTO 3380
3370 RN=RN+1:GOTO 3320:
3380 CLOSE:GOSUB 3430::RETURN
3430 GOSUB 1220:GOSUB 1230:
3440 GOSUB 1314:GOSUB 1240::IF EOF(2)=-1 THEN 3470
3460 GOTO 3440
3470 CLOSE:KILL DE$+"XXXXXXXX.XXX":RETURN
3510 PRINT U$:PRINT TAB(25)"DATA EDITING OPTIONS":PRINT:PRINT TAB(10)"1)"TAB(15)"EDIT 1 TO 10 SELECTED RECORDS":PRINT TAB(10)"2)"TAB(15)"OPTIONAL EDITING OF ALL RECORDS":PRINT TAB(10)"3)"TAB(15)"UPDATE A SELECTED FIELD OF 1 TO 10 RECORDS"
3520 PRINT TAB(10)"4)"TAB(15)"OPTIONAL UPDATING OF A SELECTED FIELD OF FILE":PRINT TAB(10)"5)"TAB(15)"DELETE 1 TO 10 SELECTED RECORDS":PRINT TAB(10)"6)"TAB(15)"OPTIONAL DELETION OF ALL RECORDS":PRINT:PRINT TAB(25);:GOSUB 935:IF I<1 OR I>6 THEN 3510
3540 ET=I:RETURN
3580 NE=0:FOR J=1 TO 10:X(J)=0:NEXT
3590 PRINT U$:INPUT"HOW MANY RECORDS ARE TO BE PROCESSED (1-10)";NE:IF NE<1 OR NE>10 THEN 3590
3610 FOR J=1 TO NE:PRINT
3620 PRINT"ENTER FILE NUMBER OF RECORD #";J;:INPUT X(J):IF X(J)<1 THEN PRINT"ERROR":GOTO 3620
3630 NEXT:GOSUB 820:IF I$="N"THEN 3590
3640 RETURN
3680 PRINT U$:I$="":INPUT"IS SELECTED FIELD A TEXT(T) FIELD OR NUMERIC(N) FIELD  (T/N)";I$:IF I$<>"T"AND I$<>"N"THEN 3680
3690 IF I$="T"THEN EZ=1ELSE EZ=2
3700 RT=EZ:GOSUB 2510::PRINT:PRINT TAB(25);:EF=0:GOSUB 935:IF I<1 THEN 3700
3730 IF EZ=1 AND I>TC THEN 3700
3740 IF EZ=2 AND I>TN THEN 3700
3750 EF=I:RETURN
3790 PRINT U$:TT$=""
3800 FOR N=1 TO PF:PRINT"ENTER TITLE OF COLUMN #";N;"(MAX CHAR =";PG(N);")";:INPUT TX$(N):TX$(N)=LEFT$(TX$(N),PG(N)):NEXT:GOSUB 820:IF I$="N"THEN 3800
3821 RETURN
3822 X=X:X=X:X=X
3830 FOR N=1 TO PF:IF PG(N)>2 THEN A$="\"+STRING$(PG(N)-2," ")+"\"ELSE A$="\\"
3840 LPRINT USING A$;TX$(N);:LPRINT" ";:NEXT::IF PP<PC THEN LPRINT" ":KQ=KQ+1:
3850 LPRINT" ":KQ=KQ+1:LPRINT STRING$(PC,"-"):KQ=KQ+1:LPRINT" ":KQ=KQ+1::RETURN
3880 GOSUB 4010
3885 PRINT U$:PX=0:K=64:PRINT TAB(25)"PRINTER FORMAT SELECTIONS":PRINT:FOR N=1 TO 10:PRINT TAB(20)N;")"TAB(25)"FORMAT ";CHR$(K+N):NEXT:PRINT TAB(21)"11 )"TAB(25)"CUSTOM FORMAT":PRINT TAB(21)"12 )"TAB(25)"FULL ITEM FORMAT":PRINT:PRINT TAB(25);:GOSUB 935
3886 IF I<1 OR I>12 THEN 3885
3890 PX=I+1:IF PX=13 THEN PX=1
3895 IF PX=1 THEN RETURN
3900 IF PX=12 THEN GOSUB 495:RETURN:
3905 IF PX<12 THEN GOSUB 4155:RETURN
3925 IF PS=1 THEN GOSUB 2199:RETURN:
3926 KQ=KQ+1:LV=10000-KQ:IF PX>1 AND LV<5 THEN KX=0:GOSUB 4300:X=X
3927 IF PX=1 AND LV<TN+(2*TC)+5 THEN GOSUB 4300
3930 IF PX=1 THEN GOSUB 2199:RETURN:
3935 IF PX=12 THEN GOSUB 3960:RETURN:
3940 GOSUB 4075:RETURN:
3960 FOR NP=1 TO PF:IF PG(NP)>2 THEN A$=STRING$(PG(NP)-2," "):A$="\"+A$+"\"
3965 IF PG(NP)<3 THEN A$="\\"
3970 B$=STRING$(PG(NP),"#"):IF PT(NP)=1 THEN LPRINT USING A$;D$(PH(NP));
3980 IF PT(NP)=2 THEN LPRINT USING B$;D(PH(NP));
3985 LPRINT" ";:NEXT:LPRINT:KQ=KQ+1:LPRINT" ":KQ=KQ+1:RETURN
4010 PRINT U$:PRINT TAB(25)"PRINTER SIZE":PRINT:PRINT TAB(20)"1)"TAB(25)"80-COLUMN":PRINT TAB(20)"2)"TAB(25)"132-COLUMN":PRINT:PRINT TAB(25);:GOSUB 935:IF I<1 OR I>2 THEN 4010
4015 PZ=I:GOSUB 1900:RETURN
4035 K=FX(NP,1):J=FX(NP,0):IF PZ=1 THEN J1(1)=13:J1(2)=15:J1(3)=28:J1(4)=17
4040 IF PZ=2 THEN J1(1)=22:J1(2)=26:J1(3)=48:J1(4)=29
4045 IF K<10 THEN A$="\"+STRING$(J1(K)-2," ")+"\"
4050 IF K=10 THEN B$="########"ELSE IF K=11 THEN B$="#####.##"
4055 RETURN
4075 XP=PX-1:FOR NP=1 TO NJ(XP):GOSUB 4035:IF K<10 THEN LPRINT USING A$;D$(J);
4085 IF K>9 THEN LPRINT USING B$;D(J);
4090 LPRINT" ";:NEXT:LPRINT:KQ=KQ+1:LPRINT" ":KQ=KQ+1:RETURN
4110 XP=PX-1:FOR NP=1 TO NJ(XP):GOSUB 4035:IF K<10 THEN LPRINT USING A$;TC$(J);
4120 IF K>9 THEN LPRINT USING"\      \";TN$(J);
4125 LPRINT" ";:NEXT:LPRINT:KQ=KQ+1:LPRINT" ":KQ=KQ+1:
4130 IF PZ=1 THEN LPRINT STRING$(80,"-"):KQ=KQ+1ELSE LPRINT STRING$(132,"-"):KQ=KQ+1
4135 LPRINT" ":KQ=KQ+1:RETURN
4155 RESTORE:XP=PX-1:FOR V=1 TO XP:READ NJ(V):FOR M=1 TO NJ(V):READ FX(M,0),FX(M,1):NEXT:NEXT:RETURN
4160 DATA 5,1,1,2,1,3,1,4,1,5,1::DATA 5,1,2,2,2,3,2,4,2,1,10::DATA 5,1,2,2,2,3,2,4,2,1,11::DATA 6,1,1,2,1,3,1,4,1,1,10,2,10::DATA 6,1,1,2,1,3,1,4,1,1,11,2,11::DATA 6,1,1,2,1,3,1,1,10,2,10,3,10::DATA 6,1,1,2,1,3,1,1,11,2,11,3,11:
4195 DATA 4,1,4,2,4,3,4,4,4::DATA 6,1,4,2,4,1,10,2,10,3,10,4,10::DATA 6,1,3,1,10,2,10,3,10,4,10,5,10:
4300 IF PS=1 THEN RETURN ELSE LPRINT CHR$(12):KQ=0:LPRINT"FILE NAME : ";F$,TI$,"DATE:";DZ$,"PAGE ";SN:KQ=KQ+1:SN=SN+1:LPRINT" ":KQ=KQ+1:LPRINT" ":KQ=KQ+1:IF PX=12 THEN GOSUB 3830
4305 IF PX>1 AND PX<12 THEN GOSUB 4110
4310 RETURN
4350 PRINT U$:I$="":INPUT"DO YOU WANT TO SAVE THE CUSTOM PRINTING CONFIGURATION (Y/N)";I$:IF I$<>"N"AND I$<>"Y"THEN 4350
4360 IF I$="N"THEN RETURN
4370 KK$=F$:KI$=FI$:KF$=FF$:CLOSE 1:GOSUB 1224:GOSUB 9900:F$=DE$+PI$+".PRT":GOSUB 1230:PRINT#1,PF,PC:IF XD=0 THEN CS=0ELSE CS=1
4390 PRINT#1,CS:IF CS=1 THEN PRINT#1,XD:FOR X=1 TO XD:PRINT#1,QP(X):NEXT
4400 FOR X=1 TO PF:PRINT#1,PT(X),PH(X),PG(X),TX$(X):NEXT:PRINT#1,TT$:CLOSE 1:F$=KK$:FI$=KI$:FF$=KF$:GOSUB 1290:IF TA=1 THEN GOSUB 1211
4401 RETURN
4440 KF$=FF$:KK$=F$:KI$=FI$:CLOSE 1:GOSUB 1340:F$=FI$+".PRT":GOSUB 1290:INPUT#1,PF,PC,CS:IF CS=1 THEN INPUT#1,XD:FOR X=1 TO XD:INPUT#1,QP(X):NEXT
4450 FOR X=1 TO PF:INPUT#1,PT(X),PH(X),PG(X),TX$(X):NEXT:INPUT#1,TT$:CLOSE:IF CS=1 THEN FOR X=1 TO XD:LPRINT CHR$(QP(X));:NEXT
4460 X=X:IF TA=1 THEN GOSUB 1211
4461 F$=KK$:GOSUB 1290
4462 RETURN
4500 PRINT U$:PRINT TAB(25)"CUSTOM PRINT SOURCE SELECTION":PRINT:PRINT TAB(20)"1)"TAB(25)"CREATE A NEW CONFIGURATION":PRINT TAB(20)"2)"TAB(25)"USE A PREVIOUSLY SAVED CONFIGURATION":PRINT:PRINT TAB(25);:GOSUB 935:IF I<1 OR I>2 THEN 4500
4510 RETURN
4550 RT=2:EV=1:PRINT U$:OX=0:INPUT"HOW MANY OPERATIONS ARE TO BE PERFORMED (1-10)";OX:IF OX>10 OR OX<1 THEN 4550
4560 FOR N=1 TO OX
4590 GOSUB 2510:PRINT TN+1;")"TAB(10)"INTERMEDIATE STORAGE":PRINT TAB(20)"***** OPERATION # ";N;" *****":PRINT TAB(25);:I=0:INPUT"ENTER 1ST OPERAND";I:IF I<1 OR I>TN+1 THEN GOTO 4590ELSE O1(N)=I:IF I=TN+1 THEN O1(N)=0
4599 PRINT U$:PRINT TAB(25)"OPERATION SELECTIONS"
4600 PRINT TAB(20)"1)"TAB(25)"RAISE TO A POWER":PRINT TAB(20)"2)"TAB(25)" +   ADDITION":PRINT TAB(20)"3)"TAB(25)" -   SUBTRACTION":PRINT TAB(20)"4)"TAB(25)" *   MULTIPLICATION":PRINT TAB(20)"5)"TAB(25)" /   DIVISION"
4601 PRINT TAB(20)"6)"TAB(25)"SIN (SINGLE OPERAND)":PRINT TAB(20)"7)"TAB(25)"COS (SINGLE OPERAND)":PRINT TAB(20)"8)"TAB(25)"TAN (SINGLE OPERAND)":PRINT TAB(20)"9)"TAB(25)"ATN (SINGLE OPERAND)"
4602 PRINT TAB(19)"10)"TAB(25)"RAD TO DEG (SINGLE OPERAND)":PRINT TAB(19)"11)"TAB(25)"EXPONENTIAL (SINGLE OPERAND)":PRINT TAB(19)"12)"TAB(25)"LOG (SINGLE OPERAND)":PRINT TAB(19)"13)"TAB(25)"SQR (SINGLE OPERAND)":PRINT TAB(25);
4610 I=0:INPUT"ENTER DESIRED OPERATION";I:IF I<1 OR I>13 THEN GOTO 4600ELSE OP(N)=I
4612 OT(N)=0:IF OP(N)>5 THEN 4640
4615 PRINT U$:I$="":INPUT"IS 2ND OPERAND TO BE A CONSTANT (Y/N)";I$:IF I$<>"Y"AND I$<>"N"THEN 4615
4616 IF I$="Y"THEN OT(N)=1ELSE OT(N)=0
4620 IF OT(N)=1 THEN GOSUB 4700:GOTO 4640:
4630 GOSUB 2510:PRINT TN+1;")"TAB(10)"INTERMEDIATE STORAGE":PRINT:PRINT TAB(25);:INPUT"SELECT 2ND OPERAND";I:IF I<1 OR I>TN+1 THEN GOTO 4630ELSE O2(N)=I:IF I=TN+1 THEN O2(N)=0
4640 GOSUB 2510:PRINT TN+1;")"TAB(10)"INTERMEDIATE STORAGE":PRINT:PRINT TAB(25);:INPUT"SELECT RESULTANT";I:IF I<1 OR I>TN+1 THEN GOTO 4640ELSE O3(N)=I:IF I=TN+1 THEN O3(N)=0
4650 I$="":PRINT U$:INPUT"IS RESULTANT TO BE TOTALIZED (Y/N)";I$:IF I$<>"Y"AND I$<>"N"THEN 4650
4651 IF I$="Y"THEN RX(N)=1ELSE RX(N)=0
4655 NEXT N
4660 RETURN
4700 PRINT U$:I=0:INPUT"ENTER DESIRED CONSTANT (DEFAULT=0)";I:GOSUB 820:IF I$="N"THEN 4700
4710 OC(N)=I:RETURN
4750 FOR N=1 TO OX:IF OT(N)=0 THEN ON OP(N)GOTO 4770,4780,4790,4800,4810,4811,4812,4813,4814,4815,4816,4817,4818
4760 IF OT(N)=1 THEN ON OP(N)GOTO 4825,4830,4840,4850,4860
4765 GOTO 4820
4770 D(O3(N))=D(O1(N))^D(O2(N)):GOTO 4820
4780 D(O3(N))=D(O1(N))+D(O2(N)):GOTO 4820
4790 D(O3(N))=D(O1(N))-D(O2(N)):GOTO 4820
4800 D(O3(N))=D(O1(N))*D(O2(N)):GOTO 4820
4810 IF D(O2(N))=0 THEN GOTO 4820ELSE D(O3(N))=D(O1(N))/D(O2(N))
4811 D(O3(N))=SIN(D(O1(N))):GOTO 4820
4812 D(O3(N))=COS(D(O1(N))):GOTO 4820
4813 D(O3(N))=TAN(D(O1(N))):GOTO 4820
4814 D(O3(N))=ATN(D(O1(N))):GOTO 4820
4815 D(O3(N))=D(O1(N))/.0174533:GOTO 4820
4816 D(O3(N))=EXP(D(O1(N))):GOTO 4820
4817 D(O3(N))=LOG(D(O1(N))):GOTO 4820
4818 D(O3(N))=SQR(D(O1(N))):GOTO 4820
4820 DD(N)=DD(N)+D(O3(N)):NEXT N:RETURN
4825 D(O3(N))=D(O1(N))^OC(N):GOTO 4820
4830 D(O3(N))=D(O1(N))+OC(N):GOTO 4870
4840 D(O3(N))=D(O1(N))-OC(N):GOTO 4870
4850 D(O3(N))=D(O1(N))*OC(N):GOTO 4870
4860 IF OC(N)=0 THEN GOTO 4870ELSE D(O3(N))=D(O1(N))/OC(N)
4870 GOTO 4820
4880 X=X
4920 GOSUB 2110:IF TN=0 THEN GOSUB 8800:RETURN
4921 GOSUB 2140:GOSUB 1520:GOSUB 4550:DD=0:IF PS=2 THEN GOSUB 4300
4930 GOSUB 1307:GOSUB 4750:GOSUB 1251:GOSUB 3925:IF EOF(1)=-1 THEN GOTO 4935ELSE GOTO 4930
4935 IF PS=2 THEN LPRINT CHR$(12):KQ=0
4940 CLOSE:PRINT U$:O$="TOTAL OF OPERATION #":FOR X=1 TO OX:IF RX(X)=1 THEN RY=1:PRINT O$;X;":";DD(X):IF PS=2L THEN PRINT " ":LPRINT O$;X;":";DD(X)
4941 NEXT X: IF PS=2 AND RY=1 THEN LPRINT CHR$(11);RY=0
4950 IF PS=1 AND RY=1 THEN GOSUB 910:RY=0
4960 GOSUB 3430:RETURN
5000 LP=0:LC=0:LR=0:FOR N=1 TO 10:LF(N)=0:FOR M=1 TO 10:LN(N,M)=0:LT(N,M)=0:LS(N,M)=0:NEXT:NEXT
5010 PRINT U$:INPUT"HOW MANY COLUMNS WIDE ARE THE LABELS";LC:IF LC<1 THEN 5010
5020 PRINT U$:INPUT"HOW MANY ROWS WILL BE PRINTED (1-10)";LR:IF LR<1 OR LR>10 THEN 5020
5025 FOR X=1 TO LR
5030 PRINT U$:PRINT"HOW MANY FIELDS WILL BE PRINTED IN ROW # ";X;" (1-10)";:INPUT LF(X):IF LF(X)<0 OR LF(X)>10 THEN 5030
5035 IF LF(X)=0 THEN  5150
5040 FOR Y=1 TO LF(X):I$=""
5050 PRINT"IS FIELD # ";Y;" OF ROW # ";X;" TEXT OR NUMERIC (T/N)";:INPUT I$:IF I$<>"T"AND I$<>"N"THEN PRINT"ERROR":GOTO 5050
5060 IF I$="T"THEN RT=1ELSE RT=2
5070 LT(X,Y)=RT
5080 GOSUB 2510::I=0:PRINT:PRINT TAB(25)"ENTER PRINT FIELD SELECTION # ";Y;:INPUT I:IF I<1 THEN 5080
5110 IF RT=1 AND I>TC THEN GOTO 5080ELSE IF RT=2 AND I>TN THEN 5080
5120 LN(X,Y)=I
5130 PRINT:PRINT"ENTER STARTING COLUMN POSITION FOR PRINT FIELD # ";Y;:INPUT LS(X,Y):IF LS(X,Y)>LC OR LS(X,Y)<0 THEN PRINT"ERROR":GOTO 5130
5135 IF Y>1 AND LS(X,Y)<LS(X,Y-1)THEN PRINT"ERROR":GOTO 5130
5140 NEXT
5150 NEXT
5160 PRINT U$:INPUT"HOW MANY ROWS SPACING BETWEEN LABELS";LP:IF LP<0 THEN 5160
5170 PRINT:PRINT:GOSUB 820:IF I$="N"THEN GOTO 5010
5180 PRINT U$:I$="":INPUT"SAVE LABEL CONFIGURATION TO DISK (Y/N)";I$:IF I$<>"Y"AND I$<>"N"THEN 5180ELSE IF I$="Y"THEN GOSUB 5240
5200 RETURN
5240 GOSUB 9900:FI$=PI$:GOSUB 1224:FF$=DE$+FI$+".LBL":GOSUB 1211:PRINT#2,LP,LC,LR:FOR X=1 TO LR:PRINT#2,LF(X):FOR Y=1 TO LF(X):PRINT#2,LT(X,Y),LN(X,Y),LS(X,Y):NEXT:NEXT:CLOSE 2:RETURN
5280 O=1:X=0:FOR M=1 TO LR:IF LF(M)=1 THEN 5320
5285 IF LF(M)=0 THEN LPRINT" ":KQ=KQ+1:GOTO 5349
5290 FOR O=1 TO LF(M):IF LT(M,O)=1 AND O<LF(M)THEN A$=LEFT$(D$(LN(M,O)),LS(M,O+1)-LS(M,O)-1)
5295 IF LT(M,O)=1 AND O=LF(M)THEN A$=LEFT$(D$(LN(M,O)),LC-LS(M,O))
5300 IF LT(M,O)=2 AND O<LF(M)THEN A$=LEFT$(STR$(D(LN(M,O))),LS(M,O+1)-LS(M,O)-1)ELSE IF LT(M,O)=2 THEN A$=LEFT$(STR$(D(LN(M,O))),LC-LS(M,O))
5310 LPRINT TAB(LS(M,O))A$;:NEXT:  GOTO 5349
5320 IF LT(M,O)=1 THEN A$=LEFT$(D$(LN(M,O)),LC-LS(M,O))
5330 IF LT(M,O)=2 THEN A$=LEFT$(STR$(D(LN(M,O))),LC-LS(M,O))
5340 LPRINT TAB(LS(M,O))A$:KQ=KQ+1
5349 NEXT M
5350 IF LP>0 THEN FOR X=1 TO LP:LPRINT" ":KQ=KQ+1:
5360 NEXT:RETURN
5400 GOSUB 1340:FF$=FI$+".LBL":GOSUB 1220:INPUT#2,LP,LC,LR:FOR X=1 TO LR:INPUT#2,LF(X):FOR Y=1 TO LF(X):INPUT#2,LT(X,Y),LN(X,Y),LS(X,Y):NEXT:NEXT:CLOSE 2:RETURN
5440 GOSUB 2110:GOSUB 2460:GOSUB 1224:CLOSE 1:F$=DE$+TA$+CO$
5450 T1=0:T2=0:FOR X=1 TO TC:K1(X)=0:PRINT U$:PRINT TC$(X):GOSUB 5660:IF I$="Y"THEN T1=T1+1:T1$(T1)=TC$(X):K1(X)=1
5480 NEXT:IF TN=0 THEN 5520
5490 FOR X=1 TO TN:K2(X)=0:PRINT U$:PRINT TN$(X):GOSUB 5660:IF I$="Y"THEN T2=T2+1:T2$(T2)=TN$(X):K2(X)=1
5510 NEXT
5520 GOSUB 820:IF I$="N"THEN 5450
5530 FOR X=1 TO T1:TC$(X)=T1$(X):NEXT:TC=T1:IF TN=0 OR T2=0 THEN 5550
5540 FOR X=1 TO T2:TN$(X)=T2$(X):NEXT:TN=T2
5550 TN=T2:GOSUB 1230:GOSUB 1235:CLOSE 1:F$=FI$+CO$:GOSUB 1290:GOSUB 1300:CLOSE 1:F$=FI$+DA$:GOSUB 1290:FF$=DE$+TA$+DA$:GOSUB 1211
5560 GOSUB 1307:K=0:FOR X=1 TO TC:IF K1(X)=1 THEN K=K+1:D$(K)=D$(X)
5570 NEXT:IF TN=0 OR T2=0 THEN 5600
5580 K=0:FOR X=1 TO TN:IF K2(X)=1 THEN K=K+1:D(K)=D(X)
5590 NEXT
5600 J=TC:K=TN:TC=T1:TN=T2:GOSUB 1251:TC=J:TN=K:IF EOF(1)=-1 THEN 5620
5610 GOTO 5560
5620 CLOSE:RETURN
5660 I$="":INPUT"COPY THIS FIELD (Y/N)";I$:IF I$<>"Y"AND I$<>"N"THEN 5660
5670 RETURN
5710 GOSUB 2110:CLOSE 1:
5720 PRINT U$:PRINT TAB(25)"RECONFIGURATION SELECTIONS":PRINT:PRINT TAB(20)"1)"TAB(25)"MODIFY FIELD NAMES IN CONFIGURATION":PRINT TAB(20)"2)"TAB(25)"ADD NEW FIELDS & COPY TO NEW FILE":PRINT:PRINT TAB(25);:GOSUB 935:IF I<1 OR I>2 THEN 5720
5730 ON I GOSUB 5740,5810:CLOSE:RETURN
5740 X$="NEW "+TE$:FOR N=1 TO TC:PRINT U$:PRINT"TEXT FIELD TITLE # ";N;" : ";TC$(N):PRINT:I$=TC$(N):GOSUB 890:TC$(N)=I$:NEXT
5750 IF TN=0 THEN GOTO 5770
5760 X$="NEW "+NU$:FOR N=1 TO TN:PRINT U$:PRINT"NUMERIC FIELD TITLE # ";N;" : ";TN$(N):PRINT:I$=TN$(N):GOSUB 890:TN$(N)=I$:NEXT
5770 GOSUB 820:IF I$="N"THEN 5740
5780 F$=FI$+CO$:GOSUB 1230:GOSUB 1235:RETURN
5810 PRINT U$:GOSUB 2460:GOSUB 1224:T1=0:T2=0:IF TN=10 AND TC=10 THEN PRINT U$:PRINT"NO FIELDS AVAILABLE":GOSUB 910:RETURN
5820 IF TC<10 THEN PRINT"HOW MANY NEW TEXT FIELDS WILL BE ADDED (0-";10-TC;")";:INPUT T1:IF T1<0 OR T1+TC>10 THEN 5820
5830 IF TN<10 THEN PRINT"HOW MANY NEW NUMERIC FIELDS WILL BE ADDED (0-";10-TN;")";:INPUT T2:IF T2+TN>10 OR T2<0 THEN 5830
5840 IF T1>0 THEN FOR N=TC+1 TO T1+TC:PRINT U$:X$=TE$:I$=TC$(N):GOSUB 890:TC$(N)=I$:NEXT
5850 IF T2>0 THEN FOR N=TN+1 TO TN+T2:PRINT U$:X$=NU$:I$=TN$(N):GOSUB 890:TN$(N)=I$:NEXT
5860 GOSUB 820:IF I$="N"THEN 5820
5870 J=TC:K=TN:TC=TC+T1:TN=TN+T2:F$=DE$+TA$+CO$:GOSUB 1230:GOSUB 1235:CLOSE:TN=K:TC=J::FF$=DE$+TA$+DA$:GOSUB 1211:F$=FI$+DA$:GOSUB 1290:IN=0
5890 GOSUB 1307:IN=IN+1:PRINT U$:PRINT"ENTRY # ";IN:GOSUB 2200
5900 IF T1>0 THEN FOR N=TC+1 TO TC+T1:X$=TC$(N):GOSUB 1120:NEXT
5910 IF T2>0 THEN FOR N=TN+1 TO TN+T2:X$=TN$(N):I=0:GOSUB 1150:D(N)=I:NEXT
5920 GOSUB 820:IF I$="N"THEN 5900
5930 J=TC:K=TN:TN=TN+T2:TC=TC+T1:GOSUB 1251:TC=J:TN=K:IF EOF(1)=-1 THEN RETURN
5950 GOTO 5890
6005 GOSUB 2110
6010 PRINT U$:I=0:PRINT TAB(25)"LABEL CONFIGURATION SOURCE":PRINT:PRINT TAB(20)"1)"TAB(25)"CUSTOM CONFIGURATION":PRINT TAB(20)"2)"TAB(25)"DISK FILE SOURCE":PRINT:PRINT TAB(25);:GOSUB 935:IF I<1 OR I>2 THEN 6010
6030 IF I=1 THEN GOSUB 5000ELSE GOSUB 5400
6040 GOSUB 1307:GOSUB 5280:IF EOF(1)=-1 THEN CLOSE:RETURN
6050 GOTO 6040
8800 PRINT U$:PRINT"NO NUMERIC FIELDS IN THIS DATA FILE!!!":CLOSE:GOSUB 910:RETURN
9000 GOSUB 2110:IF TN=0 THEN GOSUB 8800:RETURN
9010 RT=2:GOSUB 2510:SK=0:PRINT:PRINT TAB(25);:GOSUB 935:IF I<1 OR I>TN THEN 9010
9020 SK=I:GOSUB 4010:TS=0:BS=0:PRINT U$:EX=25:INPUT"ENTER VALUE FOR TOP OF SCALE";TS:PRINT:INPUT"ENTER VALUE FOR BOTTOM OF SCALE (DEFAULT = 0)";BS:IF TS<=BS THEN 9020
9030 GOSUB 820:IF I$="N"THEN 9020
9040 SC=TS-BS:PRINT U$:INPUT"READY PRINTER AND PRESS <RETURN>";I$:GOSUB 1900:X=0::PRINT U$:ID$="":LINE INPUT"ENTER DATE ";ID$:CC$="":LINE INPUT"ENTER NAME OF CHART ";CC$:IF PZ=1 THEN TX=22:SX=60:TL=25:KX=11
9190 IF PZ=2 THEN TX=40:SX=110:TL=40:KX=20
9200 LPRINT CC$:KQ=KQ+1:LPRINT"DATE :";ID$:KQ=KQ+1:LPRINT" ":KQ=KQ+1::EX=100:FOR N=1 TO KX:GOSUB 1307:DX(N)=D(SK):IF EOF(1)=-1 THEN EX=N:CLOSE:GOTO 9205
9203 NEXT
9205 M=0
9210 P=SC*((100-(2*M))/100)+BS:IF(M/5)-FIX(M/5)=0 THEN LPRINT USING"#########";P+0;
9220 LPRINT TAB(10)CHR$(124);:FOR N=1 TO KX:IF N>EX THEN LPRINT" ":KQ=KQ+1:GOTO 9260
9240 IF DX(N)=>P THEN LPRINT" *** ";ELSE LPRINT"     ";
9250 NEXT N:LPRINT" ":KQ=KQ+1:
9260 M=M+1:IF M<51 THEN 9210
9270 LPRINT"VALUE"TAB(10);CHR$(124);STRING$(SX,"-"):KQ=KQ+1::LPRINT" "TAB(10)CHR$(124);:FOR N=1 TO KX:IF N>EX THEN LPRINT" ":KQ=KQ+1:GOTO 9300
9290 LPRINT USING"####";N+X;:LPRINT CHR$(124);:NEXT
9300 LPRINT" ":KQ=KQ+1:LPRINT TAB(TL)"RECORD REFERENCE NUMBER":KQ=KQ+1::IF N>EX THEN PRINT U$:CLOSE:LPRINT CHR$(12):KQ=0:RETURN
9320 LPRINT CHR$(12):KQ=0:X=X+KX::PRINT U$:IF OP=0 THEN INPUT"PRESS <RETURN> FOR NEXT PAGE";I$
9335 GOTO 9200
9900 PI$="":PRINT U$:INPUT"ENTER DESTINATION FILE NAME (NO EXTENSION OR DRIVE LETTER)";PI$:RETURN
34999 X=X
35000 IF INSTR(3,TQ$,":")<3 THEN RETURN
35001 TQ$=MID$(TQ$,3,20):RETURN
O EX