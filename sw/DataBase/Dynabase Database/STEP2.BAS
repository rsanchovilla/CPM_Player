10 RESET
15 CLEAR 0
20 CLEAR FRE(0)-12000
30 ON ERROR GOTO 70
40 OPEN "I",1,"TEMPDATA"
50 DD=0
60 GOTO 120
70 RESUME 80
80 ON ERROR GOTO 0
90 CLOSE
100 OPEN"I",1,"B:TEMPDATA"
110 DD=1
120 ON ERROR GOTO 0
130 INPUT#1,FL$
140 INPUT#1,DR$
150 INPUT#1,N
160 INPUT#1,N1
170 INPUT#1,SIZE
180 INPUT#1,FO$
190 MAX=45
200 GOTO 230
210 REM
220 ON ERROR GOTO 830
230 DIM A$(MAX,N),A(MAX,N1),L$(10),L(10)
240 DIM P(SIZE)
250 FOR T=1 TO SIZE
260 INPUT#1,P(T)
270 NEXT
280 CLOSE 1
290 OPEN "I",1,FL$+".CON":OPEN "O",2,DR$+":"+FO$+".CON"
300 INPUT#1,N:INPUT#1,N1
310 PRINT#2,N:PRINT#2,N1
320 FOR T=1 TO N+N1
330 LINE INPUT#1,A$:PRINT#2,A$
340 NEXT T
350 CLOSE
360 OPEN "I",1,FL$+".DAT"
370 OPEN "O",2,DR$+":"+FO$+".DAT"
380 O=0
390 T=1
400 REM
410 IF T>SIZE THEN DNE=1:MAX=SIZE-O:GOTO 660
420  FOR I1=1 TO N
430  LINE INPUT#1,L$(I1)
440  NEXT I1
450  FOR I1=1 TO N1
460   INPUT#1,L(I1)
470  NEXT I1
480 REM
490 FLAG=0
500 IF O+MAX>SIZE THEN MM=SIZE ELSE MM=O+MAX
510 FOR S=O+1 TO MM
520  IF P(S)=T THEN FLAG=S:S=MM+1
530 NEXT S
540 IF FLAG=0 THEN T=T+1:GOTO 400
550 REM
560 REM
570  FOR I1=1 TO N
580   A$(FLAG-O,I1)=L$(I1)
590  NEXT I1
600  FOR I1=1 TO N1
610   A(FLAG-O,I1)=L(I1)
620  NEXT I1
630 REM
640 COUNTER=COUNTER+1
650 IF COUNTER<MAX THEN T=T+1:GOTO 400
660 REM
670  FOR T=1 TO MAX
680   FOR I1=1 TO N
690    PRINT#2,A$(T,I1)
700   NEXT I1
710   FOR I1=1 TO N1
720    PRINT#2,A(T,I1)
730   NEXT I1
740  NEXT T
750 O=O+MAX
760 COUNTER=0
770 CLOSE 1
780 IF DD =1 THEN A$="B:TEMPDATA" ELSE A$="TEMPDATA"
790 IF DNE THEN KILL A$
800 IF DNE THEN CLOSE:CLEAR 1:INPUT "PRESS RETURN WHEN PROGRAM DISK IS IN DEFAULT DRIVE ";A$:RUN "DYNABASE"
810 OPEN "I",1,FL$+".DAT"
820 GOTO 390
830 PRINT "ERROR ";ERR;" ON LINE ";ERL
840 PRINT "PRESS RETURN TO RUN DYNABASE";:INPUT A$
850 RUN "DYNABASE"

820 GOTO 390
830 PRINT "ERROR ";ERR;" ON LINE ";ERL
84