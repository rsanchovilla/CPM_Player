; 
; PL/I-80 INTERFACE ROUTINES FOR MICRO B+(tm). VERSION 4.2 3/18/81
;    (c) COPYRIGHT 1980,1981 FairCom
;
; THIS CODE IS WRITTEN FOR THE MICROSOFT MACRO ASSEMBLER. THERE ARE SOME MINOR
; DIFFERENCES BETWEEN SOME OF THE PSUEDO-OP NAMES USED HERE AND THE DIGITAL
; RESEARCH MACRO ASSEMBLER. PLEASE MAKE CHANGES AS APPROPRIATE.
;
;
; ASSUMPTIONS:
;
;	1) MICRO B+ STRING parameters are declared as PL/I-80 CHAR strings
;		WITH THE VARYING ATTRIBUTE;
;
;	2) The MICRO B+ parameter INDEX.KEY$ is initialized to the
;		appropriate length by the application program (see Section
;		4.6 of the Programmer's Guide); 
;
;	3) INDEX.KEY$ is passed by name (as opposed to value) so that it
;		may be "filled-in" by MICRO B+. Note that if its
;		maximum length is greater than the key length, MICRO B+
;		will simply "fill-in" the beginning bytes; but MICRO B+
;		will NOT reduce the length of INDEX.KEY$; and
;
;	4) The application program will be linked with these interface 
;	   routines before being linked to MICROB.REL.
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Please note that you may desire to remove portions of this interface
; which call routines not required by your application program. Otherwise,
; portions of MICROB.REL not actually used may be linked into your
; .COM file.
;
; ENTRY POINTS AND EXTERNAL DECLARATIONS
;
; THE FOLLOWING ARE THE NAMES OF THE ROUTINES SUPPLIED IN THE MICROB.REL
; LIBRARY.
;
	EXTRN	INTREE,ACCESS,ENTER,RTRIEV,SEARCH,SUCESR,NMENTR,REMOVE
	EXTRN	RSTRCT,INTLOD,LOADKY,BLDIND,ACCES1,ACCES2,NMNODE,PRDESR
	EXTRN	OPEND,CLOSED,NEWDAT,RETDAT,READD,WRITED,DATAFS,DATAFU
	EXTRN	SETERR,OPENR
;
;
; THE FOLLOWING ARE THE NAMES OF THE ROUTINES TO BE USED BY THE PL/I-80
; APPLICATION PROGRAMS.
;
	ENTRY	SETUP,OPFILE,ADDKEY,GETKEY,SERKEY,NXTKEY,NOKEYS,DELKEY
	ENTRY	CLFILE,INLOAD,SEQKEY,BUILDX,OPFIL1,OPFIL2,NMNODS,PRVKEY
	ENTRY	OPDATA,CLDATA,NEWREC,RETREC,READAT,WRTDAT,GETDFS,GETDFU
	ENTRY	ERRVAR,OPRDAT
;
; FOR EXAMPLE, INSTEAD OF
;
;		CALL ENTER(KEY%,KEY.VALUE$,DATA.RECORD%,RET.CODE%)
;
; AS GIVEN IN THE PROGRAMMER'S GUIDE, USE THE FOLLOWING
;
;		CALL ADDKEY(KEY,KEY_VALUE,DATA_RECORD,RET_CODE)
;
; WHERE ADDKEY HAS BEEN DECLARED AS FOLLOWS:
;
;		DCL ADDKEY ENTRY (FIXED,CHAR(MAX_KEY_SIZE) VAR,FIXED,FIXED);
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;
;		INTERFACE ROUTINES
;
; STORAGE SPACE FOR THREE-BYTE STRING DESCRIPTORS USED BY MICRO B+.
;
STRAD1:	DS	3	;1ST BYTE FOR LENGTH, 2ND & 3RD FOR ADDRESS
STRAD2:	DS	3	;OF STRING
IKPNTR:	DS	2	;LOCATION OF INDEX.KEY$ POINTER (FOR SERKEY)
;
; CONVERT PARAMETER ADDRESSES TO MICRO B+ FORMAT: 4 OR MORE PARAMETERS
;
; UPON ENTRY, HL=ADDRESS OF LIST OF PARAMETER ADDRESSES
; UPON EXIT,  HL=ADDRESS OF 1ST PARAMETER
;	      DE=ADDRESS OF  2ND PARAMETER
;	      BC=ADDRESS OF LIST OF REMAINING PARAMETER ADDRESSES
;
LDPAR4:	MOV	E,M
	INX	H
	MOV	D,M	;DE=ADDRESS OF 1ST PARAMETER
	PUSH	D	;SAVE ADDRESS OF 1ST PARAMETER
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M	;DE=ADDRESS OF 2ND PARAMETER
	INX	H	;HL=ADDRESS OF LIST OF REMAINING PARAMETER ADDRESSES
	MOV	B,H
	MOV	C,L	;BC=HL
	POP	H	;HL=ADDRESS OF 1ST PARAMETER
	RET
;
; CONVERT PARAMETER ADDRESSES TO MICRO B+ FORMAT: 3 PARAMETERS
;
; UPON ENTRY, HL=ADDRESS OF LIST OF 3 PARAMETER ADDRESSES
; UPON EXIT,  HL=ADDRESS OF 1ST PARAMETER
;	      DE=ADDRESS OF 2ND PARAMETER
;	      BC=ADDRESS OF 3RD PARAMETER
;
LDPAR3:	MOV	E,M
	INX	H
	MOV	D,M	;DE=ADDRESS OF 1ST PARAMETER
	PUSH	D	;SAVE ADDRESS
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M	;DE=ADDRESS OF 2ND PARAMETER
	INX	H
	MOV	C,M
	INX	H
	MOV	B,M	;BC=ADDRESS OF 3RD PARAMETER
	POP	H	;HL=ADDRESS OF 1ST PARAMETER
	RET
;
; CONVERT PARAMETER ADDRESSES TO MICRO B+ FORMAT: 2 PARAMETERS
;
; UPON ENTRY, HL=ADDRESS OF LIST OF PARAMETER ADDRESSES
; UPON EXIT,  HL=ADDRESS OF 1ST PARAMETER
;             DE=ADDRESS OF 2ND PARAMETER
;
LDPAR2:	MOV	E,M
	INX	H
	MOV	D,M	;DE=ADDRESS OF 1ST PARAMETER
	PUSH	D
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M	;DE=ADDRESS OF 2ND PARAMETER
	POP	H	;HL=ADDRESS OF 1ST PARAMETER
	RET
;
; CONVERT ADDRESS OF STRING PARAMETER (WHEN IT IS THE 2ND PARAMETER) TO THE
;	MICRO B+ THREE-BYTE VECTOR FORMAT
;
; UPON ENTRY, DE=ADDRESS OF 2ND PARAMETER
; UPON EXIT, DE=ADDRESS OF THREE-BYTE VECTOR WHICH NOW POINTS TO 2ND PARAMETER
;		AND HL & BC ARE UNCHANGED
;
SETAD1:	LDAX	D	;ACC=LENGTH OF STRING SINCE DE POINTS TO THE FIRST
			;BYTE OF A STRING PARAMETER DEFINED WITH THE VARYING
			;ATTRIBUTE
	STA	STRAD1	;SET 1ST BYTE OF THREE-BYTE STRING DESCRIPTOR VECTOR
	INX	D	;DE=ADDRESS OF 2ND BYTE OF STRING WHICH IS THE FIRST
			;CHARACTER
	MOV	A,E	;ACC=LSB OF STRING ADDRESS (LSB=LEAST SIGNIFICANT BYTE)
	STA	STRAD1+1
	MOV	A,D	;ACC=MSB OF STRING ADDRESS (MSB=MOST SIGNIFICANT BYTE)
	STA	STRAD1+2
			;TYHREE-BYTE VECTOR STRAD1 IS NOW SET
	LXI	D,STRAD1
			;DE NOW CONTAINS ADDRESS OF THREE-BYTE VECTOR
	RET
;
; B-TREE ROUTINES
;
SETUP:	CALL	LDPAR4
	CALL	INTREE
	RET
OPFILE:	CALL	PCCESS
	CALL	ACCESS
	RET
OPFIL1:	CALL	PCCESS
	CALL	ACCES1
	RET
OPFIL2:	CALL	PCCESS
	CALL	ACCES2
	RET
PCCESS:	CALL	LDPAR4
	CALL	SETAD1
	RET
ADDKEY:	CALL	LDPAR4
	CALL	SETAD1
	CALL	ENTER
	RET
GETKEY:	CALL	LDPAR3
	CALL	SETAD1
	CALL	RTRIEV
	RET
SERKEY:	CALL	LDPAR4
	CALL	SETAD1
;
; MUST STILL CONVERT ADDRESS OF INDEX.KEY$ TO THREE-BYTE VECTOR FORMAT.
;	NOTE THAT INDEX.KEY$'S ADDRESS IS NOW THE 2ND ADDRESS IN THE LIST OF
;	ADDRESSES POINTED TO BY BC
;
	PUSH	H	;SAVE ADDRESS OF 1ST PARAMETER
	INX	B
	INX	B	;BC=ADDRESS OF POINTER TO INDEX.KEY$
	PUSH	B	;SAVE POINTER
	LDAX	B	;ACC=LSB OF ADDRESS OF INDEX.KEY$
	MOV	L,A
	INX	B
	LDAX	B	;ACC=MSB OF ADDRESS OF INDEX.KEY$
	MOV	H,A	;HL=ADDRESS OF 1ST BYTE OF INDEX.KEY$ (WHICH CONTAINS
			;THE LENGTH OF THE STRING)
	MOV	A,M	;ACC=LENGTH OF INDEX.KEY$
	STA	STRAD2	;SET 1ST BYTE OF THREE-BYTE VECTOR
	INX	H	;HL=ADDRESS OF 2ND BYTE OF INDEX.KEY$ (WHICH CONTAINS
			;THE FIRST CHARACTER)
	SHLD	STRAD2+1
			;SET 2ND & 3RD BYTES OF THREE-BYTE VECTOR STRAD2
	LXI	H,STRAD2
			;HL=ADDRESS OF THREE-BYTE VECTOR STRAD2
	MOV	A,H	;ACC=MSB OF STRAD2 ADDRESS
	STAX	B
	DCX	B
	MOV	A,L	;ACC=LSB OF STRAD2 ADDRESS
	STAX	B	;PARAMETER LIST POINTED TO BY BC HAS NOW BEEN UPDATED
			;TO INCLUDE ADDRESS OF SETAD2 INSTEAD OF ADDRESS OF
			;INDEX.KEY$
	DCX	B
	DCX	B	;RESTORE BC TO ADDRESS OF BEGINNING OF LIST OF
			;ADDRESSES
	POP	H	;RECOVER INDEX.KEY$ POINTER
	SHLD	IKPNTR	;SAVE POINTER TO ENABLE RESTORATION OF PARAMETER LIST
	POP	H	;RESTORE HL
;
	CALL	SEARCH
;
; RESTORE PL/I PARAMETER LIST
;
	LHLD	STRAD2+1
	DCX	H	;HL=ADDRESS OF INDEX.KEY$
	XCHG
	LHLD	IKPNTR	;HL=ADDRESS OF INDEX.KEY$'S POSITION ON PARAMETER LIST
	MOV	M,E
	INX	H
	MOV	M,D	;PARAMETER LIST RESTORED
;
	RET
NXTKEY:	CALL	LDPAR3
;
; MUST CONVERT ADDRESS OF INDEX.KEY$ TO THREE-BYTE VECTOR FORMAT. CANNOT USE
;	SETAD1 SINCE INDEX.KEY$ IS NOT IN THE SECOND POSITION.
	CALL	CNVPR3
;
	CALL	SUCESR
	RET
PRVKEY:	CALL	LDPAR3
	CALL	CNVPR3
	CALL	PRDESR
	RET
;
; BC=ADDRESS OF STRING PARAMETER IN 3RD POSITION
;
CNVPR3:
	LDAX	B
	STA	STRAD1
	INX	B
	MOV	A,C
	STA	STRAD1+1
	MOV	A,B
	STA	STRAD1+2
	LXI	B,STRAD1
	RET
NOKEYS:	CALL	LDPAR2
	CALL	NMENTR
	RET
DELKEY:	CALL	LDPAR4
	CALL	SETAD1
	CALL	REMOVE
	RET
CLFILE:	MOV	E,M
	INX	H
	MOV	D,M
	XCHG		;HL=ADDRESS OF FIRST PARAMETER
	CALL	RSTRCT
	RET
INLOAD:	CALL	LDPAR2
	CALL	INTLOD
	RET
SEQKEY:	CALL	LDPAR3
;
; MUST STILL CONVERT ADDRESS OF KEY.VALUE$ TO THREE-BYTE VECTOR FORMAT. CANNOT
; USE SETAD1 SINCE KEY.VALUE$ IS NOT THE 2ND PARAMETER.
;
; HL=ADDRESS OF KEY.VALUE$
;
	MOV	A,M
	STA	STRAD1
	INX	H
	SHLD	STRAD1+1
	LXI	H,STRAD1
;
	CALL	LOADKY
	RET
BUILDX:	CALL	BLDIND
	RET
NMNODS:	CALL	LDPAR2
	CALL	NMNODE
	RET
OPDATA:	CALL	LDPAR3
	CALL	SETAD1
	CALL	OPEND
	RET
CLDATA:	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	CALL	CLOSED
	RET
NEWREC:	CALL	LDPAR2
	CALL	NEWDAT
	RET
RETREC:	CALL	LDPAR2
	CALL	RETDAT
	RET
READAT:	CALL	LDPAR3
	CALL	READD
	RET
WRTDAT:	CALL	LDPAR3
	CALL	WRITED
	RET
GETDFS:	CALL	LDPAR2
	CALL	DATAFS
	RET
GETDFU:	CALL	LDPAR2
	CALL	DATAFU
	RET
ERRVAR:	MOV	E,M
	INX	H
	MOV	D,M
	XCHG		;HL=ADDRESS OF FIRST PARAMETER
	CALL	SETERR
	RET
OPRDAT:	CALL	LDPAR3
	CALL	SETAD1
	CALL	OPENR
	RET
	END
