MEDIT ;ADD OR EDIT MAIN FILE. JEB 12/80
 WRITE ! DO ^MLOOK IF 'S GOTO KILL
 IF S>0 FOR I=0:1:ESIZ-1 SET A(I)=^(S+I) ;IF EDIT, GET ENTRY
 ELSE  SET A(0)=NAM FOR I=1:1:ESIZ-1 SET A(I)=""
 SET I=$PIECE(A(0),",",3),A(0)=$PIECE(A(0),",",2)_" "_$PIECE(A(0),",",1)
 IF I'="" SET A(0)=A(0)_","_I ;ADD TITLE
 SET TEXT="NAME^ADDR1^ADDR2^ADDR3^CITY^STATE^ZIP^TELE^FLAGS" ;PROMPT STRING
EDIT FOR I=0:1 SET QRY=$PIECE(TEXT,"^",I+1) QUIT:QRY=""  DO ELINE
REVIEW WRITE !!,A(0),! ;EXTERNAL NAME
 FOR I=1:1:3 WRITE:A(I)'="" A(I),! ;ADDRESS
 WRITE A(4),", ",A(5),"   ",A(6),! ;CITY, STATE   ZIP
 WRITE:A(7)'="" A(7),"  " WRITE "[",A(8),"]",! ;TELE [FLAGS]
VERIFY READ !,"F(ILE), E(DIT), OR I(GNORE)? ",X
 GOTO FILE:X="F",EDIT:X="E",MEDIT:X="I" WRITE " ?" GOTO VERIFY
 ;
KILL WRITE ! KILL A,J,N,S,QRY,NAM,TEXT QUIT
FILE IF S<0 SET S=-S,^(0)=^(0)+ESIZ FOR I=^(0):-1:S+ESIZ SET ^(I)=^(I-ESIZ) ;MAKE SPACE FOR INSERT
 ;PUT NAME IN INTERNAL FORMAT
 SET NAM=$PIECE(A(0),",",1) FOR I=1:1 QUIT:NAM'[" "  SET NAM=$PIECE(NAM," ",2,99)
 SET A(0)=NAM_","_$PIECE(A(0)," ",1,I-1)_","_$PIECE(A(0),",",2)
 FOR I=0:1:ESIZ-1 SET ^(S+I)=A(I) ;PUT DATA IN FILE
 GOTO MEDIT
 ;
 ;EDIT A PARTICULAR LINE
ELINE WRITE !,QRY," " WRITE:A(I)'="" "<",A(I)
 READ "> ",X QUIT:X=""
 IF $LENGTH(X)>LEN WRITE " [TRUNCATED]" SET X=$EXTRACT(X,1,LEN)
 GOTO UP:X="^",QUES:X="?" SET:X="-" X="" SET A(I)=X QUIT
 ;
UP SET I=I-2 SET:I>0 I=99 QUIT
QUES FOR J=0:1:4 WRITE !,$PIECE($TEXT(QT+J),";",2)
 GOTO ELINE
 ;
QT ;ENTER A NEW VALUE, OR
 ;    A NULL TO RETAIN OLD VALUE
 ;    A "-" TO DELETE CURRENT VALUE
 ;    AN "^" TO RETURN TO PREVIOUS QUERY
 ;
 ;ENTRY POINT FOR DELETE
DEL DO ^MLOOK IF 'S GOTO KILL
 GOTO DEL:S<0 ;NOTHING TO DELETE
 FOR I=S+ESIZ:1:^(0) SET ^(I-ESIZ)=^(I) ;DELETE THE ENTRY
 SET ^(0)=^(0)-ESIZ GOTO DEL
LL
 GOTO DEL:S<0 ;NOTHING TO DELETE
 FOR I=S+ESIZ:1:^(0) SET ^(I-ESIZ)=^(I) ;DELETE THE ENTRY
 SET ^(0)=^(