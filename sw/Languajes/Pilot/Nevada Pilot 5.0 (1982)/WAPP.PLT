R:   W.A.P.P. - WRITE A PILOT PROGRAM
R:   M. KAMP      UCSF     MARCH 1978
R:   J. STARKWEATHER, DISK VERSION, APRIL 1979
CH:
CA:2
T:T H I S   I S   T H E   W. A. P. P.   P R O G R A M . . . . .
CA:5
T:   W.
T:
T:   A.
T:
T:   P.
T:
T:   P.
PA:2
CA:2
CL:
T:W. A. P. P.   S T A N D S   F O R . . . . .
PA:1
CA:5,12
T:W R I T E
CA:7,12
T:A
CA:9,12
T:P I L O T
CA:11,12
T:P R O G R A M
CA:7,40
T:WRITTEN BY
CA:8,40
T:MARTY KAMP, 3/78
CA:9,40
T:& JOHN STARKWEATHER, 4/79
FOOT:
*INSTR
CH:
CA:6,5
TH:DO YOU WANT INSTRUCTIONS ?  ( Y OR N )....
AS:
M: N ,
JY:*GO
M: Y ,
JN:*INSTR
CH:
CA:2
T:   THIS PROGRAM WILL PRODUCE A PILOT PROGRAM CONTAINING
T:
T:MULTIPLE CHOICE QUESTIONS AND RESPONSES.  YOU WILL BE ASKED
T:
T:TO TYPE IN THE TEXT OF THE QUESTION ( 1-3 LINES ), 2 TO 5
T:
T:ALTERNATIVE ANSWER CHOICES,  AND ONE OR TWO LINES OF FEEDBACK
T:
T:FOR EACH OF THE CHOICES.     W.A.P.P. WILL DO ALL THE
T:
T:PROGRAMMING AND HOUSEKEEPING FOR YOU.
FOOT:
CH:
CA:2
T:   Before you tell W.A.P.P. to save a section of the program
T:
T:on disk, make sure you have a disk in unit B.  W.A.P.P. will
T:
T:not save what you have written until you have a chance to
T:
T:review your text.  You can make changes as you type in each
T:
T:line, but (in this version) once you have entered a line of
T:
T:text you must re-enter the whole question if you need to make
T:
T:changes.
FOOT:
CH:
CA:2
T:   After you type in the text of a question along with its
T:
T:answers and feedback, you can tell W.A.P.P to save it on disk.
T:
T:W.A.P.P. will generate the PILOT code and position everything
T:
T:appropriately on the screen.  Each question is saved as a
T:
T:separate file which will LOAD the next question.  You can run
T:
T:the program produced by W.A.P.P. by typing "LOAD WAP1" or
T:
T:"GET WAP1" followed by "RUN".  The program may be further
T:
T:edited and then saved like any other PILOT program.
FOOT:
*GO
R:Initialize frame count
C:I=1
CH:
CA:2
T:   IT IS POSSIBLE TO SET UP YOUR PROGRAM SO IT WILL COLLECT
T:
T:ALL RESPONSES AND WRITE THEM INTO A DISK FILE (DATA).
T:
T:IF YOU TYPE  'Y'  W.A.P.P. WILL BUILD THIS
T:
TH:CAPABILITY INTO YOUR PROGRAM, OTHERWISE HIT "RETURN"...
AS:
M: Y , YES ,
R:   IF K=1 PROGRAM WILL CONTAIN OPENF: AND WRITE: COMMANDS
CY:K=1
R:  L IS USED LATER TO OPENF: ONLY ONCE FOR DATA COLLECTION
C:L=1
CAY:10
TY:   OK, DATA WILL BE COLLECTED AND SAVED ON FILE "DATA".
FOOTY:
*BEGIN
CH:
R:   RESET TEXT VARIABLES
VNEW:$
R:   RESET DISK SAVE FLAG
C:U=0
T:YOU MAY ENTER UP TO THREE LINES OF TEXT.
CA:4
T:ENTER YOUR FIRST LINE OF TEXT.
CA:6
A:$T1
CA:4
CL:
C:M=2
T:ENTER YOUR SECOND LINE OF TEXT.
CA:7
A:$T2
C:M=3
C:F=0
C:G=0
R:   M WILL BE LINE WHERE CHOICES START
U:*ALPHA
CN:M=2
JN:*CHOICES
C:F=1
CA:4
CL:
T:ENTER YOUR THIRD LINE OF TEXT.
CA:8
A:$T3
C:M=4
U:*ALPHA
CN:M=3
CY:G=1
J:*CHOICES
*ALPHA
R:   MATCH ANY LETTER
M:E,T,O,I,N,S,H,A,U,R,Y,B,C,D,F,G,J,K,L,M,P,Q,V,W,X,Z,1,2,3,4,5,
MN:6,7,8,9,0,
E:
*CHOICES
CH:
C:Z=M-1
T(Z):$T1
C:Z=M-2
T(Z):$T2
C:Z=M-3
T(Z):$T3
CA:6
T:HERE IS HOW YOUR TEXT
T:WILL APPEAR ON THE SCREEN.....
*ENTERC
CA:9
CE:
C:P=0
T:HOW MANY CHOICES IN THIS QUESTION?
T:  ENTER A NUMBER, 2 - 5.
AS:#N
M:2,3,4,5,
JN:*ENTERC
M: 2 ,
CY:P=P+1
M: 3 ,
CY:P=P+1
M: 4 ,
CY:P=P+1
M: 5 ,
CY:P=P+1
J(P):*EC2
J:*ENTERC
*EC2
C:P=P-1
J(P):*ENTERC
R:   N = NUMBER OF CHOICES REQUESTED
R:   M = ROW WHERE CHOICE 1 WILL BE DISPLAYED
R:   O = CURRENT CHOICE NUMBER
R:   P = CURRENT ROW NUMBER
C:O=1
C:P=M
CA:M
CE:
*CLOOP
CA:10
CE:
T:ENTER CHOICE NUMBER #O.
CA:P
T:   (#O)
CA:P,8
R:   *CINPUT ACCEPTS AND STORES TEXT OF CHOICES
U:*CINPUT
R:   CHECK TO SEE IF N CHOICES HAVE BEEN ENTERED.
C:O=O+1
C:Q=O-N
J(Q):*GOOD
R:   MORE CHOICES NEEDED
C:P=P+1
J:*CLOOP
*CINPUT
R:   ACCEPTS TEXT FOR CHOICE #O  ( 1-5 )
C:Q=O-4
A(Q):$C5
E(Q):
C:Q=O-3
A(Q):$C4
E(Q):
C:Q=O-2
A(Q):$C3
E(Q):
C:Q=O-1
A(Q):$C2
E(Q):
A:$C1
E:
*GOOD
C:A=0
C:B=0
C:C=0
C:D=0
C:E=0
C:X=0
R:   SET J EQUAL TO NUMBER OF CHOICES
C:J=N
CA:9
CE:
CA:10
T:WHICH CHOICE IS CORRECT?  ( 1 TO #N )
CA:10,40
AS:
R:    Y IS THE NUMBER OF THE CORRECT CHOICE
M: 1 ,
CY:A=1
CY:Y=1
U(J):*CR
M: 2 ,
CY:B=1
CY:Y=2
U(J):*CR
M: 3 ,
CY:C=1
CY:Y=3
U(J):*CR
M: 4 ,
CY:D=1
CY:Y=4
U(J):*CR
M: 5 ,
CY:E=1
CY:Y=5
U(J):*CR
J(X):*CH1
C:X=2
*CH1
C:X=X-1
J(X):*GOOD
J:*FEEDBACK
*CR 
CY:X=X+1
C:J=J-1
E:  
*FEEDBACK
R:    COLLECT FEEDBACK TEXT
R:    Q IS CHOICE COUNTER
C:Q=1
R:    R = FIRST ROW AFTER CHOICES
C:R=M+N
R:    P = LAST ROW OF TEXT
C:P=M-1
R:    S = CHOICE LINE TO BE *** MARKED
C:S=0
*FLOOP
R:    REMOVE STARS
CA:S
T(S):   (
CA:R
CE:
CA:13
T:ENTER FIRST LINE OF FEEDBACK FOR CHOICE #Q
C:S=P+Q
R:    MARK CHOICE #Q, WHICH IS ON ROW #S
CA:S
T:***
R:    COLLECT FIRST LINE ON ROW #T
C:T=R+1
CA:T
U:*FINPUT1
CA:13
CL:
T:ENTER SECOND LINE OF FEEDBACK FOR CHOICE #Q
C:T=R+2
CA:T
U:*FINPUT2
R:    SEE IF WE HAVE DONE N CHOICES
C:Q=Q+1
C:X=Q-N
J(X):*LABEL5
J:*FLOOP
*FINPUT1
R:    COLLECTS FIRST LINE OF FEEDBACK FOR CHOICE #Q
C:W=Q-4
A(W):$FB5A
E(W):
C:W=Q-3
A(W):$FB4A
E(W):
C:W=Q-2
A(W):$FB3A
E(W):
C:W=Q-1
A(W):$FB2A
E(W):
A:$FB1A
E:  
*FINPUT2
R:    COLLECTS SECOND LINE OF FEEDBACK FOR CHOICE #Q
C:W=Q-4
A(W):$FB5B
E(W):
C:W=Q-3
A(W):$FB4B
E(W):
C:W=Q-2
A(W):$FB3B
E(W):
C:W=Q-1
A(W):$FB2B
E(W):
A:$FB1B
E:  
*LABEL5
CH:
T:PRESS Q TO DISPLAY THE QUESTION AND THE CHOICES.
CA:3
T:PRESS F TO DISPLAY THE FEEDBACK FOR THE CHOICES.
CA:5
T:PRESS D TO WRITE THIS SECTION OF PROGRAM ON THE DISK.
CA:7
T:PRESS A TO WRITE ANOTHER FRAME.
CA:9
T:PRESS E TO END THIS PROGRAM
CA:11
AS:
M: Q ,
JN:*LX1
U:*DISPLAYQ
J:*LABEL5
*LX1 M: F ,
JN:*LX2
U:*DISPLAYF
J:*LABEL5
*LX2 M: D ,
JN:*LX3
U:*DISK
J:*LABEL5
*LX3 M: E ,
JY:*END
M: A ,
JN:*LABEL5
R:IF DISK STORAGE HAS OCCURRED, THEN NEXT QUESTION
J(U):*BEGIN
CH:
CA:4
T:***   WARNING - THIS COMMAND ERASES YOUR STORED TEXT FROM
T:***   THE COMPUTER MEMORY...  MAKE SURE YOU HAVE STORED IT
T:***   ON THE DISK IF YOU WANT TO KEEP IT.
T:***
T:***   IF YOU WANT TO SAVE YOUR PROGRAM ON THE DISK, TYPE "D",
TH:***   OTHERWISE "RETURN" TO START ON A NEW QUESTION...
AS:
M: D
UY:*DISK
J:*BEGIN
*DISPLAYQ
CH:
CA:3
T:$T1
R:   DON'T DISPLAY $T2 OR $T3 IF NO TEXT THERE
T(F):$T2
T(G):$T3
C:Q=N
T(Q):   (1) $C1
C:Q=N-1
T(Q):   (2) $C2
C:Q=N-2
T(Q):   (3) $C3
C:Q=N-3
T(Q):   (4) $C4
C:Q=N-4
T(Q):   (5) $C5
FOOT:
E:
*DISPLAYF
CH:
CA:3
C:Q=N
T(Q):1. $FB1A
T(Q):   $FB1B
C:Q=N-1
T(Q):2. $FB2A
T(Q):   $FB2B
C:Q=N-2
T(Q):3. $FB3A
T(Q):   $FB3B
C:Q=N-3
T(Q):4. $FB4A
T(Q):   $FB4B
C:Q=N-4
T(Q):5. $FB5A
T(Q):   $FB5B
FOOT:
E:
*DISK
R:  OPEN FILE TO WRITE PILOT PROGRAM
CREATEF:WAP#I.PLT
OPENF:WAP#I.PLT,H
RW:H,R: PROGRAM PRODUCED BY W.A.P.P
R: PUT OPENF: ON DISK IF K=1 (DATA DESIRED)
U(L):*OPNF
J:*OPNF9
*OPNF
RW(K):H,R:  OPENF ONLY ON FIRST FRAME
RW(K):H,CREATEF:DATA
RW(K):H,OPENF:DATA,V
R: SET L=0 SO NO MORE OPENF.
C:L=0
E:
*OPNF9
RW(K):H,RW:V,QUESTION #I ( ANS. = #Y )
RW:H,*START
RW:H,CH:
RW:H,INMAX:1
R:   WRITE (M-1) LINES OF TEXT ON DISK
C:Q=M-1
RW(Q):H,T:$T1
C:Q=M-2
RW(Q):H,T:$T2
C:Q=M-3
RW(Q):H,T:$T3
R:   WRITE N CHOICES ON DISK
RW:H,T:   (1) $C1
C:Q=N-1
RW(Q):H,T:   (2) $C2
C:Q=N-2
RW(Q):H,T:   (3) $C3
C:Q=N-3
RW(Q):H,T:   (4) $C4
C:Q=N-4
RW(Q):H,T:   (5) $C5
RW:H,*ANS
R:   SKIP A LINE, PUT A-STATEMENT ON SCREEN
C:Q=M+N
C:Q=Q+1
R:   DON'T ACCEPT ANSWER ANY LOWER THAN ROW 9
C:W=Q-9
C(W):Q=9
RW:H,CA:#Q
RW:H,CL:
RW:H,A:
RW(K):H,WRITE:V
RW:H,CE:
C:Q=Q+2
R:   DON'T BEGIN FEEDBACK LOWER THAN ROW 10
C:W=Q-10
C(W):Q=10
RW:H,CA:#Q
RW:H,C:X=0
R:   CHECK FOR LEGAL ANSWER
RW:H,C:J=#N
RW:H,M(J): 1 ,
RW:H,U(J):*TD
RW:H,M(J): 2 ,
RW:H,U(J):*TD
RW:H,M(J): 3 ,
RW:H,U(J):*TD
RW:H,M(J): 4 ,
RW:H,U(J):*TD
RW:H,M(J): 5 ,
RW:H,U(J):*TD
RW:H,J(X):*TD1
RW:H,C:X=2
RW:H,*TD1
RW:H,C:X=X-1
RW:H,T(X):PLEASE TYPE A NUMBER, 1 TO #N.
RW:H,J(X):*ANS
RW:H,J:*OK
RW:H,*TD
RW:H,CY:X=X+1
RW:H,C:J=J-1
RW:H,E:
RW:H,*OK
R:   ANSWER IS LEGAL
R:   ABCDE REVEAL THE CORRECT ANSWER
R:   Y IS NUMBER OF CORRECT ANSWER
R:   N IS NUMBER OF CHOICES
C:W=M
RW:H,M: 1 ,
RW:H,JN:*L2
RW:H,CA:#W
RW:H,T:***
RW:H,PA:1
RW:H,CA:#Q
RW:H,T:$FB1A
RW:H,T:$FB1B
RW(A):H,J:*DONE
J(A):*LABEL7
RW:H,J:*ANS
*LABEL7
RW:H,*L2
C:W=M+1
RW:H,M: 2 ,
RW:H,JN:*L3
RW:H,CA:#W
RW:H,T:***
RW:H,PA:1
RW:H,CA:#Q
RW:H,T:$FB2A
RW:H,T:$FB2B
RW(B):H,J:*DONE
J(B):*LABEL8
RW:H,J:*ANS
*LABEL8
RW:H,*L3
R:   QUIT IF ONLY 2 ANSWERS ( N=2 )
C:X=3
C:X=X-N
J(X):*NOMORE
C:W=M+2
RW:H,M: 3 ,
RW:H,JN:*L4
RW:H,CA:#W
RW:H,T:***
RW:H,PA:1
RW:H,CA:#Q
RW:H,T:$FB3A
RW:H,T:$FB3B
RW(C):H,J:*DONE
J(C):*LABEL9
RW:H,J:*ANS
*LABEL9
RW:H,*L4
R:   QUIT IF ONLY THREE ANSWERS ( N=3 )
C:X=4
C:X=X-N
J(X):*NOMORE
C:W=M+3
RW:H,M: 4 ,
RW:H,JN:*L5
RW:H,CA:#W
RW:H,T:***
RW:H,PA:1
RW:H,CA:#Q
RW:H,T:$FB4A
RW:H,T:$FB4B
RW(D):H,J:*DONE
J(D):*LABEL10
RW:H,J:*ANS
*LABEL10
RW:H,*L5
R:   QUIT IF ONLY 4 ANSWERS  ( N=4 )
C:X=5
C:X=X-N
J(X):*NOMORE
C:W=M+4
RW:H,M: 5 ,
RW:H,JN:*DONE
RW:H,CA:#W
RW:H,T:***
RW:H,PA:1
RW:H,CA:#Q
RW:H,T:$FB5A
RW:H,T:$FB5B
RW(E):H,J:*DONE
J(E):*LABEL11
RW:H,J:*ANS
*LABEL11
*NOMORE
RW:H,*DONE
RW:H,CA:14
RW:H,CL:
RW:H,T:ANSWER  #Y  IS CORRECT
RW:H,*AGAIN
RW:H,CA:16
RW:H,CL:
RW:H,TH:DO YOU WANT TO REPEAT THIS QUESTION ?  ( Y OR N )...
RW:H,A:
RW:H,M: Y ,
RW:H,JY:*START
RW:H,M: N ,
RW:H,JN:*AGAIN
C:I=I+1
RW:H,LOAD:WAP#I.PLT
EOF:H
CLOSEF:H
C:U=1
   E:  END OF *DISK ROUTINE
*END
   R:END OF WAPP PROGRAM
J(U):*END2
T:*** IF YOU WISH TO SAVE THE LAST PROGRAM SECTION, TYPE "D"
T:*** OTHERWISE "RETURN" TO END WAPP OPERATION.
INMAX:1
A:
M: D
UY:*DISK
*END2
CREATEF:WAP#I.PLT
OPENF:WAP#I.PLT,H
RW(K):H,EOF:V
RW(K):H,CLOSEF:V
EOF:H
CLOSEF:H
E:
E:
                                                                                                                           