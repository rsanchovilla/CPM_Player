;File:  GRAPHICS.LIB  (c)	03/04/84	The Soft Warehouse


; This file contains the graphics functions defined in muLISP Lesson #6.

(PUTD 'DEFUN '(NLAMBDA (NAM$ EXP$)
  (PUTD NAM$ EXP$)
  NAM$ ))

(SETQ COLUMNS 80)	; Number of columns on your screen.

(SETQ ROWS 24)		; Number of rows on your screen.

; DOT is the character graphics primitive function.  It requires that
; (CURSOR row column) be properly defined for your computer and terminal.

(DEFUN DOT (LAMBDA (X-COORD Y-COORD
      % Local: % LINELENGTH )
    ((AND (LESSP (MINUS X-MAX) X-COORD X-MAX)
          (LESSP (MINUS Y-MAX) Y-COORD Y-MAX) )
      (CURSOR (DIFFERENCE Y-MAX Y-COORD) (PLUS X-MAX X-COORD))
      (PRIN1 DOT) ) ))

(SETQ X-MAX (QUOTIENT (PLUS COLUMNS 1) 2))

(SETQ Y-MAX (QUOTIENT (PLUS ROWS 1) 2))

(SETQ DOT '*)

(DEFUN GRAPHICS (LAMBDA ()
    (CLRSCRN) ))

(DEFUN TEXT (LAMBDA ()
    (CURSOR 0 0) ))

(DEFUN DRAW (NLAMBDA COMMANDS
    (GRAPHICS)
    (LOOP
      ((NULL COMMANDS))
      (EVAL (POP COMMANDS)) )
    (TEXT)
    "" ))

(DEFUN ABS (LAMBDA (N)
    ((MINUSP N)
      (MINUS N) )
    N ))

(DEFUN SIGN (LAMBDA (N)
    ((PLUSP N) 1)
    ((MINUSP N) -1)
    N ))

(DEFUN LINE (LAMBDA (X1 Y1 X2 Y2
      % Local: % DELTA-X DELTA-Y ABS-DELTA-X ABS-DELTA-Y
                 SIGN-DELTA-X SIGN-DELTA-Y)
    (SETQ DELTA-X (DIFFERENCE X2 X1))
    (SETQ DELTA-Y (DIFFERENCE Y2 Y1))
    (SETQ SIGN-DELTA-X (SIGN DELTA-X))
    (SETQ SIGN-DELTA-Y (SIGN DELTA-Y))
    (SETQ ABS-DELTA-X (ABS DELTA-X))
    (SETQ ABS-DELTA-Y (ABS DELTA-Y))
    ((GREATERP ABS-DELTA-X ABS-DELTA-Y)
      (STEEP-SLOPE) )
    (GRADUAL-SLOPE) ))

(DEFUN STEEP-SLOPE (LAMBDA (
    % Local: % ERR COUNT)
  (SETQ COUNT ABS-DELTA-X)
  (SETQ ABS-DELTA-Y (TIMES 2 ABS-DELTA-Y))
  (SETQ ERR (DIFFERENCE ABS-DELTA-Y ABS-DELTA-X))
  (SETQ ABS-DELTA-X (DIFFERENCE ABS-DELTA-Y (TIMES 2 ABS-DELTA-X)))
  (LOOP
    (DOT X1 Y1)
    ( ((PLUSP ERR)
        (SETQ Y1 (PLUS Y1 SIGN-DELTA-Y))
        (SETQ ERR (PLUS ERR ABS-DELTA-X)))
      (SETQ ERR (PLUS ERR ABS-DELTA-Y)))
    (SETQ COUNT (DIFFERENCE COUNT 1))
    ((MINUSP COUNT))
    (SETQ X1 (PLUS X1 SIGN-DELTA-X)) )))

(DEFUN GRADUAL-SLOPE (LAMBDA (
    % Local: % ERR COUNT )
  (SETQ COUNT ABS-DELTA-Y)
  (SETQ ABS-DELTA-X (TIMES 2 ABS-DELTA-X))
  (SETQ ERR (DIFFERENCE ABS-DELTA-X ABS-DELTA-Y))
  (SETQ ABS-DELTA-Y (DIFFERENCE ABS-DELTA-X (TIMES 2 ABS-DELTA-Y)))
  (LOOP
    (DOT X1 Y1)
    ( ((PLUSP ERR)
        (SETQ X1 (PLUS X1 SIGN-DELTA-X))
        (SETQ ERR (PLUS ERR ABS-DELTA-Y)))
      (SETQ ERR (PLUS ERR ABS-DELTA-X)))
    (SETQ COUNT (DIFFERENCE COUNT 1))
    ((MINUSP COUNT))
    (SETQ Y1 (PLUS Y1 SIGN-DELTA-Y)) )))

(DEFUN REDUCED-SIN (LAMBDA (DEG)
    (CONS (TIMES DEG (PLUS 1324959969 (TIMES (SETQ DEG (TIMES DEG DEG))
      (PLUS -67245 DEG)))) 75914915920) ))

(DEFUN REDUCED-COS (LAMBDA (DEG)
    (SETQ DEG (TIMES DEG DEG))
    (CONS (PLUS 266153374 (TIMES DEG (PLUS -40518 DEG))) 266153374) ))

(DEFUN SIN (LAMBDA (ANGLE)
    (SETQ ANGLE (DIVIDE (REMAINDER ANGLE 360) 45))
    (SIN-COS (CAR ANGLE) (CDR ANGLE)) ))

(DEFUN COS (LAMBDA (ANGLE)
    (SETQ ANGLE (DIVIDE (REMAINDER ANGLE 360) 45))
    (SIN-COS (PLUS 2 (CAR ANGLE)) (CDR ANGLE)) ))

(DEFUN SIN-COS (LAMBDA (N45DEG RESID)
    ((GREATERP N45DEG 3)
      (SETQ RESID (SIN-COS (DIFFERENCE N45DEG 4) RESID))
      (CONS (MINUS (CAR RESID)) (CDR RESID)))
    ((ZEROP N45DEG) (REDUCED-SIN RESID))
    ((EQ N45DEG 1) (REDUCED-COS (DIFFERENCE 45 RESID)))
    ((EQ N45DEG 2) (REDUCED-COS RESID))
    (REDUCED-SIN (DIFFERENCE 45 RESID)) ))

(DEFUN SETPOS (LAMBDA (X Y)
    (SETQ X-POS X)
    (SETQ Y-POS Y) ))

(DEFUN TURN (LAMBDA (ANGLE)
    (SETQ HEADING (REMAINDER (PLUS HEADING ANGLE) 360)) ))

(DEFUN SETHEADING (LAMBDA (ANGLE)
    (SETQ HEADING (REMAINDER ANGLE 360)) ))

(DEFUN PENDOWN (LAMBDA ()
   (SETQ PENDOWN T) ))

(DEFUN PENUP (LAMBDA ()
   (SETQ PENDOWN NIL) ))

(DEFUN TURTLE (NLAMBDA COMMANDS
    (SETPOS 0 0)
    (SETHEADING 0)
    (PENDOWN)
    (APPLY 'DRAW COMMANDS) ))

(DEFUN ROUND-TIMES (LAMBDA (INTEGER FRAC)
    (QUOTIENT (PLUS (TIMES INTEGER (CAR FRAC)) (QUOTIENT (CDR FRAC) 2))
              (CDR FRAC)) ))

(DEFUN FORWARD (LAMBDA (DISTANCE
      % Local: % X-OLD Y-OLD )
    (SETQ X-OLD X-POS)
    (SETQ Y-OLD Y-POS)
    (SETQ X-POS (PLUS X-POS (ROUND-TIMES DISTANCE (SIN HEADING))))
    (SETQ Y-POS (PLUS Y-POS (ROUND-TIMES DISTANCE (COS HEADING))))
    ((NOT PENDOWN))
    (LINE X-OLD Y-OLD X-POS Y-POS) ))

(DEFUN FORWARD-THEN-TURN (LAMBDA (DISTANCE ANGLE)
    (FORWARD DISTANCE)
    (TURN ANGLE) ))

(DEFUN POLY (LAMBDA (SIDE ANGLE
      % Local: % TOT-TURN)
    (SETQ TOT-TURN 0)
    (LOOP
      (FORWARD-THEN-TURN SIDE ANGLE)
      (SETQ TOT-TURN (REMAINDER (PLUS TOT-TURN ANGLE) 360))
      ((ZEROP TOT-TURN)) ) ))

(DEFUN CORN-POL (LAMBDA (SIDE ANGLE
      % Local: % TOT-TURN)
    ((GREATERP SIDE 1)
      (SETQ TOT-TURN 0)
      (LOOP
        (FORWARD SIDE)
        (CORN-POL (QUOTIENT SIDE 2) (MINUS ANGLE))
        (TURN ANGLE)
        (SETQ TOT-TURN (REMAINDER (PLUS TOT-TURN ANGLE) 360))
        ((ZEROP TOT-TURN)) ) ) ))

(DEFUN SPIRAL (LAMBDA (SIDE ANGLE INCR)
    (LOOP
      ((LESSP SIDE INCR))
      (FORWARD-THEN-TURN SIDE ANGLE)
      (SETQ SIDE (DIFFERENCE SIDE INCR)) ) ))

(DEFUN SPIROLAT (LAMBDA (SIDE ANGLE INCR
      % Local: % TOT-TURN)
    (SETQ TOT-TURN 0)
    (LOOP
       (SPIRAL SIDE ANGLE INCR)
       (SETQ TOT-TURN
         (REMAINDER (PLUS TOT-TURN (TIMES ANGLE (QUOTIENT SIDE INCR))) 360))
       ((ZEROP TOT-TURN)) ) ))

(DEFUN IBM-DOT (LAMBDA (X-COORD Y-COORD)
    ((AND (LESSP -161 X-COORD 160)
          (LESSP -101 Y-COORD 100) )
      (REGISTER 2 (PLUS 160 X-COORD))
      (REGISTER 3 (DIFFERENCE 100 Y-COORD))
      (REGISTER 0 COLOR)
      (INTERRUPT 16) ) ))

(DEFUN COLOR (LAMBDA (NEW-COLOR)
    (SETQ COLOR (PLUS 3071 (LENGTH (MEMBER NEW-COLOR
          '(WHITE BROWN MAGENTA RED CYAN GREEN BLUE BLACK))))) ))

(COLOR GREEN)

(DEFUN IBM-GRAPHICS (LAMBDA ()
    (REGISTER 0 5)
    (INTERRUPT 16) ))

(DEFUN EIGHTY-COLUMNS (LAMBDA ()
    (REGISTER 0 2)
    (INTERRUPT 16) ))

(DEFUN C-CURVE (LAMBDA (LENGTH)
  ((LESSP LENGTH MIN-LEN)
    (FORWARD LENGTH) )
  (SETQ LENGTH (ROUND-TIMES LENGTH SQRT-TWO))
  (TURN 45)
  (C-CURVE LENGTH)
  (TURN -90)
  (C-CURVE LENGTH)
  (TURN 45) ))

(SETQ MIN-LEN 4)
(SETQ SQRT-TWO '(169 . 239))

(RDS)
