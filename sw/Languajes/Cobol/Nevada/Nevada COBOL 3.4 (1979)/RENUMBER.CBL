0001 IDENTIFICATION DIVISION.
0002 PROGRAM-ID.
0003     RENUMBER.
0004 AUTHOR.
0005     ELLIS COMPUTING.
0006 INSTALLATION.
0007     SAN FRANCISCO PROGRAMMING CENTER.
0008 DATE-WRITTEN.
0009     JANUARY 11, 1979.
0010 DATE-COMPILED.
0011     MARCH 26, 1985.
0012 SECURITY.
0013     NONE.
0014* This program renumbers a NEVADA COBOL source program file with 4 position
0015* line numbers.
0016* To renumber ANSI programs with 6 position line numbers you must change
0017* this program and recompile.  First, make a copy of this program then
0018* make changes marked ==>                                            ANSI
0019 ENVIRONMENT DIVISION.
0020 CONFIGURATION SECTION.
0021 SOURCE-COMPUTER.
0022     8080-CPU.
0023 OBJECT-COMPUTER.
0024     8080-CPU.
0025 INPUT-OUTPUT SECTION.
0026 FILE-CONTROL.
0027     SELECT FILE-TO-RENUMBER ASSIGN TO DISK
0028       RECORD DELIMITER IS STANDARD.
0029     SELECT COPY-FILE ASSIGN TO DISK
0030       RECORD DELIMITER IS STANDARD.
0031 DATA DIVISION.
0032 FILE SECTION.
0033 FD FILE-TO-RENUMBER
0034     LABEL RECORDS ARE STANDARD
0035     VALUE OF FILE-ID IS RE-NUMBER-FILE-NAME
0036     DATA RECORD IS A-RECORD.
0037 01 A-RECORD.
0038*    02 SEQ-NUMBER PIC 9(6).                                         ANSI
0039     02 SEQ-NUMBER PIC 9999.
0040     02 CKCOPY PIC X(6).
0041     02 CKFILE.
0042     03 BYTE OCCURS 70 TIMES PIC X.
0043 01 A1-RECORD.
0044     02 FILLER PIC XXX.
0045     02 CK-SHORT PIC X(77).
0046 01 A2-RECORD.
0047     02 CK-TAB OCCURS 4 TIMES PIC X.
0048     02 FILLER PIC X(76).
0049 FD COPY-FILE
0050     LABEL RECORDS ARE STANDARD
0051     VALUE OF FILE-ID IS COPY-FILE-NAME
0052     DATA RECORD IS C-RECORD.
0053 01 C-RECORD.
0054*    02 COPY-SEQ-NUMBER PIC 9(6).                                    ANSI
0055     02 COPY-SEQ-NUMBER PIC 9999.
0056     02 FILLER PIC IS X(76).
0057 01 C1-RECORD.
0058     02 FILLER PIC XXX.
0059     02 C-CK-SHORT PIC X(77).
0060 01 C2-RECORD.
0061     02 C-CK-TAB OCCURS 4 TIMES PIC X.
0062     02 FILLER PIC X(76).
0063 WORKING-STORAGE SECTION.
0064*01 NEW-SEQ-NUMBER PIC 9(6) VALUE 000001.                            ANSI
0065 01 NEW-SEQ-NUMBER PIC 9999 VALUE 0001.
0066 01 RE-NUMBER-FILE-NAME PIC X(14)
0067         VALUE "A:FILENAME.TYP".
0068 01 COPY-FILE-NAME.
0069     02 H-LABEL.
0070     03 H-BYTE OCCURS 10 TIMES PIC X.
0071     02 FILLER PIC X(4)
0072         VALUE ".CBL".
0073 01 SUBSCRIPTS.
0074     02 X1 PIC 99
0075         VALUE 01 COMP.
0076     02 X2 PIC 99
0077         VALUE 01 COMP.
0078 PROCEDURE DIVISION.
0079 BEGIN.
0080     DISPLAY "RENUMBER REV. 3.0 (26 MAR 85)".
0081     DISPLAY "ENTER FILE NAME ".
0082     DISPLAY RE-NUMBER-FILE-NAME WITH NO ADVANCING.
0083     ACCEPT RE-NUMBER-FILE-NAME.
0084     OPEN I-O FILE-TO-RENUMBER.
0085 GET-NEXT-RECORD.
0086     MOVE SPACE TO A-RECORD.
0087     READ FILE-TO-RENUMBER
0088       AT END
0089       GO TO END-OF-JOB.
0090     IF CK-SHORT = SPACE
0091       DISPLAY "CANNOT RENUMBER BLANK LINES"
0092       GO TO SKIP-1.
0093     IF CK-TAB (1) = ""09"" OR
0094       CK-TAB (2) = ""09"" OR
0095       CK-TAB (3) = ""09"" OR
0096       CK-TAB (4) = ""09""
0097       DISPLAY "CANNOT RENUMBER TABS"
0098       GO TO SKIP-1.
0099     MOVE NEW-SEQ-NUMBER TO SEQ-NUMBER.
0100 SKIP-1.
0101     ADD 1 TO NEW-SEQ-NUMBER.
0102D    DISPLAY A-RECORD.
0103     REWRITE A-RECORD.
0104     IF CKCOPY = "  COPY "
0105            OR = "D COPY "
0106       PERFORM ALT-FILE THRU ALT-FILE-EXT.
0107     GO TO GET-NEXT-RECORD.
0108 ALT-FILE.
0109     MOVE 1 TO X1.
0110     MOVE 1 TO X2.
0111     MOVE SPACE TO H-LABEL.
0112 ALT1.
0113     IF BYTE (X1) = SPACE
0114       ADD 1 TO X1
0115       GO TO ALT1.
0116 NXT-BYTE.
0117     IF BYTE (X1) = "."
0118       GO TO OPN-2.
0119     MOVE BYTE (X1) TO H-BYTE (X2).
0120     ADD 1 TO X1.
0121     ADD 1 TO X2.
0122     IF X2 = 11
0123       GO TO OPN-2.
0124     GO TO NXT-BYTE.
0125 OPN-2.
0126     OPEN I-O COPY-FILE.
0127 NXT-C-RECORD.
0128     MOVE SPACE TO C-RECORD.
0129     READ COPY-FILE
0130       AT END
0131       GO TO END-COPY.
0132     IF C-CK-SHORT = SPACE
0133       DISPLAY "CANNOT RENUMBER BLANK LINE"
0134       GO TO SKIP-C-1.
0135     IF C-CK-TAB (1) = ""09"" OR
0136       C-CK-TAB (2) = ""09"" OR
0137       C-CK-TAB (3) = ""09"" OR
0138       C-CK-TAB (4) = ""09""
0139       DISPLAY "CANNOT RENUMBER TABS "
0140       GO TO SKIP-C-1.
0141     MOVE NEW-SEQ-NUMBER TO COPY-SEQ-NUMBER.
0142 SKIP-C-1.
0143     ADD 1 TO NEW-SEQ-NUMBER.
0144D    DISPLAY C-RECORD.
0145     REWRITE C-RECORD.
0146     GO TO NXT-C-RECORD.
0147 END-COPY.
0148     CLOSE COPY-FILE.
0149 ALT-FILE-EXT.
0150     EXIT.
0151 END-OF-JOB.
0152     CLOSE FILE-TO-RENUMBER.
0153     DISPLAY "RENUMBERING COMPLETE".
0154     STOP RUN.
0155 END PROGRAM RENUMBER.
