
100 REM *** TXT TO NORTHSTAR DATA FILE PROGRAM
110 REM ***   CONVERTS A STANDARD TEXT FILE TO
120 REM ***   A SET OF NORTH STAR BASIC STRINGS
130 REM ***   WHERE EACH STRING HAS THE CHARS
140 REM ***   OF ONE LINE, NOT TERMINATED BY
150 REM ***   ANY END OF LINE PROTOCOL.
160 REM *** (NEEDED BY PASCOM PROGRAM)
170 REM ***
180 REM *** OUTPUT FILE MUST EXIST, BE TYPE 3
190 REM *** AND BE AT LEAST AS LARGE AS THE
200 REM *** ORIGINAL TEXT FILE.
210 REM ***
220 DIM L$(120)
230 INPUT "NAME OF (TYPE 3) TEXT FILE? : ",F$
240 INPUT "NAME OF OUTPUT DATA FILE? : ", Z$
250 OPEN#0,F$
260 OPEN#1,Z$
270 L$=""
280 IF TYP(0)=0 THEN 360
290 READ#0,&C
300 IF C=10 THEN GOSUB 370
310 IF C=13 THEN 280
320 IF C=0 THEN END
330 IF C < 32 THEN GOTO 280
340 L$=L$+CHR$(C)
350 GOTO 280
360 END
370 REM SAVE LINE
380 IF LEN(L$)=0 THEN 420
390 WRITE#1, L$
400 PRINT L$
410 L$=""
420 RETURN
