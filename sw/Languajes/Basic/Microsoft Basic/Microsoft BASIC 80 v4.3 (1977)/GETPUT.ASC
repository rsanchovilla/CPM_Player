10 ' PROGRAM NAME "GETPUT"
20 ' PROGRAMMED BY:  BUD SHAMBURGER      JAN 1977
23 '                #27 RED OAK DR
25 '                CONWAY, ARK  72032     501-327-3641
30 '
40 ' A GENERAL PURPOSE UTILITY PROGRAM FOR DUMPING A RANDOM FILE
50 ' ON THE TERMINAL, EXAMINING ITS CONTENTS, AND ALTERING
60 ' IT BY TYPING IN THE LIMITS OF THE DESIRED FIELD
70 ' AND THEN ENTERING THE NEW DATA.  THE NEW RECORD IS THEN DUMP ON
80 ' THE TERMINAL FOR VISUAL INSPECTION.
90 ' AND ENTIRE DISK RECORD CAN BE CREATED USING THIS PROGRAM OR AS 
100 ' LITTLE AS ONE CHARACTER CAN BE ENTERED OR CHANGED.
110 ' EACH POSITION IN THE 128 CHARACTER RECORD IS IDENTIFIED JUST 
120 ' ABOVE ITS PRINT LOCATION.
130 '
140 '*******************************************************************
150 '*******************************************************************
160 '
170 CLEAR 500
180 BLK$="                         "
190 INPUT "ENTER FILE NAME";N$
200 INPUT "FILE NUMBER";F
210 INPUT "DRIVE SELECT";D$
220 N$=D$+":"+N$
240 R$="R"
250 OPEN R$,F,N$
260 FIELD F,128 AS A$
270 LET C=1
280 INPUT "ENTER -A- TO LOAD GLMENU";CC$
290 IF CC$="A" THEN LOAD "GLMENU.ASC",R
300 INPUT "ENTER ADDR";C
310 GET F,C
320 A1$="1...5....0....5....0....5....0....5....0"
330 A2$="         1    1    2    2    3    3    4"
340 A3$="....5....0....5....0....5....0....5....0
350 A4$="    4    5    5    6    6    7    7    8"
360 A5$="....5..8"
370 A6$="    8    9    9    0    0    1    1    2"
380 A7$="    2  2"
390 PRINT A2$;A4$
400 PRINT A1$;A3$
410 PRINT MID$(A$,1,80)
420 PRINT A6$;A7$
430 PRINT A3$;A5$
440 PRINT MID$(A$,81,128)
450 INPUT "ENTER FIELD LIMITS AS XXX-XXX";B1$
460 B2$=MID$(B1$,1,3):B2=VAL(B2$)
470 B3$=MID$(B1$,5,3):B3=VAL(B3$)
480 K=B3-B2+1
490 IF K>128 THEN PRINT "FIELD SIZE ERROR":GOTO 450
500 PRINT "ENTER NEW DATA"
510 INPUT B$
520 C$=A$
530 MID$(C$,B2,K)=B$
540 LSET A$=C$
550 PUT F,C
560 PRINT A2$;A4$
570 PRINT A1$;A3$
580 PRINT MID$(C$,1,80)
590 PRINT A6$;A7$
600 PRINT A3$;A5$
610 PRINT MID$(C$,81,128)
620 GOTO 270
630 END
