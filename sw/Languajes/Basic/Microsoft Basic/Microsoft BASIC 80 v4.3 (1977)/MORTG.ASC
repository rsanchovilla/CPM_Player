10 OUT3,1
20 REM BASIC MORTGAGE CALCULATOR PROGRAM
30 REM IN IBM BASIC REDONE FOR TDL DISK BASIC BY RICH PORTER
40 REM	INPUT THE DATA
50 ?CHR$(12):?:?:?:?'REM CLEAR THE SCREEN
60 ?"COST OF HOUSE ";:INPUTC
70 ?"YOUR DOWN PAYMENT  ";:INPUTD
80 REM
90 REM PRINCIPAL = COST - DOWN PAYMENT
100 REM
110 P=C-D
120 REM
130 ?"ANNUAL PRECENTAGE RATE  ";:INPUTI
140 REM
150 REM CONVERT PERCENT TO DECIMAL,I=I/100
160 REM MONTHLY PERCENT = I/12
170 REM
180 I=I/100
190 J=I/12
200 ?"MORTGAGE TERM  ";:INPUTT
210 REM
220 REM DATA IN PERFORM CALCULATIONS
230 REM
240 S=(1+J)^(T*12)
250 M=(P*J*S)/(S-1)
260 M=(INT(M*100))/100
270 REM OUTPUT RESULTS
280 REM
289 ?CHR$(12)
290 ?:?:?"  HOUSE COST    = ";C
300 ?"- DOWN PAYMENT  = ";D
310 ?"-------------------------"
320 ?"AMOUNT FINANCED = ";P
330 ?"INTEREST RATE   = ";I*100"%"
340 ?"MORTGAGE TERM   = ";T"YEARS"
350 ?"MONTHLY PAYMENTS= ";M
352 ?:?"   ***** P&I ONLY *****"
360 ?:?:?"AGAIN, TYPE 'YES' OR 'NO' ";:INPUTI$
370 IF I$="YES" THEN 10
372 IF I$="NO" THEN LOAD "GLMENU.ASC",R
375 OUT3,0:?CHR$(12)
