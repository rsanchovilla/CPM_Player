
10 CLEAR 2000
15 REM BY W.S. CHELLBERG, JUNE 1978, FOR INTERFACE AGE.
20 PRINT CHR$(26):REM CLEAR SCREEN
30 INPUT"WHAT IS THE PROJECT NAME";A$
40 PRINT CHR$(26)
50 PRINT TAB(20)"**  ";A$" FEASABILITY STUDY  **"
60 FOR I=1 TO 7:PRINT:NEXT I
70 INPUT "DATE ";D$
80 INPUT"COST OF LAND THAT BUILDING WILL OCCUPY (NO EXTRA VACANT LAND)";L
90 INPUT"ESTIMATED COST OF BUILDING ";C
100 INPUT"TOTAL SQUARE FEET OF BUILDING ";F
110 INPUT"NUMBER OF RENTAL UNITS ";R
120 TT=0:WT=0:OT=0:AO=0:AT=0
130 FOR I=1 TO R
140 PRINT:PRINT"UNIT # ";I;:INPUT"     SQ.FT. OF OFFICE =";O(I)
150 OT=OT+O(I):REM  OT=TOTAL OFFICE SQ.FT.
160 INPUT"          DOLLARS PER SQ.FT.=";D(I)
170 PRINT:INPUT"         SQ.FT. OF WAREHOUSE=";W(I)
180 WT=WT+W(I):REM  WT=TOTAL WAREHOUSE SQ.FT.
190 INPUT"          DOLLARS PER SQ.FT.=";E(I)
200 AO(I)=(O(I)*D(I))+(W(I)*E(I)):REM AO(I)=TENANT TOTAL ANNUAL CONTRIBUTION
210 AT=AO (I)+AT:REM AT=GROSS ANNUAL INCOME
220 NEXT I
230 TT=OT+WT:REM  TT=TOTAL SQ.FT.
240 IF TT<>F THEN 250 ELSE 260
250 PRINT:PRINT"***  WRONG SQUARE FEET - DO IT OVER ! ***":PRINT:PRINT:GOTO 120
260 PRINT:PRINT:PRINT TAB(20)"ENTER PROJECTED ANNUAL EXPENSES":PRINT
270 INPUT"                     VACANCY % ";V
280 INPUT"                  MANAGEMENT % ";M1
290 INPUT"       INSURANCE $ PER SQ.FT.  ";IN
300 INPUT"           TAXES $ PER SQ.FT.  ";T
310 INPUT"     MAINTENANCE $ PER SQ.FT.  ";M2
320 INPUT" EXPECTED INTEREST ON MORTGAGE (EXAMPLE: 9.25)";IR
330 INPUT" TERM OF MORTGAGE (IN MONTHS)";MO
335 REM     --  BEGIN COMPUTATION SECTION  --
337 REM COMPUTE MAX. FINANCING VIA INCOME APPROACH
340 V1=AT*(V/100):REM  V1=VACANCY IN $
350 M3=AT*(M1/100):REM  M3=MANAGEMENT $
360 IM=IN*F:REM   IM=INSURANCE $
370 TA=T*F:REM  TA=TOTAL REAL ESTATE TAXES
380 M4=M2*F:REM  M4=MAINTENANCE $
390 AS=AT-(M3+M4+TA+IM+V1):REM  AS=NET OPER. INCOME
400 NO=M3+M4+TA+IM+V1:REM  NO=TOTAL EXPENSES
410 TV=(AS/1.3)/.104:REM  TV=MAX. FINAN.VIA INCOME APPROACH
415 REM COMPUTE MAX. FINANCING VIA F.M.V APPROACH
420 IF C=0 THEN 400
430 S1=(L+C)*.75:REM  S1=MAX. FINAN. VIA F.M.V APPROACH
435 REM  COMPUTE ANNUAL CASH FLOW
440 IR=IR/1200
450 Q1=(1+IR)^MO
460 PM=TV*((IR*Q1)/(Q1-1)):REM  PM=MONTHLY PAYMENT
470 PY=PM*12:REM  PY=ANNUAL MORTGAGE REPAYMENT
480 CR=AS/C:CR=INT(CR*100):REM  CR=CAP. RATE
490 OP=OT/F:OP=INT(OP*100):REM  OP=% OF OFFICE
500 FOR I=1 TO R
510 F(I)=O(I)+W(I):REM  F(I)=TOTAL SQ.FT. PER TENANT
520 DS(I)=AO(I)/F(I):REM  DS(I)=TENANT AVG. RENTAL
530 NEXT I
540 P=AS-PY:REM  P=ANNUAL CASH FLOW
545 REM  BEGIN READ-OUT
610 PRINT CHR$(26)
620 PRINT TAB(20)"** ";A$" FEASABILITY STUDY **":PRINT
630 PRINT TAB(45)D$:PRINT
640 T$="  ##     #####    ##.##    #####   ##.##    ######          ##.##     $$######.##"
650 PRINT"UNIT     OFFICE     $    WAREHSE     $   TOTAL            AVG.  $      TOTAL"
660 PRINT"   NO.              SQ.FT.           ANNUAL"
670 PRINT"------------------------------------------------":PRINT
680 FOR I=1 TO R
690 PRINT USING T$;I,O(I),D(I),W(I),E(I),O(I)+W(I),DS(I),AO(I)
700 PRINT
710 NEXT I
720 PRINT"-------------------------------------------------"
730 S$="        ###### (## %)     ######         #######            $$######.##"
740 PRINT USING S$;OT,OP,WT,OT+WT,AT
750 INPUT"HIT RETURN FOR MORE";ZZ
760 R$="$$######.##"
770 PRINTTAB(10)"VACANCY     ";V"%            = ";:PRINTUSING R$;V1
780 PRINTTAB(10)"MANAGEMENT  ";M1"%            = ";:PRINTUSING R$;M3
790 PRINTTAB(10)"INSURANCE   $ ";IN"/SQ.FT.   = ";:PRINTUSING R$;IM
800 PRINTTAB(10)"TAXES       $ ";T"/SQ.FT.   = ";:PRINTUSING R$;TA
810 PRINTTAB(10)"MAINTENANCE $ ";M2"/SQ.FT.  = ";:PRINTUSING R$;M4
820 PRINT"                                           ----------"
830 PRINTTAB(10)"TOTAL PROJECTED EXPENSES      ";:PRINTUSING R$;NO
840 INPUT"HIT RETURN FOR MORE";ZZ
845 PRINT
850 PRINT"NET OPERATING INCOME                           ";:PRINTUSING R$;AS
860 PRINT:PRINTUSING"MORTGAGE REPAYMENT   (MONTHLY =#####.##)       $$######.##";PM,PY
870 PRINT"                                                ----------"
880 PRINTUSING"ANNUAL CASH FLOW                               $$######.##";P
890 PRINT:PRINT:PRINTUSING"CAP RATE ##%";CR
900 PRINT:PRINTUSING"MAXIMUM FINANCING BY INCOME APPROACH    $$######.##";TV
910 PRINT:PRINTUSING"          FAIR MARKET VALUE APPROACH    $$######.##";S1
915 PRINT
920 INPUT"WANT TO SEE IT AGAIN (Y OR N)";Z$
930 IF Z$="Y" THEN 940 ELSE 960
940 PRINT CHR$(26):GOTO 545
950 CLEAR 250:PRINTTAB(30)"THANK YOU  !":PRINT:PRINT:PRINT:PRINT
960 IF Z$="N" THEN LOAD "GLMENU.ASC",R
