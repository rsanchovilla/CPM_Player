10 ' PROGRAM NAME "COPRAN"
20 ' MITS BASIC VERSION 4.0
30 ' PROGRAMMED BY:  BUD SHAMBURGER     JAN 1977
35 '                #27 RED OAK DR
40 '                CONWAY ARK  72032     501-327-3641
50 ' A GENERAL PURPOSE UTILITY PROGRAM FOR COPYING RANDAM DATA FILES.
60 ' FILE NAMES, FILE NUMBERS AND DISK DRIVE NUMBERS ARE ENTERED FROM
70 ' THE TERMINAL.  FILE BOUNDARIES ARE ALSO ENTERED FROM THE TERMINAL.
80 ' BOTH FILES CAN RESIDE ON THE SAME DISK DRIVE PROVIDED THEIR NAMES
90 ' ARE DIFFERENT.
100 '
110 '*******************************************************************
120 '*******************************************************************
130 '
140 CLEAR 500
150 PRINT "COPY * BASIC-RANDOM-FILES *"
160 PRINT
170 LET R$="R"
180 LET S=A
190 LET Q=B
200 INPUT "ENTER -INPUT- FILE NAME";I$
210 INPUT "ENTER -OUTPUT- FILE NAME";O$
220 INPUT "ENTER -INPUT- DRIVE";X
230 INPUT "ENTER -OUTPUT- DRIVE";Y
240 INPUT "ENTER -INPUT- BEG REC#";T
250 INPUT "ENTER -INPUT- END REC#";U
260 INPUT "ENTER -OUTPUT- BEG REC#";V
270 INPUT "ENTER -OUTPUT- END REC#";W
280 INPUT "TO MOUNT THE FILES ENTER -Y-";XY$
290 IF XY$<>"Y" THEN 320
300 IF X=Y THEN UNLOAD X:MOUNT X:GOTO 320
310 UNLOAD X,Y:MOUNT X,Y
320 LET ZT=U-T
330 LET ZV=W-V
340 IF X<>A AND X<>B THEN 570
350 IF Y<>A AND Y<>B THEN 570
360 IF ZT<>ZV THEN 590
370 OPEN R$,S,I$,X
380 OPEN R$,Q,O$,Y
390 GOSUB 540
400 IF T>U THEN 610
410 GET #A,T
420 FIELD #1,128 AS A$
430 LET C$=A$
440 FIELD #2,128 AS B$
450 RSET B$=C$
460 PUT #B,V
470 IF CT=11 THEN GOSUB 540
480 PRINT USING "####  ";X;T
490 PRINT USING "####  ";Y;V
500 LET CT=CT+1
510 LET T=T+1
520 LET V=V+1
530 GOTO 400
540 LET CT=O
550 PRINT " DRIVE  REC#"
560 RETURN
570 PRINT "ERROR IN DRIVE"
580 GOTO 220
590 PRINT "INPUT AREA & OUTPUT AREA UNEQUAL"
600 GOTO 240
610 CLOSE S,Q
620 PRINT "END OF COPY"
630 LOAD "GLMENU.ASC",R
640 END

