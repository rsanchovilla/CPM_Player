10 'This loan analysis program is written in Microsoft BASIC.
20 'For some reason, their compiler doesn't handle it correctly.
30 'In particular P#(T) in line 1100 goes to 0.
40 'The good news is that the interpreter is faster.
50 'I apologize for the poor structure of this program. It
60 'was cannabalized from a much more complicated one.
70 FG$="\         \   $#####,###       $####,###       $####,###       $#####,###"
80 F$="$#####,###":G$="$####,###"
90 H$="      "
100 DEFDBL P:DEFDBL I
110 PRINT"		Loan Analysis Program"
120 PRINT"This program will solve for the principal,interest rate,"
130 PRINT"monthly payment,or number of periods to fully amortize a loan."
140 PRINT
150 PRINT"Enter zero for the variable to be calculated."
160 PRINT
170 PRINT"You may request an amortization schedule after the initial"
180 PRINT"analysis is completed."
190 PRINT
200 PRINT"This program uses the system printer for output-position"
210 PRINT"at top of form."
220 PRINT:PRINT"October 17,1980				Mike Dunn"
230 PRINT"					(213) 993-MIKE"
240 DIM P#(480)
250 GOSUB 290
260 INPUT"ONE LINE TITLE FOR THIS RUN";TL$
270 L=LEN(TL$):W=(80-L)/2:LPRINT TAB(W);TL$
280 GOTO 340
290 DIM MN$(12),NN$(12)
300 FOR J=1 TO 12:READ MN$(J):NN$(J)=LEFT$(MN$(J),3):NEXT
310 DATA JANUARY,FEBRUARY,MARCH,"APRIL","MAY","JUNE"
320 DATA"JULY","AUGUST","SEPTEMBER","OCTOBER","NOVEMBER","DECEMBER"
330 RETURN
340 INPUT"NUMBER OF MONTHS FOR LOAN REPAYMENT";N
350 LPRINT:LPRINT:LPRINT
360 IF N=0 THEN 380
370 LPRINT"THE NUMBER OF MONTHS FOR LOAN REPAYMENT IS ";N;"."
380 INPUT"NOMINAL ANNUAL INTEREST RATE.......%";I
390 IF I=0 THEN 410
400 LPRINT"THE NOMINAL ANNUAL INTEREST RATE IS ";I;"PERCENT."
410 IF I<1 THEN I=I/12  'FRACTION NOT PERCENT?
420 IF I>=1 THEN I=I/1200  'PERCENT? CONVERT TO MONTHLY RATE
430 INPUT"AMOUNT OF LOAN";P#(0)
440 IF P#(0)=0 THEN 470
450 LPRINT"THE AMOUNT BORROWED IS ";
460 LPRINT USING F$;P#(0)
470 INPUT"PAYMENT";PM
480 IF PM=0 THEN 510
490 LPRINT"THE MONTHLY PAYMENT IS ";
500 LPRINT USING G$;PM
510 REM Which one is missing?
520 IF PM<>0 THEN 580
530 REM Calc payment
540 PM=P#(0)*I/(1-(1+I)^-N):PRINT"THE FULLY AMORTIZING PAYMENT IS ";
550 LPRINT"THE COMPUTED PAYMENT IS ";
560 PRINT USING G$;PM
570 LPRINT USING G$;PM:GOTO 900
580 IF I<>0 THEN GOTO 770
590 REM Begin Neuton's method to find interest rate
600 I=.1/12  'Guess of interest rate
610 T=O
620 PV=-P#(0)+PM*(1-(1+I)^-N)/I  'CALC PRES VALUE
630 IF ABS(PV)<.1 THEN 700   'NEAR ZERO?
640 IF(ABS(PV+PX)<ABS(PV)+ABS(PX))AND ABS(I-IX)<.00001 THEN 700
650 PX=PV:IX=I'SAVE PV & I FOR NEXT TIME
660 D=PM*(-I^(-2)*(1-(1+I)^-N)+(N*(1+I)^(-N-1))/I) 'DERIVATIVE
670 T=T+1:I=I-PV/D' ITERATE TOWARD IRR
680 IF T>20 THEN 750
690 GOTO 620
700 LPRINT"THE COMPUTED NOMINAL INTEREST RATE IS:";
710 LPRINT USING"###.####";I*1200;:LPRINT" PERCENT"
720 PRINT"THE NOMINAL RATE IS ";
730 PRINT USING"###.####";I*1200
740 GOTO 900
750 PRINT"DOES NOT CONVERGE-CHECK FOR ERROR IN INPUT"
760 END
770 IF P#(0)<>0 THEN 830
780 P#(0)=PM/I*(1-(1+I)^-N):PRINT"THE PRESENT VALUE OF THE LOAN IS ";
790 PRINT USING F$;P#(0)
800 LPRINT"THE COMPUTED PRESENT VALUE OF THE LOAN IS ";
810 LPRINT USING F$;P#(0)
820 GOTO 900
830 IF N<>0 THEN 900
840 N=LOG(1-P#(0)*I*12/(12*PM))/LOG(1+I):N=INT(-N+.5)
850 XP=P#(0)
860 P#(0)=XP
870 PRINT"THE NUMBER OF MONTHS TO FULLY AMORTIZE THIS LOAN IS ";
880 PRINT N;"."
890 LPRINT"THE COMPUTED NUMBER OF MONTHS TO FULLY AMORTIZE THIS LOAN IS";N;"."
900 INPUT"AMORTIZATION SCHEDULE";A$
910 REM
920 IF LEFT$(A$,1)<>"Y"THEN 1250
930 INPUT"NUMBER OF MONTHS FOR PRINT-OUT";N
940 INPUT"WOULD YOU PREFER THAT ONLY CALENDAR YEAR TOTALS ARE PRINTED";A$
950 BT=0
960 INPUT"MONTH,YEAR FOR FIRST PAYMENT-ie.NOV,1980 ";M$,YR%
970 FOR J=1 TO 12:IF NN$(J)=LEFT$(M$,3)THEN BT=J
980 NEXT
990 IF BT=0 THEN 960
1000 LPRINT:LPRINT
1010 PM=INT(PM+.5)'ROUND TO $
1020 LPRINT"PERIOD          BEGINNING        INTEREST";
1030 LPRINT"        PRINCIPAL     ENDING"
1040 LPRINT"                PRINCIPAL          PAID ";
1050 LPRINT"           PAID        PRINCIPAL"
1060 LPRINT
1070 LPRINT SPC(14):LPRINT USING F$;P#(0)
1080 LPRINT YR%
1090 FOR T=1 TO N
1100 IP=I*P#(T-1):PR=PM-IP:P#(T)=P#(T-1)-PR
1110 IF LEFT$(A$,1)="Y" THEN 1130
1120 LPRINT USING FG$;MN$(BT),P#(T-1),IP,PR,P#(T)
1130 PT=PT+PR
1140 IT=IT+IP
1150 BT=BT+1
1160 IF BT<13 AND T<>N THEN 1240
1170 BT=1:LPRINT"YEARLY TOTALS";SPC(18);
1180 LPRINT USING G$;IT;
1190 LPRINT SPC(7);:LPRINT USING G$;PT;
1200 LPRINT SPC(7):LPRINT USING F$;P#(T)
1210 YR%=YR%+1:IT=0:PT=O
1220 IF T=N THEN 1250
1230 LPRINT:LPRINT YR%
1240 NEXT T
1250 INPUT"ANOTHER PROBLEM";A$
1260 IF LEFT$(A$,1)="Y"THEN 260
1270 IF LEFT$(A$,1)="N"THEN LOAD "GLMENU.ASC",R
