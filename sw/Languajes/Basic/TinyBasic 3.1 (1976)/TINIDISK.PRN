

                ;**************************************************************
                ;* 
                ;*                TINY BASIC FOR INTEL 8080
                ;*                      VERSION 1.0
                ;*                    BY LI-CHEN WANG
                ;*                     10 JUNE, 1976 
                ;*                       @COPYLEFT 
                ;*                  ALL WRONGS RESERVED
                ;* 
                ;**************************************************************
                ;* 
                ;*  ;*** ZERO PAGE SUBROUTINES ***
                ;* 
                ;*  THE 8080 INSTRUCTION SET LETS YOU HAVE 8 ROUTINES IN LOW 
                ;*  MEMORY THAT MAY BE CALLED BY RST N, N BEING 0 THROUGH 7. 
                ;*  THIS IS A ONE BYTE INSTRUCTION AND HAS THE SAME POWER AS 
                ;*  THE THREE BYTE INSTRUCTION CALL LLHH.  TINY BASIC WILL 
                ;*  USE RST 0 AS START AND RST 1 THROUGH RST 7 FOR 
                ;*  THE SEVEN MOST FREQUENTLY USED SUBROUTINES.
                ;*  TWO OTHER SUBROUTINES (CRLF AND TSTNUM) ARE ALSO IN THIS 
                ;*  SECTION.  THEY CAN BE REACHED ONLY BY 3-BYTE CALLS.
                ;*  IN ORDER TO CONFIGURE THE SYSTEM FOR USE WITH CPM I HAVE
                ;*  MOVED SOME OF THE ROUTINES AROUND.  START WILL NOW BE AT
                ;*  LOCATION 100H AND THIS SECTION WILL END AT LOCATION 3FH
                ;*  WITH A JUMP TO 108H.
                ;* 
                ;       ORG  8H
                ;       XTHL           ;*** TSTC OR RST 1 *** 
                ;       RST  5         ;IGNORE BLANKS AND 
                ;       CMP  M         ;TEST CHARACTER
                ;       JMP  TC1       ;REST OF THIS IS AT TC1
                ;* 
                ;CRLF   MVI  A,0DH     ;*** CRLF ***
                ;* 
                ;       PUSH PSW       ;*** OUTC OR RST 2 *** 
                ;       LDA  OCSW      ;PRINT CHARACTER ONLY
                ;       ORA  A         ;IFF OCSW SWITCH IS ON
                ;       JMP  OC2       ;REST OF THIS IS AT OC2
                ;* 
                ;       CALL EXPR2     ;*** EXPR OR RST 3 *** 
                ;       PUSH H         ;EVALUATE AN EXPRESION 
                ;       JMP  EXPR1     ;REST OF IT IS AT EXPR1
                ;       DB   'W' 
                ;* 
                ;       MOV  A,H       ;*** COMP OR RST 4 *** 
                ;       CMP  D         ;COMPARE HL WITH DE
                ;       RNZ            ;RETURN CORRECT C AND
                ;       MOV  A,L       ;Z FLAGS 
                ;       CMP  E         ;BUT OLD A IS LOST 
                ;       RET
                ;       DB   'AN'
                ;* 
                ;SS1    LDAX D         ;*** IGNBLK/RST 5 ***
                ;       CPI  40Q       ;IGNORE BLANKS 
                ;       RNZ            ;IN TEXT (WHERE DE->)
                ;       INX  D         ;AND RETURN THE FIRST
                ;       JMP  SS1       ;NON-BLANK CHAR. IN A
                ;* 
                ;       POP  PSW       ;*** FINISH/RST 6 ***
                ;       CALL FIN       ;CHECK END OF COMMAND
                ;       JMP  QWHAT     ;PRINT "WHAT?" IFF WRONG
                ;       DB   'G' 
                ;* 
                ;       RST  5         ;*** TSTV OR RST 7 *** 
                ;       SUI  100Q      ;TEST VARIABLES
                ;       RC             ;C:NOT A VARIABLE
                ;       JMP  TSTV1     ;JUMP AROUND RESERVED AREA
 0100                  ORG  100H      ;OF CPM.
 0100 C3A00A    START  JMP  NINIT      ;GO TO INITIALIZATION ROUTINE.	JIF
 0103 C21F01    TSTV1  JNZ  TV1       ;NOT "@" ARRAY 
 0106 13               INX  D         ;IT IS THE "@" ARRAY 
 0107 CDBC06           CALL PARN      ;@ SHOULD BE FOLLOWED
 010A 29               DAD  H         ;BY (EXPR) AS ITS INDEX
 010B DA6601           JC   QHOW      ;IS INDEX TOO BIG? 
 010E D5               PUSH D         ;WILL IT OVERWRITE 
 010F EB               XCHG           ;TEXT? 
 0110 CDFE06           CALL SIZE      ;FIND SIZE OF FREE 
 0113 E7               RST  4         ;AND CHECK THAT
 0114 DA0E08           JC   ASORRY    ;IFF SO, SAY "SORRY"
 0117 21000F    SS1A   LXI  H,VARBGN  ;IFF NOT, GET ADDRESS 
 011A CD9C07           CALL SUBDE     ;OF @(EXPR) AND PUT IT 
 011D D1               POP  D         ;IN HL 
 011E C9               RET            ;C FLAG IS CLEARED 
 011F FE1B      TV1    CPI  33Q       ;NOT @, IS IT A TO Z?
 0121 3F               CMC            ;IFF NOT RETURN C FLAG
 0122 D8               RC 
 0123 13               INX  D         ;IFF A THROUGH Z
 0124 21000F    TV1A   LXI  H,VARBGN  ;COMPUTE ADDRESS OF
 0127 07               RLC            ;THAT VARIABLE 
 0128 85               ADD  L         ;AND RETURN IT IN HL 
 0129 6F               MOV  L,A       ;WITH C FLAG CLEARED 
 012A 3E00             MVI  A,0 
 012C 8C               ADC  H 
 012D 67               MOV  H,A 
 012E C9               RET
                ;* 
                ;*                 TSTC   XCH  HL,(SP)   ;*** TSTC OR RST 1 *** 
                ;*                        IGNBLK         THIS IS AT LOC. 8 
                ;*                        CMP  M         AND THEN JMP HERE 
 012F 23        TC1    INX  H         ;COMPARE THE BYTE THAT 
 0130 CA3A01           JZ   TC2       ;FOLLOWS THE RST INST. 
 0133 C5               PUSH B         ;WITH THE TEXT (DE->)
 0134 4E               MOV  C,M       ;IFF NOT =, ADD THE 2ND 
 0135 0600             MVI  B,0       ;BYTE THAT FOLLOWS THE 
 0137 09               DAD  B         ;RST TO THE OLD PC 
 0138 C1               POP  B         ;I.E., DO A RELATIVE 
 0139 1B               DCX  D         ;JUMP IFF NOT = 
 013A 13        TC2    INX  D         ;IFF =, SKIP THOSE BYTES
 013B 23               INX  H         ;AND CONTINUE
 013C E3               XTHL 
 013D C9               RET
                ;* 
 013E 210000    TSTNUM LXI  H,0       ;*** TSTNUM ***
 0141 44               MOV  B,H       ;TEST IFF THE TEXT IS 
 0142 EF               RST  5         ;A NUMBER
 0143 FE30      TN1    CPI  60Q       ;IFF NOT, RETURN 0 IN 
 0145 D8               RC             ;B AND HL
 0146 FE3A             CPI  72Q       ;IFF NUMBERS, CONVERT 
 0148 D0               RNC            ;TO BINARY IN HL AND 
 0149 3EF0             MVI  A,360Q    ;SET A TO # OF DIGITS
 014B A4               ANA  H         ;IFF H>255, THERE IS NO 
 014C C26601           JNZ  QHOW      ;ROOM FOR NEXT DIGIT 
 014F 04               INR  B         ;B COUNTS # OF DIGITS
 0150 C5               PUSH B 
 0151 44               MOV  B,H       ;HL=10;*HL+(NEW DIGIT)
 0152 4D               MOV  C,L 
 0153 29               DAD  H         ;WHERE 10;* IS DONE BY
 0154 29               DAD  H         ;SHIFT AND ADD 
 0155 09               DAD  B 
 0156 29               DAD  H 
 0157 1A               LDAX D         ;AND (DIGIT) IS FROM 
 0158 13               INX  D         ;STRIPPING THE ASCII 
 0159 E60F             ANI  17Q       ;CODE
 015B 85               ADD  L 
 015C 6F               MOV  L,A 
 015D 3E00             MVI  A,0 
 015F 8C               ADC  H 
 0160 67               MOV  H,A 
 0161 C1               POP  B 
 0162 1A               LDAX D         ;DO THIS DIGIT AFTER 
 0163 F24301           JP   TN1       ;DIGIT. S SAYS OVERFLOW
 0166 D5        QHOW   PUSH D         ;*** ERROR: "HOW?" *** 
 0167 116D01    AHOW   LXI  D,HOW 
 016A C3E407           JMP  ERROR 
 016D 484F573F0DHOW    DB   'HOW?',0DH 
 0172 4F4B0D    OK     DB   'OK',0DH 
 0175 574841543FWHAT   DB   'WHAT?',0DH 
 017B 534F525259SORRY  DB   'SORRY',0DH 
                ;* 
                ;**************************************************************
                ;* 
                ;* *** MAIN ***
                ;* 
                ;* THIS IS THE MAIN LOOP THAT COLLECTS THE TINY BASIC PROGRAM
                ;* AND STORES IT IN THE MEMORY.
                ;* 
                ;* AT START, IT PRINTS OUT "(CR)OK(CR)", AND INITIALIZES THE 
                ;* STACK AND SOME OTHER INTERNAL VARIABLES.  THEN IT PROMPTS 
                ;* ">" AND READS A LINE.  IFF THE LINE STARTS WITH A NON-ZERO 
                ;* NUMBER, THIS NUMBER IS THE LINE NUMBER.  THE LINE NUMBER
                ;* (IN 16 BIT BINARY) AND THE REST OF THE LINE (INCLUDING CR)
                ;* IS STORED IN THE MEMORY.  IFF A LINE WITH THE SAME LINE
                ;* NUMBER IS ALREDY THERE, IT IS REPLACED BY THE NEW ONE.  IF
                ;* THE REST OF THE LINE CONSISTS OF A 0DHONLY, IT IS NOT STORED
                ;* AND ANY EXISTING LINE WITH THE SAME LINE NUMBER IS DELETED. 
                ;* 
                ;* AFTER A LINE ISs INSERTED, REPLACED, OR DELETED, THE PROGRAM 
                ;* LOOPS BACK AND ASK FOR ANOTHER LINE.  THIS LOOP WILL BE 
                ;* TERMINATED WHEN IT READS A LINE WITH ZERO OR NO LINE
                ;* NUMBER; AND CONTROL IS TRANSFERED TO "DIRCT".
                ;* 
                ;* TINY BASIC PROGRAM SAVE AREA STARTS AT THE MEMORY LOCATION
                ;* LABELED "TXTBGN" AND ENDED AT "TXTEND".  WE ALWAYS FILL THIS
                ;* AREA STARTING AT "TXTBGN", THE UNFILLED PORTION IS POINTED
                ;* BY THE CONTENT OF A MEMORY LOCATION LABELED "TXTUNF". 
                ;* 
                ;* THE MEMORY LOCATION "CURRNT" POINTS TO THE LINE NUMBER
                ;* THAT IS CURRENTLY BEING INTERPRETED.  WHILE WE ARE IN 
                ;* THIS LOOP OR WHILE WE ARE INTERPRETING A DIRECT COMMAND 
                ;* (SEE NEXT SECTION), "CURRNT" SHOULD POINT TO A 0. 
                ;* 
 0181 310020    RSTART LXI  SP,STACK  ;SET STACK POINTER
 0184 CD0E00    ST1    CALL CRLF      ;AND JUMP TO HERE
 0187 117201           LXI  D,OK      ;DE->STRING
 018A 97               SUB  A         ;A=0 
 018B CD7F08           CALL PRTSTG    ;PRINT STRING UNTIL 0DH
 018E 219501           LXI  H,ST2+1   ;LITERAL 0 
 0191 22C109           SHLD CURRNT    ;CURRNT->LINE # = 0
 0194 210000    ST2    LXI  H,0 
 0197 22C909           SHLD LOPVAR
 019A 22C309           SHLD STKGOS
 019D 3E3E      ST3    MVI  A,76Q     ;PROMPT '>' AND
 019F CD1408           CALL GETLN     ;READ A LINE 
 01A2 D5               PUSH D         ;DE->END OF LINE 
 01A3 11370F    ST3A   LXI  D,BUFFER  ;DE->BEGINNING OF LINE 
 01A6 CD3E01           CALL TSTNUM    ;TESt IFF IT IS A NUMBER
 01A9 EF               RST  5 
 01AA 7C               MOV  A,H       ;HL=VALUE OF THE # OR
 01AB B5               ORA  L         ;0 IFF NO # WAS FOUND 
 01AC C1               POP  B         ;BC->END OF LINE 
 01AD CAD502           JZ   DIRECT
 01B0 1B               DCX  D         ;BACKUP DE AND SAVE
 01B1 7C               MOV  A,H       ;VALUE OF LINE # THERE 
 01B2 12               STAX D 
 01B3 1B               DCX  D 
 01B4 7D               MOV  A,L 
 01B5 12               STAX D 
 01B6 C5               PUSH B         ;BC,DE->BEGIN, END 
 01B7 D5               PUSH D 
 01B8 79               MOV  A,C 
 01B9 93               SUB  E 
 01BA F5               PUSH PSW       ;A=# OF BYTES IN LINE
 01BB CD5708           CALL FNDLN     ;FIND THIS LINE IN SAVE
 01BE D5               PUSH D         ;AREA, DE->SAVE AREA 
 01BF C2D201           JNZ  ST4       ;NZ:NOT FOUND, INSERT
 01C2 D5               PUSH D         ;Z:FOUND, DELETE IT
 01C3 CD7308           CALL FNDNXT    ;FIND NEXT LINE
                ;*                                       DE->NEXT LINE 
 01C6 C1               POP  B         ;BC->LINE TO BE DELETED
 01C7 2AD509           LHLD TXTUNF    ;HL->UNFILLED SAVE AREA
 01CA CD0009           CALL MVUP      ;MOVE UP TO DELETE 
 01CD 60               MOV  H,B       ;TXTUNF->UNFILLED AREA 
 01CE 69               MOV  L,C 
 01CF 22D509           SHLD TXTUNF    ;UPDATE
 01D2 C1        ST4    POP  B         ;GET READY TO INSERT 
 01D3 2AD509           LHLD TXTUNF    ;BUT FIRT CHECK IF
 01D6 F1               POP  PSW       ;THE LENGTH OF NEW LINE
 01D7 E5               PUSH H         ;IS 3 (LINE # AND CR)
 01D8 FE03             CPI  3         ;THEN DO NOT INSERT
 01DA CA8101           JZ   RSTART    ;MUST CLEAR THE STACK
 01DD 85               ADD  L         ;COMPUTE NEW TXTUNF
 01DE 6F               MOV  L,A 
 01DF 3E00             MVI  A,0 
 01E1 8C               ADC  H 
 01E2 67               MOV  H,A       ;HL->NEW UNFILLED AREA 
 01E3 11000F    ST4A   LXI  D,TXTEND  ;CHECK TO SEE IF THERE 
 01E6 E7               RST  4         ;IS ENOUGH SPACE 
 01E7 D20D08           JNC  QSORRY    ;SORRY, NO ROOM FOR IT 
 01EA 22D509           SHLD TXTUNF    ;OK, UPDATE TXTUNF 
 01ED D1               POP  D         ;DE->OLD UNFILLED AREA 
 01EE CD0909           CALL MVDOWN
 01F1 D1               POP  D         ;DE->BEGIN, HL->END
 01F2 E1               POP  H 
 01F3 CD0009           CALL MVUP      ;MOVE NEW LINE TO SAVE 
 01F6 C39D01           JMP  ST3       ;AREA
                ;* 
                ;**************************************************************
                ;* 
                ;* *** TABLES *** DIRECT *** & EXEC ***
                ;* 
                ;* THIS SECTION OF THE CODE TESTS A STRING AGAINST A TABLE.
                ;* WHEN A MATCH IS FOUND, CONTROL IS TRANSFERED TO THE SECTION 
                ;* OF CODE ACCORDING TO THE TABLE. 
                ;* 
                ;* AT 'EXEC', DE SHOULD POINT TO THE STRING AD HL SHOULD POINT
                ;* TO THE TABLE-1.  AT 'DIRECT', DE SHOULD POINT TO THE STRING,
                ;* HL WILL BE SET UP TO POINT TO TAB1-1, WHICH IS THE TABLE OF 
                ;* ALL DIRECT AND STATEMENT COMMANDS.
                ;* 
                ;* A '.' IN THE STRING WILL TERMINATE THE TEST AND THE PARTIAL 
                ;* MATCH WILL BE CONSIDERED AS A MATCH.  E.G., 'P.', 'PR.',
                ;* 'PRI.', 'PRIN.', OR 'PRINT' WILL ALL MATCH 'PRINT'. 
                ;* 
                ;* THE TABLE CONSISTS OF ANY NUMBER OF ITEMS.  EACH ITEM 
                ;* IS A STRING OF CHARACTERS WITH BIT 7 SET TO 0 AND 
                ;* A JUMP ADDRESS STORED HI-LOW WITH BIT 7 OF THE HIGH 
                ;* BYTE SET TO 1.
                ;* 
                ;* END OF TABLE IS AN ITEM WITH A JUMP ADDRESS ONLY.  IFF THE 
                ;* STRING DOES NOT MATCH ANY OF THE OTHER ITEMS, IT WILL 
                ;* MATCH THIS NULL ITEM AS DEFAULT.
                ;* 
 01F9 =         TAB1   EQU  $         ;DIRECT COMMANDS 
 01F9 4C495354         DB   'LIST'
 01FD 8420             DB   LIST SHR 8 + 128,LIST AND 0FFH
 01FF 52554E           DB   'RUN'
 0202 8315             DB   RUN SHR 8 + 128,RUN AND 255
 0204 4E4557           DB   'NEW'
 0207 8306             DB   NEW SHR 8 + 128,NEW AND 255
 0209 4C4F4144         DB   'LOAD'
 020D 8343             DB   DLOAD SHR 8 + 128,DLOAD AND 255
 020F 53415645         DB   'SAVE'
 0213 838A             DB   DSAVE SHR 8 + 128,DSAVE AND 255
 0215 4259458000       DB   'BYE',80H,0H   ;GO BACK TO CPM
 021A =         TAB2   EQU  $         ;DIRECT/TATEMENT
 021A 4E455854         DB   'NEXT'
 021E 8508             DB   NEXT SHR 8 + 128,NEXT AND 255
 0220 4C4554           DB   'LET'
 0223 85C8             DB   LET SHR 8 + 128,LET AND 255
 0225 4F5554           DB   'OUT'
 0228 870B             DB   OUTCMD SHR 8 + 128,OUTCMD AND 255 
 022A 504F4B45         DB   'POKE'
 022E 8747             DB   POKE SHR 8 + 128,POKE AND 255
 0230 57414954         DB   'WAIT'
 0234 871F             DB   WAITCM SHR 8 + 128,WAITCM AND 255
 0236 4946             DB   'IF'
 0238 8559             DB   IFF SHR 8 + 128,IFF AND 255
 023A 474F544F         DB   'GOTO'
 023E 8334             DB   GOTO SHR 8 + 128,GOTO AND 255
 0240 474F535542       DB   'GOSUB'
 0245 8470             DB   GOSUB SHR 8 + 128,GOSUB AND 255
 0247 5245545552       DB   'RETURN'
 024D 8490             DB   RETURN SHR 8 + 128,RETURN AND 255
 024F 52454D           DB   'REM'
 0252 8555             DB   REM SHR 8 + 128,REM AND 255
 0254 464F52           DB   'FOR'
 0257 84A9             DB   FOR SHR 8 + 128,FOR AND 255
 0259 494E505554       DB   'INPUT'
 025E 8572             DB   INPUT SHR 8 + 128,INPUT AND 255
 0260 5052494E54       DB   'PRINT'
 0265 8438             DB   PRINT SHR 8 + 128,PRINT AND 255
 0267 53544F50         DB   'STOP'
 026B 830F             DB   STOP SHR 8 + 128,STOP AND 255
 026D 85C2             DB   DEFLT SHR 8 + 128,DEFLT AND 255
 026F 594F552043       DB   'YOU CAN ADD MORE' ;COMMANDS BUT
                            ;REMEMBER TO MOVE DEFAULT DOWN.
 027F =         TAB4   EQU  $         ;FUNCTIONS 
 027F 524E44           DB   'RND'
 0282 86C7             DB   RND SHR 8 + 128,RND AND 255
 0284 494E50           DB   'INP'
 0287 8738             DB   INP SHR 8 + 128,INP AND 255
 0289 5045454B         DB   'PEEK'
 028D 8757             DB   PEEK SHR 8 + 128,PEEK AND 255
 028F 555352           DB   'USR'
 0292 8761             DB   USR SHR 8 + 128,USR AND 255
 0294 414253           DB   'ABS'
 0297 86F2             DB   ABS SHR 8 + 128,ABS AND 255
 0299 53495A45         DB   'SIZE'
 029D 86FE             DB   SIZE SHR 8 + 128,SIZE AND 255
 029F 86AD             DB   XP40 SHR 8 + 128,XP40 AND 255
 02A1 594F552043       DB   'YOU CAN ADD MORE' ;FUNCTIONS BUT REMEMBER
                                      ;TO MOVE XP40 DOWN
 02B1 =         TAB5   EQU  $         ;"TO" IN "FOR" 
 02B1 544F             DB   'TO'
 02B3 84B9             DB   FR1 SHR 8 + 128,FR1 AND 255
 02B5 87E0             DB   QWHAT SHR 8 + 128,QWHAT AND 255
 02B7 =         TAB6   EQU  $         ;"STEP" IN "FOR" 
 02B7 53544550         DB   'STEP'
 02BB 84C3             DB   FR2 SHR 8 + 128,FR2 AND 255
 02BD 84C7             DB   FR3 SHR 8 + 128,FR3 AND 255
 02BF =         TAB8   EQU  $         ;RELATION OPERATORS
 02BF 3E3D             DB   '>='
 02C1 85D8             DB   XP11 SHR 8 + 128,XP11 AND 255
 02C3 23               DB   '#'
 02C4 85DE             DB   XP12 SHR 8 + 128,XP12 AND 255
 02C6 3E               DB   '>'
 02C7 85E4             DB   XP13 SHR 8 + 128,XP13 AND 255
 02C9 3D               DB   '='
 02CA 85F3             DB   XP15 SHR 8 + 128,XP15 AND 255
 02CC 3C3D             DB   '<='
 02CE 85EB             DB   XP14 SHR 8 + 128,XP14 AND 255
 02D0 3C               DB   '<'
 02D1 85F9             DB   XP16 SHR 8 + 128,XP16 AND 255
 02D3 85FF             DB   XP17 SHR 8 + 128,XP17 AND 255
                ;* 
 02D5 21F801    DIRECT LXI  H,TAB1-1  ;*** DIRECT ***
                ;* 
 02D8 =         EXEC   EQU  $         ;*** EXEC ***
 02D8 EF        EX0    RST  5         ;IGNORE LEADING BLANKS 
 02D9 D5               PUSH D         ;SAVE POINTER
 02DA 1A        EX1    LDAX D         ;IFF FOUND '.' IN STRING
 02DB 13               INX  D         ;BEFORE ANY MISMATCH 
 02DC FE2E             CPI  56Q       ;WE DECLARE A MATCH
 02DE CAF702           JZ   EX3 
 02E1 23               INX  H         ;HL->TABLE 
 02E2 BE               CMP  M         ;IFF MATCH, TEST NEXT 
 02E3 CADA02           JZ   EX1 
 02E6 3E7F             MVI  A,177Q    ;ELSE, SEE IFF BIT 7
 02E8 1B               DCX  D         ;OF TABLEIS SET, WHICH
 02E9 BE               CMP  M         ;IS THE JUMP ADDR. (HI)
 02EA DAFE02           JC   EX5       ;C:YES, MATCHED
 02ED 23        EX2    INX  H         ;NC:NO, FIND JUMP ADDR.
 02EE BE               CMP  M 
 02EF D2ED02           JNC  EX2 
 02F2 23               INX  H         ;BUMP TO NEXT TAB. ITEM
 02F3 D1               POP  D         ;RESTORE STRING POINTER
 02F4 C3D802           JMP  EX0       ;TEST AGAINST NEXT ITEM
 02F7 3E7F      EX3    MVI  A,177Q    ;PARTIAL MATCH, FIND 
 02F9 23        EX4    INX  H         ;JUMP ADDR., WHICH IS
 02FA BE               CMP  M         ;FLAGGED BY BIT 7
 02FB D2F902           JNC  EX4 
 02FE 7E        EX5    MOV  A,M       ;LOAD HL WITH THE JUMP 
 02FF 23               INX  H         ;ADDRESS FROM THE TABLE
 0300 6E               MOV  L,M 
 0301 E67F             ANI  177Q      ;MASK OFF BIT 7
 0303 67               MOV  H,A 
 0304 F1               POP  PSW       ;CLEAN UP THE GABAGE 
 0305 E9               PCHL           ;AND WE GO DO IT 
                ;* 
                ;**************************************************************
                ;* 
                ;* WHAT FOLLOWS IS THE CODE TO EXECUTE DIRECT AND STATEMENT
                ;* COMMANDS.  CONTROL IS TRANSFERED TO THESE POINTS VIA THE
                ;* COMMAND TABLE LOOKUP CODE OF 'DIRECT' AND 'EXEC' IN LAST
                ;* SECTION.  AFTER THE COMMAND IS EXECUTED, CONTROL IS 
                ;* TANSFERED TO OTHER SECTIONS AS FOLLOWS:
                ;* 
                ;* FOR 'LIST', 'NEW', AND 'STOP': GO BACK TO 'RSTART'
                ;* FOR 'RUN': GO EXECUTE THE FIRST STORED LINE IFF ANY; ELSE
                ;* GO BACK TO 'RSTART'.
                ;* FOR 'GOTO' AND 'GOSUB': GO EXECUTE THE TARGET LINE. 
                ;* FOR 'RETURN' AND 'NEXT': GO BACK TO SAVED RETURN LINE.
                ;* FOR ALL OTHERS: IFF 'CURRNT' -> 0, GO TO 'RSTART', ELSE
                ;* GO EXECUTE NEXT COMMAND.  (THIS IS DONE IN 'FINISH'.) 
                ;* 
                ;**************************************************************
                ;* 
                ;* *** NEW *** STOP *** RUN (& FRIENDS) *** & GOTO *** 
                ;* 
                ;* 'NEW(CR)' SETS 'TXTUNF' TO POINT TO 'TXTBGN'
                ;* 
                ;* 'STOP(CR)' GOES BACK TO 'RSTART'
                ;* 
                ;* 'RUN(CR)' FINDS THE FIRST STORED LINE, STORE ITS ADDRESS (IN
                ;* 'CURRNT'), AND START EXECUTE IT.  NOTE THAT ONLY THOSE
                ;* COMMANDS IN TAB2 ARE LEGAL FOR STORED PROGRAM.
                ;* 
                ;* THERE ARE 3 MORE ENTRIES IN 'RUN':
                ;* 'RUNNXL' FINDS NEXT LINE, STORES ITS ADDR. AND EXECUTES IT. 
                ;* 'RUNTSL' STORES THE ADDRESS OF THIS LINE AND EXECUTES IT. 
                ;* 'RUNSML' CONTINUES THE EXECUTION ON SAME LINE.
                ;* 
                ;* 'GOTO EXPR(CR)' EVALUATES THE EXPRESSION, FIND THE TARGET 
                ;* LINE, AND JUMP TO 'RUNTSL' TO DO IT.
                ;* 'DLOAD' LOADS A NAMED PROGRAM FROM DISK.
                ;* 'DSAVE' SAVES A NAMED PROGRAM ON DISK.
                ;* 'FCBSET' SETS UP THE FILE CONTROL BLOCK FOR SUBSEQUENT DISK I/O.
                ;* 
 0306 CDDC07    NEW    CALL ENDCHK    ;*** NEW(CR) *** 
 0309 21D709           LXI  H,TXTBGN
 030C 22D509           SHLD TXTUNF
                ;* 
 030F CDDC07    STOP   CALL ENDCHK    ;*** STOP(CR) ***
 0312 C38101           JMP RSTART
                ;* 
 0315 CDDC07    RUN    CALL ENDCHK    ;*** RUN(CR) *** 
 0318 11D709           LXI  D,TXTBGN  ;FIRST SAVED LINE
                ;* 
 031B 210000    RUNNXL LXI  H,0       ;*** RUNNXL ***
 031E CD5F08           CALL FNDLNP    ;FIND WHATEVER LINE #
 0321 DA8101           JC   RSTART    ;C:PASSED TXTUNF, QUIT 
                ;* 
 0324 EB        RUNTSL XCHG           ;*** RUNTSL ***
 0325 22C109           SHLD CURRNT    ;SET 'CURRNT'->LINE #
 0328 EB               XCHG 
 0329 13               INX  D         ;BUMP PASS LINE #
 032A 13               INX  D 
                ;* 
 032B CD8509    RUNSML CALL CHKIO     ;*** RUNSML ***
 032E 211902           LXI  H,TAB2-1  ;FIND COMMAND IN TAB2
 0331 C3D802           JMP  EXEC      ;AND EXECUTE IT
                ;* 
 0334 DF        GOTO   RST  3         ;*** GOTO EXPR *** 
 0335 D5               PUSH D         ;SAVE FOR ERROR ROUTINE
 0336 CDDC07           CALL ENDCHK    ;MUST FIND A 0DH
 0339 CD5708           CALL FNDLN     ;FIND THE TARGET LINE
 033C C26701           JNZ  AHOW      ;NO SUCH LINE #
 033F F1               POP  PSW       ;CLEAR THE "PUSH DE" 
 0340 C32403           JMP  RUNTSL    ;GO DO IT
 0005 =         CPM    EQU  5         ;DISK PARAMETERS
 005C =         FCB    EQU  5CH
 001A =         SETDMA EQU  26
 000F =         OPEN   EQU  15
 0014 =         READD  EQU  20
 0015 =         WRITED EQU  21
 0010 =         CLOSE  EQU  16
 0016 =         MAKE   EQU  22
 0013 =         DELETE EQU  19
                ;*
 0343 EF        DLOAD  RST  5         ;IGNORE BLANKS
 0344 E5               PUSH H         ;SAVE H
 0345 CDE503           CALL FCBSET    ;SET UP FILE CONTROL BLOCK
 0348 D5               PUSH D         ;SAVE THE REST
 0349 C5               PUSH B         
 034A 115C00           LXI  D,FCB     ;GET FCB ADDRESS
 034D 0E0F             MVI  C,OPEN    ;PREPARE TO OPEN FILE
 034F CD0500           CALL CPM       ;OPEN IT
 0352 FEFF             CPI  0FFH      ;IS IT THERE?
 0354 CA6601           JZ   QHOW      ;NO, SEND ERROR
 0357 AF               XRA  A         ;CLEAR A
 0358 327C00           STA  FCB+32    ;START AT RECORD 0
 035B 11D509           LXI  D,TXTUNF  ;GET BEGINNING
 035E D5        LOAD   PUSH D         ;SAVE DMA ADDRESS
 035F 0E1A             MVI  C,SETDMA  ;
 0361 CD0500           CALL CPM       ;SET DMA ADDRESS
 0364 0E14             MVI  C,READD   ;
 0366 115C00           LXI  D,FCB
 0369 CD0500           CALL CPM       ;READ SECTOR
 036C FE01             CPI  1         ;DONE?
 036E DA8103           JC   RDMORE    ;NO, READ MORE
 0371 C26601           JNZ  QHOW      ;BAD READ
 0374 0E10             MVI  C,CLOSE
 0376 115C00           LXI  D,FCB 
 0379 CD0500           CALL CPM       ;CLOSE FILE
 037C D1               POP  D         ;THROW AWAY DMA ADD.
 037D C1               POP  B         ;GET OLD REGISTERS BACK
 037E D1               POP  D
 037F E1               POP  H
 0380 F7               RST  6         ;FINISH
 0381 D1        RDMORE POP  D         ;GET DMA ADDRESS
 0382 218000           LXI  H,80H     ;GET 128
 0385 19               DAD  D         ;ADD 128 TO DMA ADD.
 0386 EB               XCHG           ;PUT IT BACK IN D
 0387 C35E03           JMP  LOAD      ;AND READ SOME MORE
                ;*
 038A EF        DSAVE  RST  5         ;IGNORE BLANKS
 038B E5               PUSH H         ;SAVE H
 038C CDE503           CALL FCBSET    ;SETUP FCB
 038F D5               PUSH D
 0390 C5               PUSH B         ;SAVE OTHERS
 0391 115C00           LXI  D,FCB
 0394 0E13             MVI  C,DELETE
 0396 CD0500           CALL CPM       ;ERASE FILE IF IT EXISTS
 0399 115C00           LXI  D,FCB  
 039C 0E16             MVI  C,MAKE
 039E CD0500           CALL CPM       ;MAKE A NEW ONE
 03A1 FEFF             CPI  0FFH      ;IS THERE SPACE?
 03A3 CA6601           JZ   QHOW      ;NO, ERROR
 03A6 AF               XRA  A         ;CLEAR A
 03A7 327C00           STA  FCB+32    ;START AT RECORD 0
 03AA 11D509           LXI  D,TXTUNF  ;GET BEGINNING
 03AD D5        SAVE   PUSH D         ;SAVE DMA ADDRESS
 03AE 0E1A             MVI  C,SETDMA  ;
 03B0 CD0500           CALL CPM       ;SET DMA ADDRESS
 03B3 0E15             MVI  C,WRITED
 03B5 115C00           LXI  D,FCB 
 03B8 CD0500           CALL CPM       ;WRITE SECTOR
 03BB B7               ORA  A         ;SET FLAGS
 03BC C26601           JNZ  QHOW      ;IF NOT ZERO, ERROR
 03BF D1               POP  D         ;GET DMA ADD. BACK
 03C0 3AD609           LDA  TXTUNF+1  ;AND MSB OF LAST ADD.
 03C3 BA               CMP  D         ;IS D SMALLER?
 03C4 DAD903           JC   SAVDON    ;YES, DONE
 03C7 C2D103           JNZ  WRITMOR   ;DONT TEST E IF NOT EQUAL
 03CA 3AD509           LDA  TXTUNF    ;IS E SMALLER?
 03CD BB               CMP  E
 03CE DAD903           JC   SAVDON    ;YES, DONE
 03D1 218000    WRITMOR LXI  H,80H 
 03D4 19               DAD  D         ;ADD 128 TO DMA ADD.
 03D5 EB               XCHG           ;GET IT BACK IN D
 03D6 C3AD03           JMP  SAVE      ;WRITE SOME MORE
 03D9 0E10      SAVDON MVI  C,CLOSE
 03DB 115C00           LXI  D,FCB 
 03DE CD0500           CALL CPM       ;CLOSE FILE
 03E1 C1               POP  B         ;GET REGISTERS BACK
 03E2 D1               POP  D
 03E3 E1               POP  H
 03E4 F7               RST  6         ;FINISH
                ;*
 03E5 215C00    FCBSET LXI  H,FCB     ;GET FILE CONTROL BLOCK ADDRESS
 03E8 3600             MVI  M,0       ;CLEAR ENTRY TYPE
 03EA 23        FNCLR  INX  H         ;NEXT LOCATION
 03EB 3620             MVI  M,' '     ;CLEAR TO SPACE
 03ED 3E64             MVI  A,FCB+8 AND 255
 03EF BD               CMP  L         ;DONE?
 03F0 C2EA03           JNZ  FNCLR     ;NO, DO IT AGAIN
 03F3 23               INX  H         ;NEXT
 03F4 3654             MVI  M,'T'     ;SET FILE TYPE TO 'TBI'
 03F6 23               INX  H
 03F7 3642             MVI  M,'B'
 03F9 23               INX  H
 03FA 3649             MVI  M,'I'
 03FC 23        EXRC   INX  H         ;CLEAR REST OF FCB
 03FD 3600             MVI  M,0
 03FF 3E6B             MVI  A,FCB+15 AND 255
 0401 BD               CMP  L         ;DONE?
 0402 C2FC03           JNZ  EXRC      ;NO, CONTINUE
 0405 215D00           LXI  H,FCB+1   ;GET FILENAME START
 0408 1A        FN     LDAX D         ;GET CHARACTER
 0409 FE0D             CPI  0DH       ;IS IT A 'CR'
 040B C8               RZ             ;YES, DONE
 040C FE21             CPI  '!'       ;LEGAL CHARACTER?
 040E DAE007           JC   QWHAT     ;NO, SEND ERROR
 0411 FE5B             CPI  '['       ;AGAIN
 0413 D2E007           JNC  QWHAT     ;DITTO
 0416 77               MOV  M,A        ;SAVE IT IN FCB
 0417 23               INX  H         ;NEXT
 0418 13               INX  D
 0419 3E65             MVI  A,FCB+9 AND 255
 041B BD               CMP  L         ;LAST?
 041C C20804           JNZ  FN        ;NO, CONTINUE
 041F C9               RET            ;TRUNCATE AT 8 CHARACTERS
                ;* 
                ;************************************************************* 
                ;* 
                ;* *** LIST *** & PRINT ***
                ;* 
                ;* LIST HAS TWO FORMS: 
                ;* 'LIST(CR)' LISTS ALL SAVED LINES
                ;* 'LIST #(CR)' START LIST AT THIS LINE #
                ;* YOU CAN STOP THE LISTING BY CONTROL C KEY 
                ;* 
                ;* PRINT COMMAND IS 'PRINT ....;' OR 'PRINT ....(CR)'
                ;* WHERE '....' IS A LIST OF EXPRESIONS, FORMATS, BACK-
                ;* ARROWS, AND STRINGS.  THESE ITEMS ARE SEPERATED BY COMMAS.
                ;* 
                ;* A FORMAT IS A POUND SIGN FOLLOWED BY A NUMBER.  IT CONTROLSs 
                ;* THE NUMBER OF SPACES THE VALUE OF A EXPRESION IS GOING TO 
                ;* BE PRINTED.  IT STAYS EFFECTIVE FOR THE REST OF THE PRINT 
                ;* COMMAND UNLESS CHANGED BY ANOTHER FORMAT.  IFF NO FORMAT IS
                ;* SPECIFIED, 6 POSITIONS WILL BE USED.
                ;* 
                ;* A STRING IS QUOTED IN A PAIR OF SINGLE QUOTES OR A PAIR OF
                ;* DOUBLE QUOTES.
                ;* 
                ;* A BACK-ARROW MEANS GENERATE A (CR) WITHOUT (LF) 
                ;* 
                ;* A (CRLF) IS GENERATED AFTER THE ENTIRE LIST HAS BEEN
                ;* PRINTED OR IFF THE LIST IS A NULL LIST.  HOWEVER IFF THE LIST 
                ;* ENDED WITH A COMMA, NO (CRL) IS GENERATED. 
                ;* 
 0420 CD3E01    LIST   CALL TSTNUM    ;TEST IFF THERE IS A #
 0423 CDDC07           CALL ENDCHK    ;IFF NO # WE GET A 0
 0426 CD5708           CALL FNDLN     ;FIND THIS OR NEXT LINE
 0429 DA8101    LS1    JC   RSTART    ;C:PASSED TXTUNF 
 042C CDED08           CALL PRTLN     ;PRINT THE LINE
 042F CD8509           CALL CHKIO     ;STOP IFF HIT CONTROL-C 
 0432 CD5F08           CALL FNDLNP    ;FIND NEXT LINE
 0435 C32904           JMP  LS1       ;AND LOOP BACK 
                ;* 
 0438 0E06      PRINT  MVI  C,6       ;C = # OF SPACES 
 043A CF               RST  1         ;IFF NULL LIST & ";"
 043B 3B               DB   73Q 
 043C 06               DB   6Q 
 043D CD0E00           CALL CRLF      ;GIVE CR-LF AND
 0440 C32B03           JMP  RUNSML    ;CONTINUE SAME LINE
 0443 CF        PR2    RST  1         ;IFF NULL LIST (CR) 
 0444 0D               DB   0DH
 0445 06               DB   6Q
 0446 CD0E00           CALL CRLF      ;ALSO GIVE CR-LF AND 
 0449 C31B03           JMP  RUNNXL    ;GO TO NEXT LINE 
 044C CF        PR0    RST  1         ;ELSE IS IT FORMAT?
 044D 23               DB   '#' 
 044E 05               DB   5Q
 044F DF               RST  3         ;YES, EVALUATE EXPR. 
 0450 4D               MOV  C,L       ;AND SAVE IT IN C
 0451 C35A04           JMP  PR3       ;LOOK FOR MORE TO PRINT
 0454 CD8B08    PR1    CALL QTSTG     ;OR IS IT A STRING?
 0457 C36704           JMP  PR8       ;IFF NOT, MUST BE EXPR. 
 045A CF        PR3    RST  1         ;IFF ",", GO FIND NEXT
 045B 2C               DB   ',' 
 045C 06               DB   6Q
 045D CDCD07           CALL FIN       ;IN THE LIST.
 0460 C34C04           JMP  PR0       ;LIST CONTINUES
 0463 CD0E00    PR6    CALL CRLF      ;LIST ENDS 
 0466 F7               RST  6 
 0467 DF        PR8    RST  3         ;EVALUATE THE EXPR 
 0468 C5               PUSH B 
 0469 CDB108           CALL PRTNUM    ;PRINT THE VALUE 
 046C C1               POP  B 
 046D C35A04           JMP  PR3       ;MORE TO PRINT?
                ;* 
                ;**************************************************************
                ;* 
                ;* *** GOSUB *** & RETURN ***
                ;* 
                ;* 'GOSUB EXPR;' OR 'GOSUB EXPR (CR)' IS LIKE THE 'GOTO' 
                ;* COMMAND, EXCEPT THAT THE CURRENT TEXT POINTER, STACK POINTER
                ;* ETC. ARE SAVE SO THAT EXECUTION CAN BE CONTINUED AFTER THE
                ;* SUBROUTINE 'RETURN'.  IN ORDER THAT 'GOSUB' CAN BE NESTED 
                ;* (AND EVEN RECURSIVE), THE SAVE AREA MUST BE STACKED.
                ;* THE STACK POINTER IS SAVED IN 'STKGOS'. THE OLD 'STKGOS' IS 
                ;* SAVED IN THE STACK.  IFF WE ARE IN THE MAIN ROUTINE, 'STKGOS'
                ;* IS ZERO (THIS WAS DONE BY THE "MAIN" SECTION OF THE CODE),
                ;* BUT WE STILL SAVE IT AS A FLAG FORr NO FURTHER 'RETURN'S.
                ;* 
                ;* 'RETURN(CR)' UNDOS EVERYHING THAT 'GOSUB' DID, AND THUS
                ;* RETURN THE EXCUTION TO THE COMMAND AFTER THE MOST RECENT
                ;* 'GOSUB'.  IFF 'STKGOS' IS ZERO, IT INDICATES THAT WE 
                ;* NEVER HAD A 'GOSUB' AND IS THUS AN ERROR. 
                ;* 
 0470 CD3409    GOSUB  CALL PUSHA     ;SAVE THE CURRENT "FOR"
 0473 DF               RST  3         ;PARAMETERS
 0474 D5               PUSH D         ;AND TEXT POINTER
 0475 CD5708           CALL FNDLN     ;FIND THE TARGET LINE
 0478 C26701           JNZ  AHOW      ;NOT THERE. SAY "HOW?" 
 047B 2AC109           LHLD CURRNT    ;FOUND IT, SAVE OLD
 047E E5               PUSH H         ;'CURRNT' OLD 'STKGOS' 
 047F 2AC309           LHLD STKGOS
 0482 E5               PUSH H 
 0483 210000           LXI  H,0       ;AND LOAD NEW ONES 
 0486 22C909           SHLD LOPVAR
 0489 39               DAD  SP
 048A 22C309           SHLD STKGOS
 048D C32403           JMP  RUNTSL    ;THEN RUN THAT LINE
 0490 CDDC07    RETURN CALL ENDCHK    ;THERE MUST BE A 0DH
 0493 2AC309           LHLD STKGOS    ;OLD STACK POINTER 
 0496 7C               MOV  A,H       ;0 MEANS NOT EXIST 
 0497 B5               ORA  L 
 0498 CAE007           JZ   QWHAT     ;SO, WE SAY: "WHAT?" 
 049B F9               SPHL           ;ELSE, RESTORE IT
 049C E1               POP  H 
 049D 22C309           SHLD STKGOS    ;AND THE OLD 'STKGOS'
 04A0 E1               POP  H 
 04A1 22C109           SHLD CURRNT    ;AND THE OLD 'CURRNT'
 04A4 D1               POP  D         ;OLD TEXT POINTER
 04A5 CD1809           CALL POPA      ;OLD "FOR" PARAMETERS
 04A8 F7               RST  6         ;AND WE ARE BACK HOME
                ;* 
                ;**************************************************************
                ;* 
                ;* *** FOR *** & NEXT ***
                ;* 
                ;* 'FOR' HAS TWO FORMS:
                ;* 'FOR VAR=EXP1 TO EXP2 STEP EXP1' AND 'FOR VAR=EXP1 TO EXP2' 
                ;* THE SECOND FORM MEANS THE SAME THING AS THE FIRST FORM WITH 
                ;* EXP1=1.  (I.E., WITH A STEP OF +1.) 
                ;* TBI WILL FIND THE VARIABLE VAR. AND SET ITS VALUE TO THE
                ;* CURRENT VALUE OF EXP1.  IT ALSO EVALUATES EXPR2 AND EXP1
                ;* AND SAVE ALL THESE TOGETHER WITH THE TEXT POINTERr ETC. IN 
                ;* THE 'FOR' SAVE AREA, WHICH CONSISTS OF 'LOPVAR', 'LOPINC',
                ;* 'LOPLMT', 'LOPLN', AND 'LOPPT'.  IFF THERE IS ALREADY SOME-
                ;* THING IN THE SAVE AREA (THIS IS INDICATED BY A NON-ZERO 
                ;* 'LOPVAR'), THEN THE OLD SAVE AREA IS SAVED IN THE STACK 
                ;* BEFORE THE NEW ONE OVERWRITES IT. 
                ;* TBI WILL THEN DIG IN THE STACK AND FIND OUT IFF THIS SAME
                ;* VARIABLE WAS USED IN ANOTHER CURRENTLY ACTIVE 'FOR' LOOP. 
                ;* IFF THAT IS THE CASE THEN THE OLD 'FOR' LOOP IS DEACTIVATED.
                ;* (PURGED FROM THE STACK..) 
                ;* 
                ;* 'NEXT VAR' SERVES AS THE LOGICAL (NOT NECESSARILLY PHYSICAL)
                ;* END OF THE 'FOR' LOOP.  THE CONTROL VARIABLE VAR. IS CHECKED
                ;* WITH THE 'LOPVAR'.  IFF THEY ARE NOT THE SAME, TBI DIGS IN 
                ;* THE STACK TO FIND THE RIGHTt ONE AND PURGES ALL THOSE THAT 
                ;* DID NOT MATCH.  EITHER WAY, TBI THEN ADDS THE 'STEP' TO 
                ;* THAT VARIABLE AND CHECK THE RESULT WITH THE LIMIT.  IFF IT 
                ;* IS WITHIN THE LIMIT, CONTROL LOOPS BACK TO THE COMMAND
                ;* FOLLOWING THE 'FOR'.  IFF OUTSIDE THE LIMIT, THE SAVE ARER 
                ;* IS PURGED AND EXECUTION CONTINUES.
                ;* 
 04A9 CD3409    FOR    CALL PUSHA     ;SAVE THE OLD SAVE AREA
 04AC CDBA07           CALL SETVAL    ;SET THE CONTROL VAR.
 04AF 2B               DCX  H         ;HL IS ITS ADDRESS 
 04B0 22C909           SHLD LOPVAR    ;SAVE THAT 
 04B3 21B002           LXI  H,TAB5-1  ;USE 'EXEC' TO LOOK
 04B6 C3D802           JMP  EXEC      ;FOR THE WORD 'TO' 
 04B9 DF        FR1    RST  3         ;EVALUATE THE LIMIT
 04BA 22CD09           SHLD LOPLMT    ;SAVE THAT 
 04BD 21B602           LXI  H,TAB6-1  ;USE 'EXEC' TO LOOK
 04C0 C3D802           JMP  EXEC      ;FOR THE WORD 'STEP'
 04C3 DF        FR2    RST  3         ;FOUND IT, GET STEP
 04C4 C3CA04           JMP  FR4 
 04C7 210100    FR3    LXI  H,1Q      ;NOT FOUND, SET TO 1 
 04CA 22CB09    FR4    SHLD LOPINC    ;SAVE THAT TOO 
 04CD 2AC109    FR5    LHLD CURRNT    ;SAVE CURRENT LINE # 
 04D0 22CF09           SHLD LOPLN 
 04D3 EB               XCHG           ;AND TEXT POINTER
 04D4 22D109           SHLD LOPPT 
 04D7 010A00           LXI  B,12Q     ;DIG INTO STACK TO 
 04DA 2AC909           LHLD LOPVAR    ;FIND 'LOPVAR' 
 04DD EB               XCHG 
 04DE 60               MOV  H,B 
 04DF 68               MOV  L,B       ;HL=0 NOW
 04E0 39               DAD  SP        ;HERE IS THE STACK 
 04E1 3E               DB   76Q 
 04E2 09        FR7    DAD  B         ;EACH LEVEL IS 10 DEEP 
 04E3 7E               MOV  A,M       ;GET THAT OLD 'LOPVAR' 
 04E4 23               INX  H 
 04E5 B6               ORA  M 
 04E6 CA0305           JZ   FR8       ;0 SAYS NO MORE IN IT
 04E9 7E               MOV  A,M 
 04EA 2B               DCX  H 
 04EB BA               CMP  D         ;SAME AS THIS ONE? 
 04EC C2E204           JNZ  FR7 
 04EF 7E               MOV  A,M       ;THE OTHER HALF? 
 04F0 BB               CMP  E 
 04F1 C2E204           JNZ  FR7 
 04F4 EB               XCHG           ;YES, FOUND ONE
 04F5 210000           LXI  H,0Q
 04F8 39               DAD  SP        ;TRY TO MOVE SP
 04F9 44               MOV  B,H 
 04FA 4D               MOV  C,L 
 04FB 210A00           LXI  H,12Q 
 04FE 19               DAD  D 
 04FF CD0909           CALL MVDOWN    ;AND PURGE 10 WORDS
 0502 F9               SPHL           ;IN THE STACK
 0503 2AD109    FR8    LHLD LOPPT     ;JOB DONE, RESTORE DE
 0506 EB               XCHG 
 0507 F7               RST  6         ;AND CONTINUE
                ;* 
 0508 FF        NEXT   RST  7         ;GET ADDRESS OF VAR. 
 0509 DAE007           JC   QWHAT     ;NO VARIABLE, "WHAT?"
 050C 22C509           SHLD VARNXT    ;YES, SAVE IT
 050F D5        NX0    PUSH D         ;SAVE TEXT POINTER 
 0510 EB               XCHG 
 0511 2AC909           LHLD LOPVAR    ;GET VAR. IN 'FOR' 
 0514 7C               MOV  A,H 
 0515 B5               ORA  L         ;0 SAYS NEVER HAD ONE
 0516 CAE107           JZ   AWHAT     ;SO WE ASK: "WHAT?"
 0519 E7               RST  4         ;ELSE WE CHECK THEM
 051A CA2705           JZ   NX3       ;OK, THEY AGREE
 051D D1               POP  D         ;NO, LET'S SEE 
 051E CD1809           CALL POPA      ;PURGE CURRENT LOOP
 0521 2AC509           LHLD VARNXT    ;AND POP ONE LEVEL 
 0524 C30F05           JMP  NX0       ;GO CHECK AGAIN
 0527 5E        NX3    MOV  E,M       ;COME HERE WHEN AGREED 
 0528 23               INX  H 
 0529 56               MOV  D,M       ;DE=VALUE OF VAR.
 052A 2ACB09           LHLD LOPINC
 052D E5               PUSH H 
 052E 19               DAD  D         ;ADD ONE STEP
 052F EB               XCHG 
 0530 2AC909           LHLD LOPVAR    ;PUT IT BACK 
 0533 73               MOV  M,E 
 0534 23               INX  H 
 0535 72               MOV  M,D 
 0536 2ACD09           LHLD LOPLMT    ;HL->LIMIT 
 0539 F1               POP  PSW       ;OLD HL
 053A B7               ORA  A 
 053B F23F05           JP   NX1       ;STEP > 0
 053E EB               XCHG 
 053F CDB207    NX1    CALL CKHLDE    ;COMPARE WITH LIMIT
 0542 D1               POP  D         ;RESTORE TEXT POINTER
 0543 DA5105           JC   NX2       ;OUTSIDE LIMIT 
 0546 2ACF09           LHLD LOPLN     ;WITHIN LIMIT, GO
 0549 22C109           SHLD CURRNT    ;BACK TO THE SAVED 
 054C 2AD109           LHLD LOPPT     ;'CURRNT' AND TEXT 
 054F EB               XCHG           ;POINTER 
 0550 F7               RST  6 
 0551 CD1809    NX2    CALL POPA      ;PURGE THIS LOOP 
 0554 F7               RST  6 
                ;* 
                ;**************************************************************
                ;* 
                ;* *** REM *** IFF *** INPUT *** & LET (& DEFLT) ***
                ;* 
                ;* 'REM' CAN BE FOLLOWED BY ANYTHING AND IS IGNORED BY TBI.
                ;* TBI TREATS IT LIKE AN 'IF' WITH A FALSE CONDITION.
                ;* 
                ;* 'IF' IS FOLLOWED BY AN EXPR. AS A CONDITION AND ONE OR MORE 
                ;* COMMANDS (INCLUDING OUTHER 'IF'S) SEPERATED BY SEMI-COLONS. 
                ;* NOTE THAT THE WORD 'THEN' IS NOT USED.  TBI EVALUATES THE 
                ;* EXPR. IFF IT IS NON-ZERO, EXECUTION CONTINUES.  IFF THE 
                ;* EXPR. IS ZERO, THE COMMANDS THAT FOLLOWS ARE IGNORED AND
                ;* EXECUTION CONTINUES AT THE NEXT LINE. 
                ;* 
                ;* 'IPUT' COMMAND IS LIKE THE 'PRINT' COMMAND, AND IS FOLLOWED
                ;* BY A LIST OF ITEMS.  IFF THE ITEM IS A STRING IN SINGLE OR 
                ;* DOUBLE QUOTES, OR IS A BACK-ARROW, IT HAS THE SAME EFFECT AS
                ;* IN 'PRINT'.  IFF AN ITEM IS A VARIABLE, THIS VARIABLE NAME IS
                ;* PRINTED OUT FOLLOWED BY A COLON.  THEN TBI WAITS FOR AN 
                ;* EXPR. TO BE TYPED IN.  THE VARIABLE ISs THEN SET TO THE
                ;* VALUE OF THIS EXPR.  IFF THE VARIABLE IS PROCEDED BY A STRING
                ;* (AGAIN IN SINGLE OR DOUBLE QUOTES), THE STRING WILL BE
                ;* PRINTED FOLLOWED BY A COLON.  TBI THEN WAITS FOR INPUT EXPR.
                ;* AND SET THE VARIABLE TO THE VALUE OF THE EXPR.
                ;* 
                ;* IFF THE INPUT EXPR. IS INVALID, TBI WILL PRINT "WHAT?",
                ;* "HOW?" OR "SORRY" AND REPRINT THE PROMPT AND REDO THE INPUT.
                ;* THE EXECUTION WILL NOT TERMINATE UNLESS YOU TYPE CONTROL-C. 
                ;* THIS IS HANDLED IN 'INPERR'.
                ;* 
                ;* 'LET' IS FOLLOWED BY A LIST OF ITEMS SEPERATED BY COMMAS. 
                ;* EACH ITEM CONSISTS OF A VARIABLE, AN EQUAL SIGN, AND AN EXPR. 
                ;* TBI EVALUATES THE EXPR. AND SET THE VARIBLE TO THAT VALUE.
                ;* TB WILL ALSO HANDLE 'LET' COMMAND WITHOUT THE WORD 'LET'.
                ;* THIS IS DONE BY 'DEFLT'.
                ;* 
 0555 210000    REM    LXI  H,0Q      ;*** REM *** 
 0558 3E               DB   76Q 
                ;* 
 0559 DF        IFF     RST  3         ;*** IFF ***
 055A 7C               MOV  A,H       ;IS THE EXPR.=0? 
 055B B5               ORA  L 
 055C C22B03           JNZ  RUNSML    ;NO, CONTINUE
 055F CD7508           CALL FNDSKP    ;YES, SKIP REST OF LINE
 0562 D22403           JNC  RUNTSL
 0565 C38101           JMP  RSTART
                ;* 
 0568 2AC709    INPERR LHLD STKINP    ;*** INPERR ***
 056B F9               SPHL           ;RESTORE OLD SP
 056C E1               POP  H         ;AND OLD 'CURRNT'
 056D 22C109           SHLD CURRNT
 0570 D1               POP  D         ;AND OLD TEXT POINTER
 0571 D1               POP  D         ;REDO INPUT
                ;* 
 0572 =         INPUT  EQU  $         ;*** INPUT *** 
 0572 D5        IP1    PUSH D         ;SAVE IN CASE OF ERROR 
 0573 CD8B08           CALL QTSTG     ;IS NEXT ITEM A STRING?
 0576 C38005           JMP  IP2       ;NO
 0579 FF               RST  7         ;YES. BUT FOLLOWED BY A
 057A DABA05           JC   IP4       ;VARIABLE?   NO. 
 057D C39005           JMP  IP3       ;YES.  INPUT VARIABLE
 0580 D5        IP2    PUSH D         ;SAVE FOR 'PRTSTG' 
 0581 FF               RST  7         ;MUST BE VARIABLE NOW
 0582 DAE007           JC   QWHAT     ;"WHAT?" IT IS NOT?
 0585 1A               LDAX D         ;GET READY FOR 'RTSTG'
 0586 4F               MOV  C,A 
 0587 97               SUB  A 
 0588 12               STAX D 
 0589 D1               POP  D 
 058A CD7F08           CALL PRTSTG    ;PRINT STRING AS PROMPT
 058D 79               MOV  A,C       ;RESTORE TEXT
 058E 1B               DCX  D 
 058F 12               STAX D 
 0590 D5        IP3    PUSH D         ;SAVE IN CASE OF ERROR 
 0591 EB               XCHG 
 0592 2AC109           LHLD CURRNT    ;ALSO SAVE 'CURRNT'
 0595 E5               PUSH H 
 0596 217205           LXI  H,IP1     ;A NEGATIVE NUMBER 
 0599 22C109           SHLD CURRNT    ;AS A FLAG 
 059C 210000           LXI  H,0Q      ;SAVE SP TOO 
 059F 39               DAD  SP
 05A0 22C709           SHLD STKINP
 05A3 D5               PUSH D         ;OLD HL
 05A4 3E3A             MVI  A,72Q     ;PRINT THIS TOO
 05A6 CD1408           CALL GETLN     ;AND GET A LINE
 05A9 11370F    IP3A   LXI  D,BUFFER  ;POINTS TO BUFFER
 05AC DF               RST  3         ;EVALUATE INPUT
 05AD 00               NOP            ;CAN BE 'CALL ENDCHK'
 05AE 00               NOP
 05AF 00               NOP
 05B0 D1               POP  D         ;OK, GET OLD HL
 05B1 EB               XCHG 
 05B2 73               MOV  M,E       ;SAVE VALUE IN VAR.
 05B3 23               INX  H 
 05B4 72               MOV  M,D 
 05B5 E1               POP  H         ;GET OLD 'CURRNT'
 05B6 22C109           SHLD CURRNT
 05B9 D1               POP  D         ;AND OLD TEXT POINTER
 05BA F1        IP4    POP  PSW       ;PURGE JUNK IN STACK 
 05BB CF               RST  1         ;IS NEXT CH. ','?
 05BC 2C               DB   ',' 
 05BD 03               DB   3Q
 05BE C37205           JMP  IP1       ;YES, MORE ITEMS.
 05C1 F7        IP5    RST  6 
                ;* 
 05C2 1A        DEFLT  LDAX D         ;*** DEFLT *** 
 05C3 FE0D             CPI  0DH       ;EMPTY LINE IS OK
 05C5 CAD105           JZ   LT1       ;ELSE IT IS 'LET'
                ;* 
 05C8 CDBA07    LET    CALL SETVAL    ;*** LET *** 
 05CB CF               RST  1         ;SET VALUE TO VAR. 
 05CC 2C               DB   ',' 
 05CD 03               DB   3Q
 05CE C3C805           JMP  LET       ;ITEM BY ITEM
 05D1 F7        LT1    RST  6         ;UNTIL FINISH
                ;* 
                ;**************************************************************
                ;* 
                ;* *** EXPR ***
                ;* 
                ;* 'EXPR' EVALUATES ARITHMETICAL OR LOGICAL EXPRESSIONS. 
                ;* <EXPR>::=<EXPR2>
                ;*          <EXPR2><REL.OP.><EXPR2>
                ;* WHERE <REL.OP.> IS ONE OF THE OPERATORSs IN TAB8 AND THE 
                ;* RESULT OF THESE OPERATIONS IS 1 IFF TRUE AND 0 IFF FALSE. 
                ;* <EXPR2>::=(+ OR -)<EXPR3>(+ OR -<EXPR3>)(....)
                ;* WHERE () ARE OPTIONAL AND (....) ARE OPTIONAL REPEATS.
                ;* <EXPR3>::=<EXPR4>(<* OR /><EXPR4>)(....)
                ;* <EXPR4>::=<VARIABLE>
                ;*           <FUNCTION>
                ;*           (<EXPR>)
                ;* <EXPR> IS RECURSIVE SO THAT VARIABLE '@' CAN HAVE AN <EXPR> 
                ;* AS INDEX, FNCTIONS CAN HAVE AN <EXPR> AS ARGUMENTS, AND
                ;* <EXPR4> CAN BE AN <EXPR> IN PARANTHESE. 
                ;* 
                ;*                 EXPR   CALL EXPR2     THIS IS AT LOC. 18
                ;*                        PUSH HL        SAVE <EXPR2> VALUE
 05D2 21BE02    EXPR1  LXI  H,TAB8-1  ;LOOKUP REL.OP.
 05D5 C3D802           JMP  EXEC      ;GO DO IT
 05D8 CD0106    XP11   CALL XP18      ;REL.OP.">=" 
 05DB D8               RC             ;NO, RETURN HL=0 
 05DC 6F               MOV  L,A       ;YES, RETURN HL=1
 05DD C9               RET
 05DE CD0106    XP12   CALL XP18      ;REL.OP."#"
 05E1 C8               RZ             ;FALSE, RETURN HL=0
 05E2 6F               MOV  L,A       ;TRUE, RETURN HL=1 
 05E3 C9               RET
 05E4 CD0106    XP13   CALL XP18      ;REL.OP.">"
 05E7 C8               RZ             ;FALSE 
 05E8 D8               RC             ;ALSO FALSE, HL=0
 05E9 6F               MOV  L,A       ;TRUE, HL=1
 05EA C9               RET
 05EB CD0106    XP14   CALL XP18      ;REL.OP."<=" 
 05EE 6F               MOV  L,A       ;SET HL=1
 05EF C8               RZ             ;REL. TRUE, RETURN 
 05F0 D8               RC 
 05F1 6C               MOV  L,H       ;ELSE SET HL=0 
 05F2 C9               RET
 05F3 CD0106    XP15   CALL XP18      ;REL.OP."="
 05F6 C0               RNZ            ;FALSE, RETRUN HL=0
 05F7 6F               MOV  L,A       ;ELSE SET HL=1 
 05F8 C9               RET
 05F9 CD0106    XP16   CALL XP18      ;REL.OP."<"
 05FC D0               RNC            ;FALSE, RETURN HL=0
 05FD 6F               MOV  L,A       ;ELSE SET HL=1 
 05FE C9               RET
 05FF E1        XP17   POP  H         ;NOT REL.OP. 
 0600 C9               RET            ;RETURN HL=<EXPR2> 
 0601 79        XP18   MOV  A,C       ;SUBROUTINE FOR ALL
 0602 E1               POP  H         ;REL.OP.'S 
 0603 C1               POP  B 
 0604 E5               PUSH H         ;REVERSE TOP OF STACK
 0605 C5               PUSH B 
 0606 4F               MOV  C,A 
 0607 CD1606           CALL EXPR2     ;GET 2ND <EXPR2> 
 060A EB               XCHG           ;VALUE IN DE NOW 
 060B E3               XTHL           ;1ST <EXPR2> IN HL 
 060C CDB207           CALL CKHLDE    ;COMPARE 1ST WITH 2ND
 060F D1               POP  D         ;RESTORE TEXT POINTER
 0610 210000           LXI  H,0Q      ;SET HL=0, A=1 
 0613 3E01             MVI  A,1 
 0615 C9               RET
                ;* 
 0616 CF        EXPR2  RST  1         ;NEGATIVE SIGN?
 0617 2D               DB   '-' 
 0618 06               DB   6Q
 0619 210000           LXI  H,0Q      ;YES, FAKE '0-'
 061C C34006           JMP  XP26      ;TREAT LIKE SUBTRACT 
 061F CF        XP21   RST  1         ;POSITIVE SIGN?  IGNORE
 0620 2B               DB   '+' 
 0621 00               DB   0Q
 0622 CD4A06    XP22   CALL EXPR3     ;1ST <EXPR3> 
 0625 CF        XP23   RST  1         ;ADD?
 0626 2B               DB   '+' 
 0627 15               DB   25Q 
 0628 E5               PUSH H         ;YES, SAVE VALUE 
 0629 CD4A06           CALL EXPR3     ;GET 2ND<EXPR3> 
 062C EB        XP24   XCHG           ;2ND IN DE 
 062D E3               XTHL           ;1ST IN HL 
 062E 7C               MOV  A,H       ;COMPARE SIGN
 062F AA               XRA  D 
 0630 7A               MOV  A,D 
 0631 19               DAD  D 
 0632 D1               POP  D         ;RESTORE TEXT POINTER
 0633 FA2506           JM   XP23      ;1ST 2ND SIGN DIFFER 
 0636 AC               XRA  H         ;1ST 2ND SIGN EQUAL
 0637 F22506           JP   XP23      ;SO ISp RESULT
 063A C36601           JMP  QHOW      ;ELSE WE HAVE OVERFLOW 
 063D CF        XP25   RST  1         ;SUBTRACT? 
 063E 2D               DB   '-' 
 063F 83               DB   203Q
 0640 E5        XP26   PUSH H         ;YES, SAVE 1ST <EXPR3> 
 0641 CD4A06           CALL EXPR3     ;GET 2ND <EXPR3> 
 0644 CDA607           CALL CHGSGN    ;NEGATE
 0647 C32C06           JMP  XP24      ;AND ADD THEM
                ;* 
 064A CDA706    EXPR3  CALL EXPR4     ;GET 1ST <EXPR4> 
 064D CF        XP31   RST  1         ;MULTIPLY? 
 064E 2A               DB   '*' 
 064F 2C               DB   54Q 
 0650 E5               PUSH H         ;YES, SAVE 1ST 
 0651 CDA706           CALL EXPR4     ;AND GET 2ND <EXPR4> 
 0654 0600             MVI  B,0Q      ;CLEAR B FOR SIGN
 0656 CDA307           CALL CHKSGN    ;CHECK SIGN
 0659 EB               XCHG           ;2ND IN DE NOW 
 065A E3               XTHL           ;1ST IN HL 
 065B CDA307           CALL CHKSGN    ;CHECK SIGN OF 1ST 
 065E 7C               MOV  A,H       ;IS HL > 255 ? 
 065F B7               ORA  A 
 0660 CA6906           JZ   XP32      ;NO
 0663 7A               MOV  A,D       ;YES, HOW ABOUT DE 
 0664 B2               ORA  D 
 0665 EB               XCHG           ;PUT SMALLER IN HL 
 0666 C26701           JNZ  AHOW      ;ALSO >, WILL OVERFLOW 
 0669 7D        XP32   MOV  A,L       ;THIS IS DUMB
 066A 210000           LXI  H,0Q      ;CLEAR RESULT
 066D B7               ORA  A         ;ADD AND COUNT 
 066E CA9906           JZ   XP35
 0671 19        XP33   DAD  D 
 0672 DA6701           JC   AHOW      ;OVERFLOW
 0675 3D               DCR  A 
 0676 C27106           JNZ  XP33
 0679 C39906           JMP  XP35      ;FINISHED
 067C CF        XP34   RST  1         ;DIVIDE? 
 067D 2F               DB   '/' 
 067E 44               DB   104Q
 067F E5               PUSH H         ;YES, SAVE 1ST <EXPR4> 
 0680 CDA706           CALL EXPR4     ;AND GET 2ND ONE 
 0683 0600             MVI  B,0Q      ;CLEAR B FOR SIGN
 0685 CDA307           CALL CHKSGN    ;CHECK SIGN OF 2ND 
 0688 EB               XCHG           ;PUT 2ND IN DE 
 0689 E3               XTHL           ;GET 1ST IN HL 
 068A CDA307           CALL CHKSGN    ;CHECK SIGN OF 1ST 
 068D 7A               MOV  A,D       ;DIVIDE BY 0?
 068E B3               ORA  E 
 068F CA6701           JZ   AHOW      ;SAY "HOW?"
 0692 C5               PUSH B         ;ELSE SAVE SIGN
 0693 CD8607           CALL DIVIDE    ;USE SUBROUTINE
 0696 60               MOV  H,B       ;RESULT IN HL NOW
 0697 69               MOV  L,C 
 0698 C1               POP  B         ;GET SIGN BACK 
 0699 D1        XP35   POP  D         ;AND TEXT POINTER
 069A 7C               MOV  A,H       ;HL MUST BE +
 069B B7               ORA  A 
 069C FA6601           JM   QHOW      ;ELSE IT IS OVERFLOW 
 069F 78               MOV  A,B 
 06A0 B7               ORA  A 
 06A1 FCA607           CM   CHGSGN    ;CHANGE SIGN IFF NEEDED 
 06A4 C34D06           JMP  XP31      ;LOOK OR MORE TERMS 
                ;* 
 06A7 217E02    EXPR4  LXI  H,TAB4-1  ;FIND FUNCTION IN TAB4 
 06AA C3D802           JMP  EXEC      ;AND GO DO IT
 06AD FF        XP40   RST  7         ;NO, NOT A FUNCTION
 06AE DAB606           JC   XP41      ;NOR A VARIABLE
 06B1 7E               MOV  A,M       ;VARIABLE
 06B2 23               INX  H 
 06B3 66               MOV  H,M       ;VALUE IN HL 
 06B4 6F               MOV  L,A 
 06B5 C9               RET
 06B6 CD3E01    XP41   CALL TSTNUM    ;OR IS IT A NUMBER 
 06B9 78               MOV  A,B       ;# OF DIGIT
 06BA B7               ORA  A 
 06BB C0               RNZ            ;OK
 06BC CF        PARN   RST  1         ;NO DIGIT, MUST BE 
 06BD 28               DB   '(' 
 06BE 05               DB   5Q
 06BF DF               RST  3         ;"(EXPR)"
 06C0 CF               RST  1 
 06C1 29               DB   ')' 
 06C2 01               DB   1Q
 06C3 C9        XP42   RET
 06C4 C3E007    XP43   JMP  QWHAT     ;ELSE SAY: "WHAT?" 
                ;* 
 06C7 CDBC06    RND    CALL PARN      ;*** RND(EXPR) *** 
 06CA 7C               MOV  A,H       ;EXPR MUST BE +
 06CB B7               ORA  A 
 06CC FA6601           JM   QHOW
 06CF B5               ORA  L         ;AND NON-ZERO
 06D0 CA6601           JZ   QHOW
 06D3 D5               PUSH D         ;SAVE BOTH 
 06D4 E5               PUSH H 
 06D5 2AD309           LHLD RANPNT    ;GET MEMORY AS RANDOM
 06D8 11B009           LXI  D,LSTROM  ;NUMBER
 06DB E7               RST  4 
 06DC DAE206           JC   RA1       ;WRAP AROUND IFF LAST 
 06DF 210001           LXI  H,START 
 06E2 5E        RA1    MOV  E,M 
 06E3 23               INX  H 
 06E4 56               MOV  D,M 
 06E5 22D309           SHLD RANPNT
 06E8 E1               POP  H 
 06E9 EB               XCHG 
 06EA C5               PUSH B 
 06EB CD8607           CALL DIVIDE    ;RND(N)=MOD(M,N)+1 
 06EE C1               POP  B 
 06EF D1               POP  D 
 06F0 23               INX  H 
 06F1 C9               RET
                ;* 
 06F2 CDBC06    ABS    CALL PARN      ;*** ABS(EXPR) *** 
 06F5 CDA307           CALL CHKSGN    ;CHECK SIGN
 06F8 7C               MOV  A,H       ;NOTE THAT -32768
 06F9 B4               ORA  H         ;CANNOT CHANGE SIGN
 06FA FA6601           JM   QHOW      ;SO SAY: "HOW?"
 06FD C9               RET
 06FE 2AD509    SIZE   LHLD TXTUNF    ;*** SIZE ***
 0701 D5               PUSH D         ;GET THE NUMBER OF FREE
 0702 EB               XCHG           ;BYTES BETWEEN 'TXTUNF'
 0703 21000F    SIZEA  LXI  H,VARBGN  ;AND 'VARBGN'
 0706 CD9C07           CALL SUBDE 
 0709 D1               POP  D 
 070A C9               RET
                ;*
                ;*********************************************************
                ;*
                ;*   *** OUT *** INP *** WAIT *** POKE *** PEEK *** & USR
                ;*
                ;*  OUT I,J(,K,L)
                ;*
                ;*  OUTPUTS EXPRESSION 'J' TO PORT 'I', AND MAY BE REPEATED
                ;*  AS IN DATA 'L' TO PORT 'K' AS MANY TIMES AS NEEDED
                ;*  THIS COMMAND MODIFIES ;*  THIS COMMAND MODIFIES 
                ;*  THIS COMMAND MODIFY'S A SMALL SECTION OF CODE LOCATED 
                ;*  JUST ABOVE ADDRESS 2K
                ;*
                ;*  INP (I)
                ;*
                ;*  THIS FUNCTION RETURNS DATA READ FROM INPUT PORT 'I' AS
                ;*  IT'S VALUE.
                ;*  IT ALSO MODIFIES CODE JUST ABOVE 2K.
                ;*
                ;*  WAIT I,J,K
                ;*
                ;*  THIS COMMAND READS THE STATUS OF PORT 'I', EXCLUSIVE OR'S
                ;*  THE RESULT WITH 'K' IF THERE IS ONE, OR IF NOT WITH 0, 
                ;*  AND'S WITH 'J' AND RETURNS WHEN THE RESULT IS NONZERO.
                ;*  ITS MODIFIED CODE IS ALSO ABOVE 2K.
                ;*
                ;*  POKE I,J(,K,L)
                ;*
                ;*  THIS COMMAND WORKS LIKE OUT EXCEPT THAT IT PUTS DATA 'J'
                ;*  INTO MEMORY LOCATION 'I'.
                ;*
                ;*  PEEK (I)
                ;*
                ;*  THIS FUNCTION WORKS LIKE INP EXCEPT IT GETS IT'S VALUE
                ;*  FROM MEMORY LOCATION 'I'.
                ;*
                ;*  USR (I(,J))
                ;*
                ;*  USR CALLS A MACHINE LANGUAGE SUBROUTINE AT LOCATION 'I'
                ;*  IF THE OPTIONAL PARAMETER 'J' IS USED ITS VALUE IS PASSED
                ;*  IN H&L.  THE VALUE OF THE FUNCTION SHOULD BE RETURNED IN H&L.
                ;*
                ;************************************************************
                ;*
 070B DF        OUTCMD RST  3 
 070C 7D               MOV  A,L
 070D 32B109           STA  OUTIO + 1
 0710 CF               RST  1
 0711 2C               DB   ','
 0712 2F               DB   2FH
 0713 DF               RST  3
 0714 7D               MOV  A,L
 0715 CDB009           CALL OUTIO
 0718 CF               RST  1
 0719 2C               DB   ','
 071A 03               DB   03H
 071B C30B07           JMP  OUTCMD 
 071E F7               RST  6
 071F DF        WAITCM RST  3
 0720 7D               MOV  A,L
 0721 32B409           STA  WAITIO + 1
 0724 CF               RST  1
 0725 2C               DB   ','
 0726 1B               DB   1BH
 0727 DF               RST  3
 0728 E5               PUSH H
 0729 CF               RST  1
 072A 2C               DB   ','
 072B 07               DB   7H
 072C DF               RST  3
 072D 7D               MOV  A,L
 072E E1               POP  H
 072F 67               MOV  H,A
 0730 C33207           JMP  $ + 2
 0733 2600             MVI  H,0
 0735 C3B309           JMP  WAITIO
 0738 CDBC06    INP    CALL PARN
 073B 7D               MOV  A,L
 073C 32BC09           STA  INPIO + 1
 073F 2600             MVI  H,0
 0741 C3BB09           JMP  INPIO
 0744 C3E007           JMP  QWHAT
 0747 DF        POKE   RST  3
 0748 E5               PUSH H
 0749 CF               RST  1
 074A 2C               DB   ','
 074B 12               DB   12H
 074C DF               RST  3
 074D 7D               MOV  A,L
 074E E1               POP  H
 074F 77               MOV  M,A
 0750 CF               RST  1
 0751 2C03             DB   ',',03H
 0753 C34707           JMP  POKE
 0756 F7               RST 6
 0757 CDBC06    PEEK   CALL PARN
 075A 6E               MOV  L,M
 075B 2600             MVI  H,0
 075D C9               RET
 075E C3E007           JMP  QWHAT
 0761 C5        USR    PUSH B
 0762 CF               RST  1
 0763 281C             DB   '(',28D    ;QWHAT
 0765 DF               RST  3          ;EXPR
 0766 CF               RST  1
 0767 2907             DB   ')',7      ;PASPARM
 0769 D5               PUSH D
 076A 118007           LXI  D,USRET
 076D D5               PUSH D
 076E E5               PUSH H
 076F C9               RET             ;CALL USR ROUTINE
 0770 CF        PASPRM RST  1
 0771 2C0E             DB   ',',14D
 0773 E5               PUSH H
 0774 DF               RST  3
 0775 CF               RST  1
 0776 2909             DB   ')',9
 0778 C1               POP  B
 0779 D5               PUSH D
 077A 118007           LXI  D,USRET
 077D D5               PUSH D
 077E C5               PUSH B
 077F C9               RET             ;CALL USR ROUTINE
 0780 D1        USRET  POP  D
 0781 C1               POP  B
 0782 C9               RET
 0783 C3E007           JMP  QWHAT
                ;*
                ;**************************************************************
                ;* 
                ;* *** DIVIDE *** SUBDE *** CHKSGN *** CHGSGN *** & CKHLDE *** 
                ;* 
                ;* 'DIVIDE' DIVIDES HL BY DE, RESULT IN BC, REMAINDER IN HL
                ;* 
                ;* 'SUBDE' SUBTRACTS DE FROM HL
                ;* 
                ;* 'CHKSGN' CHECKS SIGN OF HL.  IFF +, NO CHANGE.  IFF -, CHANGE 
                ;* SIGN AND FLIP SIGN OF B.
                ;* 
                ;* 'CHGSGN' CHNGES SIGN OF HL AND B UNCONDITIONALLY. 
                ;* 
                ;* 'CKHLE' CHECKS SIGN OF HL AND DE.  IFF DIFFERENT, HL AND DE 
                ;* ARE INTERCHANGED.  IFF SAME SIGN, NOT INTERCHANGED.  EITHER
                ;* CASE, HL DE ARE THEN COMPARED TO SET THE FLAGS. 
                ;* 
 0786 E5        DIVIDE PUSH H         ;*** DIVIDE ***
 0787 6C               MOV  L,H       ;DIVIDE H BY DE
 0788 2600             MVI  H,0 
 078A CD9107           CALL DV1 
 078D 41               MOV  B,C       ;SAVE RESULT IN B
 078E 7D               MOV  A,L       ;(REMAINDER+L)/DE
 078F E1               POP  H 
 0790 67               MOV  H,A 
 0791 0EFF      DV1    MVI  C,377Q    ;RESULT IN C 
 0793 0C        DV2    INR  C         ;DUMB ROUTINE
 0794 CD9C07           CALL SUBDE     ;DIVIDE BY SUBTRACT
 0797 D29307           JNC  DV2       ;AND COUNT 
 079A 19               DAD  D 
 079B C9               RET
                ;* 
 079C 7D        SUBDE  MOV  A,L       ;*** SUBDE *** 
 079D 93               SUB  E         ;SUBTRACT DE FROM
 079E 6F               MOV  L,A       ;HL
 079F 7C               MOV  A,H 
 07A0 9A               SBB  D 
 07A1 67               MOV  H,A 
 07A2 C9               RET
                ;* 
 07A3 7C        CHKSGN MOV  A,H       ;*** CHKSGN ***
 07A4 B7               ORA  A         ;CHECK SIGN OF HL
 07A5 F0               RP             ;IFF -, CHANGE SIGN 
                ;* 
 07A6 7C        CHGSGN MOV  A,H       ;*** CHGSGN ***
 07A7 2F               CMA            ;CHANGE SIGN OF HL 
 07A8 67               MOV  H,A 
 07A9 7D               MOV  A,L 
 07AA 2F               CMA
 07AB 6F               MOV  L,A 
 07AC 23               INX  H 
 07AD 78               MOV  A,B       ;AND ALSO FLIP B 
 07AE EE80             XRI  200Q
 07B0 47               MOV  B,A 
 07B1 C9               RET
                ;* 
 07B2 7C        CKHLDE MOV  A,H 
 07B3 AA               XRA  D         ;SAME SIGN?
 07B4 F2B807           JP   CK1       ;YES, COMPARE
 07B7 EB               XCHG           ;NO, XCH AND COMP
 07B8 E7        CK1    RST  4 
 07B9 C9               RET
                ;* 
                ;**************************************************************
                ;* 
                ;* *** SETVAL *** FIN *** ENDCHK *** & ERROR (& FRIENDS) *** 
                ;* 
                ;* "SETVAL" EXPECTS A VARIABLE, FOLLOWED BY AN EQUAL SIGN AND
                ;* THEN AN EXPR.  IT EVALUATES THE EXPR. AND SET THE VARIABLE
                ;* TO THAT VALUE.
                ;* 
                ;* "FIN" CHECKS THE END OF A COMMAND.  IFF IT ENDED WITH ";", 
                ;* EXECUTION CONTINUES.  IFF IT ENDED WITH A CR, IT FINDS THE 
                ;* NEXT LINE AND CONTINUE FROM THERE.
                ;* 
                ;* "ENDCHK" CHECKS IFF A COMMAND IS ENDED WITH CR.  THIS IS 
                ;* REQUIRED IN CERTAIN COMMANDS. (GOTO, RETURN, AND STOP ETC.) 
                ;* 
                ;* "ERROR" PRINTS THE STRING POINTED BY DE (AND ENDS WITH CR). 
                ;* IT THEN PRINTS THE LINE POINTED BY 'CURRNT' WITH A "?"
                ;* INSERTED AT WHERE THE OLD TEXT POINTER (SHOULD BE ON TOP
                ;* O THE STACK) POINTS TO.  EXECUTION OF TB IS STOPPED
                ;* AND TBI IS RESTARTED.  HOWEVER, IFF 'CURRNT' -> ZERO 
                ;* (INDICATING A DIRECT COMMAND), THE DIRECT COMMAND IS NOT
                ;*  PRINTED.  AND IFF 'CURRNT' -> NEGATIVE # (INDICATING 'INPUT'
                ;* COMMAND, THE INPUT LINE IS NOT PRINTED AND EXECUTION IS 
                ;* NOT TERMINATED BUT CONTINUED AT 'INPERR'. 
                ;* 
                ;* RELATED TO 'ERROR' ARE THE FOLLOWING: 
                ;* 'QWHAT' SAVES TEXT POINTER IN STACK AND GET MESSAGE "WHAT?" 
                ;* 'AWHAT' JUST GET MESSAGE "WHAT?" AND JUMP TO 'ERROR'. 
                ;* 'QSORRY' AND 'ASORRY' DO SAME KIND OF THING.
                ;* 'QHOW' AND 'AHOW' IN THE ZERO PAGE SECTION ALSO DO THIS 
                ;* 
 07BA FF        SETVAL RST  7         ;*** SETVAL ***
 07BB DAE007           JC   QWHAT     ;"WHAT?" NO VARIABLE 
 07BE E5               PUSH H         ;SAVE ADDRESS OF VAR.
 07BF CF               RST  1         ;PASS "=" SIGN 
 07C0 3D               DB   '=' 
 07C1 08               DB   10Q 
 07C2 DF               RST  3         ;EVALUATE EXPR.
 07C3 44               MOV  B,H       ;VALUE IN BC NOW 
 07C4 4D               MOV  C,L 
 07C5 E1               POP  H         ;GET ADDRESS 
 07C6 71               MOV  M,C       ;SAVE VALUE
 07C7 23               INX  H 
 07C8 70               MOV  M,B 
 07C9 C9               RET
 07CA C3E007    SV1    JMP  QWHAT     ;NO "=" SIGN 
                ;* 
 07CD CF        FIN    RST  1         ;*** FIN *** 
 07CE 3B               DB   73Q 
 07CF 04               DB   4Q 
 07D0 F1               POP  PSW       ;";", PURGE RET ADDR.
 07D1 C32B03           JMP  RUNSML    ;CONTINUE SAME LINE
 07D4 CF        FI1    RST  1         ;NOT ";", IS IT CR?
 07D5 0D               DB   0DH
 07D6 04               DB   4Q 
 07D7 F1               POP  PSW       ;YES, PURGE RET ADDR.
 07D8 C31B03           JMP  RUNNXL    ;RUN NEXT LINE 
 07DB C9        FI2    RET            ;ELSE RETURN TO CALLER 
                ;* 
 07DC EF        ENDCHK RST  5         ;*** ENDCHK ***
 07DD FE0D             CPI  0DH       ;END WITH CR?
 07DF C8               RZ             ;OK, ELSE SAY: "WHAT?" 
                ;* 
 07E0 D5        QWHAT  PUSH D         ;*** QWHAT *** 
 07E1 117501    AWHAT  LXI  D,WHAT    ;*** AWHAT *** 
 07E4 97        ERROR  SUB  A         ;*** ERROR *** 
 07E5 CD7F08           CALL PRTSTG    ;PRINT 'WHAT?', 'HOW?' 
 07E8 D1               POP  D         ;OR 'SORRY'
 07E9 1A               LDAX D         ;SAVE THE CHARACTER
 07EA F5               PUSH PSW       ;AT WHERE OLD DE ->
 07EB 97               SUB  A         ;AND PUT A 0 THERE 
 07EC 12               STAX D 
 07ED 2AC109           LHLD CURRNT    ;GET CURRENT LINE #
 07F0 E5               PUSH H 
 07F1 7E               MOV  A,M       ;CHECK THE VALUE 
 07F2 23               INX  H 
 07F3 B6               ORA  M 
 07F4 D1               POP  D 
 07F5 CA8101           JZ   RSTART    ;IFF ZERO, JUST RERSTART
 07F8 7E               MOV  A,M       ;IFF NEGATIVE,
 07F9 B7               ORA  A 
 07FA FA6805           JM   INPERR    ;REDO INPUT
 07FD CDED08           CALL PRTLN     ;ELSE PRINT THE LINE 
 0800 1B               DCX  D         ;UPTO WHERE THE 0 IS 
 0801 F1               POP  PSW       ;RESTORE THE CHARACTER 
 0802 12               STAX D 
 0803 3E3F             MVI  A,77Q     ;PRINTt A "?" 
 0805 D7               RST  2 
 0806 97               SUB  A         ;AND THE REST OF THE 
 0807 CD7F08           CALL PRTSTG    ;LINE
 080A C38101           JMP  RSTART
 080D D5        QSORRY PUSH D         ;*** QSORRY ***
 080E 117B01    ASORRY LXI  D,SORRY   ;*** ASORRY ***
 0811 C3E407           JMP  ERROR 
                ;* 
                ;**************************************************************
                ;* 
                ;* *** GETLN *** FNDLN (& FRIENDS) *** 
                ;* 
                ;* 'GETLN' READS A INPUT LINE INTO 'BUFFER'.  IT FIRST PROMPT
                ;* THE CHARACTER IN A (GIVEN BY THE CALLER), THEN IT FILLS THE 
                ;* THE BUFFER AND ECHOS.  IT IGNORES LF'S AND NULLS, BUT STILL 
                ;* ECHOS THEM BACK.  RUB-OUT IS USED TO CAUSE IT TO DELETE 
                ;* THE LAST CHARATER (IFF THERE IS ONE), AND ALT-MOD IS USED TO 
                ;* CAUSE IT TO DELETE THE WHOLE LINE AND START IT ALL OVER.
                ;* 0DHSIGNALS THE END OF A LINE, AND CAUE 'GETLN' TO RETURN.
                ;* 
                ;* 'FNDLN' FINDS A LINE WITH A GIVEN LINE # (IN HL) IN THE 
                ;* TEXT SAVE AREA.  DE IS USED AS THE TEXT POINTER.  IFF THE
                ;* LINE IS FOUND, DE WILL POINT TO THE BEGINNING OF THAT LINE
                ;* (I.E., THE LOW BYTE OF THE LINE #), AND FLAGS ARE NC & Z. 
                ;* IFF THAT LINE IS NOT THERE AND A LINE WITH A HIGHER LINE # 
                ;* IS FOUND, DE POINTS TO THERE AND FLAGS ARE NC & NZ.  IFF 
                ;* WE REACHED THE END OF TEXT SAVE ARE AND CANNOT FIND THE 
                ;* LINE, FLAGS ARE C & NZ. 
                ;* 'FNDLN' WILL INITIALIZE DE TO THE BEGINNING OF THE TEXT SAVE
                ;* AREA TO START THE SEARCH.  SOME OTHER ENTRIES OF THIS 
                ;* ROUTINE WILL NOT INITIALIZE DE AND DO THE SEARCH. 
                ;* 'FNDLNP' WILL START WITH DE AND SEARCH FOR THE LINE #.
                ;* 'FNDNXT' WILL BUMP DE BY 2, FIND A 0DHAND THEN START SEARCH.
                ;* 'FNDSKP' USE DE TO FIND A CR, AND THEN STRART SEARCH. 
                ;* 
 0814 D7        GETLN  RST  2         ;*** GETLN *** 
 0815 11370F           LXI  D,BUFFER  ;PROMPT AND INIT
 0818 CD8509    GL1    CALL CHKIO     ;CHECK KEYBOARD
 081B CA1808           JZ   GL1       ;NO INPUT, WAIT
 081E FE7F             CPI  177Q      ;DELETE LST CHARACTER?
 0820 CA4208           JZ   GL3       ;YES 
 0823 FE0A             CPI  12Q       ;IGNORE LF 
 0825 CA1808           JZ   GL1 
 0828 B7               ORA  A         ;IGNORE NULL 
 0829 CA1808           JZ   GL1 
 082C FE5C             CPI  134Q      ;DELETE THE WHOLE LINE?
 082E CA4F08           JZ   GL4       ;YES 
 0831 12               STAX D         ;ELSE, SAVE INPUT
 0832 13               INX  D         ;AND BUMP POINTER
 0833 FE0D             CPI  15Q       ;WAS IT CR?
 0835 C23C08           JNZ  GL2       ;NO
 0838 3E0A             MVI  A,12Q     ;YES, GET LINE FEED
 083A D7               RST  2         ;CALL OUTC AND LINE FEED
 083B C9               RET            ;WE'VE GOT A LINE
 083C 7B        GL2    MOV  A,E       ;MORE FREE ROOM?
 083D FE87             CPI  BUFEND AND 0FFH
 083F C21808           JNZ  GL1       ;YES, GET NEXT INPUT 
 0842 7B        GL3    MOV  A,E       ;DELETE LAST CHARACTER 
 0843 FE37             CPI  BUFFER AND 0FFH    ;BUT DO WE HAVE ANY? 
 0845 CA4F08           JZ   GL4       ;NO, REDO WHOLE LINE 
 0848 1B               DCX  D         ;YES, BACKUP POINTER 
 0849 3E5F             MVI  A,'_'     ;AND ECHO A BACK-SPACE 
 084B D7               RST  2 
 084C C31808           JMP  GL1       ;GO GET NEXT INPUT 
 084F CD0E00    GL4    CALL CRLF      ;REDO ENTIRE LINE
 0852 3E5E             MVI  A,136Q    ;CR, LF AND UP-ARROW 
 0854 C31408           JMP  GETLN 
                ;* 
 0857 7C        FNDLN  MOV  A,H       ;*** FNDLN *** 
 0858 B7               ORA  A         ;CHECK SIGN OF HL
 0859 FA6601           JM   QHOW      ;IT CANNT BE -
 085C 11D709           LXI  D,TXTBGN  ;INIT. TEXT POINTER
                ;* 
 085F =         FNDLNP EQU  $         ;*** FNDLNP ***
 085F E5        FL1    PUSH H         ;SAVE LINE # 
 0860 2AD509           LHLD TXTUNF    ;CHECK IFF WE PASSED END
 0863 2B               DCX  H 
 0864 E7               RST  4 
 0865 E1               POP  H         ;GET LINE # BACK 
 0866 D8               RC             ;C,NZ PASSED END 
 0867 1A               LDAX D         ;WE DID NOT, GET BYTE 1
 0868 95               SUB  L         ;IS THIS THE LINE? 
 0869 47               MOV  B,A       ;COMPARE LOW ORDER 
 086A 13               INX  D 
 086B 1A               LDAX D         ;GET BYTE 2
 086C 9C               SBB  H         ;COMPARE HIGH ORDER
 086D DA7408           JC   FL2       ;NO, NOT THERE YET 
 0870 1B               DCX  D         ;ELSE WE EITHER FOUND
 0871 B0               ORA  B         ;IT, OR IT IS NOT THERE
 0872 C9               RET            ;NC,Z:FOUND; NC,NZ:NO
                ;* 
 0873 =         FNDNXT EQU  $         ;*** FNDNXT ***
 0873 13               INX  D         ;FIND NEXT LINE
 0874 13        FL2    INX  D         ;JUST PASSED BYTE 1 & 2
                ;* 
 0875 1A        FNDSKP LDAX D         ;*** FNDSKP ***
 0876 FE0D             CPI  0DH       ;TRY TO FIND 0DH
 0878 C27408           JNZ  FL2       ;KEEP LOOKING
 087B 13               INX  D         ;FOUND CR, SKIP OVER 
 087C C35F08           JMP  FL1       ;CHECK IFF END OF TEXT
                ;* 
                ;*************************************************************
                ;* 
                ;* *** PRTSTG *** QTSTG *** PRTNUM *** & PRTLN *** 
                ;* 
                ;* 'PRTSTG' PRINTS A STRING POINTED BY DE.  IT STOPS PRINTING
                ;* AND RETURNS TO CALER WHEN EITHER A 0DHIS PRINTED OR WHEN 
                ;* THE NEXT BYTE IS THE SAME AS WHAT WAS IN A (GIVEN BY THE
                ;* CALLER).  OLD A IS STORED IN B, OLD B IS LOST.
                ;* 
                ;* 'QTSTG' LOOKS FOR A BACK-ARROW, SINGLE QUOTE, OR DOUBLE 
                ;* QUOTE.  IFF NONE OF THESE, RETURN TO CALLER.  IFF BACK-ARROW, 
                ;* OUTPUT A 0DHWITHOUT A LF.  IFF SINGLE OR DOUBLE QUOTE, PRINT 
                ;* THE STRING IN THE QUOTE AND DEMANDS A MATCHING UNQUOTE. 
                ;* AFTER THE PRINTING THE NEXT 3 BYTES OF THE CALLER IS SKIPPED
                ;* OVER (USUALLY A JUMP INSTRUCTION).
                ;* 
                ;* 'PRTNUM' PRINTS THE NUMBER IN HL.  LEADING BLANKS ARE ADDED 
                ;* IFF NEEDED TO PAD THE NUMBER OF SPACES TO THE NUMBER IN C. 
                ;* HOWEVER, IFF THE NUMBER OF DIGITS IS LARGER THAN THE # IN
                ;* C, ALL DIGITS ARE PRINTED ANYWAY.  NEGATIVE SIGN IS ALSO
                ;* PRINTED AND COUNTED IN, POSITIVE SIGN IS NOT. 
                ;* 
                ;* 'PRTLN' PRINSrA SAVED TEXT LINE WITH LINE # AND ALL. 
                ;* 
 087F 47        PRTSTG MOV  B,A       ;*** PRTSTG ***
 0880 1A        PS1    LDAX D         ;GET A CHARACTERr 
 0881 13               INX  D         ;BUMP POINTER
 0882 B8               CMP  B         ;SAME AS OLD A?
 0883 C8               RZ             ;YES, RETURN 
 0884 D7               RST  2         ;ELSE PRINT IT 
 0885 FE0D             CPI  0DH       ;WAS IT A CR?
 0887 C28008           JNZ  PS1       ;NO, NEXT
 088A C9               RET            ;YES, RETURN 
                ;* 
 088B CF        QTSTG  RST  1         ;*** QTSTG *** 
 088C 22               DB   '"' 
 088D 0F               DB   17Q 
 088E 3E22             MVI  A,42Q     ;IT IS A " 
 0890 CD7F08    QT1    CALL PRTSTG    ;PRINT UNTIL ANOTHER 
 0893 FE0D             CPI  0DH       ;WAS LAST ONE A CR?
 0895 E1               POP  H         ;RETURN ADDRESS
 0896 CA1B03           JZ   RUNNXL    ;WAS CR, RUN NEXT LINE 
 0899 23        QT2    INX  H         ;SKIP 3 BYTES ON RETURN
 089A 23               INX  H 
 089B 23               INX  H 
 089C E9               PCHL           ;RETURN
 089D CF        QT3    RST  1         ;IS IT A ' ? 
 089E 27               DB   47Q 
 089F 05               DB   5Q
 08A0 3E27             MVI  A,47Q     ;YES, DO SAME
 08A2 C39008           JMP  QT1       ;AS IN " 
 08A5 CF        QT4    RST  1         ;IS IT BACK-ARROW? 
 08A6 5F               DB   137Q
 08A7 08               DB   10Q 
 08A8 3E8D             MVI  A,215Q    ;YES, 0DHWITHOUT LF!!
 08AA D7               RST  2         ;DO IT TWICE TO GIVE 
 08AB D7               RST  2         ;TTY ENOUGH TIME 
 08AC E1               POP  H         ;RETURN ADDRESS
 08AD C39908           JMP  QT2 
 08B0 C9        QT5    RET            ;NONE OF ABOVE 
                ;* 
 08B1 D5        PRTNUM PUSH D         ;*** PRTNUM ***
 08B2 110A00           LXI  D,12Q     ;DECIMAL 
 08B5 D5               PUSH D         ;SAVE AS A FLAG
 08B6 42               MOV  B,D       ;B=SIGN
 08B7 0D               DCR  C         ;C=SPACES
 08B8 CDA307           CALL CHKSGN    ;CHECK SIGN
 08BB F2C108           JP   PN1       ;NO SIGN 
 08BE 062D             MVI  B,55Q     ;B=SIGN
 08C0 0D               DCR  C         ;'-' TAKES SPACE 
 08C1 C5        PN1    PUSH B         ;SAVE SIGN & SPACE 
 08C2 CD8607    PN2    CALL DIVIDE    ;DEVIDE HL BY 10 
 08C5 78               MOV  A,B       ;RESULT 0? 
 08C6 B1               ORA  C 
 08C7 CAD208           JZ   PN3       ;YES, WE GOT ALL 
 08CA E3               XTHL           ;NO, SAVE REMAINDER
 08CB 2D               DCR  L         ;AND COUNT SPACE 
 08CC E5               PUSH H         ;HL IS OLD BC
 08CD 60               MOV  H,B       ;MOVE RESULT TO BC 
 08CE 69               MOV  L,C 
 08CF C3C208           JMP  PN2       ;AND DIVIDE BY 10
 08D2 C1        PN3    POP  B         ;WE GOT ALL DIGITS IN
 08D3 0D        PN4    DCR  C         ;THE STACK 
 08D4 79               MOV  A,C       ;LOOK AT SPACE COUNT 
 08D5 B7               ORA  A 
 08D6 FADF08           JM   PN5       ;NO LEADING BLANKS 
 08D9 3E20             MVI  A,40Q     ;LEADING BLANKS
 08DB D7               RST  2 
 08DC C3D308           JMP  PN4       ;MORE? 
 08DF 78        PN5    MOV  A,B       ;PRINT SIGN
 08E0 D7               RST  2         ;MAYBE - OR NULL 
 08E1 5D               MOV  E,L       ;LAST REMAINDER IN E 
 08E2 7B        PN6    MOV  A,E       ;CHECK DIGIT IN E
 08E3 FE0A             CPI  12Q       ;10 IS FLAG FOR NO MORE
 08E5 D1               POP  D 
 08E6 C8               RZ             ;IFF SO, RETURN 
 08E7 C630             ADI  60Q		;ELSE CONVERT TO ASCII
 08E9 D7               RST  2         ;AND PRINT THE DIGIT 
 08EA C3E208           JMP  PN6       ;GO BACK FOR MORE
                ;* 
 08ED 1A        PRTLN  LDAX D         ;*** PRTLN *** 
 08EE 6F               MOV  L,A       ;LOW ORDER LINE #
 08EF 13               INX  D 
 08F0 1A               LDAX D         ;HIGH ORDER
 08F1 67               MOV  H,A 
 08F2 13               INX  D 
 08F3 0E04             MVI  C,4Q      ;PRINT 4 DIGIT LINE #
 08F5 CDB108           CALL PRTNUM
 08F8 3E20             MVI  A,40Q     ;FOLLOWED BY A BLANK 
 08FA D7               RST  2 
 08FB 97               SUB  A         ;AND THEN THE TEXT 
 08FC CD7F08           CALL PRTSTG
 08FF C9               RET
                ;* 
                ;**************************************************************
                ;* 
                ;* *** MVUP *** MVDOWN *** POPA *** & PUSHA ***
                ;* 
                ;* 'MVUP' MOVES A BLOCK UP FROM HERE DE-> TO WHERE BC-> UNTIL 
                ;* DE = HL 
                ;* 
                ;* 'MVDOWN' MOVES A BLOCK DOWN FROM WHERE DE-> TO WHERE HL-> 
                ;* UNTIL DE = BC 
                ;* 
                ;* 'POPA' RESTORES THE 'FOR' LOOP VARIABLE SAVE AREA FROM THE
                ;* STACK 
                ;* 
                ;* 'PUSHA' STACKS THE 'FOR' LOOP VARIABLE SAVE AREA INTO THE 
                ;* STACK 
                ;* 
 0900 E7        MVUP   RST  4         ;*** MVUP ***
 0901 C8               RZ             ;DE = HL, RETURN 
 0902 1A               LDAX D         ;GET ONE BYTE
 0903 02               STAX B         ;MOVE IT 
 0904 13               INX  D         ;INCREASE BOTH POINTERS
 0905 03               INX  B 
 0906 C30009           JMP  MVUP      ;UNTIL DONE
                ;* 
 0909 78        MVDOWN MOV  A,B       ;*** MVDOWN ***
 090A 92               SUB  D         ;TEST IFF DE = BC 
 090B C21109           JNZ  MD1       ;NO, GO MOVE 
 090E 79               MOV  A,C       ;MAYBE, OTHER BYTE?
 090F 93               SUB  E 
 0910 C8               RZ             ;YES, RETURN 
 0911 1B        MD1    DCX  D         ;ELSE MOVE A BYTE
 0912 2B               DCX  H         ;BUT FIRST DECREASE
 0913 1A               LDAX D         ;BOTH POINTERS AND 
 0914 77               MOV  M,A       ;THEN DO IT
 0915 C30909           JMP  MVDOWN    ;LOOP BACK 
                ;* 
 0918 C1        POPA   POP  B         ;BC = RETURN ADDR. 
 0919 E1               POP  H         ;RESTORE LOPVAR, BUT 
 091A 22C909           SHLD LOPVAR    ;=0 MEANS NO MORE
 091D 7C               MOV  A,H 
 091E B5               ORA  L 
 091F CA3209           JZ   PP1       ;YEP, GO RETURN
 0922 E1               POP  H         ;NOP, RESTORE OTHERS 
 0923 22CB09           SHLD LOPINC
 0926 E1               POP  H 
 0927 22CD09           SHLD LOPLMT
 092A E1               POP  H 
 092B 22CF09           SHLD LOPLN 
 092E E1               POP  H 
 092F 22D109           SHLD LOPPT 
 0932 C5        PP1    PUSH B         ;BC = RETURN ADDR. 
 0933 C9               RET
                ;* 
 0934 21AF0F    PUSHA  LXI  H,STKLMT  ;*** PUSHA *** 
 0937 CDA607           CALL CHGSGN
 093A C1               POP  B         ;BC=RETURN ADDRESS 
 093B 39               DAD  SP        ;IS STACK NEAR THE TOP?
 093C D20D08           JNC  QSORRY    ;YES, SORRY FOR THAT.
 093F 2AC909           LHLD LOPVAR    ;ELSE SAVE LOOP VAR.S
 0942 7C               MOV  A,H       ;BUT IFF LOPVAR IS 0
 0943 B5               ORA  L         ;THAT WILL BE ALL
 0944 CA5A09           JZ   PU1 
 0947 2AD109           LHLD LOPPT     ;ELSE, MORE TO SAVE
 094A E5               PUSH H 
 094B 2ACF09           LHLD LOPLN 
 094E E5               PUSH H 
 094F 2ACD09           LHLD LOPLMT
 0952 E5               PUSH H 
 0953 2ACB09           LHLD LOPINC
 0956 E5               PUSH H 
 0957 2AC909           LHLD LOPVAR
 095A E5        PU1    PUSH H 
 095B C5               PUSH B         ;BC = RETURN ADDR. 
 095C C9               RET
                ;* 
                ;**************************************************************
                ;* 
                ;* *** OUTC *** & CHKIO ****!
                ;* THESE ARE THE ONLY I/O ROUTINES IN TBI. 
                ;* 'OUTC' IS CONTROLLED BY A SOFTWARE SWITCH 'OCSW'.  IFF OCSW=0
                ;* 'OUTC' WILL JUST RETURN TO THE CALLER.  IFF OCSW IS NOT 0, 
                ;* IT WILL OUTPUT THE BYTE IN A.  IFF THAT IS A CR, A LF IS ALSO
                ;* SEND OUT.  ONLY THE FLAGS MAY BE CHANGED AT RETURN, ALL REG.
                ;* ARE RESTORED. 
                ;* 
                ;* 'CHKIO' CHECKS THE INPUT.  IFF NO INPUT, IT WILL RETURN TO 
                ;* THE CALLER WITH THE Z FLAG SET.  IFF THERE IS INPUT, Z FLAG
                ;* IS CLEARED AND THE INPUT BYTE IS IN A.  HOWERER, IFF THE 
                ;* INPUT IS A CONTROL-O, THE 'OCSW' SWITCH IS COMPLIMENTED, AND
                ;* Z FLAG IS RETURNED.  IFF A CONTROL-C IS READ, 'CHKIO' WILL 
                ;* RESTART TBI AND DO NOT RETURN TO THE CALLER.
                ;* 
                ;*                 OUTC   PUSH AF        THIS IS AT LOC. 10
                ;*                        LD   A,OCSW    CHECK SOFTWARE SWITCH 
                ;*                        IOR  A 
 095D C26209    OC2    JNZ  OC3       ;IT IS ON
 0960 F1               POP  PSW       ;IT IS OFF 
 0961 C9               RET            ;RESTORE AF AND RETURN 
 0962 F1        OC3    POP  A         ;GET OLD A BACK
 0963 C5               PUSH B         ;SAVE B ON STACK
 0964 D5               PUSH D         ;AND D
 0965 E5               PUSH H         ;AND H TOO
 0966 32BF09           STA  OUTCAR    ;SAVE CHARACTER
 0969 5F               MOV  E,A       ;PUT CHAR. IN E FOR CPM
 096A 0E02             MVI  C,2       ;GET CONOUT COMMAND
 096C CD0500           CALL CPM       ;CALL CPM AND DO IT
 096F 3ABF09           LDA  OUTCAR    ;GET CHAR. BACK
 0972 FE0D             CPI  0DH       ;WAS IT A 'CR'?
 0974 C27E09           JNZ  DONE      ;NO, DONE
 0977 1E0A             MVI  E,0AH     ;GET LINEFEED
 0979 0E02             MVI  C,2       ;AND CONOUT AGAIN
 097B CD0500           CALL CPM       ;CALL CPM
 097E 3ABF09    DONE   LDA  OUTCAR    ;GET CHARACTER BACK
 0981 E1        IDONE  POP  H         ;GET H BACK
 0982 D1               POP  D         ;AND D
 0983 C1               POP  B         ;AND B TOO
 0984 C9               RET            ;DONE AT LAST
 0985 C5        CHKIO  PUSH B         ;SAVE B ON STACK
 0986 D5               PUSH D         ;AND D
 0987 E5               PUSH H         ;THEN H
 0988 0E0B             MVI  C,11      ;GET CONSTAT WORD
 098A CD0500           CALL CPM       ;CALL THE BDOS
 098D B7               ORA  A         ;SET FLAGS
 098E C29409           JNZ  CI1       ;IF READY GET CHARACTER
 0991 C38109           JMP  IDONE     ;RESTORE AND RETURN
 0994 0E01      CI1    MVI  C,1       ;GET CONIN WORD
 0996 CD0500           CALL CPM       ;CALL THE BDOS
 0999 FE0F             CPI  0FH       ;IS IT CONTROL-O?
 099B C2A809           JNZ  CI2       ;NO, MORE CHECKING
 099E 3AC009           LDA  OCSW      ;CONTROL-O  FLIP OCSW
 09A1 2F               CMA            ;ON TO OFF, OFF TO ON
 09A2 32C009           STA  OCSW      ;AND PUT IT BACK
 09A5 C38509           JMP  CHKIO     ;AND GET ANOTHER CHARACTER
 09A8 FE03      CI2    CPI  3         ;IS IT CONTROL-C?
 09AA C28109           JNZ  IDONE     ;RETURN AND RESTORE IF NOT
 09AD C38101           JMP  RSTART    ;YES, RESTART TBI
 09B0 =         LSTROM EQU  $         ;ALL ABOVE CAN BE ROM
 09B0 D3FF      OUTIO  OUT  0FFH
 09B2 C9               RET
 09B3 DBFF      WAITIO IN   0FFH
 09B5 AC               XRA  H
 09B6 A5               ANA  L
 09B7 CAB309           JZ   WAITIO
 09BA F7               RST  6
 09BB DBFF      INPIO  IN   0FFH
 09BD 6F               MOV  L,A
 09BE C9               RET
 09BF 00        OUTCAR DB   0         ;OUTPUT CHAR. STORAGE
 09C0 FF        OCSW   DB   0FFH      ;SWITCH FOR OUTPUT
 09C1 0000      CURRNT DW   0         ;POINTS TO CURRENT LINE
 09C3 0000      STKGOS DW   0         ;SAVES SP IN 'GOSUB'
 09C5 0000      VARNXT DW   0         ;TEMPORARY STORAGE
 09C7 0000      STKINP DW   0         ;SAVES SP IN 'INPUT'
 09C9 0000      LOPVAR DW   0         ;'FOR' LOOP SAVE AREA
 09CB 0000      LOPINC DW   0         ;INCREMENT
 09CD 0000      LOPLMT DW   0         ;LIMIT
 09CF 0000      LOPLN  DW   0         ;LINE NUMBER
 09D1 0000      LOPPT  DW   0         ;TEXT POINTER
 09D3 0001      RANPNT DW   START     ;RANDOM NUMBER POINTER
 09D5 D709      TXTUNF DW   TXTBGN    ;->UNFILLED TEXT AREA
 09D7           TXTBGN DS   1         ;TEXT SAVE AREA BEGINS 
 09D8 7F7F7F5348MSG1   DB   7FH,7FH,7FH,'SHERRY BROTHERS TINY BASIC VER. 3.1',0DH 
 09FF 3EFF      INIT   MVI  A,0FFH
 0A01 32C009           STA  OCSW      ;TURN ON OUTPUT SWITCH 
 0A04 3E0C             MVI  A,0CH     ;GET FORM FEED 
 0A06 D7               RST  2         ;SEND TO CRT 
 0A07 97        PATLOP SUB  A         ;CLEAR ACCUMULATOR
 0A08 11D809           LXI  D,MSG1    ;GET INIT MESSAGE
 0A0B CD7F08           CALL PRTSTG    ;SEND IT
 0A0E 3A0700    LSTRAM LDA  7         ;GET FBASE FOR TOP
 0A11 328301           STA  RSTART+2
 0A14 3D               DCR  A         ;DECREMENT FOR OTHER POINTERS
 0A15 321901           STA  SS1A+2    ;AND FIX THEM TOO
 0A18 322601           STA  TV1A+2
 0A1B 32A501           STA  ST3A+2
 0A1E 32E501           STA  ST4A+2
 0A21 32AB05           STA  IP3A+2
 0A24 320507           STA  SIZEA+2
 0A27 321708           STA  GETLN+3
 0A2A 323609           STA  PUSHA+2
 0A2D 218401           LXI  H,ST1     ;GET NEW START JUMP
 0A30 220101           SHLD START+1   ;AND FIX IT
 0A33 C38401           JMP  ST1
                ;	RESTART TABLE
 0A50           	ORG	0A50H
                RSTBL:
 0A50 E3               XTHL           ;*** TSTC OR RST 1 *** 
 0A51 EF               RST  5         ;IGNORE BLANKS AND 
 0A52 BE               CMP  M         ;TEST CHARACTER
 0A53 C32F01           JMP  TC1       ;REST OF THIS IS AT TC1
                ;* 
 000E =         CRLF:	EQU	0EH	;EXECUTE TIME LOCATION OF THIS INSTRUCTION.
 0A56 3E0D      	MVI  A,0DH     ;*** CRLF ***
                ;* 
 0A58 F5               PUSH PSW       ;*** OUTC OR RST 2 *** 
 0A59 3AC009           LDA  OCSW      ;PRINT CHARACTER ONLY
 0A5C B7               ORA  A         ;IFF OCSW SWITCH IS ON
 0A5D C35D09           JMP  OC2       ;REST OF THIS IS AT OC2
                ;* 
 0A60 CD1606           CALL EXPR2     ;*** EXPR OR RST 3 *** 
 0A63 E5               PUSH H         ;EVALUATE AN EXPRESION 
 0A64 C3D205           JMP  EXPR1     ;REST OF IT IS AT EXPR1
 0A67 57               DB   'W' 
                ;* 
 0A68 7C               MOV  A,H       ;*** COMP OR RST 4 *** 
 0A69 BA               CMP  D         ;COMPARE HL WITH DE
 0A6A C0               RNZ            ;RETURN CORRECT C AND
 0A6B 7D               MOV  A,L       ;Z FLAGS 
 0A6C BB               CMP  E         ;BUT OLD A IS LOST 
 0A6D C9               RET
 0A6E 414E             DB   'AN'
                ;* 
 0028 =         SS1:	EQU	28H	;EXECUTE TIME LOCATION OF THIS INSTRUCTION.
 0A70 1A        	LDAX D         ;*** IGNBLK/RST 5 ***
 0A71 FE20             CPI  40Q       ;IGNORE BLANKS 
 0A73 C0               RNZ            ;IN TEXT (WHERE DE->)
 0A74 13               INX  D         ;AND RETURN THE FIRST
 0A75 C32800           JMP  SS1       ;NON-BLANK CHAR. IN A
                ;* 
 0A78 F1               POP  PSW       ;*** FINISH/RST 6 ***
 0A79 CDCD07           CALL FIN       ;CHECK END OF COMMAND
 0A7C C3E007           JMP  QWHAT     ;PRINT "WHAT?" IFF WRONG
 0A7F 47               DB   'G' 
                ;* 
 0A80 EF               RST  5         ;*** TSTV OR RST 7 *** 
 0A81 D640             SUI  100Q      ;TEST VARIABLES
 0A83 D8               RC             ;C:NOT A VARIABLE
 0A84 C30301           JMP  TSTV1     ;JUMP AROUND RESERVED AREA
                ; ROUTINE TO COPY RESTART TABLE INTO LOW MEMORY
 0008 =         RST1:	EQU	8	;LOCATION FIRST REATART ROUTINE
                
 0040 =         EOT:	EQU	40H	;LAST LOC TO BE FILLED
                
 0AA0           	ORG	0AA0H
 0AA0 210800    NINIT:	LXI	H,RST1		;POINT TO BEGINNING OF MODEL TABLE
 0AA3 11500A    	LXI	D,RSTBL
 0AA6 1A        NXT:	LDAX	D
 0AA7 77        	MOV	M,A
 0AA8 23        	INX	H
 0AA9 13        	INX	D
 0AAA 3E40      	MVI	A,EOT
 0AAC BD        	CMP	L
 0AAD C2A60A    	JNZ	NXT
 0AB0 21FF09    	LXI	H,INIT
 0AB3 220101    	SHLD	START+1
 0AB6 C30001    	JMP	START
 0F00                  ORG  0F00H
 0F00 =         TXTEND EQU  $         ;TEXT SAVE AREA ENDS 
 0F00           VARBGN DS   2*27      ;VARIABLE @(0)
 0F36                  DS   1         ;EXTRA BYTE FOR BUFFER
 0F37           BUFFER DS   80        ;INPUT BUFFER
 0F87 =         BUFEND EQU  $         ;BUFFER ENDS
 0F87                  DS   40        ;EXTRA BYTES FOR STACK
 0FAF =         STKLMT EQU  $         ;TOP LIMIT FOR STACK
 2000                  ORG  2000H
 2000 =         STACK  EQU  $         ;STACK STARTS HERE
 2000                  END
