	;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	; DEVICE DRIVER OVERLAY FOR MARK WILLIAMS COMPANY XYBASIC
	; FOR IMSAI 8080 SYSTEM WITH SIO-2
	;
	; JUNE 2014, UDO MUNK
	;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

	;XYBASIC ENTRY POINT
XYBASIC	EQU	0100H

	;PORT NUMBERS
CIN	EQU	02H		;CONSOLE DATA INPUT PORT
COUT	EQU	02H		;CONSOLE DATA OUTPUT PORT
CCTL	EQU	03H		;CONSOLE CONTROL PORT
POUT	EQU	0F6H		;PRINTER DATA OUTPUT PORT
PCTL	EQU	0F6H		;PRINTER CONTROL PORT

	;I/O BIT NUMBERS
TBA	EQU	01H		;TRANSMIT BUFFER AVAILABLE
RBR	EQU	02H		;RECEIVE BUFFER READY
PBA	EQU	04H		;PRINTER BUFFER AVAILABLE

	;CODE GENERATION FOR RST INSTRUCTIONS
	ORG	0H		;RST 0
	JMP	INIT		;INITIALIZE ON RESTART

	ORG	8H
	RET

	ORG	10H
	RET

	ORG	18H
	RET

	ORG	20H
	RET

	ORG	28H
	RET

	ORG	30H
	RET

	;DEVICE INITIALIZATION
	ORG	40H
INIT:	MVI	A,0AEH		;8251 USART INITIALIZATION
	OUT	CCTL
	MVI	A,40H
	OUT	CCTL
	MVI	A,0BAH
	OUT	CCTL
	MVI	A,37H
	OUT	CCTL
	JMP	XYBASIC		;BEGIN XYBASIC

	;I/O DEVICE DRIVERS

	;CONSOLE STATUS
CSTAT:	IN	CCTL		;READ STATUS
	ANI	RBR		;MASK TO CHARACTER READY
	RZ			;NOT READY, RETURN 0 IN A
	MVI	A,0FFH		;READY, RETURN 0FFH IN A
	RET

	;CONSOLE IN
CONIN:	IN	CCTL		;READ STATUS
	ANI	RBR		;MASK CHARACTER READY
	JZ	CONIN		;WAIT IF NOT READY
	IN	CIN		;READ THE CHARACTER
	RET

	;CONSOLE OUT
CONOUT:	IN	CCTL		;READ STATUS
	ANI	TBA		;MASK TO BUFFER AVAILABLE
	JZ	CONOUT		;WAIT IF NOT AVAILABLE
	MOV	A,C		;CHARACTER TO A
	OUT	COUT		;WRITE THE CHARACTER
	RET

	;PUNCH OUT, DEFINED TO SEND CHARACTERS TO CONSOLE
PUNOUT	EQU	CONOUT

	;READER IN, DEFINED TO READ CHARACTERS FROM CONSOLE
RDRIN	EQU	CONIN

	;LIST OUT
LOUT:	IN	PCTL		;READ STATUS
	ANI	PBA		;MASK TO BUFFER AVAILABLE
	JZ	LOUT		;WAIT IF NOT AVAILABLE
	MOV	A,C		;CHARACTER TO A
	OUT	POUT		;WRITE THE CHARACTER
	RET

	;XYBASIC DEVICE DRIVER JMP TABLE OVERLAY
	ORG	XYBASIC+18H

	;CONSOLE IN
	JMP	CONIN
	JMP	CONIN
	JMP	CONIN
	JMP	CONIN

	;CONSOLE OUT
	JMP	CONOUT
	JMP	CONOUT
	JMP	CONOUT
	JMP	CONOUT

	;READER IN
	JMP	RDRIN
	JMP	RDRIN
	JMP	RDRIN
	JMP	RDRIN

	;PUNCH OUT
	JMP	PUNOUT
	JMP	PUNOUT
	JMP	PUNOUT
	JMP	PUNOUT

	;LIST OUT
	JMP	LOUT
	JMP	LOUT
	JMP	LOUT
	JMP	LOUT

	;CONSOLE STATUS
	JMP	CSTAT
	JMP	CSTAT
	JMP	CSTAT
	JMP	CSTAT

	;MONITOR ENTRY POINT
	;RESTART XYBASIC WHEN <CONTROL-B> TYPED
	JMP	INIT

	END
